USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:		김성호
-- Create date: 2011-07-29
-- Description:	호텔 NET PRICE에 프로모션 적용하여 SELLER PRICE를 구한다
-- =============================================
CREATE FUNCTION [dbo].[FN_GET_HOTEL_SELLER_PRICE]
(
	@MASTER_CODE VARCHAR(10)
	, @SUP_CODE VARCHAR(10)
	, @DEP_DATE DATETIME
	, @NET_PRICE DECIMAL
)
RETURNS INT
AS
BEGIN
	-- 조건에 해당 하는 프로모션이 없을때에는 NET_PRICE 리턴
	SELECT TOP 1 @NET_PRICE = @NET_PRICE + ROUND((@NET_PRICE * A.DIS_PERCENT / 100), -2)
	FROM HTL_PROMOTION A WITH(NOLOCK) 
	CROSS JOIN HTL_MASTER B WITH(NOLOCK) 
	WHERE B.MASTER_CODE = @MASTER_CODE AND A.USE_YN = 'Y' AND @DEP_DATE BETWEEN A.DIS_START_DATE AND A.DIS_END_DATE
	AND @SUP_CODE = ISNULL(A.SUP_CODE, @SUP_CODE) AND @MASTER_CODE = ISNULL(A.MASTER_CODE, @MASTER_CODE)
	AND (
		(A.REGION_CODE = '000' AND A.NATION_CODE = '00' AND A.CITY_CODE = '000')
		OR (A.REGION_CODE = B.REGION_CODE AND A.NATION_CODE = '00' AND A.CITY_CODE = '000')
		OR (A.REGION_CODE = B.REGION_CODE AND A.NATION_CODE = B.NATION_CODE AND A.CITY_CODE = '000')
		OR (A.REGION_CODE = B.REGION_CODE AND A.NATION_CODE = B.NATION_CODE AND A.CITY_CODE = B.CITY_CODE)
	)
	ORDER BY DIS_LEVEL

	RETURN @NET_PRICE
END
GO
