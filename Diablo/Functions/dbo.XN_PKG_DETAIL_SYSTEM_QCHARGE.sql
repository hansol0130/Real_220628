USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*-------------------------------------------------------------------------------------------------
■ Function_Name				: XN_PKG_DETAIL_SYSTEM_QCHARGE
■ Description				: 행사정보 기준 유류할증료 검색
■ Input Parameter			:                  
		@PRO_CODE			: 행사코드
■ Output Parameter			: 
■ Output Value				: 
■ Exec						: 
---------------------------------------------------------------------------------------------------
■ Change History                   
---------------------------------------------------------------------------------------------------
	Date			Author			Description           
---------------------------------------------------------------------------------------------------
	2014-06-17		김성호			최초생성
	2014-07-03		김성호			시스템입력 유류할증료 백단위 절사
	2015-01-05		김성호			항공사별 지역구분 중 공항 매칭 데이터 우선 순위 적용
-------------------------------------------------------------------------------------------------*/ 
CREATE FUNCTION [dbo].[XN_PKG_DETAIL_SYSTEM_QCHARGE]
(	
	@PRO_CODE	VARCHAR(20)
)
RETURNS TABLE
AS
RETURN
(
	WITH LIST AS 
	(
		SELECT A.PRO_CODE, B.ARR_TRANS_CODE AS [AIRLINE_CODE], B.DEP_ARR_AIRPORT_CODE AS [AIRPORT_CODE], A.DEP_DATE, D.NATION_CODE
		FROM PKG_DETAIL A WITH(NOLOCK)
		INNER JOIN PRO_TRANS_SEAT B WITH(NOLOCK) ON A.SEAT_CODE = B.SEAT_CODE
		INNER JOIN PUB_AIRPORT C WITH(NOLOCK) ON B.DEP_ARR_AIRPORT_CODE = C.AIRPORT_CODE
		INNER JOIN PUB_CITY D WITH(NOLOCK) ON C.CITY_CODE = D.CITY_CODE
		WHERE A.PRO_CODE = @PRO_CODE AND A.SEAT_CODE > 0
		UNION ALL
		SELECT A.PRO_CODE, B.AIRLINE_CODE, B.AIRPORT_CODE, A.DEP_DATE, D.NATION_CODE
		FROM PKG_DETAIL A WITH(NOLOCK)
		INNER JOIN PKG_MASTER B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE
		LEFT JOIN PUB_AIRPORT C WITH(NOLOCK) ON B.AIRPORT_CODE = C.AIRPORT_CODE
		LEFT JOIN PUB_CITY D WITH(NOLOCK) ON C.CITY_CODE = D.CITY_CODE
		WHERE A.PRO_CODE = @PRO_CODE AND A.SEAT_CODE <= 0
	)
	SELECT TOP 1 A.PRO_CODE, A.START_DATE AS [QCHARGE_DATE], A.ADT_QCHARGE, A.CHD_QCHARGE, A.INF_QCHARGE
	FROM (
		SELECT AA.PRO_CODE, BB.START_DATE,
			DBO.XN_PUB_PRICE_CUTTING(BB.ADT_QCHARGE, 3) AS [ADT_QCHARGE],
			DBO.XN_PUB_PRICE_CUTTING(BB.CHD_QCHARGE, 3) AS [CHD_QCHARGE],
			DBO.XN_PUB_PRICE_CUTTING(BB.INF_QCHARGE, 3) AS [INF_QCHARGE], BB.NEW_DATE, (CASE WHEN BB.END_DATE > AA.DEP_DATE THEN 1 ELSE 2 END) AS [DATE_ORDER],
			(CASE WHEN PATINDEX('%' + AA.AIRPORT_CODE + '%', BB.AIRPORT_CODES) > 0 THEN 1 ELSE 2 END) AS [ZONE_ORDER]
		FROM LIST AA
		INNER JOIN AIRLINE_EXC_QCHARGE BB WITH(NOLOCK) ON AA.AIRLINE_CODE = BB.AIRLINE_CODE
		WHERE BB.START_DATE <= AA.DEP_DATE AND (PATINDEX('%' + AA.AIRPORT_CODE + '%', BB.AIRPORT_CODES) > 0 OR PATINDEX('%' + AA.NATION_CODE + '%', BB.NATION_CODES) > 0)
		UNION ALL
		SELECT AA.PRO_CODE, BB.START_DATE,
			DBO.XN_PUB_PRICE_CUTTING(BB.ADT_QCHARGE, 3) AS [ADT_QCHARGE],
			DBO.XN_PUB_PRICE_CUTTING(BB.CHD_QCHARGE, 3) AS [CHD_QCHARGE],
			DBO.XN_PUB_PRICE_CUTTING(BB.INF_QCHARGE, 3) AS [INF_QCHARGE], BB.NEW_DATE, (CASE WHEN BB.END_DATE > AA.DEP_DATE THEN 1 ELSE 2 END) AS [DATE_ORDER],
			(CASE WHEN PATINDEX('%' + AA.AIRPORT_CODE + '%', CC.AIRPORT_CODES) > 0 THEN 1 ELSE 2 END) AS [ZONE_ORDER]
		FROM LIST AA
		INNER JOIN AIRLINE_QCHARGE BB WITH(NOLOCK) ON AA.AIRLINE_CODE = BB.AIRLINE_CODE
		INNER JOIN AIRLINE_REGION CC WITH(NOLOCK) ON BB.AIRLINE_CODE = CC.AIRLINE_CODE AND BB.GROUP_SEQ = CC.GROUP_SEQ AND BB.REGION_SEQ = CC.REGION_SEQ
		WHERE BB.START_DATE <= AA.DEP_DATE AND (PATINDEX('%' + AA.AIRPORT_CODE + '%', CC.AIRPORT_CODES) > 0 OR PATINDEX('%' + AA.NATION_CODE + '%', CC.NATION_CODES) > 0)
		-- 해당 유류할증료가 없을 경우 0 반환
		UNION ALL
		SELECT @PRO_CODE, NULL, 0, 0, 0, NULL, 3, 3
	) A
	ORDER BY A.DATE_ORDER, A.NEW_DATE DESC, A.ZONE_ORDER
)
GO
