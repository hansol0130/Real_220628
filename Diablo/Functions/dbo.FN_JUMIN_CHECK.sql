USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE FUNCTION [dbo].[FN_JUMIN_CHECK]
(
	@SOC_NUM1 VARCHAR(6),
	@SOC_NUM2 VARCHAR(7)
)
RETURNS CHAR(1)
AS
BEGIN
	DECLARE @JUMIN VARCHAR(13)
	DECLARE @A_NUM CHAR(12)		-- 주민번호 검사를 위한 가중치
	DECLARE @B_NUM INT			-- 주민번호 마지막 자리수와 비교를 위한 값
	DECLARE @C_NUM INT			-- WHILE 반복값
	DECLARE @RETURN_VAL CHAR(1)	-- 리턴값
	
	SET @JUMIN = ISNULL(@SOC_NUM1, '') + ISNULL(@SOC_NUM2, '')
	
	IF (@JUMIN LIKE '[0-9][0-9][0-1][0-9][0-3][0-9][1-4][0-9][0-9][0-9][0-9][0-9][0-9]')
	BEGIN
		SET @A_NUM = '234567892345'
		SET @B_NUM = 0
		SET @C_NUM = 1
		
		WHILE @C_NUM < 13
		BEGIN
			SET @B_NUM = @B_NUM + (CAST(SUBSTRING(@JUMIN, @C_NUM, 1) AS INT) * CAST(SUBSTRING(@A_NUM, @C_NUM, 1) AS INT))
			SET @C_NUM = @C_NUM + 1
		END
		
		IF (11 - (@B_NUM % 11)) % 10 = CAST(SUBSTRING(@JUMIN, 13, 1) AS INT)
			SET @RETURN_VAL = 'T'	-- 정상 주민번호
		ELSE
			SET @RETURN_VAL = 'F'	-- 틀린 주민번호
	END
	ELSE
	BEGIN
		SET @RETURN_VAL = 'E'		-- 입력값 오류
	END
	
	RETURN @RETURN_VAL
END
GO
