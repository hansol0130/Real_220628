USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_BIZTRIP_EMPLOYEE_LOCATION_HOTEL_LIST
■ DESCRIPTION				: BTMS 출장자 위치 현황 호텔 리스트
■ INPUT PARAMETER			:
■ OUTPUT PARAMETER			: 
■ EXEC						: 
	DECLARE @TOTAL INT
	EXEC XP_COM_BIZTRIP_EMPLOYEE_LOCATION_HOTEL_LIST 1, 10, @TOTAL OUTPUT, 'AgentCode=93835&BtDate=2016-09-05&RegionCode=&EmpName=&TeamSeq='

	DECLARE @TOTAL INT
	EXEC XP_COM_BIZTRIP_EMPLOYEE_LOCATION_HOTEL_LIST 1, 10, @TOTAL OUTPUT, 'AgentCode=93853&BtDate=2016-07-29&RegionCode=&EmpName=&TeamSeq='
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR					DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-03-09		저스트고(강윤성)		최초생성
================================================================================================================*/ 

CREATE PROC [dbo].[XP_COM_BIZTRIP_EMPLOYEE_LOCATION_HOTEL_LIST]
@PAGE_INDEX INT,
@PAGE_SIZE INT,
@TOTAL_COUNT INT OUTPUT,
@KEY VARCHAR(400)

AS

BEGIN
	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000);
	DECLARE @WHERE NVARCHAR(4000);
	DECLARE @AGT_CODE VARCHAR(10), @TEAM_SEQ INT, @BT_DATE DATETIME, @EMP_NAME VARCHAR(20), @REGION_CODE VARCHAR(10)

	SELECT
		@AGT_CODE = DBO.FN_PARAM(@KEY, 'AgentCode'),
		@TEAM_SEQ = DBO.FN_PARAM(@KEY, 'TeamSeq'),
		@BT_DATE = DBO.FN_PARAM(@KEY, 'BtDate'),
		@EMP_NAME = DBO.FN_PARAM(@KEY, 'EmpName'),
		@REGION_CODE = DBO.FN_PARAM(@KEY, 'RegionCode')

	SELECT @WHERE = 'WHERE A.RES_CODE IN (SELECT RES_CODE FROM COM_BIZTRIP_DETAIL WHERE BT_CODE IN (SELECT BT_CODE FROM COM_BIZTRIP_MASTER WHERE AGT_CODE = @AGT_CODE))'
		SET @WHERE = @WHERE + 'AND B.AGT_CODE = @AGT_CODE AND F.PRO_TYPE = 3 AND B.APPROVAL_STATE = 2 AND F.RES_STATE <> 9 AND F.RES_STATE <> 7'

	IF (@TEAM_SEQ > 0)
	 BEGIN
		SET @WHERE = @WHERE + ' AND H.TEAM_SEQ = @TEAM_SEQ '
	 END

	IF (@BT_DATE <> '')
	 BEGIN
		SET @WHERE = @WHERE + ' AND F.DEP_DATE < DATEADD(DAY, 1, CONVERT(DATETIME, @BT_DATE)) AND DATEADD(DAY, 1, ISNULL(F.ARR_DATE, CONVERT(DATETIME, ''9999-12-30''))) > CONVERT(DATETIME, @BT_DATE) '
	 END

	IF (LEN(@EMP_NAME) > 0)
	 BEGIN
		SET @WHERE = @WHERE + ' AND A.CUS_NAME LIKE ''%'' + @EMP_NAME + ''%'''
	 END

	IF (@REGION_CODE <> '')
	 BEGIN
		SET @WHERE = @WHERE + ' AND J.REGION_CODE = @REGION_CODE '
	 END

	SET @SQLSTRING = N'
		SELECT @TOTAL_COUNT = COUNT(*)
		FROM  RES_CUSTOMER A
		INNER JOIN COM_BIZTRIP_MASTER B WITH(NOLOCK) ON B.BT_CODE IN (SELECT BT_CODE FROM COM_BIZTRIP_DETAIL WHERE RES_CODE = A.RES_CODE)
		INNER JOIN RES_MASTER F WITH(NOLOCK) ON A.RES_CODE = F.RES_CODE
		INNER JOIN COM_EMPLOYEE_MATCHING D WITH(NOLOCK) ON D.CUS_NO = A.CUS_NO AND D.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_EMPLOYEE E WITH(NOLOCK) ON D.EMP_SEQ = E.EMP_SEQ AND E.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_POSITION G WITH(NOLOCK) ON G.POS_SEQ = E.POS_SEQ AND G.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_TEAM H WITH(NOLOCK) ON E.TEAM_SEQ = H.TEAM_SEQ AND H.AGT_CODE = B.AGT_CODE
		LEFT JOIN RES_HTL_ROOM_MASTER K WITH(NOLOCK) ON A.RES_CODE = K.RES_CODE
		LEFT JOIN PUB_CITY C WITH(NOLOCK) ON B.BT_CITY_CODE = C.CITY_CODE
		LEFT JOIN PUB_NATION I WITH(NOLOCK) ON C.NATION_CODE = I.NATION_CODE
		LEFT JOIN PUB_REGION J WITH(NOLOCK) ON I.REGION_CODE = J.REGION_CODE
		' + @WHERE + N';

		SELECT 
			B.BT_CODE, E.EMP_SEQ, A.CUS_NO, A.CUS_NAME, G.POS_NAME, H.TEAM_NAME, K.HOTEL_NAME, F.DEP_DATE AS BT_START_DATE, F.ARR_DATE AS BT_END_DATE, C.CITY_CODE, C.KOR_NAME AS CITY_NAME, J.REGION_CODE, F.RES_CODE,
			DATEDIFF(DAY, F.DEP_DATE, F.ARR_DATE) AS [PRO_DAY], B.NEW_DATE, E.MANAGER_YN
		FROM  RES_CUSTOMER A
		INNER JOIN COM_BIZTRIP_MASTER B WITH(NOLOCK) ON B.BT_CODE IN (SELECT BT_CODE FROM COM_BIZTRIP_DETAIL WHERE RES_CODE = A.RES_CODE)
		INNER JOIN RES_MASTER F WITH(NOLOCK) ON A.RES_CODE = F.RES_CODE
		LEFT JOIN COM_EMPLOYEE_MATCHING D WITH(NOLOCK) ON D.CUS_NO = A.CUS_NO AND D.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_EMPLOYEE E WITH(NOLOCK) ON D.EMP_SEQ = E.EMP_SEQ AND E.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_POSITION G WITH(NOLOCK) ON G.POS_SEQ = E.POS_SEQ AND G.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_TEAM H WITH(NOLOCK) ON E.TEAM_SEQ = H.TEAM_SEQ AND H.AGT_CODE = B.AGT_CODE
		LEFT JOIN RES_HTL_ROOM_MASTER K WITH(NOLOCK) ON A.RES_CODE = K.RES_CODE
		LEFT JOIN PUB_CITY C WITH(NOLOCK) ON B.BT_CITY_CODE = C.CITY_CODE
		LEFT JOIN PUB_NATION I WITH(NOLOCK) ON C.NATION_CODE = I.NATION_CODE
		LEFT JOIN PUB_REGION J WITH(NOLOCK) ON I.REGION_CODE = J.REGION_CODE
		' + @WHERE + N'
		ORDER BY A.NEW_DATE DESC
		OFFSET ((@PAGE_INDEX - 1) * @PAGE_SIZE) ROWS FETCH NEXT @PAGE_SIZE ROWS ONLY '

	SET @PARMDEFINITION = N'
	@PAGE_INDEX INT,
	@PAGE_SIZE INT,
	@TOTAL_COUNT INT OUTPUT,
	@AGT_CODE VARCHAR(10),
	@TEAM_SEQ INT,
	@BT_DATE DATETIME,
	@EMP_NAME VARCHAR(20),
	@REGION_CODE VARCHAR(10)';

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
	@PAGE_INDEX,
	@PAGE_SIZE,
	@TOTAL_COUNT,
	@AGT_CODE,
	@TEAM_SEQ,
	@BT_DATE,
	@EMP_NAME,
	@REGION_CODE
END
GO
