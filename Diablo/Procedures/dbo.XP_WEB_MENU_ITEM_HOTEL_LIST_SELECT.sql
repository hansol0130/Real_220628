USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_WEB_MENU_ITEM_HOTEL_LIST_SELECT
■ DESCRIPTION				: 베스트 상품 호텔 검색
■ INPUT PARAMETER			: 
	@SITE_CODE				: 사이트 코드
	@MENU_CODE				: 메뉴 코드
	@SEC_CODE				: 섹션 코드
	@ORDER_BY				: 섹션 코드 정렬 방향 (D: DESC, 이외 ASC)
	@TOP_COUNT				: 최대 검색 수
■ OUTPUT PARAMETER			: 
■ EXEC						: 
	
	exec XP_WEB_MENU_ITEM_HOTEL_LIST_SELECT 'VGT', '107', '', 0, ''

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2013-03-25		김성호			최초생성
   2013-03-26		박형만			그룹코드 추가
   2013-05-29		김성호			SEC_CODE VARCHAR(4)로 수정
   2013-06-01		김성호			스케줄 검색 조건 반영
   2013-06-10		김성호			WITH(NOLOCK) 추가
   2015-02-24		김성호			SEC_CODE ORDER BY 정렬 방향 설정과 TOP 설정값 추가 (최근베스트 상품 검색 이유)
================================================================================================================*/ 

CREATE  PROCEDURE [dbo].[XP_WEB_MENU_ITEM_HOTEL_LIST_SELECT]
(
	@SITE_CODE	VARCHAR(3),
	@MENU_CODE	VARCHAR(20),
	@SEC_CODE	VARCHAR(4),
	@TOP_COUNT	INT,
	@ORDER_BY	VARCHAR(1)
)
AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000), @WHERE NVARCHAR(100);

	IF @SEC_CODE = ''
		SET @WHERE = ''
	ELSE
		SET @WHERE = ' AND A.SEC_CODE = @SEC_CODE'


	SET @SQLSTRING = N'
	SELECT ' + (CASE @TOP_COUNT WHEN 0 THEN '' ELSE 'TOP (@TOP_COUNT) ' END) + N'
		A.SITE_CODE, A.MENU_CODE, A.SEC_CODE, A.GRP_SEQ, A.ITEM_SEQ,
		A.IMG_URL, A.LINK_URL, A.MASTER_CODE, A.PRO_CODE, A.PRICE_SEQ, A.PRICE, A.MASTER_SEQ, A.BOARD_SEQ, A.EVT_SEQ, 
		A.DTI_ITEM1, A.DTI_ITEM2, A.DTI_ITEM3, A.DTI_ITEM4, A.ORDER_NO, A.SCH_YN
		, B.GRP_TITLE, B.GRP_CODE, B.GRP_DESC
		, C.MASTER_NAME, C.HTL_GRADE
		, (SELECT KOR_NAME FROM PUB_CITY WITH(NOLOCK) WHERE CITY_CODE = C.CITY_CODE) AS [CITY_NAME]
		, (SELECT SHORT_LOCATION FROM INF_HOTEL WITH(NOLOCK) WHERE CNT_CODE = C.CNT_CODE) AS [SHORT_LOCATION]
		, (
			SELECT TOP 1
				''/'' + AA.REGION_CODE + ''/'' + AA.NATION_CODE + ''/'' + AA.STATE_CODE + ''/'' + AA.CITY_CODE + ''/IMAGE/'' + AA.FILE_NAME_S
			FROM INF_FILE_MASTER AA WITH(NOLOCK) 
			INNER JOIN INF_FILE_MANAGER BB WITH(NOLOCK) ON AA.FILE_CODE = BB.FILE_CODE
			WHERE BB.CNT_CODE = C.CNT_CODE AND AA.FILE_TYPE = 1
			ORDER BY SHOW_ORDER
		) AS [IMAGE_PATH]
	FROM MNU_MNG_ITEM A WITH(NOLOCK)
	INNER JOIN MNU_MNG_GROUP B WITH(NOLOCK) ON A.SITE_CODE = B.SITE_CODE AND A.MENU_CODE = B.MENU_CODE AND A.SEC_CODE = B.SEC_CODE AND A.GRP_SEQ = B.GRP_SEQ
	INNER JOIN HTL_MASTER C WITH(NOLOCK) ON A.MASTER_CODE = C.MASTER_CODE
	LEFT JOIN MNU_MNG_ITEM_SCH Z WITH(NOLOCK) ON A.SITE_CODE = Z.SITE_CODE AND A.MENU_CODE = Z.MENU_CODE AND A.SEC_CODE = Z.SEC_CODE AND A.GRP_SEQ = Z.GRP_SEQ AND A.ITEM_SEQ = Z.ITEM_SEQ
	WHERE A.SITE_CODE = @SITE_CODE AND A.MENU_CODE = @MENU_CODE' + @WHERE + N'
		AND (A.SCH_YN = ''N'' OR (
			GETDATE() >= Z.START_DATE AND GETDATE() < Z.END_DATE
			--AND SUBSTRING(CONVERT(VARCHAR(20), GETDATE(), 120), 12, 8) BETWEEN Z.DAY_START_TIME AND Z.DAY_END_TIME
			AND Z.DAY_START_TIME <= SUBSTRING(CONVERT(VARCHAR(20), GETDATE(), 120), 12, 8)
			AND Z.DAY_END_TIME > SUBSTRING(CONVERT(VARCHAR(20), GETDATE(), 120), 12, 8)
			AND SUBSTRING(Z.SHOW_DAY, DATEPART(DW, GETDATE()), 1) = 1
		))
	ORDER BY A.SITE_CODE, A.MENU_CODE, A.SEC_CODE' + (CASE @ORDER_BY WHEN 'D' THEN ' DESC' ELSE '' END) + ', A.GRP_SEQ, A.ORDER_NO;'

	SET @PARMDEFINITION = N'
		@SITE_CODE	VARCHAR(3),
		@MENU_CODE	VARCHAR(20),
		@SEC_CODE	VARCHAR(4),
		@TOP_COUNT	INT';

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, 
		@SITE_CODE,
		@MENU_CODE,
		@SEC_CODE,
		@TOP_COUNT;

END
GO
