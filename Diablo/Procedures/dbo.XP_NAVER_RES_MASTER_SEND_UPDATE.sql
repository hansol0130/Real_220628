USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


/*================================================================================================================
■ USP_NAME					: XP_NAVER_RES_MASTER_SEND_UPDATE
■ DESCRIPTION				: 

네이버 예약의 동기화시 

현재것과 다른것이 있다면 
네이버에 전송후 성공시에 예약정보를 수정 

참좋은  RES_MASTER_DAMO -> 네이버 NAVER_RES_MASTER 


■ INPUT PARAMETER			:
■ OUTPUT PARAMETER			: 
■ EXEC						: XP_NAVER_RES_MASTER_SEND_UPDATE  'RP1910189536','Y'
	
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
2019-12-03	박형만	네이버 예약 비교후 틀린것이 잇으면 전송(웹서비스)한 후  NAVEr_RES_MASTER 갱신 
2019-12-13	박형만	네이버 예약 API 변경 도시,국가,마스터코드 필드 추가 
================================================================================================================*/ 
CREATE PROC [dbo].[XP_NAVER_RES_MASTER_SEND_UPDATE]
	@RES_CODE VARCHAR(12),
	@SEND_YN CHAR(1) = 'N'
AS 
BEGIN

--declare @RES_CODE VARCHAR(12)
--set @RES_CODE = 'RP1910189536' 

	-- 임시 테이블 현재꺼 넣기 
	DECLARE @TMP_RES_MASTER_NOW  TABLE 
	(	RES_CODE VARCHAR(12) ,
		SYSTEM_TYPE INT ,
		ALT_MEM_NO	VARCHAR(40) ,
		BOOK_KEY	VARCHAR(100),
		PRO_CODE	VARCHAR(30),
		PRICE_SEQ	INT ,
		PRO_NAME	VARCHAR(1000),
		NEW_DATE	DATETIME,
		DEP_DATE	DATETIME,
		ARR_DATE	DATETIME,
		TOTAL_PRICE	INT,
		ADT_COUNT	INT,
		CHD_COUNT	INT,
		INF_COUNT	INT,
		RES_NAME	VARCHAR(40),
		RES_TEL		VARCHAR(15),
		BOOKING_STATE	VARCHAR(10),
		PAY_STATE	VARCHAR(10),
		NEW_CODE	VARCHAR(7),
		EMP_NAME	VARCHAR(30),
		INNER_NUMBER VARCHAR(20),
		PRO_PC_URL	VARCHAR(500),
		PRO_MOB_URL	VARCHAR(500),
		RES_PC_URL	VARCHAR(500),
		RES_MOB_URL	VARCHAR(500),
		MEET_INFO	VARCHAR(1000),
		TC_CODE	VARCHAR(100),
		TC_YN	CHAR(1),
		TC_NUMBER VARCHAR(15),
		LAST_UPD_DATE DATETIME, 
		NATION_CODES VARCHAR(400), --2019-12 추가 
		CITY_CODES VARCHAR(1000),--2019-12 추가 
		MASTER_CODE MASTER_CODE --2019-12 추가  
		)

	-- 임시테이블에 현재예약정보 넣기 
	INSERT INTO @TMP_RES_MASTER_NOW 
	EXEC XP_NAVER_RES_MASTER_SELECT @RES_CODE 

	--네이버 예약 테이블에 있는지 확인 
	IF EXISTS ( SELECT * FROM NAVER_RES_MASTER WITH(NOLOCK) WHERE RES_CODE = @RES_CODE ) 
	BEGIN
		UPDATE A
		SET 
			 A.SYSTEM_TYPE = B.SYSTEM_TYPE
			,A.ALT_MEM_NO = B.ALT_MEM_NO
			,A.BOOK_KEY = B.BOOK_KEY
			,A.PRO_CODE = B.PRO_CODE
			,A.PRICE_SEQ = B.PRICE_SEQ
			,A.PRO_NAME = B.PRO_NAME
			,A.NEW_DATE = B.NEW_DATE
			,A.DEP_DATE = B.DEP_DATE
			,A.ARR_DATE = B.ARR_DATE
			,A.TOTAL_PRICE = B.TOTAL_PRICE
			,A.ADT_COUNT = B.ADT_COUNT
			,A.CHD_COUNT = B.CHD_COUNT
			,A.INF_COUNT = B.INF_COUNT
			,A.RES_NAME = B.RES_NAME
			,A.RES_TEL = B.RES_TEL
			,A.BOOKING_STATE = B.BOOKING_STATE
			,A.PAY_STATE = B.PAY_STATE
			,A.NEW_CODE = B.NEW_CODE
			,A.EMP_NAME = B.EMP_NAME
			,A.INNER_NUMBER = B.INNER_NUMBER
			,A.PRO_PC_URL = B.PRO_PC_URL
			,A.PRO_MOB_URL = B.PRO_MOB_URL
			,A.RES_PC_URL = B.RES_PC_URL
			,A.RES_MOB_URL = B.RES_MOB_URL
			,A.MEET_INFO = B.MEET_INFO
			,A.TC_CODE = B.TC_CODE
			,A.TC_YN = B.TC_YN
			,A.TC_NUMBER = B.TC_NUMBER
			,A.LAST_UPD_DATE = GETDATE()  --최종수정일 업데이트  . 수정과 전송이 동시에 이루어 지므로 
			,A.SEND_DATE = CASE WHEN @SEND_YN = 'Y' THEN GETDATE() ELSE A.SEND_DATE END  -- 전송Y 일경우만 전송일 업데이트 
			,A.NATION_CODES = B.NATION_CODES 
			,A.CITY_CODES = B.CITY_CODES 
			,A.MASTER_CODE = B.MASTER_CODE 

		FROM NAVER_RES_MASTER A		
			INNER JOIN @TMP_RES_MASTER_NOW  B 
				ON A.RES_CODE = B.RES_CODE 
		WHERE A.RES_CODE = @RES_CODE 
		
	END 
	ELSE  -- 없으면  
	BEGIN
		--신규 등록 
		-- 현재 정보에 있는것 새로 넣기 
		INSERT INTO NAVER_RES_MASTER 
		(RES_CODE,SYSTEM_TYPE,ALT_MEM_NO,BOOK_KEY,PRO_CODE,PRICE_SEQ,PRO_NAME,NEW_DATE,DEP_DATE,ARR_DATE,TOTAL_PRICE,ADT_COUNT,CHD_COUNT,INF_COUNT,
		RES_NAME,RES_TEL,BOOKING_STATE,PAY_STATE,NEW_CODE,EMP_NAME,INNER_NUMBER,PRO_PC_URL,PRO_MOB_URL,RES_PC_URL,RES_MOB_URL,MEET_INFO,TC_CODE,
		TC_YN,TC_NUMBER,LAST_UPD_DATE,SEND_DATE,
		NATION_CODES,CITY_CODES,MASTER_CODE) 

		SELECT 
		RES_CODE,SYSTEM_TYPE,ALT_MEM_NO,BOOK_KEY,PRO_CODE,PRICE_SEQ,PRO_NAME,NEW_DATE,DEP_DATE,ARR_DATE,TOTAL_PRICE,ADT_COUNT,CHD_COUNT,INF_COUNT,
		RES_NAME,RES_TEL,BOOKING_STATE,PAY_STATE,NEW_CODE,EMP_NAME,INNER_NUMBER,PRO_PC_URL,PRO_MOB_URL,RES_PC_URL,RES_MOB_URL,MEET_INFO,TC_CODE,
		TC_YN,TC_NUMBER,GETDATE(),CASE WHEN @SEND_YN = 'Y' THEN GETDATE() ELSE NULL END , -- 신규는 전송일 없?
		NATION_CODES , CITY_CODES ,MASTER_CODE
		FROM @TMP_RES_MASTER_NOW A 
	END 
END 
GO
