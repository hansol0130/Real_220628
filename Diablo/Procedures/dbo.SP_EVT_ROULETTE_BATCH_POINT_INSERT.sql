USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO



/*================================================================================================================
■ USP_NAME					: SP_EVT_ROULETTE_BATCH_POINT_INSERT
■ DESCRIPTION				: 룰렛 포인트 수동 적립
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 
■ EXEC						: 
EXEC SP_EVT_ROULETTE_BATCH_POINT_INSERT
SELECT * FROM EVT_ROULETTE
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2014-05-30		정지용
   2014-09-02		정지용			호텔적립 추가
================================================================================================================*/ 
CREATE PROC [dbo].[SP_EVT_ROULETTE_BATCH_POINT_INSERT]
AS 
BEGIN	
	SET NOCOUNT OFF;

	INSERT INTO EVT_ROULETTE ( EVT_ROU_SEQ, EVT_WIN_SEQ, RES_CODE, RES_SEQ_NO, CUS_CODE, NEW_CODE, NEW_DATE )
	SELECT 
		(SELECT ISNULL(MAX(EVT_ROU_SEQ), 0) FROM EVT_ROULETTE) + ROW_NUMBER() OVER (ORDER BY RES_CODE),
		NULL, 
		RES_CODE, 
		SEQ_NO, 
		CUS_NO,
		9999999,
		GETDATE() 
	FROM (
		SELECT 
			 Z.RES_CODE, SEQ_NO, CUS_NO, TOTAL_PRICE,
			 CASE 
				WHEN TOTAL_PRICE >= 10000 AND TOTAL_PRICE < 1000000 THEN '1' 	  
				WHEN TOTAL_PRICE >= 1000000 AND TOTAL_PRICE < 2000000 THEN '2' 
				WHEN TOTAL_PRICE >= 2000000 AND TOTAL_PRICE < 3000000 THEN '3' 
				WHEN TOTAL_PRICE >= 3000000 AND TOTAL_PRICE < 9000000 THEN '4' 
				WHEN TOTAL_PRICE >= 9000000 THEN '5' 
			END AS GRADE
		FROM (
			SELECT RM.RES_CODE, RC.SEQ_NO, RC.CUS_NO, 
			--((ISNULL(RC.SALE_PRICE, 0) + ISNULL(RC.CHG_PRICE, 0) + ISNULL(RC.TAX_PRICE, 0)) - ISNULL(RC.DC_PRICE, 0)) AS TOTAL_PRICE
			DBO.FN_RES_GET_TOTAL_PRICE_ONE(RM.RES_CODE, RC.SEQ_NO) AS TOTAL_PRICE
			FROM PKG_DETAIL AS PD WITH(NOLOCK)
			INNER JOIN RES_MASTER_damo AS RM WITH(NOLOCK) ON PD.PRO_CODE = RM.PRO_CODE 
			INNER JOIN RES_CUSTOMER_damo AS RC WITH(NOLOCK) ON RM.RES_CODE = RC.RES_CODE 
			INNER JOIN CUS_MEMBER AS CC WITH(NOLOCK) ON RC.CUS_NO = CC.CUS_NO
			LEFT JOIN (
				SELECT RES_CODE, CUS_CODE FROM EVT_ROULETTE WITH(NOLOCK) GROUP BY RES_CODE, CUS_CODE
			) D ON RM.RES_CODE = D.RES_CODE AND CC.CUS_NO = D.CUS_CODE
			WHERE --PD.ARR_DATE BETWEEN @START_DATE AND @END_DATE  --도착후 7일 
			RM.RES_STATE IN(3,4,5,6) -- 예약상태 결제중,결제완료,출발완료,해피콜 
			AND RC.RES_STATE IN (0) -- 출발고객상태 예약인것만
			AND PD.DEP_DATE >= '2014-06-01 00:00:00' -- 6월 1일 출발일인 사람부터
			AND CONVERT(VARCHAR(10), PD.ARR_DATE, 112) = CONVERT(VARCHAR(10), GETDATE(), 112) -- 도착일이 오늘인 사람
			AND D.RES_CODE IS NULL

			UNION ALL 

			SELECT HRM.RES_CODE, 0 AS SEQ_NO, HRM.CUS_NO,
			DBO.FN_RES_HTL_GET_TOTAL_PRICE(HRM.RES_CODE) AS TOTAL_PRICE
			FROM RES_MASTER AS HRM WITH(NOLOCK)
			INNER JOIN CUS_MEMBER AS CC WITH(NOLOCK) ON HRM.CUS_NO = CC.CUS_NO
			LEFT JOIN (
				SELECT RES_CODE, CUS_CODE FROM EVT_ROULETTE WITH(NOLOCK) GROUP BY RES_CODE, CUS_CODE
			) D ON HRM.RES_CODE = D.RES_CODE AND CC.CUS_NO = D.CUS_CODE
			WHERE 
			HRM.PRO_TYPE = 3
			AND HRM.RES_STATE IN(3,4,5,6) -- 예약상태 결제중,결제완료,출발완료,해피콜 
			AND HRM.DEP_DATE >= '2014-06-01 00:00:00' -- 6월 1일 출발일인 사람부터
			AND CONVERT(VARCHAR(10), HRM.ARR_DATE, 112) = CONVERT(VARCHAR(10), GETDATE(), 112) -- 도착일이 오늘인 사람
			AND D.RES_CODE IS NULL

		) Z
	) AA
	INNER JOIN DBO.FN_SPLIT('1,2,2,3,3,3,4,4,4,4,5,5,5,5,5,5,5,5,5,5', ',') TEMP ON AA.GRADE = TEMP.DATA
	--WHERE RES_CODE <> 'RP1201020853'
	ORDER BY RES_CODE ASC

END 



GO
