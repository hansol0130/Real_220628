USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

CREATE PROCEDURE [dbo].[SP_PKG_MASTER_SELECT_LIST]
	@FLAG				CHAR(1),
	@PAGE_SIZE			INT,
	@PAGE_INDEX			INT,

	@EMP_CODE			VARCHAR(7),
	@TEAM_CODE			VARCHAR(3),
	@MASTER_CODE		VARCHAR(10),
	@MASTER_NAME		NVARCHAR(100),
	@CITY_CODE			CHAR(3)
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000), @FROM INT, @TO INT;

	SET @FROM = @PAGE_INDEX * @PAGE_SIZE + 1;
	SET @TO = @PAGE_INDEX * @PAGE_SIZE + @PAGE_SIZE;

	-- WHERE 조건 만들기
	SET @SQLSTRING = 'WHERE 1 = 1';
	
		-- 마스터코드
	IF LEN(ISNULL(@MASTER_CODE, '')) >= 3
	BEGIN
		SET @SQLSTRING = @SQLSTRING + ' AND A.MASTER_CODE LIKE @MASTER_CODE + ''%'''
	END
	ELSE
	BEGIN
		-- 담당자코드( 없으면 팀 전체, 팀 코드가 없으면 전체)
		IF ISNULL(@TEAM_CODE, '') <> ''
		BEGIN
			IF ISNULL(@EMP_CODE, '') <> ''
				SET @SQLSTRING = @SQLSTRING + ' AND A.NEW_CODE = @EMP_CODE'
			ELSE
				SET @SQLSTRING = @SQLSTRING + ' AND A.NEW_CODE IN (SELECT EMP_CODE FROM EMP_MASTER  WITH(NOLOCK) WHERE TEAM_CODE = @TEAM_CODE)'
		END
		-- 도시코드
		IF ISNULL(@CITY_CODE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND EXISTS(SELECT 1 FROM PKG_MASTER_SCH_CITY  WITH(NOLOCK) WHERE MASTER_CODE = A.MASTER_CODE AND MAINCITY_YN = ''Y'' AND CITY_CODE = @CITY_CODE)'
		-- 상품명
		IF ISNULL(@MASTER_NAME, '') <> ''
		BEGIN
			--SET @MASTER_NAME = '%' + @MASTER_NAME + '%'
			SET @SQLSTRING = @SQLSTRING + ' AND A.MASTER_NAME LIKE (''%'' + @MASTER_NAME + ''%'')'
		END
	END

	IF @FLAG = 'C'					-- 전체 글수
	BEGIN
		SET @SQLSTRING = N'SELECT COUNT(*) FROM PKG_MASTER A  WITH(NOLOCK)  ' + @SQLSTRING;
	END
	ELSE IF @FLAG = 'L'				-- 검색
	BEGIN
		SET @SQLSTRING = N'
		WITH LIST AS
		(
			SELECT ROW_NUMBER() OVER (ORDER BY A.MASTER_CODE) AS [ROWNUMBER], A.MASTER_CODE
			FROM PKG_MASTER A
			' + @SQLSTRING + '
		)
		SELECT A.ROWNUMBER, A.MASTER_CODE, B.MASTER_NAME, B.LOW_PRICE, B.HIGH_PRICE, B.TOUR_DAY, B.SHOW_YN, B.NEW_DATE, B.EDT_DATE,
			(SELECT TOP 1 DEP_DATE FROM PKG_DETAIL  WITH(NOLOCK) WHERE MASTER_CODE = A.MASTER_CODE AND GETDATE() < DEP_DATE ORDER BY DEP_DATE ) AS [FIRST_DATE],
			(SELECT TOP 1 DEP_DATE FROM PKG_DETAIL  WITH(NOLOCK) WHERE MASTER_CODE = A.MASTER_CODE AND GETDATE() < DEP_DATE ORDER BY DEP_DATE DESC ) AS [LAST_DATE],
			(SELECT KOR_NAME FROM EMP_MASTER  WITH(NOLOCK) WHERE EMP_CODE = B.NEW_CODE) AS [NEW_NAME]
		FROM LIST A
		INNER JOIN PKG_MASTER B  WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE
		WHERE ROWNUMBER BETWEEN @FROM AND @TO;';
	END

	SET @PARMDEFINITION = N'@FROM INT, @TO INT, @EMP_CODE VARCHAR(7), @TEAM_CODE VARCHAR(3), @MASTER_CODE VARCHAR(10), @MASTER_NAME NVARCHAR(100), @CITY_CODE CHAR(3)';

	--PRINT @SQLSTRING
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @FROM, @TO, @EMP_CODE, @TEAM_CODE, @MASTER_CODE, @MASTER_NAME, @CITY_CODE;
END

--	@FLAG, @PAGE_SIZE, @PAGE_INDEX, @EMP_CODE, @TEAM_CODE, @MASTER_CODE, @MASTER_NAME, @CITY_CODE
--	SP_PKG_MASTER_SELECT_LIST 'L', 10, 0, '2008011', '529', 'cpw', '다같이', 'TYO'
--	SP_PKG_MASTER_SELECT_LIST 'L', 100, 0, '', '529', 'CPB001', '', ''
GO
