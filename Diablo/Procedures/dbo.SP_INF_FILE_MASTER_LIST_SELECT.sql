USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- AUTHOR:		<김성호>
-- CREATE DATE: <2009-04-20>
-- DESCRIPTION:	<컨텐츠 파일 정보 PAGING>
-- 2012-03-02 WITH(NOLOCK) 추가 	
-- =============================================
CREATE PROCEDURE [dbo].[SP_INF_FILE_MASTER_LIST_SELECT]
	@FLAG			CHAR(1),
	@PAGE_SIZE		INT	= 10,
	@PAGE_INDEX		INT = 0,
	@CNT_CODE		INT,
	@FILE_TYPE		INT
AS
BEGIN
	SET NOCOUNT ON;
	
	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000),	@FROM INT,	@TO INT;
	SET @SQLSTRING = '';

	SET @FROM = @PAGE_INDEX * @PAGE_SIZE + 1;
	SET @TO = @PAGE_INDEX * @PAGE_SIZE + @PAGE_SIZE;

	-- 검색된 데이타의 카운트를 돌려준다.
	IF @FLAG = 'C'
	BEGIN
		SET @SQLSTRING = N'
		SELECT COUNT(*) FROM INF_FILE_MANAGER A WITH(NOLOCK)
		INNER JOIN INF_FILE_MASTER B WITH(NOLOCK) ON A.FILE_CODE = B.FILE_CODE
		WHERE A.CNT_CODE = @CNT_CODE AND B.FILE_TYPE = @FILE_TYPE';
	END
	-- 검색된 데이타의 리스트를 돌려준다.
	ELSE IF @FLAG = 'L'
	BEGIN
		SET @SQLSTRING = N'
		WITH TMP_INF_FILE AS (
			SELECT 
				ROW_NUMBER() OVER (ORDER BY A.SHOW_ORDER) AS ROWNUMBER, A.FILE_CODE
			FROM INF_FILE_MANAGER A WITH(NOLOCK)
			INNER JOIN INF_FILE_MASTER B WITH(NOLOCK) ON A.FILE_CODE = B.FILE_CODE
			WHERE A.CNT_CODE = @CNT_CODE AND B.FILE_TYPE = @FILE_TYPE
		)
		SELECT B.*
		FROM TMP_INF_FILE A
		INNER JOIN INF_FILE_MASTER B WITH(NOLOCK) ON A.FILE_CODE = B.FILE_CODE
		WHERE ROWNUMBER BETWEEN @FROM AND @TO
		';
	END
	SET @PARMDEFINITION=N'@FROM INT, @TO INT, @CNT_CODE INT, @FILE_TYPE INT';
	--PRINT @SQLSTRING + ' ' + @PARMDEFINITION
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @FROM, @TO, @CNT_CODE, @FILE_TYPE;
END


--SP_INF_FILE_MASTER_LIST_SELECT 'C', 8, 0, 221284, 1
--SP_INF_FILE_MASTER_LIST_SELECT 'L', 8, 0, 221284, 1
GO
