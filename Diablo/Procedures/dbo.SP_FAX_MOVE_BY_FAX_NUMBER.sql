USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- Author:		 
-- Create date:  2011-11-15
-- Description:	<팩스 수신번호 이동>
-- 2011-11-29 번호로 사번찾기 
-- sys_log -> vglog..sys_log 로 이동 
-- =============================================
CREATE PROCEDURE [dbo].[SP_FAX_MOVE_BY_FAX_NUMBER]
	@OLD_EMP_CODE EMP_CODE ,
	@NEW_EMP_CODE EMP_CODE ,
	@START_DATE VARCHAR(10),
	@END_DATE VARCHAR(10),
	@OLD_FAX_NUMBER1 VARCHAR(4),
	@OLD_FAX_NUMBER2 VARCHAR(4),
	@OLD_FAX_NUMBER3 VARCHAR(4),
	@NEW_FAX_NUMBER1 VARCHAR(4),
	@NEW_FAX_NUMBER2 VARCHAR(4),
	@NEW_FAX_NUMBER3 VARCHAR(4)
AS
BEGIN	
	 
	DECLARE @RCNT INT 
	SET @RCNT = 0 
	
	DECLARE @OLD_EMP_NAME VARCHAR(20)
	SET @OLD_EMP_NAME = ''
	IF ISNULL(@OLD_EMP_CODE,'') <> ''
	BEGIN
		SELECT @OLD_EMP_NAME = KOR_NAME FROM EMP_MASTER WHERE EMP_CODE = @OLD_EMP_CODE 
	END 
	
	DECLARE @NEW_EMP_NAME VARCHAR(20)
	SET @NEW_EMP_NAME = ''
	IF ISNULL(@NEW_EMP_CODE,'') <> ''
	BEGIN
		SELECT @NEW_EMP_NAME = KOR_NAME FROM EMP_MASTER WHERE EMP_CODE = @NEW_EMP_CODE 
	END 
	
	--FAX 일괄 이동 . 번호만 변경. RCV_EMP_CODE ( 최초 담당자 정보는 수정하지 않는다)
	
	UPDATE A 
	SET A.RCV_NUMBER1 = @NEW_FAX_NUMBER1 
		, A.RCV_NUMBER2 = @NEW_FAX_NUMBER2
		, A.RCV_NUMBER3 = @NEW_FAX_NUMBER3 
	FROM FAX_RECEIVE AS A 
		INNER JOIN FAX_MASTER AS B 
			ON A.FAX_SEQ = B.FAX_SEQ 
	WHERE A.RCV_NUMBER1 = @OLD_FAX_NUMBER1
	AND A.RCV_NUMBER2 = @OLD_FAX_NUMBER2
	AND A.RCV_NUMBER3 = @OLD_FAX_NUMBER3
	
	AND B.NEW_DATE >= CONVERT(DATETIME,@START_DATE)
	AND B.NEW_DATE <= CONVERT(DATETIME,@END_DATE)
	
	
	SET @RCNT = @@ROWCOUNT
	
	INSERT INTO vglog..SYS_LOG ( CATEGORY, SYSTEM, PATH, TITLE, BODY, NEW_DATE, LOG_TYPE ) 
	VALUES ( 'ERP','System.Web','http://erp.verygoodtour.com/Manage/FaxReceiveMovePopup.aspx','팩스일괄이동로그'
		, @OLD_EMP_NAME +'('+@OLD_FAX_NUMBER1+'-'+@OLD_FAX_NUMBER2+'-'+@OLD_FAX_NUMBER3+') --> ' 
		+  @NEW_EMP_NAME + '('+@NEW_FAX_NUMBER1+'-'+@NEW_FAX_NUMBER2+'-'+@NEW_FAX_NUMBER3+')' + '총' + CONVERT(VARCHAR,@RCNT)+'건 이동'
		, GETDATE(),3) 
	
	SELECT @RCNT 
	
 END 

GO
