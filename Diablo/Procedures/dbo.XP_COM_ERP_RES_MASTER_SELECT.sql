USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_ERP_RES_MASTER_SELECT
■ DESCRIPTION				: BTMS 예약 마스터 정보 검색
■ INPUT PARAMETER			: 
	@RES_CODE				: 예약코드
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXEC XP_COM_ERP_RES_MASTER_SELECT 'RH1605103008';

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-04-14		김성호			최초생성
   2016-04-29		정지용			AGE_TYPE 잘못되어있는거 수정
   2016-05-06		이유라			AGT_NAME 추가
   2016-05-10		강태영			EMP_ID 추가
   2016-05-11		박형만			RES_CUSTOMER_damo,COM_EMPLOYEE_MATCHING LEFT 조인으로 수정 
   2016-06-02		정지용			변동가격 추가 => FN_PRO_GET_CHANGE_PRICE
   2016-07-14		이유라			EMP_SEQ 추가,  COM_EMPLOYEE_MATCHING 조인 삭제
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_ERP_RES_MASTER_SELECT]
	@RES_CODE		VARCHAR(20)
AS 
BEGIN

	-- 예약 상세 & PNR
	SELECT A.RES_CODE, A.PRO_CODE, A.SYSTEM_TYPE, A.SALE_EMP_CODE, A.RES_NAME, A.COMM_RATE, A.COMM_AMT, A.LAST_PAY_DATE, A.NEW_CODE, A.NEW_DATE, A.EDT_CODE, A.RES_STATE, A.PRO_NAME, A.DEP_DATE, A.ARR_DATE
		, (SELECT AA.KOR_NAME FROM EMP_MASTER AA WITH(NOLOCK) WHERE AA.EMP_CODE = A.SALE_EMP_CODE) AS [SALE_EMP_NAME]
		, (SELECT AA.KOR_NAME FROM EMP_MASTER AA WITH(NOLOCK) WHERE AA.EMP_CODE = A.NEW_CODE) AS [NEW_NAME]
		, (SELECT AA.KOR_NAME FROM EMP_MASTER AA WITH(NOLOCK) WHERE AA.EMP_CODE = A.EDT_CODE) AS [EDT_NAME]
		, A.NOR_TEL1, A.NOR_TEL2, A.NOR_TEL3, A.RES_EMAIL
		, ISNULL(A.EDT_DATE, A.NEW_DATE) AS EDT_DATE
		, B.AGT_CODE, E.AGT_NAME, J.EMP_ID, J.EMP_SEQ, B.BT_CODE, B.PRO_DETAIL_TYPE, B.PAY_LATER_EMP_SEQ, B.PAY_LATER_DATE, B.PAY_LATER_EMP_CODE
		, (SELECT KOR_NAME FROM COM_EMPLOYEE AA WITH(NOLOCK) WHERE AA.AGT_CODE = B.AGT_CODE AND AA.EMP_SEQ = B.PAY_LATER_EMP_SEQ) AS [PAY_LATER_EMP_NAME]
		, C.AIR_SAME_YN, C.AIR_LIKE_YN, C.AIR_DISLIKE_YN, C.HOTEL_LIKE_YN, C.HOTEL_DISLIKE_YN, C.AIR_CLASS_LIMIT, C.HOTEL_PRICE_LIMIT, C.REASON_SEQ, C.REASON_REMARK
		, D.TOTAL_COUNT, D.ADT_COUNT, D.CHD_COUNT, D.INF_COUNT
		, DBO.FN_RES_GET_TOTAL_PRICE(A.RES_CODE) AS [TOTAL_PRICE]
		, DBO.FN_RES_GET_PAY_PRICE(A.RES_CODE) AS [PAY_PRICE]
		, E.PAY_LATER_YN, A.PRO_CODE, F.INS_YN, A.CXL_CODE, A.CXL_DATE, G.KOR_NAME AS [CXL_NAME]
		, DBO.FN_PRO_GET_CHANGE_PRICE(A.PRO_CODE) AS [CHG_PRICE]
	FROM RES_MASTER_damo A WITH(NOLOCK)
	LEFT JOIN COM_BIZTRIP_DETAIL B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE
	LEFT JOIN COM_BIZTRIP_RULE_REMARK C WITH(NOLOCK) ON A.RES_CODE = C.RES_CODE	
	LEFT JOIN COM_EMPLOYEE_MATCHING I WITH(NOLOCK) ON I.CUS_NO = A.CUS_NO
	LEFT JOIN COM_EMPLOYEE J WITH(NOLOCK) ON J.AGT_CODE = B.AGT_CODE AND J.EMP_SEQ = I.EMP_SEQ
	LEFT JOIN (
		SELECT A.RES_CODE
			, COUNT(*) AS [TOTAL_COUNT]
			, SUM(CASE WHEN A.AGE_TYPE = 0 THEN 1 ELSE 0 END) AS [ADT_COUNT]
			, SUM(CASE WHEN A.AGE_TYPE = 1 THEN 1 ELSE 0 END) AS [CHD_COUNT]
			, SUM(CASE WHEN A.AGE_TYPE = 2 THEN 1 ELSE 0 END) AS [INF_COUNT]
		FROM RES_CUSTOMER_damo A WITH(NOLOCK)
		WHERE A.RES_CODE = @RES_CODE AND A.RES_STATE = 0
		GROUP BY A.RES_CODE	
	) D ON D.RES_CODE = A.RES_CODE	
	LEFT JOIN AGT_MASTER E WITH(NOLOCK) ON B.AGT_CODE = E.AGT_CODE
	LEFT JOIN PKG_DETAIL F WITH(NOLOCK) ON A.PRO_CODE = F.PRO_CODE
	LEFT JOIN EMP_MASTER G WITH(NOLOCK) ON A.CXL_CODE = G.EMP_CODE
	WHERE A.RES_CODE = @RES_CODE

END 

GO
