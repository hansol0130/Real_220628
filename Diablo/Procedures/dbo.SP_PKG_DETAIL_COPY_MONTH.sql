USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================    
■ USP_Name			: SP_PKG_DETAIL_COPY_MONTH      
■ Description		: 행사 일자별 복사
■ Input Parameter   :                      
	@PRO_CODE		: 상품코드
	@DAYS			: 일자들
	@EMP_CODE		: 생성자인코드  
■ Output Parameter	:                      
■ Output Value		:                     
■ Exec				: 
■ Memo				:    
------------------------------------------------------------------------------------------------------------------    
■ Change History
------------------------------------------------------------------------------------------------------------------    
Date		Author	Description
------------------------------------------------------------------------------------------------------------------    
2009-07-20	김성호	최초생성      
2011-09-02	김성호	PRO_TYPE 입력 추가
2013-05-08	김성호	상세항목 전체 선택 시 수정 항목에 MEET_COUNTER 추가
2013-06-27	김성호	항공코드 중복으로 인해 검색 조건 강화
2014-06-25	김성호	총액표시제에 따른 스키마 수정 반영,6/30 PKG_PRICE_DETAIL 컬럼복사 수정
2014-07-25	김성호	총액표시제에 따른 옵션정보 복사
2017-03-03	김성호	PKG_CODE, MASTER_CODE 생성 시 대문자 처리
2019-02-19	김성호	서버 문제시 일시 정지 기능 추가 -> UPDATE COD_PUBLIC SET PUB_VALUE = 'Y' WHERE PUB_TYPE = 'PKG.COPY.CHECK' AND PUB_CODE = '1'
2019-03-07	박형만	네이버 관련 행사 컬럼 추가 
2019-04-07	박형만	좌석복사시 좌석등급추가 
2019-05-16	이명훈	상품상세 셀링포인트 추가(PKG_DETAIL_SELL_POINT)
2019-08-19	김남훈	네이버 사용자 정의 추가, 이동 수단 추가
2020-03-24	김성호	트리거 예외처리 추가 (트리거에 CONTEXT_INFO() 체크하여 트리거 무시하는 로직 추가 필요)
2021-03-04	오준혁	네이버 연동 관련 제거
================================================================================================================*/   
CREATE PROCEDURE [dbo].[SP_PKG_DETAIL_COPY_MONTH]
	@PRO_CODE		VARCHAR(20),	-- 상품코드
	@DAYS			VARCHAR(1000),	-- 일자들
	@EMP_CODE		CHAR(7)			-- 생성인코드
AS
BEGIN
	
	-- 트리거 동작 제외
	SET CONTEXT_INFO 0x21884680;
	
	SET NOCOUNT ON;

	DECLARE @BIT_CODE VARCHAR(10), @DIFF_DAY1 INT, @DIFF_DAY2 INT, @DIFF_DAY3 INT
	DECLARE @NEW_PRO_CODE VARCHAR(20), @NEW_MASTER_CODE VARCHAR(10), @NEW_SEAT_CODE INT
	DECLARE @CHK_SEAT_CODE INT, @LAST_PAY_INTERVAL INT
	DECLARE @CONTINUE INT, @DAY VARCHAR(10), @STRIKE INT
	
	-- 결과저장 임시파일
	CREATE TABLE #MSG_TEMP (
		[ERROR_SEQ] INT IDENTITY,	[ERROR_DATE] DATETIME,
		[ERROR_TYPE] INT,			[ERROR_MESSAGE] NVARCHAR(2048))


-- 서버 부하 발생 시 동작 일시 중지용
IF ISNULL((SELECT PUB_VALUE FROM COD_PUBLIC A WITH(NOLOCK) WHERE A.PUB_TYPE = 'PKG.COPY.CHECK' AND A.PUB_CODE = '1'), 'Y') = 'Y'
BEGIN 

	-- 출발일 대비 입금마감일
	SET @LAST_PAY_INTERVAL = -7
	SET @CONTINUE = 1
	
	-- 상품 항공 정보 및 날짜 차이 계산, 기준 행사 운송코드
	SELECT 
--		@DEP_TRANS_CODE = A.DEP_TRANS_CODE, @DEP_TRANS_NUMBER = A.DEP_TRANS_NUMBER
--		, @ARR_TRANS_CODE = A.ARR_TRANS_CODE, @ARR_TRANS_NUMBER = A.ARR_TRANS_NUMBER
--		, 
--		@DIFF_DAY = ISNULL(DATEDIFF(DAY, A.DEP_DEP_DATE, A.ARR_ARR_DATE), 0), 
		@CHK_SEAT_CODE = B.SEAT_CODE,
		@DIFF_DAY1 = ISNULL(DATEDIFF(DAY, A.DEP_DEP_DATE, A.DEP_ARR_DATE), 0),
		@DIFF_DAY2 = ISNULL(DATEDIFF(DAY, A.DEP_DEP_DATE, A.ARR_DEP_DATE), 0),
		@DIFF_DAY3 = ISNULL(DATEDIFF(DAY, A.DEP_DEP_DATE, A.ARR_ARR_DATE), 0)
	FROM PRO_TRANS_SEAT A WITH(NOLOCK)
	INNER JOIN PKG_DETAIL B WITH(NOLOCK) ON A.SEAT_CODE = B.SEAT_CODE
	WHERE B.PRO_CODE = @PRO_CODE AND B.TRANSFER_TYPE <= 2
	
	SELECT @DIFF_DAY3 = (TOUR_DAY - 1), @CHK_SEAT_CODE = SEAT_CODE
	FROM PKG_DETAIL WITH(NOLOCK) WHERE PRO_CODE = @PRO_CODE AND TOUR_DAY > 0 AND TRANSFER_TYPE >= 3
	
	-- 박 수 세팅
	IF @DIFF_DAY3 IS NULL
		SET @DIFF_DAY3 = 0
	
	-- 행사정보
--	SELECT @BIT_CODE = SUBSTRING(PRO_CODE, 14, 10), @NEW_MASTER_CODE = MASTER_CODE
	SELECT @BIT_CODE = SUBSTRING(PRO_CODE, (CHARINDEX('-', PRO_CODE) + 7), 10)
		, @NEW_MASTER_CODE = MASTER_CODE
	FROM PKG_DETAIL WITH(NOLOCK) WHERE PRO_CODE = @PRO_CODE


		
	WHILE @CONTINUE = 1
	BEGIN	
		IF CHARINDEX('|', @DAYS) > 0
		BEGIN
			SET @DAY = SUBSTRING(@DAYS, 1, CHARINDEX('|', @DAYS) - 1)
			
			SET @NEW_SEAT_CODE = 0

			SELECT TOP 1 @NEW_SEAT_CODE = B.SEAT_CODE
			FROM PRO_TRANS_SEAT A WITH(NOLOCK)
			INNER JOIN PRO_TRANS_SEAT B WITH(NOLOCK) ON A.DEP_TRANS_CODE = B.DEP_TRANS_CODE AND A.DEP_TRANS_NUMBER = B.DEP_TRANS_NUMBER
				AND A.DEP_DEP_AIRPORT_CODE = B.DEP_DEP_AIRPORT_CODE AND A.DEP_ARR_AIRPORT_CODE = B.DEP_ARR_AIRPORT_CODE
				AND A.ARR_TRANS_CODE = B.ARR_TRANS_CODE AND A.ARR_TRANS_NUMBER = B.ARR_TRANS_NUMBER
				AND A.ARR_DEP_AIRPORT_CODE = B.ARR_DEP_AIRPORT_CODE AND A.ARR_ARR_AIRPORT_CODE = B.ARR_ARR_AIRPORT_CODE
				AND A.FARE_SEAT_TYPE = B.FARE_SEAT_TYPE -- 2019-04-09 좌석등급 추가 
			WHERE A.SEAT_CODE = @CHK_SEAT_CODE
				AND B.DEP_DEP_DATE = @DAY --AND B.ARR_ARR_DATE = DATEADD(DAY, @DIFF_DAY, @DAY)
				AND B.DEP_ARR_DATE = DATEADD(DAY, @DIFF_DAY1, @DAY)
				AND B.ARR_DEP_DATE = DATEADD(DAY, @DIFF_DAY2, @DAY)
				AND B.ARR_ARR_DATE = DATEADD(DAY, @DIFF_DAY3, @DAY)

			--SELECT @NEW_SEAT_CODE = SEAT_CODE FROM PRO_TRANS_SEAT WITH(NOLOCK) 
			--WHERE DEP_TRANS_CODE = @DEP_TRANS_CODE AND DEP_TRANS_NUMBER = @DEP_TRANS_NUMBER
			--AND ARR_TRANS_CODE = @ARR_TRANS_CODE AND ARR_TRANS_NUMBER = @ARR_TRANS_NUMBER
			--AND DEP_DEP_DATE = @DAY AND ARR_ARR_DATE = DATEADD(DAY, @DIFF_DAY, @DAY)
			
			IF @NEW_SEAT_CODE > 0 OR @CHK_SEAT_CODE = 0
			BEGIN
				BEGIN TRY
					BEGIN TRAN

					--SET @NEW_PRO_CODE = (@NEW_MASTER_CODE + SUBSTRING(CONVERT(VARCHAR(10), GETDATE(), 112), 3, 10) + @BIT_CODE)
					SET @NEW_PRO_CODE = (@NEW_MASTER_CODE + '-' + SUBSTRING(ISNULL(REPLACE(@DAY, '-', ''), ''), 3, 10) + @BIT_CODE)

					IF NOT EXISTS(SELECT 1 FROM PKG_DETAIL  WITH(NOLOCK) WHERE PRO_CODE = @NEW_PRO_CODE)
					BEGIN
						-- 행사 복사
						INSERT INTO PKG_DETAIL (
							PRO_CODE, PRO_NAME, MASTER_CODE, TRANSFER_TYPE, SEAT_CODE, DEP_DATE, ARR_DATE
							, TOUR_NIGHT, TOUR_DAY, SEAT_STATUS, FAKE_COUNT, MAX_COUNT, MIN_COUNT, LAST_PAY_DATE
							, SENDING_YN, SENDING_DATE, TC_YN, TC_CODE, TC_NAME, RES_ADD_YN, RES_EDT_YN, DEP_CFM_YN, CONFIRM_YN, VISA_YN
							, PASS_INFO, FIRST_MEET, SALE_TYPE, PRICE_TYPE, AIR_GDS, HOTEL_GDS, AIRLINE, PKG_SUMMARY, PKG_INC_SPECIAL
							, PKG_REVIEW, PKG_CONTRACT, PKG_REMARK, RES_REMARK, SHOW_YN, NEW_DATE, NEW_CODE
							, TOUR_JOURNEY, PKG_SHOPPING_REMARK, HOTEL_REMARK, OPTION_REMARK, PKG_TOUR_REMARK, PKG_PASSPORT_REMARK, PRO_TYPE, MEET_COUNTER
							, GUIDE_YN, AIR_CFM_YN, ROOM_CFM_YN, SCHEDULE_CFM_YN, PRICE_CFM_YN )
						SELECT
							UPPER(@NEW_PRO_CODE), PRO_NAME, UPPER(MASTER_CODE), TRANSFER_TYPE, @NEW_SEAT_CODE, @DAY, DATEADD(DAY, @DIFF_DAY3, @DAY)
							, TOUR_NIGHT, TOUR_DAY, SEAT_STATUS, 0, MAX_COUNT, MIN_COUNT, DATEADD(DAY, @LAST_PAY_INTERVAL, @DAY)
							, SENDING_YN, NULL, TC_YN, NULL, NULL, 'Y', 'Y', 'N', 'N', VISA_YN
							, PASS_INFO, FIRST_MEET, SALE_TYPE, PRICE_TYPE, AIR_GDS, HOTEL_GDS, AIRLINE, PKG_SUMMARY, PKG_INC_SPECIAL
							, PKG_REVIEW, PKG_CONTRACT, PKG_REMARK, RES_REMARK, SHOW_YN, GETDATE(), NEW_CODE
							, TOUR_JOURNEY, PKG_SHOPPING_REMARK, HOTEL_REMARK, OPTION_REMARK, PKG_TOUR_REMARK, PKG_PASSPORT_REMARK, PRO_TYPE, MEET_COUNTER 
							, GUIDE_YN, AIR_CFM_YN, ROOM_CFM_YN, SCHEDULE_CFM_YN, PRICE_CFM_YN
						FROM PKG_DETAIL WITH(NOLOCK) WHERE PRO_CODE = @PRO_CODE

						-- 상세일정 복사
						INSERT INTO PKG_DETAIL_SCH_MASTER
						SELECT @NEW_PRO_CODE, SCH_SEQ, SCH_NAME FROM PKG_DETAIL_SCH_MASTER WITH(NOLOCK) 
						WHERE PRO_CODE = @PRO_CODE

						INSERT INTO PKG_DETAIL_SCH_DAY
						SELECT @NEW_PRO_CODE, SCH_SEQ, DAY_SEQ, DAY_NUMBER ,FREE_SCH_YN  --자유일정유무 19.03 추가 
						FROM PKG_DETAIL_SCH_DAY WITH(NOLOCK) 
						WHERE PRO_CODE = @PRO_CODE

						--DELETE FROM NAVER_PKG_DETAIL_SCH_DAY_TRANSPORT WHERE PRO_CODE = @NEW_PRO_CODE

						/*
						--네이버 이동수단 복사(2019-08-19)
						INSERT INTO NAVER_PKG_DETAIL_SCH_DAY_TRANSPORT
						SELECT @NEW_PRO_CODE, SCH_SEQ, DAY_SEQ, DAY_NUMBER, TRANSPORT_SEQ, TRANSPORT_TYPE, TRANSPORT_DESC
						FROM NAVER_PKG_DETAIL_SCH_DAY_TRANSPORT WITH(NOLOCK)
						WHERE PRO_CODE = @PRO_CODE
						*/
						
						INSERT INTO PKG_DETAIL_SCH_CITY
						SELECT @NEW_PRO_CODE, SCH_SEQ, DAY_SEQ, CITY_SEQ, CITY_CODE, MAINCITY_YN, CITY_SHOW_ORDER
						FROM PKG_DETAIL_SCH_CITY WITH(NOLOCK) 
						WHERE PRO_CODE = @PRO_CODE

						INSERT INTO PKG_DETAIL_SCH_CONTENT
						SELECT @NEW_PRO_CODE, SCH_SEQ, DAY_SEQ, CITY_SEQ, CNT_SEQ, CNT_CODE, CNT_INFO, CNT_SHOW_ORDER
						FROM PKG_DETAIL_SCH_CONTENT WITH(NOLOCK) 
						WHERE PRO_CODE = @PRO_CODE

						--DELETE FROM NAVER_PKG_DETAIL_SCH_CONTENT WHERE PRO_CODE = @NEW_PRO_CODE
						
						/*
						--네이버 사용자 정의 복사(2019-08-19)
						INSERT INTO NAVER_PKG_DETAIL_SCH_CONTENT
						SELECT @NEW_PRO_CODE, SCH_SEQ, DAY_SEQ, CITY_SEQ, CNT_SEQ, CNT_SUBJECT, CNT_INFO
						FROM NAVER_PKG_DETAIL_SCH_CONTENT WITH(NOLOCK)
						WHERE PRO_CODE = @PRO_CODE
						*/
						
						-- 옵션정보 복사
						INSERT INTO PKG_DETAIL_OPTION (PRO_CODE, OPT_SEQ, OPT_NAME, OPT_CONTENT, OPT_PRICE, OPT_USETIME, OPT_REPLACE, OPT_PLACE, OPT_COMPANION)
						SELECT @NEW_PRO_CODE, A.OPT_SEQ, A.OPT_NAME, A.OPT_CONTENT, A.OPT_PRICE, A.OPT_USETIME, A.OPT_REPLACE, A.OPT_PLACE, A.OPT_COMPANION
						FROM PKG_DETAIL_OPTION A WITH(NOLOCK)
						WHERE A.PRO_CODE = @PRO_CODE

						-- 쇼핑정보 복사
						INSERT INTO PKG_DETAIL_SHOPPING (PRO_CODE, SHOP_SEQ, SHOP_NAME, SHOP_PLACE, SHOP_TIME, SHOP_REMARK)
						SELECT @NEW_PRO_CODE, A.SHOP_SEQ, A.SHOP_NAME, A.SHOP_PLACE, A.SHOP_TIME, A.SHOP_REMARK
						FROM PKG_DETAIL_SHOPPING A WITH(NOLOCK)
						WHERE A.PRO_CODE = @PRO_CODE

						-- 셀링포인트 복사 19.03 추가 
						INSERT INTO PKG_DETAIL_SELL_POINT 
						(PRO_CODE,TRAFFIC_POINT,STAY_POINT,TOUR_POINT,EAT_POINT,DISCOUNT_POINT,OTHER_POINT,INNER_PKG_GUIDANCE,INNER_CONTENT_URL)
						SELECT @NEW_PRO_CODE , TRAFFIC_POINT,STAY_POINT,TOUR_POINT,EAT_POINT,DISCOUNT_POINT,OTHER_POINT,INNER_PKG_GUIDANCE,INNER_CONTENT_URL
						FROM PKG_DETAIL_SELL_POINT  WITH(NOLOCK)
						WHERE PRO_CODE = @PRO_CODE

						-- 가격정보 복사
						INSERT INTO PKG_DETAIL_PRICE (
							PRO_CODE, PRICE_SEQ, PRICE_NAME, SEASON, SCH_SEQ, PKG_INCLUDE, PKG_NOT_INCLUDE
							, ADT_PRICE, CHD_PRICE, INF_PRICE, SGL_PRICE, CUR_TYPE, EXC_RATE, FLOATING_YN
							, POINT_RATE, POINT_PRICE, POINT_YN
							, QCHARGE_TYPE, ADT_QCHARGE, CHD_QCHARGE, INF_QCHARGE, ADT_TAX, CHD_TAX, INF_TAX, QCHARGE_DATE
							)
						SELECT @NEW_PRO_CODE, PRICE_SEQ, PRICE_NAME, SEASON, SCH_SEQ, PKG_INCLUDE, PKG_NOT_INCLUDE
							, ADT_PRICE, CHD_PRICE, INF_PRICE, SGL_PRICE, CUR_TYPE, EXC_RATE, FLOATING_YN
							, POINT_RATE, POINT_PRICE, POINT_YN
							, QCHARGE_TYPE, ADT_QCHARGE, CHD_QCHARGE, INF_QCHARGE, ADT_TAX, CHD_TAX, INF_TAX, QCHARGE_DATE
						FROM PKG_DETAIL_PRICE WITH(NOLOCK) 
				 		WHERE PRO_CODE = @PRO_CODE

						--가격 포함/불포함(네이버) 복사 19.03 추가 
						INSERT INTO PKG_DETAIL_PRICE_INOUT (PRO_CODE,PRICE_SEQ,INOUT_CODE,IN_YN)
						SELECT @NEW_PRO_CODE, A.PRICE_SEQ, A.INOUT_CODE, A.IN_YN
						FROM PKG_DETAIL_PRICE_INOUT A WITH(NOLOCK)
						WHERE A.PRO_CODE = @PRO_CODE 

						--가격호텔 복사 19.03 식사코드 추가 
						INSERT INTO PKG_DETAIL_PRICE_HOTEL 
						(PRO_CODE, PRICE_SEQ, DAY_NUMBER, HTL_MASTER_CODE, SUP_CODE, STAY_TYPE, STAY_INFO, DINNER_1,DINNER_2,DINNER_3,DINNER_CODE_1,DINNER_CODE_2,DINNER_CODE_3)
						SELECT @NEW_PRO_CODE, PRICE_SEQ, DAY_NUMBER, HTL_MASTER_CODE, SUP_CODE, STAY_TYPE, STAY_INFO,DINNER_1,DINNER_2,DINNER_3,DINNER_CODE_1,DINNER_CODE_2,DINNER_CODE_3
						FROM PKG_DETAIL_PRICE_HOTEL WITH(NOLOCK) 
						WHERE PRO_CODE = @PRO_CODE

						-- 공동경비 복사
						INSERT INTO PKG_DETAIL_PRICE_GROUP_COST (PRO_CODE, PRICE_SEQ, COST_SEQ, COST_NAME, CURRENCY, ADT_COST, CHD_COST, INF_COST, USE_YN)
						SELECT @NEW_PRO_CODE, A.PRICE_SEQ, A.COST_SEQ, A.COST_NAME, A.CURRENCY, A.ADT_COST, A.CHD_COST, A.INF_COST, A.USE_YN
						FROM PKG_DETAIL_PRICE_GROUP_COST A WITH(NOLOCK)
						WHERE A.PRO_CODE = @PRO_CODE
						
						-- 파일정보 복사
						INSERT INTO PKG_DETAIL_FILE
						SELECT @NEW_PRO_CODE, FILE_CODE, SHOW_ORDER
						FROM PKG_DETAIL_FILE  WITH(NOLOCK) WHERE PRO_CODE = @PRO_CODE

						-- LOG INSERT
						INSERT INTO #MSG_TEMP 
						SELECT @DAY, 1, '복사 성공'
					END
					ELSE IF @PRO_CODE = @NEW_PRO_CODE
					BEGIN
						-- LOG INSERT
						INSERT INTO #MSG_TEMP 
						SELECT @DAY, 1, '기준 코드.'
					END
					ELSE
					BEGIN
						-- LOG INSERT
						INSERT INTO #MSG_TEMP 
						SELECT @DAY, 2, '중복 코드.'
					END

					COMMIT TRAN
				END TRY
				BEGIN CATCH

					ROLLBACK TRAN

					-- LOG INSERT
					INSERT INTO #MSG_TEMP 
					SELECT @DAY, 2, ('[' + @NEW_PRO_CODE + '] ' + ERROR_MESSAGE())
					
				END CATCH
			END
			ELSE
			BEGIN
				INSERT INTO #MSG_TEMP 
				SELECT @DAY, 2, '좌석정보없음'
			END

			-- 일자변경
			SET @STRIKE = LEN(@DAY) + 1
			SET @DAYS = LTRIM(RIGHT(@DAYS, LEN(@DAYS) - @STRIKE))
			
		END
		ELSE
		BEGIN
			SET @CONTINUE = 0
		END
	END

	-- 트리거 예외처리 후 마스터 업데이트
	EXEC DBO.SP_PKG_MASTER_RESETTING @NEW_MASTER_CODE;

END
ELSE
BEGIN
	INSERT INTO #MSG_TEMP 
	SELECT GETDATE(), 2, '일시동작중지(개발팀)'
END

	-- 결과리턴
	SELECT * FROM #MSG_TEMP;
	DROP TABLE #MSG_TEMP;
	
END
GO
