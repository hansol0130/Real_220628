USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO




-- =============================================
-- Author:		이규식
-- Create date: 2009-11-16
-- Description:	예약자별 수익현황 (행사코드버전)
-- 2011-07-21 PRO_TYPE 컬럼 추가
-- =============================================
CREATE PROCEDURE [dbo].[SP_SET_GET_RES_COMPLETE_LIST_3]
(
	@PRO_CODE VARCHAR(20)
)
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	SET NOCOUNT ON

	DECLARE @TEMP_TABLE  TABLE 
	(
		PRO_CODE	VARCHAR(20),
		PRO_TYPE	INT,
		RES_CODE	VARCHAR(20),
		SEQ_NO		INT,
		CUS_NAME	VARCHAR(40), 
		LAST_NAME	VARCHAR(20),
		FIRST_NAME	VARCHAR(20),
		AGE_TYPE	INT,
		GENDER		CHAR(1),
		SALE_PRICE	MONEY,
		RES_STATE	INT,
		PROFIT_EMP_CODE VARCHAR(10),
		PROFIT_TEAM_CODE VARCHAR(10),
		PROFIT_TEAM_NAME VARCHAR(50),
		PERSON_PRICE	MONEY,
		PERSON_PROFIT	MONEY,
		PERSON_ETC_PRICE	MONEY,
		AIR_PROFIT	MONEY,
		AIR_PRICE	MONEY,
		AIR_SALE_PRICE	MONEY,
		GROUP_PRICE	MONEY,
		GROUP_PROFIT	MONEY,
		AGENT_COM_PRICE	MONEY,
		PAY_COM_PRICE	MONEY,
		AIR_ETC_PROFIT	MONEY,
		AIR_ETC_PRICE	MONEY,
		AIR_ETC_COM_PRICE	MONEY,
		AIR_ETC_COM_PROFIT	MONEY,
		LAND_COM_PRICE	MONEY,
		LAND_PRICE	MONEY,
		IS_PROFIT	CHAR(1)
	);


	INSERT INTO @TEMP_TABLE
	SELECT * FROM DBO.FN_SET_GET_PRO_COMPLETE(@PRO_CODE)

	SELECT 
		A.PRO_CODE,
		A.PRO_TYPE,
		A.RES_CODE,
		B.PRO_NAME,
		B.DEP_DATE,
		(SELECT KOR_NAME FROM PUB_REGION Z WHERE Z.SIGN = C.SIGN_CODE) AS REGION_NAME,
		DBO.FN_RES_GET_RES_COUNT(A.RES_CODE) AS RES_COUNT,
		--SUM(ISNULL(SALE_PRICE, 0)) AS SALE_PRICE,
		dbo.FN_RES_GET_TOTAL_PRICE(A.RES_CODE) AS [SALE_PRICE],
		PROFIT_EMP_CODE,
		PROFIT_TEAM_CODE,
		PROFIT_TEAM_NAME,
		(SELECT KOR_NAME FROM EMP_MASTER WHERE EMP_CODE = PROFIT_EMP_CODE) AS PROFIT_EMP_NAME,
		SUM(ISNULL(PERSON_PRICE, 0)) AS PERSON_PRICE,
		SUM(ISNULL(PERSON_PROFIT, 0)) AS PERSON_PROFIT,
		SUM(ISNULL(PERSON_ETC_PRICE, 0)) AS PERSON_ETC_PRICE,
		SUM(ISNULL(AIR_PROFIT, 0)) AS AIR_PROFIT,
		SUM(ISNULL(AIR_PRICE, 0)) AS AIR_PRICE,
		SUM(ISNULL(AIR_SALE_PRICE, 0)) AS AIR_SALE_PRICE,
		SUM(ISNULL(GROUP_PRICE, 0)) AS GROUP_PRICE,
		SUM(ISNULL(GROUP_PROFIT, 0)) AS GROUP_PROFIT,
		SUM(ISNULL(AGENT_COM_PRICE, 0)) AS AGENT_COM_PRICE,
		SUM(ISNULL(PAY_COM_PRICE, 0)) AS PAY_COM_PRICE,
		SUM(ISNULL(AIR_ETC_PROFIT, 0)) AS AIR_ETC_PROFIT,
		SUM(ISNULL(AIR_ETC_PRICE, 0)) AS AIR_ETC_PRICE,
		SUM(ISNULL(AIR_ETC_COM_PRICE, 0)) AS AIR_ETC_COM_PRICE,
		SUM(ISNULL(AIR_ETC_COM_PROFIT, 0)) AS AIR_ETC_COM_PROFIT,
		SUM(ISNULL(LAND_COM_PRICE, 0)) AS LAND_COM_PRICE,
		SUM(ISNULL(LAND_PRICE, 0)) AS LAND_PRICE,
		IS_PROFIT
	FROM @TEMP_TABLE A
	INNER JOIN PKG_DETAIL B WITH(NOLOCK) ON B.PRO_CODE = A.PRO_CODE
	INNER JOIN PKG_MASTER C WITH (NOLOCK) ON C.MASTER_CODE= B.MASTER_CODE
	GROUP BY 
		A.PRO_CODE,
		A.PRO_TYPE,
		A.RES_CODE,
		B.PRO_NAME,
		B.DEP_DATE,
		C.SIGN_CODE,
		A.PROFIT_EMP_CODE,
		A.PROFIT_TEAM_CODE,
		A.PROFIT_TEAM_NAME,
		A.IS_PROFIT
	ORDER BY 
		A.RES_CODE

	SET NOCOUNT OFF
END




GO
