USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_BIZTRIP_GROUP_SELECT
■ DESCRIPTION				: BTMS 출장자 그룹 규정 검색
■ INPUT PARAMETER			: 
	@AGT_CODE				: 거래처코드
	@BT_SEQ					: 출장그룹 순번 (0이면 정렬순서 마지막 번호 리턴)
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXEC DBO.XP_COM_BIZTRIP_GROUP_SELECT 92756, '1'
	select * from COM_BIZTRIP_GROUP

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-01-30		김성호			최초생성
   2016-02-05		정지용			COM_HOTEL_RULE => BT_SEQ, HOTEL_RULE_SEQ, NEW_SEQ 추가
   2016-02-15		정지용			EDT_NAME 추가
   2016-02-19		정지용			수정자 정보 추가
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_BIZTRIP_GROUP_SELECT]
	@AGT_CODE		VARCHAR(10),
	@BT_SEQ			INT
AS 
BEGIN

	IF @BT_SEQ = 0
	BEGIN
		SELECT @AGT_CODE
			, (ISNULL((SELECT MAX(A.ORDER_NUM) FROM COM_BIZTRIP_GROUP A WITH(NOLOCK) WHERE A.AGT_CODE = @AGT_CODE), 0) + 1) AS [ORDER_NUM]
	END
	ELSE
	BEGIN

		WITH AGT_EMP_LIST AS
		(
			SELECT 
				A.AGT_CODE, A.EMP_SEQ, A.KOR_NAME, B.TEAM_NAME, C.POS_NAME
			FROM COM_EMPLOYEE A WITH(NOLOCK)
			LEFT JOIN COM_TEAM B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.TEAM_SEQ = B.TEAM_SEQ
			LEFT JOIN COM_POSITION C WITH(NOLOCK) ON A.AGT_CODE = C.AGT_CODE AND A.POS_SEQ = C.POS_SEQ
			WHERE A.AGT_CODE = @AGT_CODE
		)
		SELECT A.*, B.TEAM_NAME, B.POS_NAME, B.KOR_NAME AS [EDT_NAME]
		FROM COM_BIZTRIP_GROUP A WITH(NOLOCK)
		LEFT JOIN AGT_EMP_LIST B ON A.AGT_CODE = B.AGT_CODE AND ISNULL(A.EDT_SEQ, 0) = B.EMP_SEQ	
		WHERE A.AGT_CODE = @AGT_CODE AND A.BT_SEQ = @BT_SEQ;

		SELECT A.*
		FROM COM_AIR_RULE A WITH(NOLOCK)
		WHERE A.AGT_CODE = @AGT_CODE AND A.BT_SEQ = @BT_SEQ;

		WITH MASTER_LIST AS
		(
			SELECT A.AGT_CODE, A.REG_MASTER_SEQ, A.REGION_NAME, A.ALL_YN
			FROM COM_REGION_MASTER A WITH(NOLOCK)
			WHERE A.AGT_CODE = @AGT_CODE AND A.USE_YN = 'Y'
		), DETAIL_LIST AS
		(
			SELECT A.*, B.REG_DETAIL_SEQ, B.REG_TYPE, B.REG_CODE
			FROM MASTER_LIST A
			INNER JOIN COM_REGION_DETAIL B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.REG_MASTER_SEQ = B.REG_MASTER_SEQ
			WHERE A.ALL_YN = 'N' AND B.USE_YN = 'Y'
		), LAST_LIST AS
		(
			SELECT A.AGT_CODE, A.REG_MASTER_SEQ, B.REGION_NAME, (MAX(REGION) + MAX(NATION) + MAX(CITY)) AS [REGION_LIST]
			FROM (
				SELECT A.AGT_CODE, A.REG_MASTER_SEQ, '전지역 ' AS [REGION], '' AS [NATION], '' AS [CITY]
				FROM MASTER_LIST A
				WHERE A.ALL_YN = 'Y'
				UNION ALL
				SELECT A.AGT_CODE, A.REG_MASTER_SEQ, (
					SELECT (KOR_NAME + ',') AS [text()] 
					FROM DETAIL_LIST AA
					INNER JOIN PUB_REGION BB WITH(NOLOCK) ON AA.REG_MASTER_SEQ = A.REG_MASTER_SEQ AND AA.REG_CODE = BB.REGION_CODE
					WHERE AA.REG_TYPE = 'R' FOR XML PATH('')), '', ''
				FROM MASTER_LIST A
				UNION ALL
				SELECT A.AGT_CODE, A.REG_MASTER_SEQ, '', (
					SELECT (KOR_NAME + ',') AS [text()] 
					FROM DETAIL_LIST AA
					INNER JOIN PUB_NATION BB WITH(NOLOCK) ON AA.REG_MASTER_SEQ = A.REG_MASTER_SEQ AND AA.REG_CODE = BB.NATION_CODE
					WHERE AA.REG_TYPE = 'N' FOR XML PATH('')), ''
				FROM MASTER_LIST A
				UNION ALL
				SELECT A.AGT_CODE, A.REG_MASTER_SEQ, '', '', (
					SELECT (KOR_NAME + ',') AS [text()] 
					FROM DETAIL_LIST AA
					INNER JOIN PUB_CITY BB WITH(NOLOCK) ON AA.REG_MASTER_SEQ = A.REG_MASTER_SEQ AND AA.REG_CODE = BB.CITY_CODE
					WHERE AA.REG_TYPE = 'C' FOR XML PATH('')) AS [CITY_NAME]
				FROM MASTER_LIST A
			) A
			INNER JOIN MASTER_LIST B ON A.AGT_CODE = B.AGT_CODE AND A.REG_MASTER_SEQ = B.REG_MASTER_SEQ
			GROUP BY A.AGT_CODE, A.REG_MASTER_SEQ, B.REGION_NAME
		)
		SELECT A.AGT_CODE, @BT_SEQ AS [BT_SEQ], ISNULL(B.HOTEL_RULE_SEQ, 0) AS [HOTEL_RULE_SEQ], A.REG_MASTER_SEQ
			, A.REGION_NAME, A.REGION_LIST, B.LIMIT_PRICE, ISNULL(B.USE_YN, 'N') AS [USE_YN], B.NEW_SEQ
		FROM LAST_LIST A
		LEFT JOIN COM_HOTEL_RULE B WITH(NOLOCK) ON B.AGT_CODE = @AGT_CODE AND B.BT_SEQ = @BT_SEQ AND A.AGT_CODE = B.AGT_CODE AND A.REG_MASTER_SEQ = B.REG_MASTER_SEQ
		--SELECT A.AGT_CODE, A.BT_SEQ, A.HOTEL_RULE_SEQ, A.REG_MASTER_SEQ, B.REGION_NAME, B.REGION_LIST, A.LIMIT_PRICE, A.USE_YN, A.NEW_SEQ
		--FROM COM_HOTEL_RULE A WITH(NOLOCK)
		--INNER JOIN LAST_LIST B ON A.AGT_CODE = B.AGT_CODE AND A.REG_MASTER_SEQ = B.REG_MASTER_SEQ
		--WHERE A.AGT_CODE = @AGT_CODE AND A.BT_SEQ = @BT_SEQ
	END

END


/*

DECLARE @AGT_CODE VARCHAR(10)
SET @AGT_CODE = '92756';


WITH MASTER_LIST AS
(
	SELECT A.AGT_CODE, A.REG_MASTER_SEQ, A.REGION_NAME, A.ALL_YN
	FROM COM_REGION_MASTER A WITH(NOLOCK)
	WHERE A.AGT_CODE = @AGT_CODE AND A.USE_YN = 'Y'
), DETAIL_LIST AS
(
	SELECT A.*, B.REG_DETAIL_SEQ, B.REG_TYPE, B.REG_CODE
	FROM MASTER_LIST A
	INNER JOIN COM_REGION_DETAIL B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.REG_MASTER_SEQ = B.REG_MASTER_SEQ
	WHERE A.ALL_YN = 'N' AND B.USE_YN = 'Y'
), LAST_LIST AS
(
	SELECT A.AGT_CODE, A.REG_MASTER_SEQ, (MAX(REGION_NAME) + MAX(NATION_NAME) + MAX(CITY_NAME)) AS [REGION_NAME]
	FROM (
		SELECT A.AGT_CODE, A.REG_MASTER_SEQ, '전지역 ' AS [REGION_NAME], '' AS [NATION_NAME], '' AS [CITY_NAME]
		FROM MASTER_LIST A
		WHERE A.ALL_YN = 'Y'
		UNION ALL
		SELECT A.AGT_CODE, A.REG_MASTER_SEQ, (
			SELECT (KOR_NAME + ',') AS [text()] 
			FROM DETAIL_LIST AA
			INNER JOIN PUB_REGION BB WITH(NOLOCK) ON AA.REG_MASTER_SEQ = A.REG_MASTER_SEQ AND AA.REG_CODE = BB.REGION_CODE
			WHERE AA.REG_TYPE = 'R' FOR XML PATH('')), '', ''
		FROM MASTER_LIST A
		UNION ALL
		SELECT A.AGT_CODE, A.REG_MASTER_SEQ, '', (
			SELECT (KOR_NAME + ',') AS [text()] 
			FROM DETAIL_LIST AA
			INNER JOIN PUB_NATION BB WITH(NOLOCK) ON AA.REG_MASTER_SEQ = A.REG_MASTER_SEQ AND AA.REG_CODE = BB.NATION_CODE
			WHERE AA.REG_TYPE = 'N' FOR XML PATH('')), ''
		FROM MASTER_LIST A
		UNION ALL
		SELECT A.AGT_CODE, A.REG_MASTER_SEQ, '', '', (
			SELECT (KOR_NAME + ',') AS [text()] 
			FROM DETAIL_LIST AA
			INNER JOIN PUB_CITY BB WITH(NOLOCK) ON AA.REG_MASTER_SEQ = A.REG_MASTER_SEQ AND AA.REG_CODE = BB.CITY_CODE
			WHERE AA.REG_TYPE = 'C' FOR XML PATH('')) AS [CITY_NAME]
		FROM MASTER_LIST A
	) A
	GROUP BY A.AGT_CODE, A.REG_MASTER_SEQ
)



INSERT INTO COM_REGION_MASTER (AGT_CODE, REG_MASTER_SEQ, REGION_NAME, ALL_YN, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, '부분지역', 'N', 'Y', GETDATE(), 100


INSERT INTO COM_REGION_DETAIL (AGT_CODE, REG_MASTER_SEQ, REG_DETAIL_SEQ, REG_TYPE, REG_CODE, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, 1, 'R', '110', 'Y', GETDATE(), 100

INSERT INTO COM_REGION_DETAIL (AGT_CODE, REG_MASTER_SEQ, REG_DETAIL_SEQ, REG_TYPE, REG_CODE, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, 2, 'N', 'GB', 'Y', GETDATE(), 100

INSERT INTO COM_REGION_DETAIL (AGT_CODE, REG_MASTER_SEQ, REG_DETAIL_SEQ, REG_TYPE, REG_CODE, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, 3, 'N', 'JP', 'Y', GETDATE(), 100

INSERT INTO COM_REGION_DETAIL (AGT_CODE, REG_MASTER_SEQ, REG_DETAIL_SEQ, REG_TYPE, REG_CODE, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, 4, 'C', 'BKK', 'Y', GETDATE(), 100

INSERT INTO COM_REGION_DETAIL (AGT_CODE, REG_MASTER_SEQ, REG_DETAIL_SEQ, REG_TYPE, REG_CODE, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, 5, 'C', 'CNX', 'Y', GETDATE(), 100

INSERT INTO COM_REGION_DETAIL (AGT_CODE, REG_MASTER_SEQ, REG_DETAIL_SEQ, REG_TYPE, REG_CODE, USE_YN, NEW_DATE, NEW_SEQ)
SELECT '92756', 2, 6, 'C', 'USM', 'Y', GETDATE(), 100


--DELETE FROM COM_REGION_DETAIL

*/
GO
