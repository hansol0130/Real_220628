USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_BBS_DETAIL_ERP_SELECT
■ DESCRIPTION				: 게시물 검색
■ INPUT PARAMETER			: 
	@MASTER_SEQ				: 마스터 순번
	@BOARD_SEQ				: 게시물 순번
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	exec XP_COM_BBS_DETAIL_ERP_SELECT 4, 266, '', ''

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-04-27		저스트고-이유라    최초생성
================================================================================================================*/ 

CREATE  PROCEDURE [dbo].[XP_COM_BBS_DETAIL_ERP_SELECT]
(
	@MASTER_SEQ	INT,
	@BOARD_SEQ	INT,
	@AGT_CODE VARCHAR(20),
	@COUNT_YN	VARCHAR(1)
)

AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000), @SUBSQL NVARCHAR(1000);

	IF @COUNT_YN = 'Y'
	BEGIN
		UPDATE COM_BBS_DETAIL SET SHOW_COUNT = ISNULL(SHOW_COUNT, 0) + 1
		WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ;
	END

	SET @SQLSTRING = N'
	-- 본문
	
WITH LIST AS
	(
		SELECT MASTER_SEQ, BOARD_SEQ FROM COM_BBS_DETAIL WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ AND BOARD_SEQ = PARENT_SEQ
	)
	SELECT A.*,	C.KOR_NAME AS NATION_NAME,D.KOR_NAME AS REGION_NAME,D.REGION_CODE AS REGION_CODE,C.NATION_CODE AS NATION_CODE, 
				E.KOR_NAME AS COM_EMP_NAME, K.TEAM_NAME, L.POS_NAME, E.HP_NUMBER, E.EMAIL,
				O.KOR_NAME AS ERP_EMP_NAME, P.TEAM_NAME AS ERP_TEAM_NAME,
				(SELECT PUB_VALUE FROM COD_PUBLIC WITH(NOLOCK) WHERE PUB_TYPE=''EMP.POSTYPE'' AND PUB_CODE=O.POS_TYPE) AS [ERP_POS_NAME],
				AGT.KOR_NAME AS AGT_NAME, M.CATEGORY_NAME AS CATEGORY_NAME, N.CATEGORY_NAME AS REQUEST_NAME,
				STUFF(( SELECT '','' + B.AGT_NAME FROM COM_BBS_NOTICE B WHERE A.MASTER_SEQ = B.MASTER_SEQ AND A.BOARD_SEQ = B.BOARD_SEQ ORDER BY B.AGT_NAME FOR XML PATH ('''')),1,1,'''') AS NOTICE_AGENT_NAME,
				STUFF(( SELECT '','' + B.AGT_CODE FROM COM_BBS_NOTICE B WHERE A.MASTER_SEQ = B.MASTER_SEQ AND A.BOARD_SEQ = B.BOARD_SEQ ORDER BY B.AGT_NAME FOR XML PATH ('''')),1,1,'''') AS NOTICE_AGENT_CODE
	FROM LIST Z
	INNER JOIN COM_BBS_DETAIL A ON A.MASTER_SEQ = Z.MASTER_SEQ AND A.BOARD_SEQ = Z.BOARD_SEQ
	LEFT JOIN COM_EMPLOYEE E ON A.NEW_SEQ = E.EMP_SEQ  AND A.AGT_CODE = E.AGT_CODE
	LEFT JOIN AGT_MASTER AGT ON A.AGT_CODE = AGT.AGT_CODE
	LEFT JOIN PUB_NATION C ON A.NATION_CODE = C.NATION_CODE
	LEFT JOIN PUB_REGION D ON A.REGION_CODE = D.REGION_CODE
	LEFT JOIN COM_TEAM K WITH(NOLOCK) ON E.AGT_CODE = K.AGT_CODE AND E.TEAM_SEQ = K.TEAM_SEQ
    LEFT JOIN COM_POSITION L WITH(NOLOCK) ON E.AGT_CODE = L.AGT_CODE AND E.POS_SEQ = L.POS_SEQ  
	LEFT JOIN COM_BBS_CATEGORY M ON A.MASTER_SEQ = M.MASTER_SEQ AND A.CATEGORY_SEQ = M.CATEGORY_SEQ
	LEFT JOIN COM_BBS_CATEGORY N ON A.MASTER_SEQ = N.MASTER_SEQ AND A.REQUEST_SEQ = N.CATEGORY_SEQ
	LEFT JOIN EMP_MASTER O ON A.EMP_CODE = O.EMP_CODE
	LEFT JOIN EMP_TEAM P ON O.TEAM_CODE = P.TEAM_CODE
	ORDER BY A.BOARD_SEQ;

	-- 파일
	WITH LIST AS
	(
		SELECT MASTER_SEQ, BOARD_SEQ FROM COM_BBS_DETAIL WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ
		UNION
		SELECT MASTER_SEQ, BOARD_SEQ FROM COM_BBS_DETAIL WHERE MASTER_SEQ = @MASTER_SEQ AND PARENT_SEQ = @BOARD_SEQ
	)
	SELECT A.*
	FROM LIST Z
	INNER JOIN COM_BBS_FILE A ON A.MASTER_SEQ = Z.MASTER_SEQ AND A.BOARD_SEQ = Z.BOARD_SEQ;

	-- 답글
	WITH LIST AS
	(
		SELECT MASTER_SEQ, BOARD_SEQ FROM COM_BBS_DETAIL WHERE MASTER_SEQ = @MASTER_SEQ AND PARENT_SEQ = @BOARD_SEQ AND BOARD_SEQ <> PARENT_SEQ 
	)
	SELECT A.*,	C.KOR_NAME AS NATION_NAME,D.KOR_NAME AS REGION_NAME,D.REGION_CODE AS REGION_CODE,C.NATION_CODE AS NATION_CODE, E.KOR_NAME AS COM_EMP_NAME,
				F.KOR_NAME AS ERP_EMP_NAME, (SELECT PUB_VALUE FROM COD_PUBLIC WITH(NOLOCK) WHERE PUB_TYPE=''EMP.POSTYPE'' AND PUB_CODE=F.POS_TYPE) AS [ERP_POS_NAME]
	FROM LIST Z
	INNER JOIN COM_BBS_DETAIL A ON A.MASTER_SEQ = Z.MASTER_SEQ AND A.BOARD_SEQ = Z.BOARD_SEQ
	LEFT JOIN COM_EMPLOYEE E ON A.NEW_SEQ = E.EMP_SEQ
	LEFT JOIN PUB_NATION C ON A.NATION_CODE = C.NATION_CODE
	LEFT JOIN PUB_REGION D ON A.REGION_CODE = D.REGION_CODE
	LEFT JOIN EMP_MASTER F ON A.EMP_CODE = F.EMP_CODE
	ORDER BY A.BOARD_SEQ;

	-- 이전글
	WITH LIST AS
	(
		SELECT * FROM COM_BBS_DETAIL WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ  AND BOARD_SEQ = PARENT_SEQ AND DEL_YN =''N''
	)
	SELECT TOP 1 A.*
	FROM LIST Z
	INNER JOIN COM_BBS_DETAIL A ON A.MASTER_SEQ = @MASTER_SEQ AND A.NOTICE_YN = Z.NOTICE_YN  AND A.DEL_YN =''N'' AND A.PARENT_SEQ < @BOARD_SEQ 
	ORDER BY A.BOARD_SEQ DESC;

	-- 다음글
	WITH LIST AS
	(
		SELECT * FROM COM_BBS_DETAIL WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ  AND BOARD_SEQ = PARENT_SEQ  AND DEL_YN =''N''
	)
	SELECT TOP 1 A.*
	FROM LIST Z
	INNER JOIN COM_BBS_DETAIL A ON A.MASTER_SEQ = @MASTER_SEQ AND A.NOTICE_YN = Z.NOTICE_YN AND A.DEL_YN=''N'' AND A.PARENT_SEQ > @BOARD_SEQ 
	ORDER BY A.BOARD_SEQ ASC;

	'
	SET @PARMDEFINITION = N'
		@MASTER_SEQ	INT,
		@AGT_CODE VARCHAR(20),
		@BOARD_SEQ	INT';


	PRINT @SQLSTRING
		
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@MASTER_SEQ,
		@AGT_CODE,
		@BOARD_SEQ;

END
GO
