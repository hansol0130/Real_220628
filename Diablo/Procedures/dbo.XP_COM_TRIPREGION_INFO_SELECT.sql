USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_TRIPREGION_INFO_SELECT
■ DESCRIPTION				: 출장자 메인 출장지역정보 조회
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 
■ EXEC						: 	
	EXEC XP_COM_TRIPREGION_INFO_SELECT '93861', 102
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-04-28		저스트고이유라			최초생성
   2016-05-27		정지용					BT_STATE, INFO_USE_YN 제거
   2016-05-30		저스트고이유라			출발자에 포함된 출장도 검색되도록 수정
   2016-06-02		저스트고이유라			최신안전소식은 전체국가로 수정
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_TRIPREGION_INFO_SELECT]
	@AGT_CODE VARCHAR(10),
	@EMP_SEQ INT
AS 
BEGIN

	DECLARE @CITY_CODE VARCHAR(3), @NATION_CODE VARCHAR(2), @WEATHER_CITY VARCHAR(5), @KEY VARCHAR(20);

	SET @CITY_CODE = (
		SELECT TOP 1 BT_CITY_CODE
		FROM COM_BIZTRIP_MASTER   A
		INNER JOIN COM_BIZTRIP_DETAIL B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.BT_CODE = B.BT_CODE
		INNER JOIN RES_MASTER_DAMO C WITH(NOLOCK) ON B.RES_CODE = C.RES_CODE
		WHERE A.AGT_CODE = @AGT_CODE AND BT_START_DATE >= GETDATE() AND BT_CITY_CODE != ''  AND  (
			NEW_SEQ = @EMP_SEQ OR EXISTS(
					SELECT 1 
					FROM RES_CUSTOMER_DAMO AA WITH(NOLOCK)
					INNER JOIN COM_EMPLOYEE_MATCHING BB WITH(NOLOCK) ON AA.CUS_NO = BB.CUS_NO 
					WHERE AA.RES_CODE = C.RES_CODE AND 
					BB.AGT_CODE = A.AGT_CODE AND BB.EMP_SEQ = @EMP_SEQ)
				)
  ORDER BY BT_START_DATE ASC
  )
	
	IF(@CITY_CODE = '' OR @CITY_CODE IS NULL)
	BEGIN 
		SET @CITY_CODE = 'SEL'
	END

	SET @NATION_CODE = (
		SELECT B.NATION_CODE FROM PUB_CITY A
		LEFT JOIN PUB_NATION B ON A.NATION_CODE = B.NATION_CODE
		WHERE CITY_CODE = @CITY_CODE)

	--매핑안된 도시일 경우 서울로 SELECT
	IF((SELECT COUNT(*)
	FROM SAFE_INFO_CONTACT A
	WHERE A.COUNTRY_NAME = ( SELECT KOR_NAME FROM SAFE_INFO_NATION_CATEGORY_MAP WHERE NATION_CODE = @NATION_CODE)) = 0) 
	BEGIN 
		SET @CITY_CODE = 'SEL'
		SET @NATION_CODE = 'KR'
	END 

	SET @WEATHER_CITY = (
		SELECT TOP 1 W_CITY_CODE
		FROM WEATHERI_CITY_MAP 
		WHERE CITY_CODE = @CITY_CODE)

    --기본정보
	SELECT C.KOR_NAME AS REGION_NAME, B.KOR_NAME AS NATION_NAME, A.KOR_NAME AS CITY_NAME, A.CITY_CODE, 
	--(SELECT TOP 1 UTC	FROM WEATHERI_CITY_MAP_2 WHERE W_CITY_CODE_OLD = @WEATHER_CITY) AS GMT
	ISNULL((SELECT TOP 1 CONVERT(FLOAT, UTC)+1 FROM WEATHERI_CITY_MAP_2 WHERE GETDATE() BETWEEN SUMMER_TIME_START AND SUMMER_TIME_END AND W_CITY_CODE_OLD = @WEATHER_CITY AND CITY_CODE IS NOT NULL ), 
	(SELECT TOP 1 UTC	FROM WEATHERI_CITY_MAP_2 WHERE W_CITY_CODE_OLD = @WEATHER_CITY)) AS GMT
	FROM PUB_CITY A
	LEFT JOIN PUB_NATION B ON A.NATION_CODE = B.NATION_CODE
	LEFT JOIN PUB_REGION C ON B.REGION_CODE = C.REGION_CODE
	WHERE CITY_CODE = @CITY_CODE

	--국기정보
	SELECT 	A.ID, A.CONTINENT, A.COUNTRY_NAME, A.IMG_URL1, A.IMG_URL2
	FROM SAFE_INFO_CONTACT A
	WHERE A.COUNTRY_NAME = (
		SELECT KOR_NAME --SAFE_KOR_NAME
		FROM SAFE_INFO_NATION_CATEGORY_MAP
		WHERE NATION_CODE = @NATION_CODE
		)

	--도시별 날씨정보 
	EXEC DBO.XP_COM_WEATHER_CAST_LIST_SELECT @WEATHER_CITY

	
	--SET @KEY = 'NationCode=' + (SELECT TOP 1 SAFE_NATION_CODE FROM SAFE_INFO_NATION_CATEGORY_MAP WHERE NATION_CODE = @NATION_CODE)

	--안전소식
	EXEC DBO.XP_COM_SAFE_INFO_NOTICE_LIST_SELECT 1, 6, 6, @KEY
END


GO
