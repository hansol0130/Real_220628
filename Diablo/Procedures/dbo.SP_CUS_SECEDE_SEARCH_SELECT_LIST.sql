USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- AUTHOR:		최미선
-- CREATE DATE: 2010-04-27
-- DESCRIPTION:	탈퇴목록 페이징\
-- 2010-06-03 박형만 암호화 적용
-- 2010-12-09 김성호 회원테이블 분리
-- 2012-03-02 박형만 WITH(NOLOCK) 추가
-- 2015-03-03 김성호 주민번호 삭제, 생년월일 추가
-- =============================================
CREATE PROCEDURE [dbo].[SP_CUS_SECEDE_SEARCH_SELECT_LIST]
(
	@FLAG			CHAR(1),
	@PAGE_SIZE		INT,
	@PAGE_INDEX		INT,
	
	@MEM_YN			VARCHAR(3),
	@CUS_NAME		VARCHAR(20),
	@CUS_ID			VARCHAR(20),
--	@SOC_NUM1		VARCHAR(6),
--	@SOC_NUM2		VARCHAR(7),
	@BIRTH_DATE		DATETIME,
	@EMAIL			VARCHAR(40),
	@NOR_TEL1		VARCHAR(6),
	@NOR_TEL2		VARCHAR(5),
	@NOR_TEL3		VARCHAR(4),
	@START_DATE		DATETIME,
	@END_DATE		DATETIME
)
AS
BEGIN

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000), @FROM INT, @TO INT;

	SET @FROM = @PAGE_INDEX * @PAGE_SIZE + 1;
	SET @TO = @PAGE_INDEX * @PAGE_SIZE + @PAGE_SIZE;

	-- WHERE 조건 만들기
	SET @SQLSTRING = 'WHERE 1=1';

	BEGIN
		-- 고객구분
		IF (@MEM_YN = 'ON')
			SET @SQLSTRING = @SQLSTRING + ' AND B.CUS_ID IS NOT NULL AND B.CUS_ID <> '''''
		ELSE IF (@MEM_YN = 'OFF')
			SET @SQLSTRING = @SQLSTRING + ' AND (B.CUS_ID IS NULL OR B.CUS_ID = '''')'
		--고객이름
		IF ISNULL(@CUS_NAME, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.CUS_NAME = @CUS_NAME'
		--고객아이디
		IF ISNULL(@CUS_ID, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.CUS_ID = @CUS_ID'
		----주민번호1
		--IF ISNULL(@SOC_NUM1, '') <> ''
		--	SET @SQLSTRING = @SQLSTRING + ' AND B.SOC_NUM1 = @SOC_NUM1'
		----주민번호2
		--IF ISNULL(@SOC_NUM2, '') <> ''
		--BEGIN
		--	--SET @SQLSTRING = @SQLSTRING + ' AND B.SOC_NUM2 = @SOC_NUM2'
		--	SET @SQLSTRING = @SQLSTRING + ' 
		--	AND B.SEC1_SOC_NUM2 = damo.dbo.pred_meta_plain_v (@SOC_NUM2,''DIABLO'',''dbo.CUS_CUSTOMER'',''SOC_NUM2'') '
		--END
		-- 생년월일
		IF ISNULL(@BIRTH_DATE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.BIRTH_DATE = @BIRTH_DATE'
		--이메일
		IF ISNULL(@EMAIL, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.EMAIL = @EMAIL'
		--휴대폰번호1
		IF ISNULL(@NOR_TEL1, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.NOR_TEL1 = @NOR_TEL1'
		--휴대폰번호2
		IF ISNULL(@NOR_TEL2, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.NOR_TEL2 = @NOR_TEL2'
		--휴대폰번호3
		IF ISNULL(@NOR_TEL3, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.NOR_TEL3 = @NOR_TEL3'
		--탈퇴날짜
		IF ISNULL(@START_DATE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NEW_DATE >= @START_DATE'
		IF ISNULL(@END_DATE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NEW_DATE <= @END_DATE'

	END

	IF @FLAG = 'C'					-- 전체 글수
	BEGIN
		SET @SQLSTRING = N'SELECT COUNT(*) FROM CUS_SECEDE A WITH(NOLOCK) LEFT JOIN CUS_CUSTOMER_damo B WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO ' + @SQLSTRING;
	END
	ELSE IF @FLAG = 'L'				-- 검색
	BEGIN
		SET @SQLSTRING = N'
		WITH LIST AS
		(
			SELECT ROW_NUMBER() OVER (ORDER BY A.NEW_DATE DESC, B.CUS_NAME) AS [ROWNUMBER], A.CUS_NO
			FROM CUS_SECEDE A WITH(NOLOCK)
			LEFT JOIN CUS_CUSTOMER_damo B WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO
			' + @SQLSTRING + '
		)
		SELECT A.ROWNUMBER,
			 (
				CASE
					WHEN ISNULL(C.CUS_ID, '''') <> '''' THEN ''ON''
					ELSE ''OFF''
				END
			) AS [ONOFF],
			B.CUS_NO,
			C.CUS_ID,
			C.CUS_NAME,
--			C.SOC_NUM1,
--			C.SOC_NUM2,
--			damo.dbo.dec_varchar(''DIABLO'',''dbo.CUS_CUSTOMER'',''SOC_NUM2'', C.SEC_SOC_NUM2) AS SOC_NUM2,
			C.BIRTH_DATE,
			C.GENDER,
			C.NOR_TEL1,
			C.NOR_TEL2,
			C.NOR_TEL3,
			B.REMARK, 
			B.NEW_DATE AS SECEDE_DATE,
			DATEADD(DAY, 15, B.NEW_DATE) AS SECEDE_AUTH
		FROM LIST A
		INNER JOIN CUS_SECEDE B WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO 
		LEFT JOIN CUS_CUSTOMER_damo C WITH(NOLOCK) ON A.CUS_NO = C.CUS_NO 
		WHERE ROWNUMBER BETWEEN @FROM AND @TO
		ORDER BY B.NEW_DATE DESC, C.CUS_NAME;';
	END

	SET @PARMDEFINITION = N'
		@FROM INT,
		@TO INT,
		@FLAG CHAR(1),
		@PAGE_SIZE INT,
		@PAGE_INDEX INT,
		@MEM_YN VARCHAR(3),
		@CUS_NAME VARCHAR(20),
		@CUS_ID VARCHAR(20),
		@BIRTH_DATE DATETIME,
		@EMAIL VARCHAR(40),
		@NOR_TEL1 VARCHAR(6),
		@NOR_TEL2 VARCHAR(5),
		@NOR_TEL3 VARCHAR(4),
		@START_DATE DATETIME,
		@END_DATE DATETIME'

	--PRINT @SQLSTRING

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@FROM,
		@TO,
		@FLAG,
		@PAGE_SIZE,
		@PAGE_INDEX,
		@MEM_YN,
		@CUS_NAME,
		@CUS_ID,
		@BIRTH_DATE,
		@EMAIL,
		@NOR_TEL1,
		@NOR_TEL2,
		@NOR_TEL3,
		@START_DATE,
		@END_DATE;

END



--exec  [dbo].[SP_CUS_SECEDE_SEARCH_SELECT_LIST] 'l', 15, 0, 0, '', '', '', '', '', '', '', '', '2009-06-01', '2009-12-30'
GO
