USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_BIZTRIP_REGION_SELECT
■ DESCRIPTION				: BTMS 출장자 규정 지역 검색
■ INPUT PARAMETER			: 
	@AGT_CODE				: 거래처코드
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXEC DBO.XP_COM_BIZTRIP_REGION_SELECT 92756, 2

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-01-31		김성호			최초생성
   2016-02-19		정지용			수정자 정보 추가
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_BIZTRIP_REGION_SELECT]
	@AGT_CODE			VARCHAR(10),
	@REG_MASTER_SEQ		INT
AS 
BEGIN

	WITH AGT_EMP_LIST AS
	(
		SELECT 
			A.AGT_CODE, A.EMP_SEQ, A.KOR_NAME, B.TEAM_NAME, C.POS_NAME
		FROM COM_EMPLOYEE A WITH(NOLOCK)
		LEFT JOIN COM_TEAM B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.TEAM_SEQ = B.TEAM_SEQ
		LEFT JOIN COM_POSITION C WITH(NOLOCK) ON A.AGT_CODE = C.AGT_CODE AND A.POS_SEQ = C.POS_SEQ
		WHERE A.AGT_CODE = @AGT_CODE
	)
	SELECT 
		A.AGT_CODE, A.REG_MASTER_SEQ, A.REGION_NAME, A.ALL_YN, A.USE_YN, 
		B.TEAM_NAME, B.POS_NAME, ISNULL(A.EDT_DATE, A.NEW_DATE) AS [EDT_DATE], ISNULL(A.EDT_SEQ, A.NEW_SEQ) AS [EDT_SEQ], B.KOR_NAME AS [EDT_NAME]
		--ISNULL(A.EDT_DATE, A.NEW_DATE) AS [EDT_DATE], ISNULL(A.EDT_SEQ, A.NEW_SEQ) AS [EDT_SEQ], B.KOR_NAME AS [EDT_NAME]
	FROM COM_REGION_MASTER A WITH(NOLOCK)
	LEFT JOIN AGT_EMP_LIST B ON A.AGT_CODE = B.AGT_CODE AND ISNULL(A.EDT_SEQ, A.NEW_SEQ) = B.EMP_SEQ
	--LEFT JOIN COM_EMPLOYEE B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND ISNULL(A.EDT_SEQ, A.NEW_SEQ) = B.EMP_SEQ
	WHERE A.AGT_CODE = @AGT_CODE AND A.REG_MASTER_SEQ = @REG_MASTER_SEQ;

	WITH LIST AS
	(
		SELECT A.*
		FROM COM_REGION_DETAIL A WITH(NOLOCK)
		WHERE A.AGT_CODE = @AGT_CODE AND A.REG_MASTER_SEQ = @REG_MASTER_SEQ AND A.USE_YN = 'Y'
	)
	SELECT A.AGT_CODE, A.REG_MASTER_SEQ, A.REG_DETAIL_SEQ, A.REG_TYPE, A.REG_CODE, B.KOR_NAME
	FROM LIST A
	INNER JOIN PUB_REGION B ON A.REG_CODE = B.REGION_CODE
	WHERE A.REG_TYPE = 'R'
	UNION ALL
	SELECT A.AGT_CODE, A.REG_MASTER_SEQ, A.REG_DETAIL_SEQ, A.REG_TYPE, A.REG_CODE, B.KOR_NAME
	FROM LIST A
	INNER JOIN PUB_NATION B ON A.REG_CODE = B.NATION_CODE
	WHERE A.REG_TYPE = 'N'
	UNION ALL
	SELECT A.AGT_CODE, A.REG_MASTER_SEQ, A.REG_DETAIL_SEQ, A.REG_TYPE, A.REG_CODE, B.KOR_NAME
	FROM LIST A
	INNER JOIN PUB_CITY B ON A.REG_CODE = B.CITY_CODE
	WHERE A.REG_TYPE = 'C'

END
GO
