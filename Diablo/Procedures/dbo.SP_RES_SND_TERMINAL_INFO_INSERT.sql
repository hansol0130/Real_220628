USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: SP_RES_SND_TERMINAL_INFO_INSERT (SP_SND_T2_RES_CUSTOMER_LIST 대체)
■ DESCRIPTION				: 제2출국장 출발 고객 ( 1시간 배치 기준 : 24시간 전 출발 고객 / ICN 출발 기준 'KE','DL','AF','KL' 항공만.. )
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	DECLARE @SEND_COUNT VARCHAR(4000)
	EXEC SP_RES_SND_TERMINAL_INFO_INSERT NULL, 'f1e8319931392a3c30bb6fdef415f67ef87aa58c', @SEND_COUNT OUTPUT
	SELECT @SEND_COUNT

	SELECT LEN('26,01026476695,01027367562,01031625284,01032537519,01036413489,01037684413,01040119038,01040164402,01040961796,01041189581,01045734868,01050069581,01050244319,01063381173,01064807415,01074771238,01075757727,01077272313,01086676769,01086687036
,01087244319,01088433007,01088958399,01094193007,01094901238,01099381769')

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2018-07-23		김성호			최초생성 (SP_SND_T2_RES_CUSTOMER_LIST 대체)
   2018-12-18		김성호			제2터미널 대상 항공사 수정 및 알림톡 LMS로 대체
   2018-12-21		김성호			템플릿 변경에 따른 수정
   2019-01-30		박형만			카운터 공사 관계로 임시로 N존 창가(14번 출입구) 로 변경 
   2019-06-26		김남훈			행사 마스터 제외 조건 추가
   2019-12-06		박형만			1,2터미널미팅장소 변경 
================================================================================================================*/ 
CREATE PROC [dbo].[SP_RES_SND_TERMINAL_INFO_INSERT]
	@CHECK_DATE		DATETIME = NULL,
	@SENDER_KEY		VARCHAR(40),
	@SEND_COUNT		VARCHAR(4000) OUTPUT
AS 
BEGIN

	-- 대한항공,델타항공,에어프랑스,네덜란드항공,에어로멕시코항공,이태리항공,중화항공,체코항공,하문항공,러시아항공,가루다항공
	DECLARE @AIRLINES VARCHAR(100) = 'KE,DL,AF,KL,AM,AZ,CI,OK,MF,SU,GA'

	-- 발송일시 체크
	IF ISDATE(@CHECK_DATE) < 1	-- 1: 날짜형, 0: 비날짜형
	BEGIN
		SET @CHECK_DATE = DATEADD(DD, 1, GETDATE());
	END

	DECLARE
		@TMPL_CD VARCHAR(20) = 'SYS_AT_0065',								-- @TMPL_CD : 사용 템플릿 번호
		@END_DATE DATETIME = DATEADD(DAY, 1, CONVERT(DATE, @CHECK_DATE)),	-- @END_DATE : 검색완료일자
		@CONTS_TXT VARCHAR(4000),											-- @CONTS_TXT : 발송문자
		@REQ_DTM VARCHAR(14);												-- @REQ_DTM : 발송요청일시

	SELECT
		--@CONTS_TXT = REPLACE(REPLACE(REPLACE(A.CONTS_TXT, '#{제1터미널미팅장소안내}', 'M2카운터(12번 출입구 앞) 여행사테이블'), '#{제2터미널미팅장소안내}', 'H 카운터( 8번 출입구 앞) 여행사테이블'), '#{예약내역url}', 'http://vgt.kr/u/mV'),
		--@CONTS_TXT = REPLACE(REPLACE(REPLACE(A.CONTS_TXT, '#{제1터미널미팅장소안내}', 'N존 창가(14번 출입구) 여행사테이블'), '#{제2터미널미팅장소안내}', 'H 카운터( 8번 출입구 앞) 여행사테이블'), '#{예약내역url}', 'http://vgt.kr/u/mV'),
		@CONTS_TXT = REPLACE(REPLACE(REPLACE(A.CONTS_TXT, '#{제1터미널미팅장소안내}', '3층 14번 출입구 N카운터(N존) 옆 ''참좋은여행'' 테이블'), '#{제2터미널미팅장소안내}', '3층 8번 출입구 H카운터(H존) 옆 ''참좋은여행'' 테이블'), '#{예약내역url}', 'http://vgt.kr/u/mV'),
		@REQ_DTM = CONVERT(VARCHAR(8), GETDATE(), 112)
			 + RIGHT(('0' + CONVERT(VARCHAR(2), DATEPART(HH, @CHECK_DATE))), 2)
			 + RIGHT(('0' + CONVERT(VARCHAR(2), DATEPART(MI, @CHECK_DATE))), 2)
			 + RIGHT(('0' + CONVERT(VARCHAR(2), DATEPART(SS, @CHECK_DATE))), 2)
	FROM SEND.dbo.NVMOBILECONTENTS A WITH(NOLOCK)
	WHERE A.KAKAO_TMPL_CD = @TMPL_CD; -- AND A.KAKAO_INSP_STATUS = 'ARP'

	-- 임시테이블 생성
	IF OBJECT_ID('TEMPDB..#ALT_T2_RES_LIST') IS NOT NULL
		DROP TABLE #ALT_T2_RES_LIST;
	CREATE TABLE #ALT_T2_RES_LIST (RES_CODE VARCHAR(12) NOT NULL, SEQ_NO INT NOT NULL, PHONE_NUM VARCHAR(15) NOT NULL, CUS_NO INT, AIRLINE_NAME VARCHAR(100));

	WITH RES_LIST AS
	(	-- 패키지
		SELECT A.RES_CODE, B.SEQ_NO, B.CUS_NO, ISNULL((B.NOR_TEL1 + B.NOR_TEL2 + B.NOR_TEL3), (E.NOR_TEL1 + E.NOR_TEL2 + E.NOR_TEL3)) AS [PHONE_NUM], F.KOR_NAME AS [AIRLINE_NAME]--, D.DEP_DEP_DATE, D.DEP_DEP_TIME
		FROM RES_MASTER_damo A WITH(NOLOCK)
		INNER LOOP JOIN RES_CUSTOMER_damo B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE
		INNER JOIN PKG_DETAIL C WITH(NOLOCK) ON A.PRO_CODE = C.PRO_CODE
		INNER LOOP JOIN PRO_TRANS_SEAT D WITH(NOLOCK) ON C.SEAT_CODE = D.SEAT_CODE
		INNER JOIN CUS_CUSTOMER_damo E WITH(NOLOCK) ON B.CUS_NO = E.CUS_NO
		INNER JOIN PUB_AIRLINE F WITH(NOLOCK) ON D.DEP_TRANS_CODE = F.AIRLINE_CODE
		WHERE A.DEP_DATE >= CONVERT(DATE, @CHECK_DATE) AND A.DEP_DATE < @END_DATE AND A.RES_STATE < 7 AND B.RES_STATE = 0 AND A.PRO_TYPE = 1
			AND D.DEP_DEP_AIRPORT_CODE = 'ICN' AND D.DEP_TRANS_CODE IN (SELECT A.Data FROM DBO.FN_SPLIT(@AIRLINES, ',') A WHERE A.Data <> '')
			--중국팀 이화정 과장의 요청으로 블라디보스톡 SU 운항에서는 1터미널이라 혼선이 와서 제외처리, 강효진 계장 요청 추가. 남태평양팀 제외 추가
			AND C.MASTER_CODE NOT IN ('CPP9012','APP0662','APP0668','APP0717','APP1309','APP4369','APP0839','PPP282','PPP202','PPP200','PPP2020','PPP201')
		UNION ALL
		-- 항공예약
		SELECT A.RES_CODE, B.SEQ_NO, B.CUS_NO, ISNULL((B.NOR_TEL1 + B.NOR_TEL2 + B.NOR_TEL3), (D.NOR_TEL1 + D.NOR_TEL2 + D.NOR_TEL3)) AS [PHONE_NUM], E.KOR_NAME AS [AIRLINE_NAME]--, C.DEP_DEP_DATE, C.DEP_DEP_TIME
		FROM RES_MASTER_damo A WITH(NOLOCK)
		INNER LOOP JOIN RES_CUSTOMER_damo B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE
		INNER LOOP JOIN RES_AIR_DETAIL C WITH(NOLOCK) ON A.RES_CODE = C.RES_CODE
		INNER JOIN CUS_CUSTOMER D WITH(NOLOCK) ON B.CUS_NO = D.CUS_NO
		INNER JOIN PUB_AIRLINE E WITH(NOLOCK) ON C.AIRLINE_CODE = E.AIRLINE_CODE
		WHERE A.DEP_DATE >= CONVERT(DATE, @CHECK_DATE) AND A.DEP_DATE < @END_DATE AND A.RES_STATE < 7 AND B.RES_STATE = 0 AND A.PRO_TYPE = 2
			AND C.DEP_DEP_AIRPORT_CODE = 'ICN' AND C.AIRLINE_CODE IN (SELECT A.Data FROM DBO.FN_SPLIT(@AIRLINES, ',') A WHERE A.Data <> '')
	)
	, SEND_LIST AS
	(
		-- 전화번호 중복 제거
		SELECT A.RES_CODE, MIN(A.SEQ_NO) AS [SEQ_NO], A.PHONE_NUM, (CASE WHEN MIN(A.CUS_NO) > 1 THEN MIN(A.CUS_NO) ELSE NULL END) AS [CUS_NO], MIN(A.AIRLINE_NAME) AS [AIRLINE_NAME]
		FROM RES_LIST A
		WHERE ISNULL(A.PHONE_NUM, '') <> ''
		GROUP BY A.RES_CODE, A.PHONE_NUM
	)
	INSERT INTO #ALT_T2_RES_LIST (RES_CODE, SEQ_NO, PHONE_NUM, CUS_NO, AIRLINE_NAME)
	SELECT A.RES_CODE, A.SEQ_NO, A.PHONE_NUM, A.CUS_NO, A.AIRLINE_NAME --, B.*
	FROM SEND_LIST A
	LEFT JOIN (
		SELECT AA.RES_CODE, BB.PHONE_NUM
		FROM SEND.[dbo].[ALT_MESSAGE_MASTER] AA WITH(NOLOCK)
		INNER JOIN SEND.DBO.MZSENDLOG BB WITH(NOLOCK) ON AA.SN = BB.SN AND BB.TMPL_CD = @TMPL_CD
		WHERE AA.RES_CODE IN (SELECT DISTINCT RES_CODE FROM RES_LIST)
		GROUP BY AA.RES_CODE, BB.PHONE_NUM
	) B ON A.RES_CODE = B.RES_CODE AND A.PHONE_NUM = B.PHONE_NUM
	WHERE B.PHONE_NUM IS NULL	-- 중복 발송 제거

	-- 테스트용 임시
	--DELETE FROM #ALT_T2_RES_LIST;
	--INSERT INTO #ALT_T2_RES_LIST (RES_CODE, SEQ_NO, PHONE_NUM, CUS_NO) VALUES ('RP0000000000', 1, '01088870428', NULL);

	-- 커서 사용변수
	DECLARE @RES_CODE VARCHAR(12), @SEQ_NO INT, @PHONE_NUM VARCHAR(15), @CUS_NO INT, @AIRLINE_NAME VARCHAR(100), @TMP_CONTS_TXT VARCHAR(4000)

	DECLARE USER_CURSOR CURSOR FOR 
		SELECT A.RES_CODE, A.SEQ_NO, A.PHONE_NUM, A.CUS_NO, A.AIRLINE_NAME
		FROM #ALT_T2_RES_LIST A
	OPEN USER_CURSOR
	FETCH NEXT FROM USER_CURSOR	INTO @RES_CODE, @SEQ_NO, @PHONE_NUM, @CUS_NO, @AIRLINE_NAME

	WHILE @@FETCH_STATUS = 0
	BEGIN

		-- 이용항공사 치환
		SELECT @TMP_CONTS_TXT = REPLACE(@CONTS_TXT, '#{이용항공사}', @AIRLINE_NAME)--, @PHONE_NUM = '01032536841'

		-- 알림톡 발송
		EXEC SEND.dbo.XP_ALT_MESSAGE_INSERT @SENDER_KEY, @PHONE_NUM, @TMPL_CD, @TMP_CONTS_TXT, @REQ_DTM, '[참좋은여행] 인천공항 제2터미널 이용 안내', '02-2188-4000', '529', '9999999', 'Y', 'R', @RES_CODE, @CUS_NO, NULL, NULL, NULL

-- LMS 임시 발송 
-- EXEC SEND.DBO.XP_SMS_MESSAGE_INSERT @PHONE_NUM, '0221884000', @CONTS_TXT, '[참좋은여행] 제2터미널안내', '529', '9999999', @RES_CODE, @CUS_NO, NULL, NULL, NULL;

		--PRINT @PHONE_NUM
		--PRINT @TMP_CONTS_TXT

		FETCH NEXT FROM USER_CURSOR	INTO @RES_CODE, @SEQ_NO, @PHONE_NUM, @CUS_NO, @AIRLINE_NAME
	END

	CLOSE USER_CURSOR
	DEALLOCATE USER_CURSOR

	-- 발송건수,연락번호 리턴
	SELECT @SEND_COUNT = '[기준: ' + CONVERT(VARCHAR(10), @CHECK_DATE, 120) + '] ' + CONVERT(VARCHAR(10), ISNULL((SELECT COUNT(*) FROM #ALT_T2_RES_LIST), 0))		-- 발송건수
		+ ISNULL((SELECT ',' + A.PHONE_NUM AS [text()] FROM #ALT_T2_RES_LIST A FOR XML PATH('')), '')	-- 핸드폰번호

END
 
GO
