USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


-- =============================================
-- AUTHOR:		<김성호>
-- CREATE DATE: <2009-08-30>
-- DESCRIPTION:	<좌석시간 일괄 수정>
-- =============================================
CREATE PROCEDURE [dbo].[_SP_PRO_TRANS_SEAT_UPDATE]
	@SEAT_CODE		INT,			-- 좌석코드
	@START			VARCHAR(10),	-- 복사시작일
	@END			VARCHAR(10),	-- 복사종료일
	@WEEK_DAY_TYPE	VARCHAR(7),		-- 요일
	@EMP_CODE		CHAR(7),		-- 생성인코드
	@DEP_DEP_TIME	VARCHAR(5),
	@DEP_ARR_TIME	VARCHAR(5),
	@ARR_DEP_TIME	VARCHAR(5),
	@ARR_ARR_TIME	VARCHAR(5)
AS
BEGIN
	SET NOCOUNT ON;
	
	DECLARE @START_DATE DATETIME, @END_DATE DATETIME, @DIFF_DAY INT, @DEP_DIFF INT, @ARR_DIFF INT
	DECLARE @DEP_SPEND VARCHAR(5), @ARR_SPEND VARCHAR(5), @DEP_MINUTE INT, @ARR_MINUTE INT, @NEW_SEAT_CODE INT
	DECLARE @FIRST CHAR(1)

	SET @START_DATE = CONVERT(DATETIME, @START);	-- 업데이트 시작일
	SET @END_DATE = CONVERT(DATETIME, @END);		-- 업데이트 종료일
	SET @FIRST = '1'								-- 최초 체크

	SELECT
		@DIFF_DAY = DATEDIFF(DAY, DEP_DEP_DATE, ARR_DEP_DATE)
		, @DEP_DIFF = DATEDIFF(DAY, DEP_DEP_DATE, DEP_ARR_DATE)
		, @ARR_DIFF = DATEDIFF(DAY, DEP_DEP_DATE, ARR_ARR_DATE)
		, @DEP_MINUTE = DATEDIFF(MI, (CONVERT(VARCHAR(10), DEP_DEP_DATE, 120) + ' ' + @DEP_DEP_TIME)
			, (CONVERT(VARCHAR(10), DEP_ARR_DATE, 120) + ' ' + @DEP_ARR_TIME))
		, @ARR_MINUTE = DATEDIFF(MI, (CONVERT(VARCHAR(10), ARR_DEP_DATE, 120) + ' ' + @ARR_DEP_TIME)
			, (CONVERT(VARCHAR(10), ARR_ARR_DATE, 120) + ' ' + @ARR_ARR_TIME))
	FROM PRO_TRANS_SEAT WITH(NOLOCK)  WHERE SEAT_CODE = @SEAT_CODE

	-- 소요시간
	SELECT @DEP_SPEND = RIGHT('0' + CONVERT(VARCHAR(2), (@DEP_MINUTE / 60)), 2) + ':' + RIGHT('0' + CONVERT(VARCHAR(2), (@DEP_MINUTE % 60)), 2)
		, @ARR_SPEND = RIGHT('0' + CONVERT(VARCHAR(2), (@ARR_MINUTE / 60)), 2) + ':' + RIGHT('0' + CONVERT(VARCHAR(2), (@ARR_MINUTE % 60)), 2)

	CREATE TABLE #MSG_TEMP (
		[ERROR_SEQ] INT IDENTITY,	[ERROR_DATE] DATETIME,
		[ERROR_TYPE] INT,			[ERROR_MESSAGE] NVARCHAR(2048))

	WHILE (@END_DATE >= @START_DATE)
	BEGIN
		BEGIN TRY
			IF '1' = SUBSTRING(@WEEK_DAY_TYPE, DATEPART(WEEKDAY, @START_DATE), 1)
			BEGIN
				SET @NEW_SEAT_CODE = 0

				-- 선택좌석도 수정
				IF @FIRST = '1'
				BEGIN
					UPDATE PRO_TRANS_SEAT SET
						DEP_DEP_TIME = @DEP_DEP_TIME, DEP_ARR_TIME = @DEP_ARR_TIME, DEP_SPEND_TIME = @DEP_SPEND
						, ARR_DEP_TIME = @ARR_DEP_TIME, ARR_ARR_TIME = @ARR_ARR_TIME, ARR_SPEND_TIME = @ARR_SPEND
						, EDT_DATE = GETDATE(), EDT_CODE = @EMP_CODE
					WHERE SEAT_CODE = @SEAT_CODE
					
					INSERT INTO #MSG_TEMP 
					SELECT @START_DATE, 1, '성공'
					
					SET @FIRST = '0'
				END
				
				SELECT @NEW_SEAT_CODE = B.SEAT_CODE FROM PRO_TRANS_SEAT A WITH(NOLOCK) 
				INNER JOIN PRO_TRANS_SEAT B  WITH(NOLOCK) ON A.DEP_TRANS_CODE = B.DEP_TRANS_CODE AND A.DEP_TRANS_NUMBER = B.DEP_TRANS_NUMBER
					AND A.DEP_DEP_AIRPORT_CODE = B.DEP_DEP_AIRPORT_CODE AND A.DEP_ARR_AIRPORT_CODE = B.DEP_ARR_AIRPORT_CODE
					AND A.ARR_TRANS_CODE = B.ARR_TRANS_CODE AND A.ARR_TRANS_NUMBER = B.ARR_TRANS_NUMBER
					AND A.ARR_DEP_AIRPORT_CODE = B.ARR_DEP_AIRPORT_CODE AND A.ARR_ARR_AIRPORT_CODE = B.ARR_ARR_AIRPORT_CODE
				WHERE A.SEAT_CODE = @SEAT_CODE
					AND B.DEP_DEP_DATE = @START_DATE AND B.DEP_ARR_DATE = DATEADD(DAY, @DEP_DIFF, @START_DATE)
					AND B.ARR_DEP_DATE = DATEADD(DAY, @DIFF_DAY, @START_DATE) AND B.ARR_ARR_DATE = DATEADD(DAY, @ARR_DIFF, @START_DATE)
				
				-- 해당일자에 좌석이 있는지 체크
				IF ISNULL(@NEW_SEAT_CODE, 0) > 0
				BEGIN
					UPDATE PRO_TRANS_SEAT SET
						DEP_DEP_TIME = @DEP_DEP_TIME, DEP_ARR_TIME = @DEP_ARR_TIME, DEP_SPEND_TIME = @DEP_SPEND
						, ARR_DEP_TIME = @ARR_DEP_TIME, ARR_ARR_TIME = @ARR_ARR_TIME, ARR_SPEND_TIME = @ARR_SPEND
						, EDT_DATE = GETDATE(), EDT_CODE = @EMP_CODE
					WHERE SEAT_CODE = @NEW_SEAT_CODE
					
					INSERT INTO #MSG_TEMP 
					SELECT @START_DATE, 1, '성공'
				END
				ELSE
				BEGIN
					INSERT INTO #MSG_TEMP 
					SELECT @START_DATE, 2, '좌석정보없음'
				END
			END

			--PRINT @START_DATE
		END TRY
		BEGIN CATCH
			-- LOG INSERT
			INSERT INTO #MSG_TEMP 
			SELECT @START_DATE, 2, ERROR_MESSAGE()
		END CATCH

		SET @START_DATE = DATEADD(DAY, 1, @START_DATE)
	END

	SELECT * FROM #MSG_TEMP
END

GO
