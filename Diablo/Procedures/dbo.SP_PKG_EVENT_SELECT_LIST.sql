USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*==================================================================================
	기본내용
====================================================================================
- SP 명 : SP_PKG_EVENT_SELECT_LIST
- 기 능 : 할인예약상품목록조회
====================================================================================
	참고내용
====================================================================================
- 예제
 EXEC SP_PKG_EVENT_SELECT_LIST 0
====================================================================================
	변경내역
====================================================================================
- 2011-02-01 박형만 신규 작성 
- 2011-03-11 MAX_COUNT(혜택인원)
- 2012-04-20 MAX_COUNT(혜택인원) 30명 -> 40 명까지 
- 2012-05-20 VGIF PKG_DETAIL 파일 읽어오기로 변경 
===================================================================================*/
CREATE PROC [dbo].[SP_PKG_EVENT_SELECT_LIST]
	@EVT_SEQ INT -- 0 = 현재기간에해당되는상품들(기본) , 0이상=이벤트번호있을경우 해당이벤트
AS 
SET NOCOUNT ON 
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
--DECLARE @EVT_SEQ INT 
--SET @EVT_SEQ = 0 -- 1

	SELECT PE.EVT_SEQ, PE.PRO_CODE, PE.PRICE_SEQ, PE.START_DATE, PE.END_DATE, 
		PD.DEP_DATE,
		PDP.ADT_PRICE,PDP.ADT_PRICE AS DC_ADT_PRICE, PE.ORDER_SEQ, PE.REMARK, 
		PE.NEW_CODE, PE.NEW_DATE, PE.EDT_CODE, PE.EDT_DATE  ,
		DBO.FN_PRO_GET_RES_COUNT(PD.PRO_CODE) AS RES_COUNT,
		PD.FAKE_COUNT,
		CASE WHEN PD.MAX_COUNT > 40 OR  PD.MAX_COUNT = 0  THEN 40 ELSE PD.MAX_COUNT END AS MAX_COUNT  ,
		PD.PKG_SUMMARY ,
		PM.PKG_COMMENT,
		PD.PRO_NAME AS MASTER_NAME, --긴급수정 행사명
		--PM.MASTER_NAME,
		IFM.FILE_CODE ,IFM.REGION_CODE,IFM.NATION_CODE,IFM.STATE_CODE,IFM.CITY_CODE,
		(SELECT KOR_NAME FROM PUB_REGION B WHERE B.REGION_CODE = IFM.REGION_CODE) AS REGION_NAME,
		(SELECT KOR_NAME FROM PUB_CITY B WHERE B.CITY_CODE = IFM.CITY_CODE) AS CITY_NAME,
		IFM.KOR_NAME,IFM.ENG_NAME,
		IFM.FILE_TYPE,IFM.FILE_NAME,IFM.EXTENSION_NAME,IFM.FILE_SIZE,
		IFM.FILE_NAME_S,IFM.FILE_NAME_M,IFM.FILE_NAME_L,
		IFM.FILE_REMARK,IFM.FILE_TAG,IFM.RESOLUTION,
		IFM.COPYRIGHT,
		IFM.COPYRIGHT_REMARK,
		IFM.SHOW_YN
		
	FROM PKG_EVENT AS PE WITH(NOLOCK)
		INNER JOIN PKG_DETAIL AS PD WITH(NOLOCK)
			ON	PE.PRO_CODE = PD.PRO_CODE  
		INNER JOIN PKG_DETAIL_PRICE AS PDP WITH(NOLOCK)
			ON	PE.PRO_CODE = PDP.PRO_CODE  
			AND PE.PRICE_SEQ = PDP.PRICE_SEQ 
		INNER JOIN PKG_MASTER AS PM WITH(NOLOCK)
			ON	PM.MASTER_CODE = PD.MASTER_CODE  
	
		INNER JOIN PKG_DETAIL_FILE AS PDF WITH(NOLOCK) 
			ON PD.PRO_CODE = PDF.PRO_CODE  
			AND PDF.FILE_CODE = (  SELECT TOP 1 FILE_CODE FROM PKG_DETAIL_FILE S1
					WHERE S1.PRO_CODE = PDF.PRO_CODE 
					ORDER  BY SHOW_ORDER , FILE_CODE ) 
			
		LEFT OUTER JOIN INF_FILE_MASTER AS IFM WITH(NOLOCK) 
			ON PDF.FILE_CODE = IFM.FILE_CODE
			--ON PM.MAIN_FILE_CODE = IFM.FILE_CODE  
		
	WHERE (@EVT_SEQ = -1
		OR (@EVT_SEQ = 0 AND GETDATE() BETWEEN [START_DATE] AND END_DATE )
		OR (@EVT_SEQ > 0 AND EVT_SEQ = @EVT_SEQ))
		
	AND PE.SHOW_YN = 'Y' 
	ORDER BY EVT_SEQ DESC, PE.ORDER_SEQ 
	
END 
GO
