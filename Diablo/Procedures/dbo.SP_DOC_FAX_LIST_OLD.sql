USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*
	2012-03-02 READ UNCOMMITTED 설정
*/
CREATE PROCEDURE [dbo].[SP_DOC_FAX_LIST_OLD]
	@FLAG			CHAR(1),
	@PAGESIZE		INT,
	@IPAGE			INT,
	@EMP_CODE		CHAR(7),
	@START_DATE		VARCHAR(10),
	@END_DATE		VARCHAR(10),
	@SUBJECT		VARCHAR(100)
AS
BEGIN

SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @FROM INT, @TO INT, @SQLSTRING NVARCHAR(1000), @PARMDEFINITION NVARCHAR(1000)

	SET @FROM = @IPAGE * @PAGESIZE + 1;
	SET @TO = @IPAGE * @PAGESIZE + @PAGESIZE;
	
	-- WHERE 조건 만들기
	SET @SQLSTRING = '';
	
		-- 수신일자
	IF ISNULL(@START_DATE, '') <> '' AND ISNULL(@END_DATE, '') <> ''
	BEGIN
		SET @SQLSTRING = @SQLSTRING + ' AND NEW_DATE BETWEEN @START_DATE AND @END_DATE'
	END
	ELSE IF ISNULL(@START_DATE, '') <> '' AND ISNULL(@END_DATE, '') = ''
	BEGIN
		SET @SQLSTRING = @SQLSTRING + ' AND NEW_DATE >= @START_DATE'
	END
	ELSE IF ISNULL(@START_DATE, '') = '' AND ISNULL(@END_DATE, '') <> ''
	BEGIN
		SET @SQLSTRING = @SQLSTRING + ' AND NEW_DATE =< @END_DATE'
	END
		
		-- 검색어
	IF ISNULL(LTRIM(RTRIM(@SUBJECT)), '') <> ''
	BEGIN
		SET @SQLSTRING = @SQLSTRING + ' AND SUBJECT LIKE (''%'' + @SUBJECT + ''%'')'
	END
	
	IF @FLAG = 'C'					-- 전체 글수
	BEGIN
		SET @SQLSTRING = N'
		SELECT COUNT(*) FROM FAX_MASTER WHERE FAX_GROUP IN (
			SELECT PUB_VALUE2 FROM COD_PUBLIC WHERE PUB_TYPE = ''FAX.TEAM'' AND PUB_CODE IN (
				SELECT TEAM_CODE FROM EMP_MASTER_damo WHERE EMP_CODE = @EMP_CODE
			)
		) AND FAX_TYPE = ''2''' + @SQLSTRING;
	END
	ELSE IF @FLAG = 'L'				-- 검색
	BEGIN
		SET @SQLSTRING = N'
		WITH LIST AS
		(
			SELECT FAX_SEQ, ROW_NUMBER() OVER (ORDER BY FAX_SEQ DESC) AS [ROWNUMBER]
			FROM FAX_MASTER WHERE FAX_GROUP IN (
				SELECT PUB_VALUE2 FROM COD_PUBLIC WHERE PUB_TYPE = ''FAX.TEAM'' AND PUB_CODE IN (
					SELECT TEAM_CODE FROM EMP_MASTER_damo WHERE EMP_CODE = @EMP_CODE
				)
			) AND FAX_TYPE = ''2'' ' + @SQLSTRING + '
		)
		SELECT A.ROWNUMBER, B.* FROM LIST A
		INNER JOIN FAX_MASTER B ON A.FAX_SEQ = B.FAX_SEQ
		WHERE ROWNUMBER BETWEEN @FROM AND @TO;';
	END
	
	SET @PARMDEFINITION = N'@FROM INT, @TO INT, @EMP_CODE VARCHAR(7), @START_DATE VARCHAR(10), @END_DATE VARCHAR(10), @SUBJECT VARCHAR(100)'
	
	--PRINT @SQLSTRING
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @FROM, @TO, @EMP_CODE, @START_DATE, @END_DATE, @SUBJECT;
END

-- SP_DOC_FAX_LIST 'C', '2008011', 10, 0, '2009-01-01', '', ''
-- SP_DOC_FAX_LIST 'L', '2008011', 10, 0, '2009-01-01', '', ''
GO
