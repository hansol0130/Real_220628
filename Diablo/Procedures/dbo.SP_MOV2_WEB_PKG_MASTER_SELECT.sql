USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: SP_MOV2_WEB_PKG_MASTER_SELECT ( <== XP_WEB_PKG_MASTER_SELECT )
■ DESCRIPTION				: 행사 마스터 검색
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 
	@MASTER_CODE VARCHAR(10): 마스터코드
■ EXEC						: 

	exec SP_MOV2_WEB_PKG_MASTER_SELECT 'APP5020'

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2013-05-07		김성호			최초생성
   2013-06-10		김성호			WITH(NOLOCK) 추가
   2013-06-26		김성호			사진이미지 TOP 4 삭제
   2015-01-20		정지용			이벤트 리스트 조건 추가 PED.SHOW_YN = 'Y' AND PE.SHOW_YN = 'Y'
   2016-04-27		김성호			관련기획전 기간조건 추가 및 최근등록 기획전부터 보이도록 정렬 순서 추가
   2017-07-12		오준욱			관련이벤트 추가 
   2017-09-19		정지용			ATT_NAME 추가
   2017-09-21		오준욱			이벤트 카운트시 이벤트 종료일자 반영 
================================================================================================================*/ 

CREATE  PROCEDURE [dbo].[SP_MOV2_WEB_PKG_MASTER_SELECT]
(
	@MASTER_CODE	VARCHAR(10)
)
AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000);
		
	SET @SQLSTRING = N'
	--마스터정보
	SELECT
		MASTER_CODE, MASTER_NAME, PKG_COMMENT, SIGN_CODE, ATT_CODE, TOUR_DAY, TOUR_JOURNEY, PKG_SUMMARY, 
		PKG_INC_SPECIAL, PKG_REVIEW, PKG_CONTRACT, PKG_REMARK, RES_REMARK, PKG_SHOPPING_REMARK, HOTEL_REMARK, OPTION_REMARK, 
		AIRLINE, SHOW_YN, LOW_PRICE, HIGH_PRICE, REGION_ORDER, THEME_ORDER, GROUP_ORDER, KEYWORD, MAIN_FILE_CODE, NEXT_DATE, LAST_DATE, 
		EVENT_PRO_CODE, EVENT_PRICE_SEQ, EVENT_NAME, EVENT_DEP_DATE, NEW_DATE, NEW_CODE, EDT_DATE, EDT_CODE, PKG_TOUR_REMARK, 
		PKG_PASSPORT_REMARK, SECTION_YN, LOW_POINT_RATE, HIGH_POINT_RATE, BRANCH_CODE, BRAND_TYPE,
		DBO.FN_GET_TOUR_NIGHY_DAY_TEXT(MASTER_CODE,1) AS TOUR_NIGHT_DAY,
		(SELECT COUNT(*) FROM PKG_FILE_MANAGER AA WITH(NOLOCK) INNER JOIN INF_FILE_MASTER BB WITH(NOLOCK) ON AA.FILE_CODE = BB.FILE_CODE WHERE AA.MASTER_CODE = A.MASTER_CODE AND BB.FILE_TYPE = 1 AND BB.SHOW_YN = ''Y'') AS [IMAGE_COUNT],
		(SELECT KOR_NAME FROM PUB_REGION AA WITH(NOLOCK) WHERE AA.SIGN = A.SIGN_CODE) AS [SIGN_NAME]

		, (SELECT TOP 1 PUB_VALUE FROM COD_PUBLIC AA WITH(NOLOCK) WHERE PUB_TYPE = ''PKG.ATTRIBUTE'' AND AA.PUB_CODE = ATT_CODE) AS [ATT_NAME]
		, (SELECT COUNT(*) FROM VR_CONTENT V2 WITH(NOLOCK) INNER JOIN VR_MASTER VM WITH(NOLOCK) ON V2.VR_NO = VM.VR_NO WHERE A.MASTER_CODE = V2.MASTER_CODE AND VM.VR_TYPE = 1) AS [VR_COUNT]
		, (SELECT COUNT(*) FROM PUB_EVENT_DATA A2 WITH(NOLOCK) INNER JOIN PUB_EVENT B2 WITH(NOLOCK) ON A2.EVT_SEQ = B2.EVT_SEQ WHERE B2.EVT_YN = ''Y'' AND A2.SHOW_YN = ''Y'' AND B2.SHOW_YN = ''Y'' AND A2.MASTER_CODE = A.MASTER_CODE AND B2.END_DATE >= GETDATE()) AS [EVENT_COUNT]
		, A.TAG AS [TAG]

	FROM PKG_MASTER A WITH(NOLOCK)
	WHERE A.MASTER_CODE = @MASTER_CODE

	--관련이벤트
	SELECT TOP 10 PE.* 
	FROM PUB_EVENT_DATA PED WITH(NOLOCK) 
	INNER JOIN PUB_EVENT PE WITH(NOLOCK) ON PED.EVT_SEQ = PE.EVT_SEQ
	WHERE PED.MASTER_CODE = @MASTER_CODE AND PED.SHOW_YN = ''Y'' AND PE.SHOW_YN = ''Y''
		AND ISNULL(PE.END_DATE, ''2099-12-31'') > GETDATE()
	ORDER BY ISNULL(PE.EDT_DATE, PE.NEW_DATE) DESC
--	ORDER BY ISNULL(PED.EDT_DATE, PED.NEW_DATE) DESC

	--사진정보
	SELECT B.*
	FROM PKG_FILE_MANAGER A WITH(NOLOCK)
	INNER JOIN INF_FILE_MASTER B WITH(NOLOCK) ON A.FILE_CODE = B.FILE_CODE
	WHERE A.MASTER_CODE = @MASTER_CODE AND B.FILE_TYPE = 1 AND B.SHOW_YN = ''Y''
	ORDER BY SHOW_ORDER;'

	SET @PARMDEFINITION = N'
		@MASTER_CODE VARCHAR(10)';

--	PRINT @SQLSTRING
		
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@MASTER_CODE;

END



GO
