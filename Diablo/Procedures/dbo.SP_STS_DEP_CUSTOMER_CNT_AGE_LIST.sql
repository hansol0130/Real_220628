USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*==================================================================================
	기본내용
====================================================================================
- SP 명 : SP_STS_DEP_CUSTOMER_CNT_AGE_LIST
- 기 능 : 참좋은여행 출발자 통계 리스트 
====================================================================================
	참고내용
====================================================================================
SQL.
- 예제
 EXEC SP_STS_DEP_CUSTOMER_CNT_AGE_LIST 0 , '2011-03-01' , '2011-03-31' 
====================================================================================
	변경내역
====================================================================================
- 2012-01-13 박형만 신규 작성
- 2015-03-03 김성호 주민번호 삭제, 생년월일 추가
===================================================================================*/
CREATE PROC [dbo].[SP_STS_DEP_CUSTOMER_CNT_AGE_LIST]
	@SEARCH_TYPE INT,  -- 0 출발일 , 1 예약일
	@START_DATE VARCHAR(10), 
	@END_DATE VARCHAR(10)  --하루 플러스 해야함
AS 
SET NOCOUNT ON 
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
BEGIN

--DECLARE @SEARCH_TYPE INT,
--@START_DATE VARCHAR(10), 
--@END_DATE VARCHAR(10)
--SET @SEARCH_TYPE = 0 
--SET @START_DATE = '2011-03-01'
--SET @END_DATE = '2011-03-31'

	--날짜 하루 뒤로
	SET @END_DATE = CONVERT(VARCHAR(10),DATEADD( D , 1 , CONVERT(DATETIME, @END_DATE) ) ,121)
	SELECT
		ROW_NUMBER() OVER ( ORDER BY SIGN_CODE, ATT_CODE, GENDER)   AS ROW_NUM,
		CASE WHEN A.SIGN_CODE = 'ZZZ' THEN '합계' ELSE
			 (SELECT KOR_NAME FROM PUB_REGION WHERE [SIGN] = A.SIGN_CODE)  END AS [SIGN_NAME]
		, (SELECT PUB_VALUE FROM COD_PUBLIC WHERE PUB_TYPE = 'PKG.ATTRIBUTE' AND PUB_CODE = A.ATT_CODE) AS [ATT_NAME]
		, (CASE GENDER WHEN 'M' THEN '남성' WHEN 'F' THEN '여성' END) AS [GENDER_NAME]
		, A.*
	FROM (
		SELECT
			(CASE SIGN WHEN 'AAA' THEN SIGN_CODE ELSE SIGN END) AS [SIGN_CODE]
			, (CASE SIGN WHEN 'AAA' THEN ATT_CODE ELSE SIGN END) AS [ATT_CODE]
			, (CASE SIGN WHEN 'AAA' THEN GENDER ELSE SIGN END) AS [GENDER]
			, ISNULL(SUM(CASE WHEN AGE >= 0 AND AGE < 20 THEN 1 END), 0) AS [0-19]
			, ISNULL(SUM(CASE WHEN AGE >= 20 AND AGE < 30 THEN 1 END), 0) AS [20-29]
			, ISNULL(SUM(CASE WHEN AGE >= 30 AND AGE < 40 THEN 1 END), 0) AS [30-39]
			, ISNULL(SUM(CASE WHEN AGE >= 40 AND AGE < 50 THEN 1 END), 0) AS [40-49]
			, ISNULL(SUM(CASE WHEN AGE >= 50 AND AGE < 60 THEN 1 END), 0) AS [50-59]
			, ISNULL(SUM(CASE WHEN AGE >= 60 THEN 1 END), 0) AS [60-]
			, ISNULL(SUM(CASE WHEN AGE < 0 THEN 1 END), 0) AS [NOAGE]
			, ISNULL(SUM(1), 0) AS [TOTAL]
		FROM (
			SELECT A.RES_CODE, (CASE WHEN A.GENDER = 'Y' THEN 'M' WHEN A.GENDER = 'N' THEN 'F' ELSE A.GENDER END) AS GENDER
				--, A.SOC_NUM1, DBO.FN_CUS_GET_AGE(A.SOC_NUM1, DAMO.DBO.DEC_VARCHAR('DIABLO', 'DBO.RES_CUSTOMER', 'SOC_NUM2', A.SEC_SOC_NUM2), GETDATE()) AS [AGE]
				, A.BIRTH_DATE, DBO.FN_CUS_GET_AGE(A.BIRTH_DATE, GETDATE()) AS [AGE]
				, (SELECT SIGN_CODE FROM PKG_MASTER WHERE MASTER_CODE = B.MASTER_CODE) AS [SIGN_CODE]
				, (SELECT ATT_CODE FROM PKG_MASTER WHERE MASTER_CODE = B.MASTER_CODE) AS [ATT_CODE]
			FROM RES_CUSTOMER_DAMO A
			INNER JOIN RES_MASTER_DAMO B ON A.RES_CODE = B.RES_CODE
			WHERE( (@SEARCH_TYPE = 0 AND B.DEP_DATE >= CONVERT(DATETIME,@START_DATE) AND B.DEP_DATE < CONVERT(DATETIME,@END_DATE) )
				OR (@SEARCH_TYPE = 1 AND B.NEW_DATE >= CONVERT(DATETIME,@START_DATE) AND B.NEW_DATE < CONVERT(DATETIME,@END_DATE) ))
			AND B.RES_STATE <= 7
			AND A.RES_STATE IN (0, 3)
		) A
		CROSS JOIN (SELECT 'AAA' AS [SIGN] UNION ALL SELECT 'ZZZ') B
		GROUP BY (CASE SIGN WHEN 'AAA' THEN SIGN_CODE ELSE SIGN END), (CASE SIGN WHEN 'AAA' THEN ATT_CODE ELSE SIGN END), (CASE SIGN WHEN 'AAA' THEN GENDER ELSE SIGN END)
	) A
	ORDER BY SIGN_CODE, ATT_CODE, GENDER
END 
GO
