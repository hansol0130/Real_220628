USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[SP_PRO_TRANS_MASTER_SELECT_LIST]
	@FLAG				CHAR(1),
	@PAGE_SIZE			INT,
	@PAGE_INDEX			INT,
	@TRANS_TYPE			INT,
	@DEP_DEP_AIRPORT_CODE	VARCHAR(3),
	@DEP_ARR_AIRPORT_CODE	VARCHAR(3),
	@ARR_DEP_AIRPORT_CODE	VARCHAR(3),
	@ARR_ARR_AIRPORT_CODE	VARCHAR(3),
	@TRANS_CODE			VARCHAR(2),
	@DEP_TRANS_CODE		VARCHAR(3),
	@ARR_TRANS_CODE		VARCHAR(3)
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000), @FROM INT, @TO INT;

	SET @FROM = @PAGE_INDEX * @PAGE_SIZE + 1;
	SET @TO = @PAGE_INDEX * @PAGE_SIZE + @PAGE_SIZE;

	-- WHERE 조건 만들기
	SET @SQLSTRING = ' WHERE DEP_AIRPORT_CODE = @DEP_DEP_AIRPORT_CODE AND ARR_AIRPORT_CODE = @DEP_ARR_AIRPORT_CODE';

	IF ISNULL(@TRANS_CODE, '') != ''
		SET @SQLSTRING = @SQLSTRING + ' AND TRANS_CODE = @TRANS_CODE';
	IF (@TRANS_TYPE > 0)
		SET @SQLSTRING = @SQLSTRING + ' AND TRANS_TYPE = @TRANS_TYPE';

	IF @FLAG = 'C'					-- 전체 글수
	BEGIN
		SET @SQLSTRING = N'SELECT COUNT(*) FROM PRO_TRANS_MASTER A WITH(NOLOCK) ' + @SQLSTRING;
	END
	ELSE IF @FLAG = 'L'				-- 검색
	BEGIN
		SET @SQLSTRING = N'
		WITH DOCUMENTLIST AS
		(
			SELECT
				ROW_NUMBER() OVER (ORDER BY A.TRANS_CODE, A.TRANS_NUMBER) AS [ROWNUMBER]
				, A.TRANS_CODE, A.TRANS_NUMBER, A.DEP_AIRPORT_CODE, A.ARR_AIRPORT_CODE
			FROM PRO_TRANS_MASTER A WITH(NOLOCK) 
			' + @SQLSTRING + '
		)
		SELECT B.*
			, (SELECT ISNULL(KOR_NAME, ENG_NAME) FROM PUB_AIRLINE  WITH(NOLOCK) WHERE AIRLINE_CODE = A.TRANS_CODE) AS [TRANS_NAME]
			, (SELECT ISNULL(KOR_NAME, ENG_NAME) FROM PUB_AIRPORT  WITH(NOLOCK) WHERE AIRPORT_CODE = b.DEP_AIRPORT_CODE) AS [DEP_AIRPORT_NAME]
			, (SELECT ISNULL(KOR_NAME, ENG_NAME) FROM PUB_AIRPORT  WITH(NOLOCK) WHERE AIRPORT_CODE = b.ARR_AIRPORT_CODE) AS [ARR_AIRPORT_NAME]
		FROM DOCUMENTLIST A
		INNER JOIN PRO_TRANS_MASTER B  WITH(NOLOCK) ON A.TRANS_CODE = B.TRANS_CODE AND A.TRANS_NUMBER = B.TRANS_NUMBER
			AND A.DEP_AIRPORT_CODE = B.DEP_AIRPORT_CODE AND A.ARR_AIRPORT_CODE = B.ARR_AIRPORT_CODE
		WHERE ROWNUMBER BETWEEN @FROM AND @TO;';
	END

	SET @PARMDEFINITION = N'@FROM INT, @TO INT, @TRANS_TYPE INT, @DEP_DEP_AIRPORT_CODE VARCHAR(3), @DEP_ARR_AIRPORT_CODE VARCHAR(3), @ARR_DEP_AIRPORT_CODE VARCHAR(3), @ARR_ARR_AIRPORT_CODE VARCHAR(3), @TRANS_CODE	VARCHAR(2)';

	--PRINT @SQLSTRING
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @FROM, @TO, @TRANS_TYPE, @DEP_DEP_AIRPORT_CODE, @DEP_ARR_AIRPORT_CODE, @ARR_DEP_AIRPORT_CODE, @ARR_ARR_AIRPORT_CODE, @TRANS_CODE;
END


GO
