USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*-------------------------------------------------------------------------------------------------
■ USP_Name					: XP_PRO_TRANS_SEAT_LIST_BY_PKG_MASTER  
■ Description				: 마스터의 좌석 정보 조회(검색) 
■ Input Parameter			:                  


■ Output Parameter			:                  
■ Output Value				:                 
■ Exec						: EXEC XP_PRO_TRANS_SEAT_LIST_BY_PKG_MASTER 'APPT334' , '2019-07-16' , '2019-12-31' , 'TW' , 5   , 1  , 1 ,'TW141-TW142_TAE-HAN/HAN-TAE_5D[+0,+0]'
■ Author					: 박형만  
■ Date						: 2019-07-16
---------------------------------------------------------------------------------------------------
■ Change History                   
---------------------------------------------------------------------------------------------------
   Date				Author			Description           
---------------------------------------------------------------------------------------------------
   2019-07-16		박형만			최초생성  
-------------------------------------------------------------------------------------------------*/ 
CREATE PROC [dbo].[XP_PRO_TRANS_SEAT_LIST_BY_PKG_MASTER] 
	@MASTER_CODE VARCHAR(20),
	@START_DATE DATETIME,
	@END_DATE DATETIME,

	@DEP_TRANS_CODE VARCHAR(2) = NULL,
	@TOT_DAY INT = NULL,
	@SEAT INT = NULL,
	@DEP_WK INT = NULL,
	@SEAT_GROUP  VARCHAR(200) = NULL 
AS 
BEGIN


	DECLARE @SQLSTRING NVARCHAR(MAX) = '', @PARMDEFINITION NVARCHAR(MAX) = ''
	,@SQLWHERE NVARCHAR(MAX) = '' 

	SET @SQLWHERE = @SQLWHERE + '  '


	IF ISNULL(@DEP_TRANS_CODE,'') <> ''
	BEGIN
		SET @SQLWHERE = @SQLWHERE + 'AND B.DEP_TRANS_CODE = @DEP_TRANS_CODE '
	END 
	IF ISNULL(@TOT_DAY,0) > 0 
	BEGIN
		SET @SQLWHERE = @SQLWHERE + 'AND B.TOT_DAY = @TOT_DAY '
	END 
	IF ISNULL(@SEAT,0) > 0 
	BEGIN
		SET @SQLWHERE = @SQLWHERE + 'AND B.SEAT = @SEAT '
	END 
	IF ISNULL(@DEP_WK,0) > 0 
	BEGIN
		SET @SQLWHERE = @SQLWHERE + 'AND B.DEP_WK = @DEP_WK '
	END 
	IF ISNULL(@SEAT_GROUP,'') <> ''
	BEGIN
		SET @SQLWHERE = @SQLWHERE + 'AND B.SEAT_GROUP = @SEAT_GROUP '
	END 


	SET @SQLSTRING = '
	SELECT  A.PRO_CODE, 
	CONVERT(VARCHAR(5), A.DEP_DATE ,110)  AS DEP_DATE ,  
	CONVERT(VARCHAR(5), A.ARR_DATE ,110)  AS ARR_DATE ,
	LEFT(DATENAME(DW, CONVERT(DATETIME, B.DEP_DEP_DATE)),1)  AS DEP_WEEK , 

	(SELECT COUNT(*) FROM PRO_TRANS_SEAT_SEGMENT WHERE SEAT_CODE = B.SEAT_CODE  AND TRANS_SEQ = 1 ) AS DEP_SEG,
	(SELECT COUNT(*) FROM PRO_TRANS_SEAT_SEGMENT WHERE SEAT_CODE = B.SEAT_CODE  AND TRANS_SEQ = 2 ) AS ARR_SEG,
	ISNULL(C.EDT_DATE,C.NEW_dATE) AS EDT_DATE ,  ISNULL(C.EDT_CODE,C.NEW_CODE) AS EDT_CODE , 

	( SELECT KOR_NAME FROM EMP_MASTER WHERE EMP_CODE =  ISNULL(C.EDT_CODE,C.NEW_CODE)  ) AS SEAT_EMP_NAME ,
	( SELECT TEAM_NAME FROM EMP_TEAM WHERE TEAM_CODE = ( SELECT TEAM_CODE FROM EMP_MASTER WHERE EMP_CODE =  ISNULL(C.EDT_CODE,C.NEW_CODE) )) AS SEAT_TEAM_NAME ,

	( SELECT KOR_NAME FROM EMP_MASTER WHERE EMP_CODE =  A.NEW_CODE  ) AS NEW_EMP_NAME ,
	( SELECT TEAM_NAME FROM EMP_TEAM WHERE TEAM_CODE = ( SELECT TEAM_CODE FROM EMP_MASTER WHERE EMP_CODE =  A.NEW_CODE  ))  AS NEW_TEAM_NAME ,

	CASE WHEN CHARINDEX(''*'',C.DEP_SPEND_TIME) > 0 THEN 0 ELSE DBO.FN_AIR_PRO_TRANS_FLYING_MINUTE(C.SEAT_CODE,1) END AS DEP_SPEND_MINUTE ,-- 비행시간	text	
	CASE WHEN CHARINDEX(''*'',C.ARR_SPEND_TIME) > 0 THEN 0 ELSE DBO.FN_AIR_PRO_TRANS_FLYING_MINUTE(C.SEAT_CODE,2) END AS ARR_SPEND_MINUTE ,-- 비행시간	text

	B.*   ,
	A.SHOW_YN

	FROM PKG_DETAIL A WITH(NOLOCK)
		INNER JOIN VIEW_PRO_TRANS_SEAT B WITH(NOLOCK)
			ON A.SEAT_CODE = B.SEAT_CODE 
		INNER JOIN PRO_TRANS_SEAT C WITH(NOLOCK)
			ON A.SEAT_CODE = C.SEAT_CODE 
	WHERE MASTER_CODE=  @MASTER_CODE
	AND A.DEP_dATE >= @START_DATE
	AND A.DEP_dATE <  DATEADD(D,1, @END_DATE)
	' + @SQLWHERE + ' 
	ORDER BY A.DEP_DATE , B.DEP_TRAN '


	SET @PARMDEFINITION = N'
	@MASTER_CODE VARCHAR(20),
	@START_DATE DATETIME,
	@END_DATE DATETIME,

	@DEP_TRANS_CODE VARCHAR(2) ,
	@TOT_DAY INT ,
	@SEAT INT ,
	@DEP_WK INT ,
	@SEAT_GROUP  VARCHAR(200)';

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, 
		@MASTER_CODE,
		@START_DATE ,
		@END_DATE ,

		@DEP_TRANS_CODE ,
		@TOT_DAY,
		@SEAT ,
		@DEP_WK ,
		@SEAT_GROUP 
END 
GO
