USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD
■ DESCRIPTION				: 현대카드 상품연동
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 
■ EXEC						: SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD ''
SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD 'EPP460'
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
2015-09-07			정지용			최초생성
2015-11-20			정지용			항공코드 PUKWAN FERRY PK 부관훼리 -> PF , PANSTAR LINE PS 팬스타 PS -> FS  
2016-04-19			정지용			도시코드 없는 상품 제외 조건 추가
2016-12-20			정지용			국내상품 제외
2017-01-23			정지용			세인대리 요청으로 사용안하는 모상품은 연동 안되도록 USE_YN = 'Y' 추가
2017-02-21			정지용			현대카드 요청으로 LEVDC 예약 상태값 추가(출발확정)
2017-03-28			정지용			BAF_AMT 공백과 같이 리턴 되어서 RTRIM 추가, 상품코드가 21자이상되면 안된다고함 --; 20자까지만 조회되도록 수정
2017-06-15			박형만			프리비아요청 (행사노출여부)DEL_YN 필드 추가  , 행사의 SHOW_YN 조건 빼기 
2017-10-24			정지용			최소출발인원, 잔여좌석 추가 
2017-11-03			정지용			속성이 자유여행인것 추가
2017-11-16			정지용			현대카드 조건에 해당되는 상품이 없을 경우 마스터도 조회 안되도록 수정
2017-11-30			정지용			프리미엄 상품은 THE PLUS 상품만..
2017-12-26			정지용			메인도시가 체크되어있는거로 복수로 조회되도록 수정
2019-03-25			박형만			FARE_SEAT_TYPE 비즈니스 추가 
2019-11-27			박형만			DEP_CFM_YN = 'F' 출발확정(가) 도 확정으로 전송되도록 수정 
================================================================================================================*/ 
CREATE PROC [dbo].[SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD]	
	@MASTER_CODE VARCHAR(10)
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @PROVIDER_HYUNDAICARD INT 
	SET @PROVIDER_HYUNDAICARD = 29;

	WITH MASTER_LIST AS
	(
		SELECT
			UPPER(A.MASTER_CODE) AS  MASTER_CODE, A.MASTER_NAME, A.PKG_SUMMARY, A.TOUR_DAY, A.MAIN_FILE_CODE, A.ATT_CODE, A.BRAND_TYPE, B.USE_YN
		FROM PKG_MASTER A WITH(NOLOCK)
		INNER JOIN PKG_MASTER_AFFILIATE B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.USE_YN = 'Y'
		WHERE B.PROVIDER = @PROVIDER_HYUNDAICARD AND SUBSTRING(A.MASTER_CODE, 1, 1) <> 'K' AND A.SIGN_CODE <> 'K' AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE))
		--WHERE B.PROVIDER = @PROVIDER_HYUNDAICARD AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE))
	),
	CITY_LIST AS
	(
		SELECT ROW_NUMBER() OVER (PARTITION BY A.MASTER_CODE ORDER BY MAX(A.CITY_SHOW_ORDER) DESC) AS [ROW_NUMBER]
			, UPPER(A.MASTER_CODE) AS MASTER_CODE, D.HYC_CITY_CODE AS [CITY_CODE], B.KOR_NAME AS [CITY_NAME], C.NATION_CODE, C.KOR_NAME AS [NATION_NAME]
		FROM MASTER_LIST Z
		INNER JOIN PKG_MASTER_SCH_CITY A WITH(NOLOCK) ON Z.MASTER_CODE = A.MASTER_CODE
		INNER JOIN PUB_CITY B WITH(NOLOCK) ON A.CITY_CODE = B.CITY_CODE
		INNER JOIN PUB_NATION C WITH(NOLOCK) ON B.NATION_CODE = C.NATION_CODE
		INNER JOIN HYUNDAICARD_CITY_MAP D WITH(NOLOCK) ON B.CITY_CODE = D.CITY_CODE
		WHERE A.MAINCITY_YN = 'Y'
		GROUP BY A.MASTER_CODE, D.HYC_CITY_CODE, B.KOR_NAME, C.NATION_CODE, C.KOR_NAME
	)

	SELECT 
		UPPER(Z.MASTER_CODE) AS PKG_MST_CODE, Z.MASTER_NAME AS PKG_MST_NAME
	FROM MASTER_LIST Z
	INNER JOIN PKG_DETAIL A WITH(NOLOCK) ON Z.MASTER_CODE = A.MASTER_CODE --AND A.SHOW_YN = 'Y'
	INNER JOIN PRO_TRANS_SEAT B WITH(NOLOCK) ON A.SEAT_CODE = B.SEAT_CODE
	INNER JOIN PKG_DETAIL_PRICE D WITH(NOLOCK) ON A.PRO_CODE = D.PRO_CODE
	LEFT JOIN (
		SELECT *
		FROM CITY_LIST AA
		WHERE AA.ROW_NUMBER = 1
	) E ON A.MASTER_CODE = E.MASTER_CODE
	LEFT JOIN INF_FILE_MASTER F WITH(NOLOCK) ON Z.MAIN_FILE_CODE = F.FILE_CODE
	WHERE A.DEP_DATE > GETDATE() AND E.CITY_CODE IS NOT NULL AND LEN(A.PRO_CODE + '|' + CONVERT(VARCHAR, D.PRICE_SEQ)) < 21
	GROUP BY Z.MASTER_CODE, Z.MASTER_NAME;

	/*
	SELECT
		UPPER(A.MASTER_CODE) AS PKG_MST_CODE, A.MASTER_NAME AS PKG_MST_NAME
	FROM PKG_MASTER A WITH(NOLOCK)
	INNER JOIN PKG_MASTER_AFFILIATE B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.USE_YN = 'Y'
	WHERE B.PROVIDER = @PROVIDER_HYUNDAICARD AND SUBSTRING(A.MASTER_CODE, 1, 1) <> 'K' AND A.SIGN_CODE <> 'K' AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE));
	--WHERE B.PROVIDER = @PROVIDER_HYUNDAICARD AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE));
	*/
	-- DETAIL
	WITH MASTER_LIST AS
	(
		SELECT
			UPPER(A.MASTER_CODE) AS  MASTER_CODE, A.MASTER_NAME, A.PKG_SUMMARY, A.TOUR_DAY, A.MAIN_FILE_CODE, A.ATT_CODE, A.BRAND_TYPE, B.USE_YN
		FROM PKG_MASTER A WITH(NOLOCK)
		INNER JOIN PKG_MASTER_AFFILIATE B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.USE_YN = 'Y'
		WHERE B.PROVIDER = @PROVIDER_HYUNDAICARD AND SUBSTRING(A.MASTER_CODE, 1, 1) <> 'K' AND A.SIGN_CODE <> 'K' AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE))
		--WHERE B.PROVIDER = @PROVIDER_HYUNDAICARD AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE))
	),
	CITY_LIST AS
	(
		SELECT ROW_NUMBER() OVER (PARTITION BY A.MASTER_CODE ORDER BY MAX(A.CITY_SHOW_ORDER) DESC) AS [ROW_NUMBER]
			, UPPER(A.MASTER_CODE) AS MASTER_CODE, D.HYC_CITY_CODE AS [CITY_CODE], B.KOR_NAME AS [CITY_NAME], C.NATION_CODE, C.KOR_NAME AS [NATION_NAME]
		FROM MASTER_LIST Z
		INNER JOIN PKG_MASTER_SCH_CITY A WITH(NOLOCK) ON Z.MASTER_CODE = A.MASTER_CODE
		INNER JOIN PUB_CITY B WITH(NOLOCK) ON A.CITY_CODE = B.CITY_CODE
		INNER JOIN PUB_NATION C WITH(NOLOCK) ON B.NATION_CODE = C.NATION_CODE
		INNER JOIN HYUNDAICARD_CITY_MAP D WITH(NOLOCK) ON B.CITY_CODE = D.CITY_CODE
		WHERE A.MAINCITY_YN = 'Y'
		GROUP BY A.MASTER_CODE, D.HYC_CITY_CODE, B.KOR_NAME, C.NATION_CODE, C.KOR_NAME
	)
	
	SELECT
		UPPER(A.MASTER_CODE) AS PKG_MST_CODE
		,Z.MASTER_NAME AS PKG_MST_NAME
		,ISNULL(Z.PKG_SUMMARY, '') AS PKG_MST_DESC
		, CASE 
			WHEN B.FARE_SEAT_TYPE = 3 THEN 'BUSINESS' 
			WHEN Z.BRAND_TYPE IS NOT NULL AND Z.BRAND_TYPE = 1 THEN 'PREMIUM'  -- The Plus만 프리미엄 상품으로..
			WHEN Z.ATT_CODE = 'W' THEN 'HONEYMOON' 
			WHEN Z.ATT_CODE = 'F' THEN 'FREE'
			ELSE 'PACKAGE' END AS MAIN_ETC_CODE
		, A.TOUR_DAY
		, ISNULL(STUFF((SELECT ('-' + CITY_CODE ) AS [text()] FROM CITY_LIST CL WHERE CL.MASTER_CODE = A.MASTER_CODE GROUP BY CITY_CODE FOR XML PATH('')), 1, 1, '') , '') AS CUM_CITY_CODE
		, ISNULL(STUFF((SELECT ('-' + NATION_CODE ) AS[text()] FROM CITY_LIST CL WHERE CL.MASTER_CODE = A.MASTER_CODE GROUP BY NATION_CODE FOR XML PATH('')), 1, 1, ''), '') AS CUM_COUNTRY_CODE
		--,E.CITY_CODE AS CUM_CITY_CODE  -- 2017-12-26
		--,E.NATION_CODE AS CUM_COUNTRY_CODE  -- 2017-12-26
		,A.PRO_CODE + '|' + CONVERT(VARCHAR, D.PRICE_SEQ) AS PKG_CODE
		,A.PRO_NAME AS PKG_NAME
		,(CONVERT(VARCHAR(8), B.DEP_DEP_DATE, 112)) AS [START_DATE]
		, LEFT(DATENAME(WEEKDAY, B.DEP_DEP_DATE), 1) AS START_DY
		,CASE DEP_TRANS_CODE WHEN 'PK' THEN 'PF' WHEN 'PS' THEN 'FS' ELSE DEP_TRANS_CODE END AS START_AIR_CODE
		,C.KOR_NAME AS START_AIR_KNAME
		,REPLACE(B.DEP_DEP_TIME, ':', '') AS [START_TIME]		
		,D.ADT_PRICE AS ADULT_AMT
		,CASE WHEN Z.USE_YN = 'Y' THEN '1' ELSE '2' END AS DAY_COLOR
		, (CASE
			WHEN F.FILE_NAME_S = '' THEN ''
			WHEN F.FILE_NAME_M = '' THEN ('http://contents.verygoodtour.com/content/' + F.REGION_CODE + '/' + F.NATION_CODE + '/' + F.STATE_CODE + '/' + F.CITY_CODE + '/image/' + F.FILE_NAME_S)
			ELSE ('http://contents.verygoodtour.com/content/' + F.REGION_CODE + '/' + F.NATION_CODE + '/' + F.STATE_CODE + '/' + F.CITY_CODE + '/image/' + F.FILE_NAME_M)
		END) AS [PKG_IMG_URL]
		,'7' AS COMM_RATE
		--, (SELECT DBO.FN_PUB_GET_CITY_NAME(AA.CITY_CODE) FROM PUB_AIRPORT AA WITH(NOLOCK) WHERE AA.AIRPORT_CODE = B.DEP_DEP_AIRPORT_CODE) AS [START_CITY]
		,(SELECT AA.CITY_CODE FROM PUB_AIRPORT AA WITH(NOLOCK) WHERE AA.AIRPORT_CODE = B.DEP_DEP_AIRPORT_CODE) AS [START_CITY]
		,A.TOUR_NIGHT AS NIGHTS
		,D.CHD_PRICE AS CHILD_AMT
		,RTRIM(ISNULL(dbo.FN_ARR_SPLIT(dbo.XN_PRO_DETAIL_QCHARGE_PRICE_ALL(A.PRO_CODE,D.PRICE_SEQ),'|',1),'0')) AS BAF_AMT
		,ISNULL(dbo.XN_PRO_DETAIL_SALE_PRICE_CUTTING(A.PRO_CODE, D.PRICE_SEQ), '0') AS TOTAL_AMT
		, CASE WHEN Z.BRAND_TYPE IS NOT NULL THEN 'PREMU' ELSE 'SBTNC' END AS PKG_PRICE
		,(CONVERT(VARCHAR(8), B.ARR_ARR_DATE, 112)) AS ARVDT
		,REPLACE(B.ARR_ARR_TIME, ':', '') AS ARVTM
		, CASE 
			WHEN A.RES_ADD_YN = 'Y' AND A.MAX_COUNT = -1 THEN 'ARBKG' -- 대기예약
			WHEN A.RES_ADD_YN = 'Y' AND (A.MAX_COUNT = 0 OR (A.MAX_COUNT - DBO.FN_PRO_GET_RES_COUNT(A.PRO_CODE) > 0)) AND A.DEP_CFM_YN IN('Y','F') THEN 'LEVDC' -- 출발확정
			WHEN A.RES_ADD_YN = 'Y' AND (A.MAX_COUNT = 0 OR (A.MAX_COUNT - DBO.FN_PRO_GET_RES_COUNT(A.PRO_CODE) > 0)) THEN 'RSVPS' -- 예약가능
			ELSE 'RSVCD' END AS RSERV_STAT -- 예약마감
		, CASE WHEN A.SHOW_YN = 'N' THEN 'Y' ELSE 'N' END AS DEL_YN
		, A.MIN_COUNT AS MIN_NUM
		, ((CASE WHEN A.MAX_COUNT = 0 THEN 99 WHEN A.MAX_COUNT = -1 THEN 0 ELSE A.MAX_COUNT END) - (DBO.FN_PRO_GET_RES_COUNT(A.PRO_CODE) + A.FAKE_COUNT)) AS SEAT_NUM
	FROM MASTER_LIST Z
	INNER JOIN PKG_DETAIL A WITH(NOLOCK) ON Z.MASTER_CODE = A.MASTER_CODE --AND A.SHOW_YN = 'Y'
	INNER JOIN PRO_TRANS_SEAT B WITH(NOLOCK) ON A.SEAT_CODE = B.SEAT_CODE
	INNER JOIN PKG_DETAIL_PRICE D WITH(NOLOCK) ON A.PRO_CODE = D.PRO_CODE
	LEFT JOIN PUB_AIRLINE C WITH(NOLOCK) ON B.DEP_TRANS_CODE = C.AIRLINE_CODE
	LEFT JOIN (
		SELECT *
		FROM CITY_LIST AA
		WHERE AA.ROW_NUMBER = 1
	) E ON A.MASTER_CODE = E.MASTER_CODE
	LEFT JOIN INF_FILE_MASTER F WITH(NOLOCK) ON Z.MAIN_FILE_CODE = F.FILE_CODE
	WHERE A.DEP_DATE > GETDATE() AND E.CITY_CODE IS NOT NULL AND LEN(A.PRO_CODE + '|' + CONVERT(VARCHAR, D.PRICE_SEQ)) < 21;
END
/*
EXEC SP_PKG_HYUNDAICARD_SELECT_LIST ''
SELECT TOP 100 PRO_CODE, COUNT(1) FROM PKG_DETAIL_PRICE WITH(NOLOCK) GROUP BY PRO_CODE HAVING COUNT(PRO_CODE) > 1

SELECT * FROM (
SELECT TOP 10000 B.PRO_CODE FROM PKG_MASTER_AFFILIATE A WITH(NOLOCK)
INNER JOIN PKG_DETAIL B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.SHOW_YN = 'Y'
INNER JOIN PKG_DETAIL_PRICE C WITH(NOLOCK) ON B.PRO_CODE = C.PRO_CODE
WHERE A.PROVIDER = 29 AND B.DEP_DATE > GETDATE()
) B GROUP BY B.PRO_CODE HAVING COUNT(B.PRO_CODE) > 1
*/

GO
