USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_STS_HOTEL_BEST_HOTEL_SELECT
■ DESCRIPTION				: ERP BTMS BEST 호텔 현황
■ INPUT PARAMETER			: 
	@KEY		VARCHAR(400): 검색 키
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXEC DBO.XP_COM_STS_HOTEL_BEST_HOTEL_SELECT 'AgentCode=&TeamSeq=&EmpSeq=&StartDate=2016-06-01&EndDate=2016-09-30&OrderType=CA'

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-06-15	 저스트고_강태영	  최초생성
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_STS_HOTEL_BEST_HOTEL_SELECT]

@KEY VARCHAR(400)

AS
BEGIN
	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000);
	DECLARE @WHERE NVARCHAR(4000);
	DECLARE @ORDERBY NVARCHAR(4000);
	DECLARE @AGT_CODE VARCHAR(10), @TEAM_SEQ INT, @EMP_SEQ INT, @START_DATE VARCHAR(10), @END_DATE VARCHAR(10), @ORDER_TYPE VARCHAR(2);

	SELECT
		@AGT_CODE = DBO.FN_PARAM(@KEY, 'AgentCode'),
		@TEAM_SEQ = DBO.FN_PARAM(@KEY, 'TeamSeq'),
		@EMP_SEQ = DBO.FN_PARAM(@KEY, 'EmpSeq'),
		@START_DATE = DBO.FN_PARAM(@KEY, 'StartDate'),
		@END_DATE = DBO.FN_PARAM(@KEY, 'EndDate'),
		@ORDER_TYPE = DBO.FN_PARAM(@KEY, 'OrderType')

	SELECT @WHERE = ' WHERE B.PRO_TYPE = 3 AND B.RES_STATE IN (4, 5) AND E.BTMS_YN = ''Y'' AND A.RES_CODE IN (SELECT C.RES_CODE FROM COM_BIZTRIP_DETAIL C WHERE C.PRO_DETAIL_TYPE = 3) '
	
	IF LEN(@AGT_CODE) > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND E.AGT_CODE = @AGT_CODE '
	END

	IF @TEAM_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND M.TEAM_SEQ = @TEAM_SEQ '
	END

	IF @EMP_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND K.EMP_SEQ = @EMP_SEQ '
	END

	IF LEN(@START_DATE) > 0 AND LEN(@END_DATE) > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND CONVERT(DATETIME, A.CHECK_IN, 121) >= CONVERT(DATETIME, @START_DATE, 121) AND CONVERT(DATETIME, A.CHECK_IN, 121) < DATEADD(DAY, 1, CONVERT(DATETIME, @END_DATE, 121)) '
	END

	IF LEN(@ORDER_TYPE) > 0
	BEGIN
		IF @ORDER_TYPE = 'HA'
		BEGIN
			SET @ORDERBY = ' ORDER BY A.HOTEL_NAME ASC '
		END

		IF @ORDER_TYPE = 'HD'
		BEGIN
			SET @ORDERBY = ' ORDER BY A.HOTEL_NAME DESC '
		END

		IF @ORDER_TYPE = 'CA'
		BEGIN
			SET @ORDERBY = ' ORDER BY F.KOR_NAME ASC '
		END

		IF @ORDER_TYPE = 'CD'
		BEGIN
			SET @ORDERBY = ' ORDER BY F.KOR_NAME DESC '
		END

		IF @ORDER_TYPE = 'NA'
		BEGIN
			SET @ORDERBY = ' ORDER BY G.KOR_NAME ASC '
		END

		IF @ORDER_TYPE = 'ND'
		BEGIN
			SET @ORDERBY = ' ORDER BY G.KOR_NAME DESC '
		END
	END

	ELSE

	BEGIN
		SET @ORDERBY = ' ORDER BY A.HOTEL_NAME ASC '
	END

	SET @SQLSTRING = N'
		SELECT
			A.HOTEL_NAME,
			A.MASTER_CODE,
			F.KOR_NAME AS CITY_NAME,
			G.KOR_NAME AS NATION_NAME,
			COUNT(A.HOTEL_NAME) AS HOTEL_CNT,
			SUM(A.SALE_PRICE) AS SALE_PRICE
		FROM RES_HTL_ROOM_MASTER A
		LEFT JOIN RES_MASTER_damo B ON A.RES_CODE = B.RES_CODE
		LEFT JOIN COM_BIZTRIP_DETAIL C ON C.RES_CODE = B.RES_CODE
		LEFT JOIN COM_BIZTRIP_MASTER D ON D.BT_CODE = C.BT_CODE
		LEFT JOIN AGT_MASTER E ON E.AGT_CODE = D.AGT_CODE
		LEFT JOIN PUB_CITY F ON F.CITY_CODE = D.BT_CITY_CODE
		LEFT JOIN PUB_NATION G ON G.NATION_CODE = F.NATION_CODE
		LEFT JOIN PUB_REGION H ON H.REGION_CODE = G.REGION_CODE
		LEFT JOIN PAY_MATCHING I ON I.RES_CODE = B.RES_CODE
		LEFT JOIN COM_EMPLOYEE_MATCHING J ON J.CUS_NO = B.CUS_NO AND J.AGT_CODE = E.AGT_CODE
		LEFT JOIN COM_EMPLOYEE K ON K.EMP_SEQ = J.EMP_SEQ AND K.AGT_CODE = E.AGT_CODE
		LEFT JOIN COM_TEAM M ON M.TEAM_SEQ = K.TEAM_SEQ AND M.AGT_CODE = E.AGT_CODE
		' + @WHERE + N'
		GROUP BY A.HOTEL_NAME, A.MASTER_CODE, F.KOR_NAME, G.KOR_NAME '
		+ @ORDERBY

	SET @PARMDEFINITION = N'
		@AGT_CODE VARCHAR(10),
		@TEAM_SEQ INT,
		@EMP_SEQ INT,
		@START_DATE VARCHAR(10),
		@END_DATE VARCHAR(10),
		@ORDER_TYPE VARCHAR(2)';

	--PRINT @SQLSTRING

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@AGT_CODE,
		@TEAM_SEQ,
		@EMP_SEQ,
		@START_DATE,
		@END_DATE,
		@ORDER_TYPE

END
GO
