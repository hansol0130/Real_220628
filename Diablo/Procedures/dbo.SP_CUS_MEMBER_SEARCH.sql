USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


/*==================================================================================
	기본내용
====================================================================================
- SP 명 : SP_CUS_MEMBER_SEARCH
- 기 능 : 정회원 검색
====================================================================================
	참고내용
====================================================================================
- ID찾기,비밀번호찾기, 회원가입시 사용
- 예제
 EXEC SP_CUS_MEMBER_SEARCH @CUS_ID = NULL ,@IPIN_DUP_INFO= NULL 
	,@CUS_NAME = '정계재' 
	,@BIRTH_DATE ='1971-08-25' 
	,@GENDER = 'M'
	,@EMAIL = 'jeje19@naver.com'
	,@NOR_TEL1 = ''
	,@NOR_TEL2 = ''
	,@NOR_TEL3 = ''
====================================================================================
	변경내역
====================================================================================
- 2012-12-13 박형만 신규 작성 
- 2014-02-24 박형만 회원검색시 다음 email 두가지 같은 메일로 인식하도록 
- 2015-09-03 박형만 휴면계정과 JOIN 
- 2021-02-09 김영민 SNS회원추가  CUS_SNS_INFO
===================================================================================*/
CREATE PROC [dbo].[SP_CUS_MEMBER_SEARCH]
	@CUS_ID VARCHAR(20),
	@IPIN_DUP_INFO CHAR(64),
	@CUS_NAME VARCHAR(20),
	@BIRTH_DATE DATETIME ,
	@GENDER CHAR(1),
	@EMAIL  VARCHAR(100),
	@NOR_TEL1   VARCHAR(6),
	@NOR_TEL2   VARCHAR(5),
	@NOR_TEL3  VARCHAR(4)
AS 
BEGIN 

--DECLARE @CUS_ID VARCHAR(20)
--,@IPIN_DUP_INFO CHAR(64)
--,@CUS_NAME VARCHAR(20)
--,@BIRTH_DATE DATETIME 
--,@GENDER CHAR(1)
--,@EMAIL  VARCHAR(100)
--,@NOR_TEL1   VARCHAR(6)
--,@NOR_TEL2   VARCHAR(5)
--,@NOR_TEL3  VARCHAR(4)
--SELECT @CUS_ID ='nicemani'

--SELECT @CUS_ID = NULL  -- 유니크
--,@IPIN_DUP_INFO= NULL  -- 유니크
--,@CUS_ID = 'nicemani' 
--,@CUS_NAME = '박형만' 
--,@BIRTH_DATE ='1980-07-08' 
--,@GENDER = 'M'
--,@EMAIL = ''
--,@NOR_TEL1 = '010'
--,@NOR_TEL2 = '9185'
--,@NOR_TEL3 = '2481'

	DECLARE @STR_WHERE VARCHAR(1000)
	SET @STR_WHERE = '' 

	DECLARE @SQLSTRING NVARCHAR(4000)
	DECLARE @PARMDEFINITION NVARCHAR(1000)

	IF(ISNULL(@CUS_ID,'') <>'')
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.CUS_ID = @CUS_ID '
	END 
	IF(ISNULL(@IPIN_DUP_INFO,'') <>'')
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.IPIN_DUP_INFO = @IPIN_DUP_INFO '
	END 
	IF(ISNULL(@CUS_NAME,'') <>'')
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.CUS_NAME = @CUS_NAME '
	END 
	IF(@BIRTH_DATE IS NOT NULL) 
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.BIRTH_DATE = @BIRTH_DATE '
	END 
	IF(ISNULL(@GENDER,'') <>'') 
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.GENDER = @GENDER '
	END 
	IF(ISNULL(@EMAIL,'') <>'') 
	BEGIN
		--예외. daum.net hanmail.net 같은 메일로 인식하도록
		IF( CHARINDEX('@daum.net',@EMAIL) > 0 OR  CHARINDEX('@hanmail.net',@EMAIL) > 0 )
		BEGIN
			DECLARE @EMAIL_ID VARCHAR(50)
			SET @EMAIL_ID = SUBSTRING(@EMAIL , 0 , CHARINDEX('@',@EMAIL) )
			
			SET @STR_WHERE = @STR_WHERE +' 
			AND A.EMAIL IN ('''+@EMAIL_ID  +'@daum.net'','''+@EMAIL_ID  +'@hanmail.net'') '
		END 
		ELSE 
		BEGIN
			SET @STR_WHERE = @STR_WHERE +' 
			AND A.EMAIL = @EMAIL '
		END  

		
	END 
	IF(ISNULL(@NOR_TEL1,'') <>'') 
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.NOR_TEL1 = @NOR_TEL1 '
	END 
	IF(ISNULL(@NOR_TEL2,'') <>'') 
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.NOR_TEL2 = @NOR_TEL2 '
	END 
	IF(ISNULL(@NOR_TEL3,'') <>'') 
	BEGIN
		SET @STR_WHERE = @STR_WHERE +' 
		AND A.NOR_TEL3 = @NOR_TEL3 '
	END 

	SET @SQLSTRING = N'
SELECT 
	A.CUS_NO, A.CUS_ID, A.CUS_PASS, A.CUS_STATE, A.CUS_NAME, A.LAST_NAME, A.FIRST_NAME, A.NICKNAME, A.EMAIL, A.GENDER, A.SOC_NUM1, A.NOR_TEL1, A.NOR_TEL2, A.NOR_TEL3, 
	A.COM_TEL1, A.COM_TEL2, A.COM_TEL3, A.HOM_TEL1, A.HOM_TEL2, A.HOM_TEL3, A.FAX_TEL1, A.FAX_TEL2, A.FAX_TEL3, A.VISA_YN, A.PASS_YN, A.PASS_NUM, A.PASS_EXPIRE, A.PASS_ISSUE, 
	A.[NATIONAL], A.FOREIGNER_YN, A.CUS_GRADE, A.ETC, A.ETC2, A.BIRTHDAY, A.LUNAR_YN, A.RCV_EMAIL_YN, A.RCV_SMS_YN, A.ADDRESS1, A.ADDRESS2, A.ZIP_CODE, A.NEW_CODE,
	A.EDT_MESSAGE, A.OLD_YN, CU_YY, A.CU_SEQ, A.RCV_DM_YN, A.POINT_CONSENT, A.POINT_CONSENT_DATE, A.VSOC_NUM, 
	A.IPIN_DUP_INFO, A.IPIN_CONN_INFO, A.IPIN_ACC_DATE, A.TERMS2_YN, A.TERMS3_YN, A.SAFE_ID, A.BIRTH_DATE, A.OCB_AGREE_YN, A.OCB_AGREE_DATE, A.OCB_AGREE_EMP_CODE, A.OCB_CARD_NUM, 
	A.JOIN_TYPE, A.CERT_YN, A.LAST_LOGIN_DATE, A.SNS_MEM_YN, B.SNS_COMPANY
FROM CUS_MEMBER A WITH(NOLOCK) LEFT JOIN CUS_SNS_INFO B WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO
WHERE A.CUS_STATE = ''Y''
AND A.CUS_ID IS NOT NULL AND A.CUS_ID <> '''' ' 
	+ @STR_WHERE 

	SET @SQLSTRING = @SQLSTRING + N'
UNION ALL 
SELECT 
	A.CUS_NO, A.CUS_ID, A.CUS_PASS, A.CUS_STATE, A.CUS_NAME, A.LAST_NAME, A.FIRST_NAME, A.NICKNAME, A.EMAIL, A.GENDER, A.SOC_NUM1, A.NOR_TEL1, A.NOR_TEL2, A.NOR_TEL3, 
	A.COM_TEL1, A.COM_TEL2, A.COM_TEL3, A.HOM_TEL1, A.HOM_TEL2, A.HOM_TEL3, A.FAX_TEL1, A.FAX_TEL2, A.FAX_TEL3, A.VISA_YN, A.PASS_YN, A.PASS_NUM, A.PASS_EXPIRE, A.PASS_ISSUE, 
	A.[NATIONAL], A.FOREIGNER_YN, A.CUS_GRADE, A.ETC, A.ETC2, A.BIRTHDAY, A.LUNAR_YN, A.RCV_EMAIL_YN, A.RCV_SMS_YN, A.ADDRESS1, A.ADDRESS2, A.ZIP_CODE, A.NEW_CODE,
	A.EDT_MESSAGE, A.OLD_YN, CU_YY, A.CU_SEQ, A.RCV_DM_YN, A.POINT_CONSENT, A.POINT_CONSENT_DATE, A.VSOC_NUM, 
	A.IPIN_DUP_INFO, A.IPIN_CONN_INFO, A.IPIN_ACC_DATE, A.TERMS2_YN, A.TERMS3_YN, A.SAFE_ID, A.BIRTH_DATE, A.OCB_AGREE_YN, A.OCB_AGREE_DATE, A.OCB_AGREE_EMP_CODE, A.OCB_CARD_NUM, 
	A.JOIN_TYPE, A.CERT_YN, A.LAST_LOGIN_DATE, A.SNS_MEM_YN, B.SNS_COMPANY
FROM CUS_MEMBER_SLEEP A WITH(NOLOCK) LEFT JOIN CUS_SNS_INFO B WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO
WHERE A.CUS_STATE = ''Y''
AND A.CUS_ID IS NOT NULL AND A.CUS_ID <> '''' ' 
	+ @STR_WHERE 

	SET @PARMDEFINITION = N'@CUS_ID VARCHAR(20)
	,@IPIN_DUP_INFO CHAR(64)
	,@CUS_NAME VARCHAR(20)
	,@BIRTH_DATE DATETIME 
	,@GENDER CHAR(1)
	,@EMAIL  VARCHAR(100)
	,@NOR_TEL1   VARCHAR(6)
	,@NOR_TEL2   VARCHAR(5)
	,@NOR_TEL3  VARCHAR(4)'
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @CUS_ID 
	,@IPIN_DUP_INFO 
	,@CUS_NAME  
	,@BIRTH_DATE 
	,@GENDER 
	,@EMAIL   
	,@NOR_TEL1  
	,@NOR_TEL2   
	,@NOR_TEL3  

	PRINT @SQLSTRING
END 
GO
