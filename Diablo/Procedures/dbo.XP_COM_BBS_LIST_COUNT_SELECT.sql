USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: XP_COM_BBS_LIST_COUNT_SELECT
■ DESCRIPTION				: BTMS 온라인 상담 게시판 분류별 카운트 검색
■ INPUT PARAMETER			: 
	@KEY		VARCHAR(400): 검색 키  
■ EXEC						: 
	exec XP_COM_BBS_LIST_COUNT_SELECT N'MasterSeq=4&ResCode=RP1606028009&isErp=Y'
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-04-09		이유라			최초생성
   2016-05-26		이유라			답변완료된 상담건 제외
   2016-07-18		이유라			res_code로 넘길시 bt_code단위로 검색
================================================================================================================*/ 

CREATE  PROCEDURE [dbo].[XP_COM_BBS_LIST_COUNT_SELECT]
(
	@KEY		VARCHAR(400)
)

AS  

BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000),  @WHERE NVARCHAR(4000);
	DECLARE @MASTER_SEQ INT,@AGT_CODE VARCHAR(20), @BOARD_TYPE VARCHAR(1), @CATEGORY_SEQ INT, @NATION_CODE VARCHAR(30), @REGION_CODE VARCHAR(30), 
			@BT_CODE varchar(20), @MASTER_CODE VARCHAR(10), @SEARCH_TYPE VARCHAR(1), @SEARCH_TEXT VARCHAR(100), @NOTICE_YN VARCHAR(1), @DEL_YN VARCHAR(1), @EMPLOYEE_YN VARCHAR(1), 
			@NTYPE VARCHAR(1), @ERP_YN VARCHAR(1), @START_DATE VARCHAR(20), @END_DATE VARCHAR(20), @REQUEST_SEQ INT, @COMMENT_COUNT VARCHAR(1), @RES_CODE VARCHAR(20),@MANAGER_EMP_CODE VARCHAR(7);
	DECLARE @TOTAL_COUNT INT, @AIR_TOTAL_COUNT INT, @HOTEL_TOTAL_COUNT INT, @VISA_TOTAL_COUNT INT, @CAR_TOTAL_COUNT INT, @ETCRESV_TOTAL_COUNT INT, @ETC_TOTAL_COUNT INT;

	SELECT
		@MASTER_SEQ = CONVERT(INT, DBO.FN_PARAM(@KEY, 'MasterSeq')),
		@BOARD_TYPE = DBO.FN_PARAM(@KEY, 'BoardType'),
		@AGT_CODE = DBO.FN_PARAM(@KEY, 'AgtCode'),
		@CATEGORY_SEQ = CONVERT(INT, DBO.FN_PARAM(@KEY, 'CategorySeq')),
		@NATION_CODE = DBO.FN_PARAM(@KEY, 'NationCode'),
		@REGION_CODE = DBO.FN_PARAM(@KEY, 'RegionCode'),
		@BT_CODE = DBO.FN_PARAM(@KEY, 'BtCode'),
		@MASTER_CODE = DBO.FN_PARAM(@KEY, 'MasterCode'),
		@SEARCH_TYPE = DBO.FN_PARAM(@KEY, 'SearchType'),
		@SEARCH_TEXT = DBO.FN_PARAM(@KEY, 'SearchText'),
		@NOTICE_YN = DBO.FN_PARAM(@KEY, 'IsNotice'),
		@DEL_YN = DBO.FN_PARAM(@KEY, 'IsDel'),
		@NTYPE = DBO.FN_PARAM(@KEY, 'Ntype'),
		@ERP_YN = DBO.FN_PARAM(@KEY, 'isErp'),
		@START_DATE = DBO.FN_PARAM(@KEY, 'StartDate'),
		@END_DATE = DBO.FN_PARAM(@KEY, 'EndDate'),
		@REQUEST_SEQ = DBO.FN_PARAM(@KEY, 'RequestSeq'),
		@COMMENT_COUNT = DBO.FN_PARAM(@KEY, 'CommentCount'),
		@RES_CODE = DBO.FN_PARAM(@KEY, 'ResCode'),
		@MANAGER_EMP_CODE = DBO.FN_PARAM(@KEY, 'ManagerEmpCode');

	IF(@RES_CODE <> '')
	BEGIN 
		SET @BT_CODE = ( SELECT BT_CODE FROM COM_BIZTRIP_DETAIL WITH(NOLOCK) WHERE RES_CODE = @RES_CODE )
	END

	IF @NOTICE_YN = 'Y'
	BEGIN
		SELECT
			@WHERE = 'WHERE A.MASTER_SEQ = @MASTER_SEQ AND A.NOTICE_YN = ''N'' AND A.DEL_YN = ''N'' '
	END
	ELSE
	BEGIN
		SELECT
			@WHERE = 'WHERE A.MASTER_SEQ = @MASTER_SEQ AND A.PARENT_SEQ = A.BOARD_SEQ AND A.NOTICE_YN = ''N'' AND A.DEL_YN = ''N'''
	END

	IF (@ERP_YN <> '' AND @NOTICE_YN = 'Y')
	BEGIN
		SELECT
			@WHERE = 'WHERE A.MASTER_SEQ = @MASTER_SEQ AND A.NOTICE_YN = ''Y'' AND A.DEL_YN = ''N'' '
	END

	-- ERP, 상담게시판의 경우 삭제된것도 노출
	IF(@ERP_YN <> '' AND @MASTER_SEQ = '4')
	BEGIN
		SELECT
			@WHERE = 'WHERE A.MASTER_SEQ = @MASTER_SEQ AND A.PARENT_SEQ = A.BOARD_SEQ AND A.NOTICE_YN = ''N'' '
	END

	-- 공지사항만 TOP으로 끊어오는 경우 사용
	IF @NTYPE <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND A.NOTICE_YN = @NTYPE'
	END
	IF @DEL_YN <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND A.DEL_YN = @DEL_YN  WHERE AA.MASTER_SEQ = A.MASTER_SEQ AND AA.BOARD_SEQ = A.PARENT_SEQ AND AA.DEL_YN = @DEL_YN))'
	END

	IF @CATEGORY_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND A.CATEGORY_SEQ = @CATEGORY_SEQ'
	END

	IF @REQUEST_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND A.REQUEST_SEQ = @REQUEST_SEQ'
	END

	IF @COMMENT_COUNT <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND ISNULL((SELECT STATUS FROM COM_BBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = A.MASTER_SEQ AND PARENT_SEQ = A.BOARD_SEQ AND BOARD_SEQ <> PARENT_SEQ),1) = @COMMENT_COUNT '
	END

	IF @MASTER_CODE <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND A.MASTER_CODE = @MASTER_CODE'
	END

	IF @NATION_CODE <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND NATION_CODE = @NATION_CODE'
	END

	IF @REGION_CODE <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND REGION_CODE = @REGION_CODE'
	END

	IF @BT_CODE <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND BT_CODE = @BT_CODE'
	END

	IF (@AGT_CODE <>'')
	BEGIN
		SET @WHERE = @WHERE + ' AND A.AGT_CODE = @AGT_CODE  '
	END

	IF LEN(@START_DATE) > 0
	BEGIN
		SELECT @WHERE = @WHERE + (  
			' AND A.NEW_DATE >= CONVERT(DATETIME, @START_DATE, 121)  '
		)
	END

	IF LEN(@END_DATE) > 0
	BEGIN
		SELECT @WHERE = @WHERE + (  
			' AND A.NEW_DATE < CONVERT(DATETIME, @END_DATE, 121) '
		)
	END

	IF(@MANAGER_EMP_CODE <> '')
	BEGIN 
		SELECT @WHERE = @WHERE + (  
			' AND  A.AGT_CODE IN  ( SELECT AGT_CODE FROM COM_MANAGER WHERE EMP_CODE = @MANAGER_EMP_CODE )  '
		)
	END

	IF (@SEARCH_TYPE <> '' AND @SEARCH_TEXT <> '')
	BEGIN
		--IF (@SEARCH_TYPE <> 4 AND CHARINDEX(' ', @SEARCH_TEXT) > 0)
		IF @SEARCH_TYPE <> 4 AND  @SEARCH_TYPE <> 5
			SELECT @SEARCH_TEXT = STUFF((SELECT (' AND "' + Data + '"') AS [text()] FROM [dbo].[FN_SPLIT](@SEARCH_TEXT, ' ') FOR XML PATH('')), 1, 5, '')
		-- 제목내용 = 1, 제목 = 2, 내용 = 3, 작성자 = 4, 출장번호 = 5
		SET @WHERE = @WHERE + (
			CASE @SEARCH_TYPE
				WHEN '1' THEN ' AND CONTAINS((A.CONTENTS, A.SUBJECT), @SEARCH_TEXT)'
				WHEN '2' THEN ' AND CONTAINS(A.SUBJECT, @SEARCH_TEXT)'
				WHEN '3' THEN ' AND CONTAINS(A.CONTENTS, @SEARCH_TEXT)'
				WHEN '4' THEN ' AND (A.NEW_SEQ IN (SELECT EMP_sEQ FROM COM_EMPLOYEE AA WHERE AA.KOR_NAME LIKE ''%'' + @SEARCH_TEXT + ''%'' AND A.AGT_CODE = AA.AGT_CODE)) '
				WHEN '5' THEN ' AND A.BT_CODE =  @SEARCH_TEXT '
				ELSE ' AND CONTAINS(A.SUBJECT, @SEARCH_TEXT)'
			END
		)
	END

	--완료된것 무조건 카운트에서 제외
	SET @WHERE = @WHERE + ' AND ISNULL((SELECT STATUS FROM COM_BBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = A.MASTER_SEQ AND PARENT_SEQ = A.BOARD_SEQ AND BOARD_SEQ <> PARENT_SEQ),1) != 3  '

	SET @SQLSTRING = N'

	SELECT TOTAL_COUNT = COUNT(*),
		SUM(CASE WHEN A.CATEGORY_SEQ = ''1'' THEN 1 ELSE 0 END) AS AIR_TOTAL_COUNT,
		SUM(CASE WHEN A.CATEGORY_SEQ = ''2'' THEN 1 ELSE 0 END) AS HOTEL_TOTAL_COUNT,
		SUM(CASE WHEN A.CATEGORY_SEQ = ''3'' THEN 1 ELSE 0 END) AS VISA_TOTAL_COUNT,
		SUM(CASE WHEN A.CATEGORY_SEQ = ''4'' THEN 1 ELSE 0 END) AS CAR_TOTAL_COUNT,
		SUM(CASE WHEN A.CATEGORY_SEQ = ''5'' THEN 1 ELSE 0 END) AS ETCRESV_TOTAL_COUNT,
		SUM(CASE WHEN A.CATEGORY_SEQ = ''6'' THEN 1 ELSE 0 END) AS ETC_TOTAL_COUNT
	FROM COM_BBS_DETAIL A WITH(NOLOCK)
	LEFT JOIN COM_MANAGER B ON A.AGT_CODE = B.AGT_CODE AND A.CATEGORY_SEQ = B.MANAGER_TYPE
	' + @WHERE + N''

	--PRINT @SQLSTRING

	SET @PARMDEFINITION = N'
		@TOTAL_COUNT INT,
		@MASTER_SEQ INT,
		@CATEGORY_SEQ INT,
		@NATION_CODE VARCHAR(30),
		@REGION_CODE VARCHAR(30),
		@BT_CODE VARCHAR(20),
		@MASTER_CODE VARCHAR(10),
		@AGT_CODE VARCHAR(20),
		@SEARCH_TEXT VARCHAR(100),
		@DEL_YN VARCHAR(1),
		@NTYPE VARCHAR(1),
		@ERP_YN VARCHAR(1),
		@START_DATE VARCHAR(20),
		@END_DATE VARCHAR(20),
		@REQUEST_SEQ INT,
		@COMMENT_COUNT VARCHAR(1),
		@AIR_TOTAL_COUNT INT,
		@HOTEL_TOTAL_COUNT INT,
		@VISA_TOTAL_COUNT INT,
		@CAR_TOTAL_COUNT INT,
		@ETCRESV_TOTAL_COUNT INT,
		@ETC_TOTAL_COUNT INT,
		@MANAGER_EMP_CODE VARCHAR(7)';

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@TOTAL_COUNT,
		@MASTER_SEQ,
		@CATEGORY_SEQ,
		@NATION_CODE,
		@REGION_CODE,
		@BT_CODE,
		@MASTER_CODE,
		@AGT_CODE,
		@SEARCH_TEXT,
		@DEL_YN,
		@NTYPE,
		@ERP_YN,
		@START_DATE,
		@END_DATE,
		@REQUEST_SEQ,
		@COMMENT_COUNT,
		@AIR_TOTAL_COUNT,
		@HOTEL_TOTAL_COUNT,
		@VISA_TOTAL_COUNT,
		@CAR_TOTAL_COUNT,
		@ETCRESV_TOTAL_COUNT,
		@ETC_TOTAL_COUNT,
		@MANAGER_EMP_CODE;
END




GO
