USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
-- =============================================
-- Author:		<김성호>
-- Create date: <2009-07-20>
-- Description:	<행사 상세일정 마스터 수정>
-- 
-- 2019-03-15	박형만	네이버 관련 컬럼 추가 
-- =============================================
CREATE PROCEDURE [dbo].[SP_PKG_DETAIL_UPDATE_NEW_SCHEDULE]
	@PRO_CODE		VARCHAR(20),
	@SCH_SEQ		INT,
	@SCH_NAME		NVARCHAR(50),
	@FLAG			CHAR(1)
AS
BEGIN
	IF @FLAG = 'I'					-- 신규
	BEGIN
	
		DECLARE @NEW_SCH_SEQ INT
		SELECT @NEW_SCH_SEQ = ISNULL(MAX(SCH_SEQ), 0) + 1 FROM PKG_DETAIL_SCH_MASTER   WHERE PRO_CODE = @PRO_CODE
	
		IF EXISTS(SELECT 1 FROM PKG_DETAIL_SCH_MASTER   WHERE PRO_CODE = @PRO_CODE AND SCH_SEQ = @SCH_SEQ)
		BEGIN
			-- 행사일정 마스터 저장
			INSERT INTO PKG_DETAIL_SCH_MASTER VALUES (@PRO_CODE, @NEW_SCH_SEQ, @SCH_NAME)

			-- 행사일정 일자 저장
			INSERT INTO PKG_DETAIL_SCH_DAY
			SELECT @PRO_CODE, @NEW_SCH_SEQ, DAY_SEQ, DAY_NUMBER,FREE_SCH_YN FROM PKG_DETAIL_SCH_DAY   -- 19.03 자유일정유무추가 
			WHERE PRO_CODE = @PRO_CODE AND SCH_SEQ = @SCH_SEQ

			-- 행사일정 도시 저장
			INSERT INTO PKG_DETAIL_SCH_CITY
			SELECT @PRO_CODE, @NEW_SCH_SEQ, DAY_SEQ, CITY_SEQ, CITY_CODE, MAINCITY_YN, CITY_SHOW_ORDER FROM PKG_DETAIL_SCH_CITY  
			WHERE PRO_CODE = @PRO_CODE AND SCH_SEQ = @SCH_SEQ

			-- 행사일정 컨텐츠 저장
			INSERT INTO PKG_DETAIL_SCH_CONTENT
			SELECT @PRO_CODE, @NEW_SCH_SEQ, DAY_SEQ, CITY_SEQ, CNT_SEQ, CNT_CODE, CNT_INFO, CNT_SHOW_ORDER FROM PKG_DETAIL_SCH_CONTENT  
			WHERE PRO_CODE = @PRO_CODE AND SCH_SEQ = @SCH_SEQ
		END
		ELSE
		BEGIN
			-- 행사일정 마스터 저장
			INSERT INTO PKG_DETAIL_SCH_MASTER VALUES (@PRO_CODE, @NEW_SCH_SEQ, @SCH_NAME)
			
			DECLARE @TOURDAY INT, @SCHEDULEDAY INT
			SELECT @TOURDAY = ISNULL(TOUR_DAY, 0), @SCHEDULEDAY = 0 FROM PKG_DETAIL   WHERE PRO_CODE = @PRO_CODE
			
			WHILE (@SCHEDULEDAY < @TOURDAY)
			BEGIN
				SET @SCHEDULEDAY = @SCHEDULEDAY + 1
				INSERT INTO PKG_DETAIL_SCH_DAY(PRO_CODE,SCH_SEQ,DAY_SEQ,DAY_NUMBER) -- 컬럼 명시 19.03 
				VALUES (@PRO_CODE, @NEW_SCH_SEQ, @SCHEDULEDAY, @SCHEDULEDAY);
			END
		END
		
		-- 일정 마스터 순번 리턴
		SELECT @NEW_SCH_SEQ
	END
	ELSE										-- 수정
	BEGIN
		UPDATE PKG_DETAIL_SCH_MASTER SET SCH_NAME = @SCH_NAME WHERE PRO_CODE = @PRO_CODE AND SCH_SEQ = @SCH_SEQ
		-- 일정 마스터 순번 리턴
		SELECT @SCH_SEQ
	END
END



GO
