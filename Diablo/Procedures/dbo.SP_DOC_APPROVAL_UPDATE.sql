USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROCEDURE [dbo].[SP_DOC_APPROVAL_UPDATE]
	@EDI_CODE		CHAR(10),
	@APP_CODE		CHAR(7),
	@APP_TYPE		CHAR(1)
AS
BEGIN
	SET NOCOUNT ON;

	DECLARE @RETURN INT

	DECLARE @APP_STATUS CHAR(1)
	
	-- 결재 타입별 시작 체크
	IF EXISTS(SELECT * FROM EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_TYPE = @APP_TYPE)
	BEGIN
		SELECT @APP_STATUS = APP_STATUS
		FROM EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_CODE = @APP_CODE AND APP_TYPE = @APP_TYPE
	END
	ELSE
	BEGIN
		SELECT @APP_STATUS = APP_STATUS
		FROM EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_CODE = @APP_CODE
	END

	-- 현재 입력자가 이전 결재자인지 현재 결재자인지 체크

	-- 현재결재자
	IF @APP_STATUS = 'N'
	BEGIN
		SET @RETURN = 1

		UPDATE EDI_APPROVAL SET APP_STATUS = 'Y', APP_DATE = GETDATE()
		WHERE EDI_CODE = @EDI_CODE AND APP_CODE = @APP_CODE AND APP_TYPE = @APP_TYPE
	END

	-- 이전결재자 및 작성자일 경우는 무조건 반송이기 때문에 다음 결재자를 삭제한다.

	-- 문서를 읽지않은 결재자가 있을때...
	IF EXISTS(SELECT 1 FROM EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_STATUS = 'N' AND READ_YN = 'N' AND APP_TYPE = @APP_TYPE)
		OR NOT EXISTS(SELECT 1 FROM EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_TYPE = @APP_TYPE)
	BEGIN
		IF @APP_STATUS = 'Y'		-- 이전결재자
		BEGIN
			SET @RETURN = 2

			DELETE EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_STATUS = 'N' AND APP_TYPE = @APP_TYPE
		END
		ELSE IF EXISTS(SELECT 1 FROM EDI_MASTER_DAMO WHERE EDI_CODE = @EDI_CODE AND NEW_CODE = @APP_CODE)
		BEGIN						-- 작성자일때
			SET @RETURN = 3

			DELETE EDI_APPROVAL WHERE EDI_CODE = @EDI_CODE AND APP_STATUS = 'N' AND APP_TYPE = @APP_TYPE
		END
	END

	SELECT @RETURN
END

GO
