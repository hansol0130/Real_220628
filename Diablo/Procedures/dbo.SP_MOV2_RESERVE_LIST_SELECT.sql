USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: SP_MOV2_RESERVE_LIST_SELECT
■ DESCRIPTION				: 검색_Mov2_예약목록
■ INPUT PARAMETER			: CUS_NO, SELECT_TYPE, PAGE_INDEX, PAGE_SIZE
■ SELECT_TYPE				: 1: 전체, 2: 진행중인 예약	
■ EXEC						: 
    -- exec SP_MOV2_RESERVE_LIST_SELECT '1111111', 2, '0', '20'

■ MEMO						: 예약목록을 가져오기.
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			        DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2017-07-09		오준욱(IBSOLUTION)		최초생성
================================================================================================================*/ 
CREATE PROCEDURE [dbo].[SP_MOV2_RESERVE_LIST_SELECT]
	@CUS_NO			INT,
	@SELECT_TYPE	INT,
	@PAGE_INDEX		INT,
	@PAGE_SIZE		INT
AS
BEGIN

	DECLARE @MEM_YN VARCHAR(1);  --정회원 여부 
	DECLARE @SQLSTRING NVARCHAR(MAX), @ADD_STRING1 VARCHAR(100), @ADD_STRING2 VARCHAR(100), @PARMDEFINITION NVARCHAR(1000);

	--회원정보 조회 
	IF EXISTS ( SELECT * FROM CUS_MEMBER  WITH(NOLOCK) WHERE CUS_NO = @CUS_NO AND CUS_STATE = 'Y'  )
		BEGIN
			SET @MEM_YN = 'Y' --정회원
		END 
	ELSE 
		BEGIN
			SET @MEM_YN = 'N' --비회원
		END 

	IF @SELECT_TYPE = 2
		BEGIN
			SET @ADD_STRING1 = ', P1.NEW_DATE DESC'
			SET @ADD_STRING2 = 'AND P1.ONGOING = 0'
		END
	ELSE
		BEGIN
			SET @ADD_STRING1 = ''
			SET @ADD_STRING2 = ''
		END


	SET @SQLSTRING = '

		SELECT * FROM (	
			SELECT ROW_NUMBER() OVER (ORDER BY P1.ONGOING DESC ' + @ADD_STRING1 + '  
				) AS ROWNUM, * FROM ( 	
	
				SELECT 
					(CASE WHEN A.RES_STATE IN(7,8,9) THEN -2 
							WHEN IIF(A.ARR_DATE IS NULL, IIF(C.ARR_DATE IS NULL, GETDATE()-1, C.ARR_DATE), A.ARR_DATE) < GETDATE()-1 THEN -1  ELSE 0 END ) ONGOING,

					RES.*, 
					A.PRO_CODE, A.PRO_NAME AS RES_MASTER_PRO_NAME,
					A.SALE_COM_CODE , 
					dbo.FN_RES_GET_RES_AGE_COUNT(RES.RES_CODE, 0) AS ADT_COUNT, 
					dbo.FN_RES_GET_RES_AGE_COUNT(RES.RES_CODE, 1) AS CHD_COUNT, 
					dbo.FN_RES_GET_RES_AGE_COUNT(RES.RES_CODE, 2) AS INF_COUNT,
					C.TRANSFER_TYPE,
					D.ATT_CODE,			
					K.FILENAME FREE_FILENAME,
					H.CFM_YN AS CFM_YN,  --여행자 계약서 동의여부	  
					H.CFM_DATE AS CFM_DATE,
					C.PRO_NAME, C.TOUR_DAY, C.TOUR_NIGHT, C.DEP_DATE AS DEP_DATE2, C.ARR_DATE AS ARR_DATE2,
					E.FILE_CODE, E.REGION_CODE, E.NATION_CODE, E.STATE_CODE, E.CITY_CODE, E.FILE_NAME_L, E.FILE_NAME_M, E.FILE_NAME_S, E.FILE_TYPE , 
		
					(SELECT TOP 1 (
						''/CONTENT/'' + BB.REGION_CODE + ''/'' + BB.NATION_CODE + ''/''  
						+ RTRIM(BB.STATE_CODE) + ''/'' + BB.CITY_CODE + ''/IMAGE/'' + (  
						CASE WHEN BB.FILE_NAME_M <> '''' THEN BB.FILE_NAME_M ELSE BB.FILE_NAME_S  END))  
						FROM INF_FILE_MANAGER AA WITH(NOLOCK) 
						INNER JOIN INF_FILE_MASTER BB WITH(NOLOCK) ON BB.FILE_CODE = AA.FILE_CODE AND BB.FILE_TYPE = 1  
						WHERE AA.CNT_CODE = I.CNT_CODE  
					) AS HTL_IMG_URL,
				
					CASE WHEN A.PRO_TYPE = 1 THEN J.DEP_TRANS_CODE ELSE F.AIRLINE_CODE END AIRLINE_CODE, 
					CASE WHEN A.PRO_TYPE = 2 THEN 
						(SELECT TOP 1 OP_AIRLINE_CODE FROM RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE AND OP_AIRLINE_CODE IS NOT NULL) ELSE NULL END  AS  OP_AIRLINE_CODE, 
					CASE WHEN A.PRO_TYPE = 2 THEN 
						(SELECT KOR_NAME FROM PUB_AIRLINE WHERE AIRLINE_CODE = (SELECT TOP 1 OP_AIRLINE_CODE FROM RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE AND OP_AIRLINE_CODE IS NOT NULL)) ELSE NULL END  AS  OP_AIRLINE_NAME, 
					(SELECT KOR_NAME FROM PUB_AIRLINE WHERE AIRLINE_CODE = (CASE WHEN A.PRO_TYPE = 1 THEN J.DEP_TRANS_CODE ELSE F.AIRLINE_CODE END)) AS AIRLINE_NAME, 
					CASE WHEN A.PRO_TYPE = 2 THEN 
						(SELECT TOP 1 FLIGHT FROM RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE ) ELSE J.DEP_TRANS_NUMBER END  AS  FLIGHT_NUMBER, 
	
					CASE WHEN A.PRO_TYPE = 2 THEN 
						(SELECT SUM(CASE WHEN SEAT_STATUS = ''HK'' THEN 1 ELSE 0 END) - COUNT(*) FROM RES_SEGMENT  WITH(NOLOCK) 
						WHERE RES_CODE = A.RES_CODE ) ELSE 0 END AS HK_COUNT --좌석별 확약건수 - 전체좌석 (0건이면 확정)	
		
				FROM RES_MASTER_damo A WITH(NOLOCK)
					INNER JOIN ( 
						SELECT * FROM ( 
							SELECT A.RES_STATE, A.DEP_DATE, A.ARR_DATE, A.PRO_TYPE , A.RES_CODE , ''Y'' AS MASTER_YN , A.NEW_DATE , A.RES_NAME AS RES_NAME, ''N'' AS ADULT_YN, 
							ISNULL( (SELECT TOP 1 SEQ_NO FROM RES_CUSTOMER_DAMO A1 WITH(NOLOCK) WHERE A1.RES_CODE = A.RES_CODE AND A1.CUS_NO = A.CUS_NO ), 0 ) AS SEQ_NO,
							(SELECT COUNT(*) FROM RES_CUSTOMER_DAMO A1 WITH(NOLOCK) WHERE A1.RES_CODE = A.RES_CODE AND A1.CUS_NO = A.CUS_NO ) AS RND
							FROM RES_MASTER_damo A WITH(NOLOCK) 
							WHERE CUS_NO = ' + CONVERT(varchar(20), @CUS_NO) + '
							-- AND SUBSTRING(A.RES_CODE,2,1)  IN ( ''P'',''T'',''H'')  -- 패키지, 자유여행, 호텔		
							AND A.VIEW_YN =''Y'' --노출여부
							-- AND A.RES_STATE < 7
							AND (( ''' + @MEM_YN + ''' = ''N'' AND A.DEP_DATE > DATEADD(DD,-30,GETDATE()) ) OR ''' + @MEM_YN + ''' = ''Y'' )  --비회원 출발일 30일 지난것은 표시 안함 
							UNION ALL 
							SELECT A.RES_STATE, A.DEP_DATE, A.ARR_DATE, A.PRO_TYPE , A.RES_CODE , ''N'' AS MASTER_YN , A.NEW_DATE , B.CUS_NAME AS RES_NAME, IIF(B.AGE_TYPE = 0, ''Y'', ''N'') AS ADULT_YN, B.SEQ_NO AS SEQ_NO, 0 AS RND
							FROM RES_MASTER_damo A WITH(NOLOCK) 
								INNER JOIN RES_CUSTOMER_DAMO B WITH(NOLOCK) 
									ON A.RES_CODE = B.RES_CODE 
									AND A.CUS_NO  <> B.CUS_NO
							WHERE B.CUS_NO = ' + CONVERT(varchar(20), @CUS_NO) + '
							-- AND SUBSTRING(A.RES_CODE,2,1)  IN ( ''P'',''T'',''H'') -- 패키지, 자유여행, 호텔						
							AND B.RES_STATE = 0  -- 정상출발자만 
							AND B.VIEW_YN = ''Y'' -- 노출여부 
							-- AND A.RES_STATE < 7
							AND (( ''' + @MEM_YN + ''' = ''N'' AND A.DEP_DATE > DATEADD(DD,-30,GETDATE()) ) OR ''' + @MEM_YN + ''' = ''Y'' )  --비회원 출발일 30일 지난것은 표시 안함 
						) TBL 
					) RES ON A.RES_CODE = RES.RES_CODE 
			
					LEFT JOIN PKG_DETAIL  C  WITH(NOLOCK)
						ON A.PRO_CODE = C.PRO_CODE 
			
					LEFT JOIN PKG_MASTER D WITH(NOLOCK) 
						ON A.MASTER_CODE = D.MASTER_CODE		
			
					LEFT JOIN INF_FILE_MASTER E WITH(NOLOCK) 
						ON D.MAIN_FILE_CODE = E.FILE_CODE AND E.SHOW_YN = ''Y''		
			
					LEFT JOIN RES_AIR_DETAIL F WITH(NOLOCK) 
						ON A.RES_CODE = F.RES_CODE		
			
					LEFT JOIN  RES_CONTRACT H WITH(NOLOCK)
						ON A.RES_CODE = H.RES_CODE 
						AND H.CONTRACT_NO = (SELECT MAX(CONTRACT_NO) FROM RES_CONTRACT WHERE RES_CODE = H.RES_CODE  )
			
					LEFT JOIN HTL_MASTER I WITH(NOLOCK) 
						ON A.MASTER_CODE = I.MASTER_CODE
						
					LEFT JOIN PRO_TRANS_SEAT J WITH(NOLOCK) 
						ON C.SEAT_CODE = J.SEAT_CODE		
				
					LEFT JOIN APP_FREETOUR_GUIDEFILE K WITH(NOLOCK) 
						ON A.RES_CODE = K.RES_CODE 
			
	
				) P1
				WHERE 1= 1 ' + @ADD_STRING2 + '
			) P2
		WHERE P2.ROWNUM BETWEEN (' + CONVERT(varchar(20), @PAGE_INDEX) + ' * ' + CONVERT(varchar(20), @PAGE_SIZE) + ' + 1) AND (' + CONVERT(varchar(20), @PAGE_INDEX) + ' + 1) * ' + CONVERT(varchar(20), @PAGE_SIZE) + '

	';

	

	SET @PARMDEFINITION = N'@CUS_NO INT,@SELECT_TYPE INT,@PAGE_INDEX INT,@PAGE_SIZE INT';
	-- PRINT @SQLSTRING + ' ' + @PARMDEFINITION

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @CUS_NO, @SELECT_TYPE, @PAGE_INDEX, @PAGE_SIZE;

	-- EXEC (@SQLSTRING)

END           



GO
