USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD_DOMESTIC
■ DESCRIPTION				: 현대카드 국내 상품연동
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 								
■ EXEC						:	EXEC SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD_DOMESTIC ''
								EXEC SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD_DOMESTIC 'KPP666'
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
2016-12-14			정지용			최초생성
2017-06-15			박형만			프리비아요청 (행사노출여부)DEL_YN 필드 추가  , 행사의 SHOW_YN 조건 빼기
2017-06-16			박형만			가격여러개일때 행사가격명표시 
================================================================================================================*/ 
CREATE PROC [dbo].[SP_PKG_MASTER_SELECT_PRODUCT_HYUNDAICARD_DOMESTIC]	
	@MASTER_CODE VARCHAR(10)
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	--DECLARE @MASTER_CODE VARCHAR(10)
	--SET @MASTER_CODE = 'KPP666' 

	DECLARE @PROVIDER_HYUNDAICARD INT 
	SET @PROVIDER_HYUNDAICARD = 29;

	/* 모상품 정보 */
	WITH MASTER_LIST AS (
		SELECT
				A.MASTER_CODE
			,	A.MASTER_NAME AS PKG_MST_NAME
			,	A.PKG_COMMENT AS PKG_MST_DESC
			,	A.ATT_CODE
			,	ISNULL((
					SELECT TOP 1 
						CASE WHEN PTS.ARR_DEP_AIRPORT_CODE <> '' AND (PD.TRANSFER_TYPE = 1 OR PD.TRANSFER_TYPE = 3) THEN 1 -- 1: 항공 / 3: 기타이고 공항코드가 있을 경우에는 제주여행이라 생각함.
						ELSE 2 END AS TRANS_TYPE
					FROM PKG_DETAIL PD WITH(NOLOCK) 
						INNER JOIN PRO_TRANS_SEAT PTS WITH(NOLOCK) ON PD.SEAT_CODE = PTS.SEAT_CODE
							WHERE PD.MASTER_CODE = A.MASTER_CODE AND PD.DEP_DATE > DATEADD(D,-1,GETDATE()) AND PD.SHOW_YN = 'Y' ORDER BY PD.DEP_DATE DESC 
				), 2) AS TRANSFER_TYPE
		FROM PKG_MASTER A WITH(NOLOCK)
		INNER JOIN PKG_MASTER_AFFILIATE B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.USE_YN = 'Y'
		WHERE (SUBSTRING(B.MASTER_CODE, 1, 1) = 'K' OR B.AFF_TYPE = 7)  -- 현대카드 해외 :6 / 국내 : 7 구분
			AND A.LAST_DATE > DATEADD(D,2,GETDATE()) 
			AND A.SIGN_CODE = 'K' AND A.ATT_CODE <> 'G' AND SUBSTRING(A.MASTER_CODE, 3, 1) NOT IN ('C', 'G') -- 렌터카, 골프 제외
			AND B.PROVIDER = @PROVIDER_HYUNDAICARD 
			AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE))
	),
	MASTER_LIST_2 AS (
		SELECT 
				UPPER(A.MASTER_CODE) AS PKG_MST_CODE
			,	PKG_MST_NAME
			,	PKG_MST_DESC
			,	A.ATT_CODE
			,	ISNULL((
					SELECT TOP 1 
						TOUR_DAY 
					FROM PKG_DETAIL PD WITH(NOLOCK) 
					WHERE PD.MASTER_CODE = A.MASTER_CODE AND PD.DEP_DATE > DATEADD(D,-1,GETDATE()) AND PD.SHOW_YN = 'Y' ORDER BY PD.DEP_DATE DESC
				), '') AS TOUR_DAY
			,	DBO.FN_GET_TOUR_NIGHT(A.MASTER_CODE, 0) AS NIGHTS
			,	(
					SELECT TOP 1 CITY_NAME FROM (
						SELECT 
							ROW_NUMBER() OVER (ORDER BY AA.DAY_SEQ, AA.CITY_SHOW_ORDER ASC) AS [ROW_NUMBER]
							, UPPER(AA.MASTER_CODE) AS MASTER_CODE, CC.KOR_NAME AS [CITY_NAME], DD.KOR_NAME AS [STATE_NAME]
						FROM PKG_MASTER_SCH_CITY AA WITH(NOLOCK)
						INNER JOIN (
							SELECT MIN(SCH_SEQ) AS SCH_SEQ, MASTER_CODE FROM PKG_MASTER_SCH_CITY WITH(NOLOCK) GROUP BY MASTER_CODE
						) BB ON AA.MASTER_CODE = BB.MASTER_CODE AND AA.SCH_SEQ = BB.SCH_SEQ
						INNER JOIN PUB_CITY CC WITH(NOLOCK) ON AA.CITY_CODE = CC.CITY_CODE
						INNER JOIN PUB_STATE DD WITH(NOLOCK) ON CC.NATION_CODE = DD.NATION_CODE AND CC.STATE_CODE = DD.STATE_CODE
						WHERE AA.MASTER_CODE = A.MASTER_CODE
					) DD WHERE ROW_NUMBER = 1
			) AS DEPA_CITY 
			,	(
					SELECT TOP 1 STATE_NAME FROM (
						SELECT 
							ROW_NUMBER() OVER (ORDER BY MAINCITY_YN DESC, SCH_SEQ, AA.DAY_SEQ, AA.CITY_SHOW_ORDER ASC) AS [ROW_NUMBER]
							, UPPER(AA.MASTER_CODE) AS MASTER_CODE, CC.KOR_NAME AS [CITY_NAME], DD.KOR_NAME AS [STATE_NAME], MAINCITY_YN
						FROM PKG_MASTER_SCH_CITY AA WITH(NOLOCK)
						INNER JOIN PUB_CITY CC WITH(NOLOCK) ON AA.CITY_CODE = CC.CITY_CODE
						INNER JOIN PUB_STATE DD WITH(NOLOCK) ON CC.NATION_CODE = DD.NATION_CODE AND CC.STATE_CODE = DD.STATE_CODE
						WHERE AA.MASTER_CODE = A.MASTER_CODE
					) DD WHERE (DD.MAINCITY_YN = 'Y' AND ROW_NUMBER = 1) OR (DD.MAINCITY_YN = 'N' AND ROW_NUMBER = 2)
			) AS DEST_CITY
			,	CASE 
					WHEN A.TRANSFER_TYPE = 1 THEN
						ISNULL((
							SELECT TOP 1
								PTS.DEP_TRANS_CODE							
							FROM PKG_DETAIL PD WITH(NOLOCK)
							INNER JOIN PRO_TRANS_SEAT PTS WITH(NOLOCK) ON PD.SEAT_CODE = PTS.SEAT_CODE
							WHERE PD.MASTER_CODE = A.MASTER_CODE AND PD.DEP_DATE > DATEADD(D,-1,GETDATE()) AND PD.SHOW_YN = 'Y' ORDER BY PD.DEP_DATE DESC), '')
					ELSE 
						CASE SUBSTRING(A.MASTER_CODE, 3, 1)
							WHEN 'B' THEN 'TC' -- 버스
							WHEN 'T' THEN 'HK' -- 기차
							WHEN 'I' THEN 'HO' -- 섬여행(선박)
						ELSE 'DD' END -- 기타
					END AS TRAFFIC
			,	ISNULL(( 		
					SELECT TOP 1 
						PTS.DEP_DEP_AIRPORT_CODE
					FROM PKG_DETAIL PD WITH(NOLOCK) 
						INNER JOIN PRO_TRANS_SEAT PTS WITH(NOLOCK) ON PD.SEAT_CODE = PTS.SEAT_CODE
							WHERE PD.MASTER_CODE = A.MASTER_CODE AND PD.DEP_DATE > DATEADD(D,-1,GETDATE()) AND PD.SHOW_YN = 'Y' ORDER BY PD.DEP_DATE DESC 
				), '') AS DEPA_AIRP
			,	ISNULL(( 		
					SELECT TOP 1 
						PTS.ARR_DEP_AIRPORT_CODE
					FROM PKG_DETAIL PD WITH(NOLOCK) 
						INNER JOIN PRO_TRANS_SEAT PTS WITH(NOLOCK) ON PD.SEAT_CODE = PTS.SEAT_CODE
							WHERE PD.MASTER_CODE = A.MASTER_CODE AND PD.DEP_DATE > DATEADD(D,-1,GETDATE()) AND PD.SHOW_YN = 'Y' ORDER BY PD.DEP_DATE DESC 
				), '') AS DEST_AIRP
		FROM MASTER_LIST A				
	)
	/*
	[MasterCode] 3, 1자리 기준 분류
	B -- 버스,G -- 골프,I -- 섬,P -- 패키지,T -- 기차
	PACKAGE, AIRCARTEL, CARTEL, BUS, TRAIN, ISLAND
	*/
	SELECT 
			PKG_MST_CODE
		,	PKG_MST_NAME
		,	PKG_MST_DESC
		,	CASE 
				WHEN SUBSTRING(A.PKG_MST_CODE, 3, 1) = 'P' THEN 'GOODS001' -- 국내(국내팀과 이야기 한거임)
			ELSE 'GOODS002' END AS TRAVEL_CATEGORY -- 내륙
		,	CASE 
				WHEN SUBSTRING(A.PKG_MST_CODE, 3, 1) = 'P' AND ATT_CODE = 'P' THEN 'PACKAGE'
				WHEN SUBSTRING(A.PKG_MST_CODE, 3, 1) = 'P' AND ATT_CODE = 'F' THEN 'AIRCARTEL'
				WHEN SUBSTRING(A.PKG_MST_CODE, 3, 1) = 'B' THEN 'BUS'
				WHEN SUBSTRING(A.PKG_MST_CODE, 3, 1) = 'T' THEN 'TRAIN'
				WHEN SUBSTRING(A.PKG_MST_CODE, 3, 1) = 'I' THEN 'ISLAND'
			ELSE '' END AS MAIN_ETC_CODE
		,	TOUR_DAY
		,	NIGHTS
		,	CASE DEPA_CITY 
				WHEN '김포' THEN 'S' -- 김포공항은 서울
				WHEN '서울' THEN 'S'
				WHEN '부산' THEN 'P'
				WHEN '광주' THEN 'K'
				WHEN '청주' THEN 'C'
				WHEN '인천' THEN 'I'
				WHEN '원주' THEN 'W'
				WHEN '대구' THEN 'T'
				WHEN '무안' THEN 'M'
				WHEN '울진' THEN 'U'
				WHEN '강릉' THEN 'G'
				WHEN '동해' THEN 'D'
				WHEN '목포' THEN 'H'
				WHEN '포항' THEN 'A'
			ELSE '0'
			END AS DEPA_CITY
		,	CASE DEST_CITY
				WHEN '제주도' THEN 'C'
				WHEN '경상북도' THEN 'K'
				WHEN '경상남도' THEN 'K'
				WHEN '전라남도' THEN 'J'
				WHEN '전라북도' THEN 'J'
				WHEN '광주광역시' THEN 'J' -- 전라도속
				WHEN '대구광역시' THEN 'K' -- 경상북도
				WHEN '충청남도' THEN 'Q'
				WHEN '충청북도' THEN 'Q'
				WHEN '대전광역시' THEN 'Q' -- 충청도속
				WHEN '경기도' THEN 'G'
				WHEN '인천광역시' THEN 'G' -- 경기도속
				WHEN '부산광역시' THEN 'K' -- 경상도속
				WHEN '강원도' THEN 'W'
				WHEN '서울특별시' THEN 'S'
			ELSE DEST_CITY END AS DEST_CITY
		,	TRAFFIC
		,	LTRIM(RTRIM(DEPA_AIRP)) AS DEPA_AIRP
		,	LTRIM(RTRIM(DEST_AIRP)) AS DEST_AIRP
	FROM MASTER_LIST_2 A;

	/*==============================================================================================================================================================================================*/

	/* 자상품 정보 */
	WITH MASTER_LIST AS (
		SELECT
			UPPER(A.MASTER_CODE) AS  MASTER_CODE, A.MASTER_NAME, A.PKG_SUMMARY, A.TOUR_DAY, A.MAIN_FILE_CODE, A.ATT_CODE, A.BRAND_TYPE, B.USE_YN
		FROM PKG_MASTER A WITH(NOLOCK)
		INNER JOIN PKG_MASTER_AFFILIATE B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.USE_YN = 'Y'
		WHERE (SUBSTRING(B.MASTER_CODE, 1, 1) = 'K' OR B.AFF_TYPE = 7) -- 현대카드 해외 :6 / 국내 : 7 구분
			AND A.LAST_DATE > DATEADD(D,2,GETDATE()) 
			AND A.SIGN_CODE = 'K' AND A.ATT_CODE <> 'G' AND SUBSTRING(A.MASTER_CODE, 3, 1) NOT IN ('C', 'G') -- 렌터카, 골프 제외
			AND B.PROVIDER = @PROVIDER_HYUNDAICARD 
			AND ((@MASTER_CODE = '') OR (A.MASTER_CODE = @MASTER_CODE))
	)
	SELECT
			UPPER(A.MASTER_CODE) AS PKG_MST_CODE
		,	A.PRO_CODE + '|' + CONVERT(VARCHAR, D.PRICE_SEQ) AS PKG_CODE
		,	A.PRO_NAME +  
			CASE 
				WHEN (SELECT COUNT(*) FROM PKG_DETAIL_PRICE WHERE A.PRO_CODE = PRO_CODE ) > 1 THEN 
					'(' + D.PRICE_NAME +')' ELSE '' END  AS PKG_NAME
		,	CASE 
				WHEN B.DEP_DEP_DATE IS NOT NULL THEN 
					CONVERT(VARCHAR(8), B.DEP_DEP_DATE, 112)
				ELSE 
					CONVERT(VARCHAR(8), A.DEP_DATE, 112)
			END AS [START_DATE]
		,	CASE 
				WHEN B.DEP_DEP_DATE IS NOT NULL THEN 
					LEFT(DATENAME(WEEKDAY, B.DEP_DEP_DATE), 1)
				ELSE 
					LEFT(DATENAME(WEEKDAY, A.DEP_DATE), 1)
			END AS [START_DY]
		,	CASE
				WHEN DEP_TRANS_CODE IS NOT NULL THEN
					B.DEP_TRANS_CODE
				ELSE 
					CASE SUBSTRING(A.MASTER_CODE, 3, 1)
						WHEN 'B' THEN 'TC' -- 버스
						WHEN 'T' THEN 'HK' -- 기차
						WHEN 'I' THEN 'HO' -- 섬여행(선박)
					ELSE 'DD' END -- ?? 나머지는 이걸루 해야되나??
			END	AS START_AIR_CODE
		,	CASE 
				WHEN B.DEP_DEP_DATE IS NOT NULL THEN 
					REPLACE(B.DEP_DEP_TIME, ':', '')
				ELSE 
					'0000' -- 항공외에는 따로 시간이 없어서 기본시간 셋팅
			END AS [START_TIME]
		,	D.ADT_PRICE AS ADULT_AMT
		,	CASE WHEN Z.USE_YN = 'Y' THEN '1' ELSE '2' END AS DAY_COLOR
		,	(CASE
				WHEN F.FILE_NAME_S = '' THEN ''
				WHEN F.FILE_NAME_M = '' THEN ('http://contents.verygoodtour.com/content/' + F.REGION_CODE + '/' + F.NATION_CODE + '/' + F.STATE_CODE + '/' + F.CITY_CODE + '/image/' + F.FILE_NAME_S)
				ELSE ('http://contents.verygoodtour.com/content/' + F.REGION_CODE + '/' + F.NATION_CODE + '/' + F.STATE_CODE + '/' + F.CITY_CODE + '/image/' + F.FILE_NAME_M)
			END) AS [PKG_IMG_URL]
		,	'6' AS COMM_RATE -- 국내는 6%
		,	D.CHD_PRICE AS CHILD_AMT
		,	LTRIM(RTRIM(ISNULL(dbo.FN_ARR_SPLIT(dbo.XN_PRO_DETAIL_QCHARGE_PRICE_ALL(A.PRO_CODE,D.PRICE_SEQ),'|',1),'0'))) AS BAF_AMT
		,	ISNULL(dbo.XN_PRO_DETAIL_SALE_PRICE_CUTTING(A.PRO_CODE, D.PRICE_SEQ), '0') AS TOTAL_AMT
		,	CASE WHEN Z.BRAND_TYPE IS NOT NULL THEN 'PREMU' ELSE 'SBTNC' END AS PKG_PRICE
		,	CASE 
				WHEN B.DEP_DEP_DATE IS NOT NULL THEN 
					CONVERT(VARCHAR(8), B.ARR_ARR_DATE, 112)
				ELSE 
					CONVERT(VARCHAR(8), A.ARR_DATE, 112)
			END AS ARVDT
		,	CASE 
				WHEN B.ARR_ARR_TIME IS NOT NULL THEN 
					REPLACE(B.ARR_ARR_TIME, ':', '')
				ELSE 
					'0000' -- 항공외에는 따로 시간이 없어서 기본시간 셋팅
			END AS ARVTM
		,	CASE 
				WHEN A.RES_ADD_YN = 'Y' AND A.MAX_COUNT = -1 THEN 'ARBKG' -- 대기예약
				WHEN A.RES_ADD_YN = 'Y' AND (A.MAX_COUNT = 0 OR (A.MAX_COUNT - DBO.FN_PRO_GET_RES_COUNT(A.PRO_CODE) > 0)) THEN 'RSVPS' -- 예약가능
			ELSE 'RSVCD' END AS RSERV_STAT -- 예약마감
		, CASE WHEN A.SHOW_YN = 'N' THEN 'Y' ELSE 'N' END AS DEL_YN 
	FROM MASTER_LIST Z
	INNER JOIN PKG_DETAIL A WITH(NOLOCK) ON Z.MASTER_CODE = A.MASTER_CODE --AND A.SHOW_YN = 'Y'
	LEFT JOIN PRO_TRANS_SEAT B WITH(NOLOCK) ON A.SEAT_CODE = B.SEAT_CODE
	LEFT JOIN PUB_AIRLINE C WITH(NOLOCK) ON B.DEP_TRANS_CODE = C.AIRLINE_CODE
	INNER JOIN PKG_DETAIL_PRICE D WITH(NOLOCK) ON A.PRO_CODE = D.PRO_CODE	
	LEFT JOIN INF_FILE_MASTER F WITH(NOLOCK) ON Z.MAIN_FILE_CODE = F.FILE_CODE
	WHERE A.DEP_DATE > GETDATE();

END

GO
