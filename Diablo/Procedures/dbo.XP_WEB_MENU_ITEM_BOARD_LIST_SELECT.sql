USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: XP_WEB_MENU_ITEM_BOARD_LIST_SELECT
■ DESCRIPTION				: 베스트 상품 게시물 검색
■ INPUT PARAMETER			: 
	@SITE_CODE				: 사이트 코드
	@MENU_CODE				: 메뉴 코드
	@SEC_CODE				: 섹션 코드
	@TOP_COUNT				: 최대 검색 수
	@ORDER_BY				: 섹션 코드 정렬 방향 (D: DESC, 이외 ASC)
■ OUTPUT PARAMETER			: 
■ EXEC						: 
	
	exec XP_WEB_MENU_ITEM_BOARD_LIST_SELECT 'VGT', '900', '', 0, ''

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2013-03-19		김성호			최초생성
   2013-05-29		김성호			SEC_CODE VARCHAR(4)로 수정
   2013-06-01		김성호			스케줄 검색 조건 반영
   2013-06-10		김성호			WITH(NOLOCK) 추가
   2013-06-18		김성호			MNU_MNG_ITEM 컬럼명 수정 (NEW_DATE가 겹쳐서 *를 풀어서 씀)
   2015-02-24		김성호			SEC_CODE ORDER BY 정렬 방향 설정과 TOP 설정값 추가 (최근베스트 상품 검색 이유)
   2018-06-21		박형만			CUS_CUSTOMER_DAMO LEFT JOIN (직원이작성한 여행기의경우 안나오는현상해결) , 카테고리 추가 
================================================================================================================*/ 
CREATE  PROCEDURE [dbo].[XP_WEB_MENU_ITEM_BOARD_LIST_SELECT]
(
	@SITE_CODE	VARCHAR(3),
	@MENU_CODE	VARCHAR(20),
	@SEC_CODE	VARCHAR(4),
	@TOP_COUNT	INT,
	@ORDER_BY	VARCHAR(1)
)
AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000), @WHERE NVARCHAR(100);

	IF @SEC_CODE = ''
		SET @WHERE = ''
	ELSE
		SET @WHERE = ' AND A.SEC_CODE = @SEC_CODE'

	SET @SQLSTRING = N'
	SELECT ' + (CASE @TOP_COUNT WHEN 0 THEN '' ELSE 'TOP (@TOP_COUNT) ' END) + N'
		A.SITE_CODE, A.MENU_CODE, A.SEC_CODE, A.GRP_SEQ, A.ITEM_SEQ, A.IMG_URL, A.LINK_URL, A.MASTER_CODE, A.PRO_CODE, A.PRICE_SEQ, A.PRICE
		, A.MASTER_SEQ, A.BOARD_SEQ, A.EVT_SEQ, A.PRO_NAME, A.PKG_COMMENT, A.DTI_ITEM1, A.DTI_ITEM2, A.DTI_ITEM3, A.DTI_ITEM4, A.ORDER_NO, A.SCH_YN
		, B.GRP_TITLE, B.GRP_CODE, B.GRP_DESC, C.SUBJECT, C.CONTENTS, C.NEW_DATE, C.NEW_CODE, D.CUS_ID, D.CUS_NAME
		, C.CATEGORY_SEQ , (SELECT CATEGORY_NAME FROM HBS_CATEGORY WHERE MASTER_SEQ = C.MASTER_SEQ AND CATEGORY_SEQ = C.CATEGORY_SEQ) AS CATEGORY_NAME  , REGION_NAME 
	FROM MNU_MNG_ITEM A WITH(NOLOCK)
	INNER JOIN MNU_MNG_GROUP B WITH(NOLOCK) ON A.SITE_CODE = B.SITE_CODE AND A.MENU_CODE = B.MENU_CODE AND A.SEC_CODE = B.SEC_CODE AND A.GRP_SEQ = B.GRP_SEQ
	INNER JOIN HBS_DETAIL C WITH(NOLOCK) ON A.MASTER_SEQ = C.MASTER_SEQ AND A.BOARD_SEQ = C.BOARD_SEQ
	LEFT JOIN CUS_CUSTOMER_damo D WITH(NOLOCK) ON C.NEW_CODE = D.CUS_NO
	LEFT JOIN MNU_MNG_ITEM_SCH Z WITH(NOLOCK) ON A.SITE_CODE = Z.SITE_CODE AND A.MENU_CODE = Z.MENU_CODE AND A.SEC_CODE = Z.SEC_CODE AND A.GRP_SEQ = Z.GRP_SEQ AND A.ITEM_SEQ = Z.ITEM_SEQ
	WHERE A.SITE_CODE = @SITE_CODE AND A.MENU_CODE = @MENU_CODE' + @WHERE + N'
		AND (A.SCH_YN = ''N'' OR (
			GETDATE() >= Z.START_DATE AND GETDATE() < Z.END_DATE
			--AND SUBSTRING(CONVERT(VARCHAR(20), GETDATE(), 120), 12, 8) BETWEEN Z.DAY_START_TIME AND Z.DAY_END_TIME
			AND Z.DAY_START_TIME <= SUBSTRING(CONVERT(VARCHAR(20), GETDATE(), 120), 12, 8)
			AND Z.DAY_END_TIME > SUBSTRING(CONVERT(VARCHAR(20), GETDATE(), 120), 12, 8)
			AND SUBSTRING(Z.SHOW_DAY, DATEPART(DW, GETDATE()), 1) = 1
		))
	ORDER BY A.SITE_CODE, A.MENU_CODE, A.SEC_CODE' + (CASE @ORDER_BY WHEN 'D' THEN ' DESC' ELSE '' END) + ', A.GRP_SEQ' + (CASE @ORDER_BY WHEN 'D' THEN ' DESC' ELSE '' END) + ', A.ORDER_NO;'

	SET @PARMDEFINITION = N'
		@SITE_CODE	VARCHAR(3),
		@MENU_CODE	VARCHAR(20),
		@SEC_CODE	VARCHAR(4),
		@TOP_COUNT	INT';

	--PRINT @SQLSTRING

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, 
		@SITE_CODE,
		@MENU_CODE,
		@SEC_CODE,
		@TOP_COUNT;

END
GO
