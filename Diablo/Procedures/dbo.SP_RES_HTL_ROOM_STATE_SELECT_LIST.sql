USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


CREATE PROCEDURE [dbo].[SP_RES_HTL_ROOM_STATE_SELECT_LIST]
	@RES_CODE VARCHAR(12),
	@CITY_CODE VARCHAR(3),
	@MASTER_CODE VARCHAR(10),
	@MASTER_NAME VARCHAR(50),
	@CUS_NAME VARCHAR(20),
	@SEARCH_TYPE INT,
	@START_DATE VARCHAR(10),
	@END_DATE VARCHAR(10),
	@ROOM_YN CHAR(1),
	@PAY_STATE INT,
	@RES_TYPE INT,
	@PRO_TYPE INT
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000)

	-- WHERE 조건 만들기
	IF LEN(ISNULL(@RES_CODE, '')) = 12
	BEGIN
		SET @SQLSTRING = 'WHERE A.RES_CODE = @RES_CODE';
	END
	ELSE
	BEGIN
			-- 상품타입, OK/WT
		SET @SQLSTRING = 'WHERE C.PRO_TYPE = @PRO_TYPE AND A.ROOM_YN = @ROOM_YN';

			-- 도시코드
		IF ISNULL(@CITY_CODE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.CITY_CODE = @CITY_CODE'

			-- 호텔코드
		IF ISNULL(@MASTER_CODE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.MASTER_CODE = @MASTER_CODE'

			-- 호텔명
		IF ISNULL(@MASTER_NAME, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND B.MASTER_NAME LIKE (''%'' + @MASTER_NAME + ''%'')'

			-- 예약자명
		IF ISNULL(@CUS_NAME, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND C.RES_NAME = @CUS_NAME'

			-- 예약일
		IF ISNULL(@START_DATE, '') <> '' AND ISNULL(@END_DATE, '') <> ''
		BEGIN
			IF @SEARCH_TYPE = 1
				SET @SQLSTRING = @SQLSTRING + ' AND A.CHECK_IN BETWEEN @START_DATE AND @END_DATE'
			ELSE IF @SEARCH_TYPE = 2
				SET @SQLSTRING = @SQLSTRING + ' AND A.CHECK_OUT BETWEEN @START_DATE AND @END_DATE'
				--SET @SQLSTRING = @SQLSTRING + ' AND C.NEW_DATE BETWEEN @START_DATE AND @END_DATE'
		END

			-- 유입처
		IF ISNULL(@RES_TYPE, 10) <> 10
			SET @SQLSTRING = @SQLSTRING + ' AND C.RES_TYPE = @RES_TYPE'
	END

	SET @SQLSTRING = N'
	SELECT A.*
	FROM (
		SELECT
			 A.RES_CODE, A.RES_STATE, A.CHECK_IN, A.CHECK_OUT, A.LAST_CXL_DATE, A.ROOM_YN
			, DBO.FN_RES_HTL_GET_PAY_STATE(A.RES_CODE) AS [PAY_STATE]
			, B.MASTER_NAME, C.NEW_DATE, C.NEW_CODE, C.PROVIDER, C.RES_NAME, C.LAST_PAY_DATE
			, DBO.FN_RES_HTL_GET_TOTAL_PRICE(A.RES_CODE) AS [PAY_PRICE]
			, (SELECT KOR_NAME FROM EMP_MASTER WHERE EMP_CODE = B.NEW_CODE) AS [NEW_NAME]
		FROM RES_HTL_ROOM_MASTER A
		INNER JOIN HTL_MASTER B ON A.MASTER_CODE = B.MASTER_CODE
		INNER JOIN RES_MASTER_damo C ON A.RES_CODE = C.RES_CODE
		' + @SQLSTRING + '
	) A';

		-- 미수여부
	IF LEN(ISNULL(@RES_CODE, '')) <> 12 AND ISNULL(@PAY_STATE, 9) <> 9
		SET @SQLSTRING = @SQLSTRING + ' WHERE A.PAY_STATE = @PAY_STATE'

	SET @PARMDEFINITION = N'@RES_CODE VARCHAR(12), @CITY_CODE VARCHAR(3), @MASTER_CODE VARCHAR(10), @MASTER_NAME VARCHAR(50), @CUS_NAME VARCHAR(20), @SEARCH_TYPE INT, @START_DATE VARCHAR(10), @END_DATE VARCHAR(10), @ROOM_YN CHAR(1), @PAY_STATE INT, @RES_TYPE INT, @PRO_TYPE INT'

	--PRINT @SQLSTRING
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @RES_CODE, @CITY_CODE, @MASTER_CODE, @MASTER_NAME, @CUS_NAME, @SEARCH_TYPE, @START_DATE, @END_DATE, @ROOM_YN, @PAY_STATE, @RES_TYPE, @PRO_TYPE
END

--	@RES_CODE, @CITY_CODE, @MASTER_CODE, @CUS_NAME, @SEARCH_TYPE, @START_DATE, @END_DATE, @ROOM_YN, @PAY_STATE, @RES_TYPE, @PRO_TYPE
--	SP_RES_HTL_ROOM_STATE_SELECT_LIST '', '', '', '', '', 1, '', '', 'N', 0, 10, ''
--	SP_RES_HTL_ROOM_STATE_SELECT_LIST 'RH0901300001', 'TYO', 'JHH00001', '도쿄 힐튼', '이왕복', 1, '2009-01-20', '2009-01-31', 'Y', 0, 0, 3
--	SP_RES_HTL_ROOM_STATE_SELECT_LIST '', 'TYO', 'JHH00001', '도쿄 힐튼', '이왕복', 1, '2009-01-20', '2009-01-31', 'Y', 0, 0, 3
GO
