USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

-- =============================================
-- 2019-01-17 김남훈 최초생성
-- 2019-10-08 박형만 @INFLOWTYPE varchar(1) -> varchar(3)
-- =============================================
CREATE PROCEDURE [dbo].[SP_CUS_MEMBER_CONSULT_SEARCH_SELECT_LIST]
(
	@FLAG			CHAR(1),
	@PAGE_SIZE		INT,
	@PAGE_INDEX		INT,
	@MEM_YN			VARCHAR(3),
	@CUS_GRADE		INT,
	@CUS_NAME		VARCHAR(20),
	@CUS_ID			VARCHAR(20),
	--@SOC_NUM1		VARCHAR(6),
	--@SOC_NUM2		VARCHAR(7),
	@BIRTH_DATE		DATETIME,
	@EMAIL			VARCHAR(40),
	@NOR_TEL1		VARCHAR(6),
	@NOR_TEL2		VARCHAR(5),
	@NOR_TEL3		VARCHAR(4),
	@RES_CODE		CHAR(12),
	@START_DATE		DATETIME,
	@END_DATE		DATETIME,
	@INFLOWTYPE     VARCHAR(3)
)
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	
	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000), @FROM INT, @TO INT;

	SET @FROM = @PAGE_INDEX * @PAGE_SIZE + 1;
	SET @TO = @PAGE_INDEX * @PAGE_SIZE + @PAGE_SIZE;

	-- WHERE 조건 만들기
	SET @SQLSTRING = 'WHERE 1 = 1';

	BEGIN
		-- 고객구분
		IF (@MEM_YN = 'ON')
			SET @SQLSTRING = @SQLSTRING + ' AND A.CUS_ID IS NOT NULL AND A.CUS_ID <> '''''
		ELSE IF (@MEM_YN = 'OFF')
			SET @SQLSTRING = @SQLSTRING + ' AND (A.CUS_ID IS NULL OR A.CUS_ID = '''')'
		-- 고객등급
		IF @CUS_GRADE < 9
			SET @SQLSTRING = @SQLSTRING + ' AND A.CUS_GRADE = @CUS_GRADE'
		--고객이름
		IF ISNULL(@CUS_NAME, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.CUS_NAME = @CUS_NAME'
		--고객아이디
		IF ISNULL(@CUS_ID, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.CUS_ID = @CUS_ID'
		----주민번호1
		IF ISNULL(@BIRTH_DATE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.BIRTH_DATE = @BIRTH_DATE'
		--이메일
		IF ISNULL(@EMAIL, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.EMAIL LIKE @EMAIL + ''%'''
		--휴대폰번호1
		IF ISNULL(@NOR_TEL1, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NOR_TEL1 = @NOR_TEL1'
		--휴대폰번호2
		IF ISNULL(@NOR_TEL2, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NOR_TEL2 = @NOR_TEL2'
		--휴대폰번호3
		IF ISNULL(@NOR_TEL3, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NOR_TEL3 = @NOR_TEL3'
		--예약코드
		IF ISNULL(@RES_CODE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND CUS_NO IN (SELECT CUS_NO FROM RES_MASTER_damo  WITH(NOLOCK) WHERE RES_CODE = @RES_CODE UNION SELECT CUS_NO FROM RES_CUSTOMER_damo  WITH(NOLOCK) WHERE RES_CODE = @RES_CODE)'
		--가입날짜
		IF ISNULL(@START_DATE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NEW_DATE >= @START_DATE'
		IF ISNULL(@END_DATE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.NEW_DATE <= DATEADD(SECOND, -1, (DATEADD(DAY, 1, @END_DATE)))'
		IF ISNULL(@INFLOWTYPE, '') <> ''
			SET @SQLSTRING = @SQLSTRING + ' AND A.INFLOW_TYPE = ' +@INFLOWTYPE;
	END

	IF @FLAG = 'C'					-- 전체
	BEGIN
		SET @SQLSTRING = N'SELECT COUNT(*) FROM CUS_MEMBER A WITH(NOLOCK) ' + @SQLSTRING;
	END
	ELSE IF @FLAG = 'L'				-- 검색
	BEGIN
		SET @SQLSTRING = N'
		WITH LIST AS
		(
			SELECT ROW_NUMBER() OVER (ORDER BY A.NEW_DATE DESC, A.CUS_NAME) AS [ROWNUMBER], A.CUS_NO
			FROM CUS_MEMBER A WITH(NOLOCK)
			' + @SQLSTRING + '
		)
		SELECT A.ROWNUMBER
			, (
				CASE
					WHEN ISNULL(B.CUS_ID, '''') <> '''' THEN ''ON''
					ELSE ''OFF''
				END
			) AS [ONOFF],
			B.CUS_NO,
			B.CUS_GRADE,
			B.CUS_ID,
			B.CUS_NAME,
--			B.SOC_NUM1,
			--B.SOC_NUM2,
--			damo.dbo.dec_varchar(''DIABLO'',''dbo.CUS_CUSTOMER'',''SOC_NUM2'', B.SEC_SOC_NUM2) AS SOC_NUM2 , 
			B.BIRTH_DATE,
			B.GENDER,
			B.NOR_TEL1,
			B.NOR_TEL2,
			B.NOR_TEL3,
			B.EMAIL,
			(SELECT COUNT(*) FROM RES_MASTER_damo  WITH(NOLOCK) WHERE CUS_NO = B.CUS_NO) AS [RES_COUNT],
			(SELECT COUNT(*) FROM RES_CUSTOMER_damo  WITH(NOLOCK) WHERE CUS_NO = B.CUS_NO) AS [DEP_COUNT],
			B.NEW_DATE
		FROM LIST A
		INNER JOIN CUS_MEMBER B  WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO 
		WHERE ROWNUMBER BETWEEN @FROM AND @TO
		ORDER BY B.NEW_DATE DESC;';
	END

	SET @PARMDEFINITION = N'@FROM INT, @TO INT, @FLAG CHAR(1), @PAGE_SIZE INT, @PAGE_INDEX INT, @MEM_YN VARCHAR(3), @CUS_GRADE INT, @CUS_NAME VARCHAR(20), @CUS_ID VARCHAR(20)
	,@BIRTH_DATE DATETIME, @EMAIL VARCHAR(40), @NOR_TEL1 VARCHAR(6), @NOR_TEL2 VARCHAR(5), @NOR_TEL3 VARCHAR(4), @RES_CODE CHAR(12), @START_DATE DATETIME, @END_DATE DATETIME'
	--PRINT @SQLSTRING
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @FROM, @TO, @FLAG, @PAGE_SIZE, @PAGE_INDEX, @MEM_YN, @CUS_GRADE, @CUS_NAME, @CUS_ID
	,@BIRTH_DATE, @EMAIL, @NOR_TEL1, @NOR_TEL2, @NOR_TEL3, @RES_CODE, @START_DATE, @END_DATE;

END


--exec  [dbo].[SP_CUS_CONSULT_SEARCH_SELECT_LIST] 'l', 15, 0, '', 0, '', '', '', '', '', '', '', '', '', '2010-05-01', '2010-05-07'





GO
