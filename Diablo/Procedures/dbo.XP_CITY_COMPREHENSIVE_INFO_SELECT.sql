USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: XP_CITY_COMPREHENSIVE_INFO_SELECT
■ DESCRIPTION				: 도시 종합 정보 ( 시차 / 날씨 / 환율 조회 )
■ INPUT PARAMETER			: NONE
■ OUTPUT PARAMETER			: '도시코드', '통화코드'
■ EXEC						: 
	EXEC XP_CITY_COMPREHENSIVE_INFO_SELECT 'HKT', 'THB'
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2017-06-12		정지용			최초생성
================================================================================================================*/ 
CREATE PROC [dbo].[XP_CITY_COMPREHENSIVE_INFO_SELECT]
	@CITY_CODE VARCHAR(10),
	@CURRENCY_CODE VARCHAR(3)
AS
BEGIN

	SELECT
		W_CODE, CITY_CODE, CITY_NAME, CAST_DATE, MIN(WEATHER_CODE) AS WEATHER_CODE, MIN(WEATHER_TEXT) AS WEATHER_TEXT, MIN(MIN_TEMPER) AS MIN_TEMPER, MIN(MAX_TEMPER) AS MAX_TEMPER,
		ISNULL((
			SELECT TOP 1 
				CASE 
					WHEN B.SUMMER_TIME_START IS NULL AND B.SUMMER_TIME_END IS NULL THEN B.UTC
					ELSE CONVERT(FLOAT, B.UTC)+1 
				END AS UTC				
			FROM WEATHERI_CITY_MAP_2 B WITH(NOLOCK)
			WHERE B.W_CITY_CODE_OLD = W_CODE
				AND ((B.SUMMER_TIME_START IS NULL AND B.SUMMER_TIME_END IS NULL AND 1 = 1) OR (GETDATE() BETWEEN B.SUMMER_TIME_START AND B.SUMMER_TIME_END))
				AND B.CITY_CODE IS NOT NULL
		), '') AS UTC
	FROM (
		SELECT 
			ROW_NUMBER() OVER (PARTITION BY WC.W_CITY_CODE ORDER BY MAX(WC.CAST_DATE) DESC) AS [ROW_NUMBER],
			WC.W_CITY_CODE AS W_CODE,
			WCM.CITY_CODE,
			PC.KOR_NAME AS CITY_NAME,
			WC.CAST_DATE,
			WC.WEATHER_CODE,
			WC.WEATHER_TEXT,
			WC.MIN_TEMPER,
			WC.MAX_TEMPER,
			WC.NEW_DATE
		FROM WEATHERI_CAST AS WC WITH(NOLOCK)
		INNER JOIN  WEATHERI_CITY_MAP AS WCM WITH(NOLOCK)
			ON WC.W_CITY_CODE = WCM.W_CITY_CODE 
		LEFT JOIN PUB_CITY AS PC WITH(NOLOCK)
			ON WCM.CITY_CODE = PC.CITY_CODE
		WHERE  
		WCM.CITY_CODE = @CITY_CODE
		AND WC.CAST_DATE < CONVERT(VARCHAR(8),DATEADD(DD,1,GETDATE()),112)  
		GROUP BY WC.W_CITY_CODE, WCM.CITY_CODE, PC.KOR_NAME, WC.CAST_DATE, WC.WEATHER_CODE, WC.WEATHER_TEXT, WC.MIN_TEMPER, WC.MAX_TEMPER, WC.NEW_DATE
	) A WHERE A.[ROW_NUMBER] = 1
	GROUP BY W_CODE, CITY_CODE, CITY_NAME, CAST_DATE;

	SELECT
		A.SEQ_NO,
		A.CURRENCY_CODE,
		B.CODE_KOR,
		B.CURRENCY_KOR,
		(B.CODE_KOR + ' ' + B.CURRENCY_KOR + ' ' + A.CURRENCY_CODE) AS CURRENCY_NAME,
		BUY_CASH,
		SELL_CASH,
		SEND_CASH,
		RECV_CASH,
		SELL_CHECK,
		EXCHANGE_RATE,
		USD_RATE,
		CONVERT_RATE,
		A.USE_YN,
		A.UPDATE_DATE,
		A.UPDATE_COUNT
	FROM COM_EXCHANGE_RATE A
	LEFT JOIN COM_CURRENCY_CODE B ON A.CURRENCY_CODE = B.CURRENCY_CODE AND B.USE_YN = 'Y'
	WHERE A.CURRENCY_CODE = @CURRENCY_CODE AND A.USE_YN = 'Y';
END
GO
