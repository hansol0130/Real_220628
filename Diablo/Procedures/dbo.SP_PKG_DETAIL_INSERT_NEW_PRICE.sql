USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================    
■ USP_Name			: SP_PKG_DETAIL_INSERT_NEW_PRICE
■ Description		: 마스터 가격정보 행사로 복사
■ Input Parameter   : 
■ Output Parameter	:                      
■ Output Value		:                     
■ Exec				: 
■ Memo				:    
------------------------------------------------------------------------------------------------------------------    
■ Change History
------------------------------------------------------------------------------------------------------------------    
 Date   Author   Description               
------------------------------------------------------------------------------------------------------------------    
2014-06-27	김성호	총액표시제에 따른 스키마 수정 반영,6/30 PKG_PRICE_DETAIL 컬럼복사 수정
2019-03-15  박형만	네이버관련 컬럼추가 
2020-03-24	김성호	트리거 예외처리 추가 (트리거에 CONTEXT_INFO() 체크하여 트리거 무시하는 로직 추가 필요)
================================================================================================================*/
CREATE PROCEDURE [dbo].[SP_PKG_DETAIL_INSERT_NEW_PRICE]
	@PRO_CODE		VARCHAR(20)
AS
BEGIN
	
	-- 트리거 동작 제외
	SET CONTEXT_INFO 0x21884680;
	
	DECLARE @MASTER_CODE VARCHAR(20)
	
	SELECT @MASTER_CODE = PD.MASTER_CODE FROM PKG_DETAIL PD WITH(NOLOCK) WHERE PD.PRO_CODE = @PRO_CODE;
	 	
	
	-- 행사 가격 정보 저장
	INSERT INTO PKG_DETAIL_PRICE (PRO_CODE, PRICE_SEQ, PRICE_NAME, SEASON, SCH_SEQ, PKG_INCLUDE, PKG_NOT_INCLUDE
		, ADT_PRICE, CHD_PRICE, INF_PRICE, SGL_PRICE, CUR_TYPE, EXC_RATE, FLOATING_YN
		, POINT_RATE, POINT_PRICE, POINT_YN
		, QCHARGE_TYPE, ADT_QCHARGE, CHD_QCHARGE, INF_QCHARGE, ADT_TAX, CHD_TAX, INF_TAX, QCHARGE_DATE
		)
	SELECT
		@PRO_CODE, A.PRICE_SEQ, A.PRICE_NAME, A.SEASON, A.SCH_SEQ, A.PKG_INCLUDE, A.PKG_NOT_INCLUDE
		, A.ADT_PRICE, A.CHD_PRICE, A.INF_PRICE, A.SGL_PRICE, A.CUR_TYPE, A.EXC_RATE, A.FLOATING_YN
		, POINT_RATE, POINT_PRICE, POINT_YN
		, QCHARGE_TYPE, ADT_QCHARGE, CHD_QCHARGE, INF_QCHARGE, ADT_TAX, CHD_TAX, INF_TAX, QCHARGE_DATE
	FROM PKG_MASTER_PRICE A  
	WHERE MASTER_CODE = @MASTER_CODE 

	-- 행사 호텔 세부 내역 저장
	INSERT INTO PKG_DETAIL_PRICE_HOTEL (PRO_CODE, PRICE_SEQ, DAY_NUMBER, HTL_MASTER_CODE, SUP_CODE, STAY_TYPE, STAY_INFO, DINNER_1, DINNER_2, DINNER_3,
		DINNER_CODE_1,DINNER_CODE_2,DINNER_CODE_3)
	SELECT @PRO_CODE, A.PRICE_SEQ, A.DAY_NUMBER, A.HTL_MASTER_CODE, A.SUP_CODE, A.STAY_TYPE, A.STAY_INFO, A.DINNER_1, A.DINNER_2, A.DINNER_3,
		A.DINNER_CODE_1,A.DINNER_CODE_2,A.DINNER_CODE_3	--식사코드 19.03 추가 
	FROM PKG_MASTER_PRICE_HOTEL A WITH(NOLOCK)  
	WHERE MASTER_CODE = @MASTER_CODE

	-- 공동 경비
	INSERT INTO PKG_DETAIL_PRICE_GROUP_COST (PRO_CODE, PRICE_SEQ, COST_SEQ, COST_NAME, CURRENCY, ADT_COST, CHD_COST, INF_COST, USE_YN)
	SELECT @PRO_CODE, A.PRICE_SEQ, A.COST_SEQ, A.COST_NAME, A.CURRENCY, A.ADT_COST, A.CHD_COST, A.INF_COST, A.USE_YN
	FROM PKG_MASTER_PRICE_GROUP_COST A WITH(NOLOCK)
	WHERE A.MASTER_CODE = @MASTER_CODE

	--가격 포함/불포함(네이버) 복사 19.03 추가 
	INSERT INTO PKG_DETAIL_PRICE_INOUT (PRO_CODE,PRICE_SEQ,INOUT_CODE,IN_YN)
	SELECT @PRO_CODE, A.PRICE_SEQ, A.INOUT_CODE, A.IN_YN
	FROM PKG_MASTER_PRICE_INOUT A WITH(NOLOCK)
	WHERE A.MASTER_CODE = @MASTER_CODE
	
	-- 마스터 상세와 관광일수 차이가 있을때 실행
	DECLARE @TOURDAY INT, @SCHEDULEDAY INT, @PRICE_SEQ INT 
	SELECT @TOURDAY = ISNULL(TOUR_DAY, 0) FROM PKG_DETAIL  WITH(NOLOCK)   WHERE PRO_CODE = @PRO_CODE
	
	-- 멀티 가격 관련 커서
	DECLARE PRICE_CURSOR CURSOR FOR
	SELECT PRICE_SEQ FROM PKG_DETAIL_PRICE  WITH(NOLOCK)   WHERE PRO_CODE = @PRO_CODE
	
	OPEN PRICE_CURSOR
	
	FETCH NEXT FROM PRICE_CURSOR
	INTO @PRICE_SEQ
	
	WHILE @@FETCH_STATUS = 0
	BEGIN
		SELECT @SCHEDULEDAY = ISNULL(MAX(DAY_NUMBER), 0)
		FROM PKG_DETAIL_PRICE_HOTEL  WITH(NOLOCK)   WHERE PRO_CODE = @PRO_CODE AND PRICE_SEQ = @PRICE_SEQ
		
		IF (@SCHEDULEDAY < @TOURDAY)
		BEGIN
			WHILE (@SCHEDULEDAY < @TOURDAY)
			BEGIN
				SET @SCHEDULEDAY = @SCHEDULEDAY + 1
				INSERT INTO PKG_DETAIL_PRICE_HOTEL (PRO_CODE, PRICE_SEQ, DAY_NUMBER) VALUES (@PRO_CODE, @PRICE_SEQ, @SCHEDULEDAY);
			END
		END
		ELSE IF (@SCHEDULEDAY > @TOURDAY)
		BEGIN
			DELETE FROM PKG_DETAIL_PRICE_HOTEL WHERE PRO_CODE = @PRO_CODE AND PRICE_SEQ = @PRICE_SEQ AND DAY_NUMBER > @TOURDAY
		END
	
		--PRINT @MASTER_SCH_SEQ
		
		FETCH NEXT FROM PRICE_CURSOR
		INTO @PRICE_SEQ
	END
	CLOSE PRICE_CURSOR
	DEALLOCATE PRICE_CURSOR
	
	
	-- 트리거 예외처리 후 마스터 업데이트
	EXEC DBO.SP_PKG_MASTER_RESETTING @MASTER_CODE;
	
END



GO
