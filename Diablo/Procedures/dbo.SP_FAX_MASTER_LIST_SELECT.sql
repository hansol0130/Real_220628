USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: SP_FAX_MASTER_LIST_SELECT
■ DESCRIPTION				: 팩스 수신함 검색
■ INPUT PARAMETER			: 

	DECLARE @TOTAL_COUNT INT
	EXEC SP_FAX_MASTER_LIST_SELECT 1, 15, @TOTAL_COUNT OUTPUT, '', '', '', 0, N'518', '', NULL, N'2015-09-01', N'2015-11-30', 0
	SELECT @TOTAL_COUNT

	
	DECLARE @TOTAL_COUNT INT
	EXEC SP_FAX_MASTER_LIST_SELECT 1, 15, @TOTAL_COUNT OUTPUT, '', '', '', 0, N'540', '', NULL,N'2015-09-01', N'2015-11-30', 0
	SELECT @TOTAL_COUNT

■ OUTPUT PARAMETER			: 
■ EXEC						: 
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2015-06-02		김성호			최초생성
   2015-06-02		김성호			쿼리 튜닝 (리스트, 전체 카운터, 팩스파일 리스트 일괄 통합)
   2015-10-28		정지용			쿼리 튜닝 ( FAX_RECEIVE에 강제 인덱스 추가 - 인덱스를 안타는 현상때문에 )
   2017-11-09		박형만			날짜 버그 수정(+1일 이전)
   2018-11-20		이명훈			읽음유무 조건 추가
================================================================================================================*/ 
CREATE PROCEDURE [dbo].[SP_FAX_MASTER_LIST_SELECT]
	@PAGE_INDEX INT,
	@PAGE_SIZE INT,
	@TOTAL_COUNT INT OUTPUT,

	@RCV_NUMBER1 VARCHAR(4),
	@RCV_NUMBER2 VARCHAR(4),
	@RCV_NUMBER3 VARCHAR(4),

	@SEARCH_TYPE INT, -- 0: 기본 FAX 수신함 조회. 재직자 사원정보의 FAX 번호로 검색 , 1: 관리자 = 퇴사자도 포함 조회 
	@TEAM_CODE TEAM_CODE,
	@EMP_CODE EMP_CODE,
	@SUBJECT VARCHAR(20),
	@START_DATE VARCHAR(10),
	@END_DATE VARCHAR(10),
	@FAX_CAT_SEQ INT,
	@READ_YN VARCHAR(1)
AS
BEGIN	

	DECLARE @STR_QUERY NVARCHAR(4000), @STR_WHERE NVARCHAR(500), @STR_SUB_WHERE NVARCHAR(500), @STR_PARAMS NVARCHAR(500)
	SELECT @STR_QUERY = '', @STR_WHERE = '', @STR_SUB_WHERE = ''

	IF LEN(@RCV_NUMBER1) > 0 AND LEN(@RCV_NUMBER2) > 0 AND LEN(@RCV_NUMBER3) > 0
	BEGIN
		SET @STR_WHERE = ' AND B.RCV_NUMBER1 = @RCV_NUMBER1 AND B.RCV_NUMBER2 = @RCV_NUMBER2 AND B.RCV_NUMBER3 = @RCV_NUMBER3'
	END
	ELSE
	BEGIN
		IF @SEARCH_TYPE = 0
		BEGIN
			SET @STR_SUB_WHERE = @STR_SUB_WHERE + '	AND WORK_TYPE = 1' 
		END
		IF LEN(@EMP_CODE) > 0
		BEGIN
			SET @STR_SUB_WHERE = @STR_SUB_WHERE + ' AND EMP_CODE = @EMP_CODE' 
		END

		IF ISNULL(@START_DATE,'0001-01-01') <> '0001-01-01'
		BEGIN
			SET @STR_WHERE = @STR_WHERE + ' AND A.NEW_DATE >= CONVERT(DATETIME,@START_DATE)' 
		END 
		IF ISNULL(@END_DATE,'9999-12-31') <>'9999-12-31'
		BEGIN
			SET @STR_WHERE = @STR_WHERE + ' AND A.NEW_DATE < DATEADD(D,1,CONVERT(DATETIME,@END_DATE))' 
		END
		IF @FAX_CAT_SEQ > 0
		BEGIN
			SET @STR_WHERE = @STR_WHERE + ' AND A.FAX_CAT_SEQ = @FAX_CAT_SEQ' 
		END 
		IF LEN(@SUBJECT) > 0
		BEGIN
			SET @STR_WHERE = @STR_WHERE + ' AND A.[SUBJECT] LIKE ''%''+@SUBJECT+''%'''
		END 
		
		IF @STR_WHERE <> ''
		BEGIN
			SET @STR_WHERE = N'WHERE ' + SUBSTRING(@STR_WHERE, 6, LEN(@STR_WHERE));
		END

		IF @STR_SUB_WHERE <> ''
		BEGIN
			SET @STR_WHERE = N'INNER JOIN (
				SELECT FAX_NUMBER1, FAX_NUMBER2, FAX_NUMBER3
				FROM EMP_MASTER WITH(NOLOCK)
				WHERE TEAM_CODE = @TEAM_CODE 
				' + @STR_SUB_WHERE + N'
				GROUP BY FAX_NUMBER1, FAX_NUMBER2, FAX_NUMBER3 
			) C ON B.RCV_NUMBER1 = C.FAX_NUMBER1 AND B.RCV_NUMBER2 = C.FAX_NUMBER2 AND B.RCV_NUMBER3 = C.FAX_NUMBER3
			' + @STR_WHERE
		END

		IF @READ_YN <> 'A'
		BEGIN
			SET @STR_WHERE = @STR_WHERE + ' AND A.READ_YN = @READ_YN'
		END

	END

	SET @STR_QUERY = N'
		-- 전체카운트
		SELECT @TOTAL_COUNT = COUNT(A.FAX_SEQ)
		FROM FAX_MASTER A WITH(NOLOCK) 
		INNER JOIN FAX_RECEIVE B WITH(INDEX = IDX_FAX_RECEIVE_1, NOLOCK) ON A.FAX_SEQ = B.FAX_SEQ AND A.FAX_TYPE = 2 AND A.DEL_YN=''N''
		' + @STR_WHERE + N';

		WITH DOCUMENTLIST AS
		( 
			SELECT ROW_NUMBER() OVER (ORDER BY A.FAX_SEQ DESC) AS ROW_NUM, A.*, B.RCV_NUMBER1 , B.RCV_NUMBER2 , B.RCV_NUMBER3 , B.RCV_EMP_CODE
			FROM FAX_MASTER A WITH(NOLOCK)
			INNER JOIN FAX_RECEIVE B WITH(INDEX = IDX_FAX_RECEIVE_1, NOLOCK) ON A.FAX_SEQ = B.FAX_SEQ AND A.FAX_TYPE = 2 AND A.DEL_YN = ''N''
			' + @STR_WHERE + N'
			ORDER BY A.FAX_SEQ DESC
			OFFSET ((@PAGE_INDEX - 1) * @PAGE_SIZE) ROWS FETCH NEXT @PAGE_SIZE
			ROWS ONLY
		)
		SELECT 
			(SELECT [SUBJECT] FROM FAX_CATEGORY WITH(NOLOCK) WHERE FAX_CAT_SEQ =A.FAX_CAT_SEQ  AND FAX_GROUP = A.FAX_GROUP AND DEL_YN=''N'') AS CAT_NAME, 
			(SELECT KOR_NAME FROM EMP_MASTER WITH(NOLOCK) WHERE EMP_CODE =A.RCV_EMP_CODE) AS RCV_EMP_NAME, * 
		FROM  DOCUMENTLIST AS A;
		
		WITH DOCUMENTLIST AS
		( 
			SELECT A.FAX_SEQ
			FROM FAX_MASTER A WITH(NOLOCK)
			INNER JOIN FAX_RECEIVE B WITH(INDEX = IDX_FAX_RECEIVE_1, NOLOCK) ON A.FAX_SEQ = B.FAX_SEQ AND A.FAX_TYPE = 2 AND A.DEL_YN=''N''
			' + @STR_WHERE + N'
			ORDER BY A.FAX_SEQ DESC
			OFFSET ((@PAGE_INDEX - 1) * @PAGE_SIZE) ROWS FETCH NEXT @PAGE_SIZE
			ROWS ONLY
		)
		SELECT *
		FROM FAX_FILE A WITH(NOLOCK)
		WHERE A.FAX_SEQ IN (SELECT FAX_SEQ FROM DOCUMENTLIST)';

	--PRINT @STR_QUERY
	
	SET @STR_PARAMS =N'
		@PAGE_INDEX INT,
		@PAGE_SIZE INT,
		@TOTAL_COUNT INT OUTPUT,
		@RCV_NUMBER1 VARCHAR(4),
		@RCV_NUMBER2 VARCHAR(4),
		@RCV_NUMBER3 VARCHAR(4),
		@SEARCH_TYPE INT,
		@TEAM_CODE TEAM_CODE,
		@EMP_CODE EMP_CODE,
		@SUBJECT VARCHAR(20),
		@START_DATE VARCHAR(10),
		@END_DATE VARCHAR(10),
		@FAX_CAT_SEQ INT,
		@READ_YN VARCHAR(1)';

	EXEC SP_EXECUTESQL @STR_QUERY, @STR_PARAMS,
		@PAGE_INDEX,
		@PAGE_SIZE,
		@TOTAL_COUNT OUTPUT,
		@RCV_NUMBER1,
		@RCV_NUMBER2,
		@RCV_NUMBER3,
		@SEARCH_TYPE,
		@TEAM_CODE,
		@EMP_CODE,
		@SUBJECT, 
		@START_DATE,
		@END_DATE,
		@FAX_CAT_SEQ,
		@READ_YN;
END 
GO
