USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
CREATE PROC [dbo].[SP_STS_AIR_ROUTE_SHARE_LIST] --null, null, null, '2011-06-01', '2011-06-15'
(
	@REGION_CODE VARCHAR(3),
	@CITY_CODE VARCHAR(3),
	@DATE_TYPE CHAR(1),
	@START_DATE DATETIME,
	@END_DATE DATETIME
) AS
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
DECLARE @SQLSTRING AS NVARCHAR(4000)
DECLARE @WHERE AS NVARCHAR(2000)
DECLARE @PARMDEFINITION AS NVARCHAR(1000)

SET @WHERE = '
WHERE 1 = 1'

IF(@START_DATE IS NOT NULL AND @END_DATE IS NOT NULL)
BEGIN
	-- 출발일
	IF(@DATE_TYPE = '1')
	SET @WHERE = @WHERE + '
		AND A.START_DATE >= @START_DATE AND A.START_DATE < @END_DATE + 1'

	-- 도착일
	IF(@DATE_TYPE = '2')
	SET @WHERE = @WHERE + '
		AND A.END_DATE >= @START_DATE AND A.END_DATE < @END_DATE + 1'

	-- 발권일
	IF(@DATE_TYPE = '3')
	SET @WHERE = @WHERE + '
		AND A.ISSUE_DATE >= @START_DATE AND A.END_DATE < @END_DATE + 1'
END

IF(@REGION_CODE IS NOT NULL)
SET @WHERE = @WHERE + '
	AND D.SIGN = @REGION_CODE'
	
IF(@CITY_CODE IS NOT NULL)
SET @WHERE = @WHERE + '
	AND A.CITY_CODE = @CITY_CODE'

SET @SQLSTRING = N'SELECT
	ISNULL(A.AIRLINE_CODE, ''??'') AS AIRLINE_CODE, COUNT(*) AS COUNT, ISNULL(D.REGION_CODE, ''zzz'') AS REGION_CODE, 
	ISNULL(MAX(D.KOR_NAME), ''알수없음'') AS REGION_NAME, ISNULL(B.CITY_CODE, ''zzz'') AS CITY_CODE, ISNULL(MAX(B.KOR_NAME), ''알수없음'') AS CITY_NAME
FROM DSR_TICKET A
	LEFT JOIN PUB_CITY B ON A.CITY_CODE = B.CITY_CODE
	LEFT JOIN PUB_NATION C ON B.NATION_CODE = C.NATION_CODE
	LEFT JOIN PUB_REGION D ON C.REGION_CODE = D.REGION_CODE' 
	+ @WHERE + '
GROUP BY A.AIRLINE_CODE, D.REGION_CODE, B.CITY_CODE
ORDER BY ISNULL(A.AIRLINE_CODE, ''zz''), ISNULL(D.REGION_CODE, ''zzz''), ISNULL(B.CITY_CODE, ''zzz'')';

--PRINT @SQLSTRING

SET @PARMDEFINITION = N'@REGION_CODE VARCHAR(3), @CITY_CODE VARCHAR(3), @DATE_TYPE CHAR(1), @START_DATE DATETIME, @END_DATE DATETIME'

EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @REGION_CODE, @CITY_CODE, @DATE_TYPE, @START_DATE, @END_DATE;
GO
