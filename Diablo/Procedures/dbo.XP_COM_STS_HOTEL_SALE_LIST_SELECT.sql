USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_STS_HOTEL_SALE_LIST_SELECT
■ DESCRIPTION				: ERP BTMS 호텔 일일 매출 현황
■ INPUT PARAMETER			: 
	@KEY		VARCHAR(400): 검색 키
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXEC DBO.XP_COM_STS_HOTEL_SALE_LIST_SELECT 'AgentCode=92756&TeamSeq=&EmpSeq=125&StartDate=2016-06-01&EndDate=2016-09-30&DateType=P'

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-06-10	 저스트고_강태영	  최초생성
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_STS_HOTEL_SALE_LIST_SELECT]

@KEY VARCHAR(400)

AS
BEGIN
	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000);
	DECLARE @WHERE NVARCHAR(4000);
	DECLARE @AGT_CODE VARCHAR(10), @TEAM_SEQ INT, @EMP_SEQ INT, @START_DATE VARCHAR(10), @END_DATE VARCHAR(10), @DATE_TYPE VARCHAR(1);

	SELECT
		@AGT_CODE = DBO.FN_PARAM(@KEY, 'AgentCode'),
		@TEAM_SEQ = DBO.FN_PARAM(@KEY, 'TeamSeq'),
		@EMP_SEQ = DBO.FN_PARAM(@KEY, 'EmpSeq'),
		@START_DATE = DBO.FN_PARAM(@KEY, 'StartDate'),
		@END_DATE = DBO.FN_PARAM(@KEY, 'EndDate'),
		@DATE_TYPE = DBO.FN_PARAM(@KEY, 'DateType')

	SELECT @WHERE = ' WHERE B.PRO_TYPE = 3 AND B.RES_STATE IN (4, 5) AND E.BTMS_YN = ''Y'' AND A.RES_CODE IN (SELECT C.RES_CODE FROM COM_BIZTRIP_DETAIL C WHERE C.PRO_DETAIL_TYPE = 3) '
	
	IF LEN(@AGT_CODE) > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND E.AGT_CODE = @AGT_CODE '
	END

	IF @TEAM_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND M.TEAM_SEQ = @TEAM_SEQ '
	END

	IF @EMP_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND K.EMP_SEQ = @EMP_SEQ '
	END

	IF LEN(@DATE_TYPE) > 0
	BEGIN
		IF @DATE_TYPE = 'P'
		BEGIN
			SET @WHERE = @WHERE + ' AND CONVERT(DATETIME, I.NEW_DATE, 121) >= CONVERT(DATETIME, @START_DATE, 121) AND CONVERT(DATETIME, I.NEW_DATE, 121) < DATEADD(DAY, 1, CONVERT(DATETIME, @END_DATE, 121)) '
			SET @WHERE = @WHERE + ' ORDER BY I.NEW_DATE ASC '
		END

		IF @DATE_TYPE = 'I'
		BEGIN
			SET @WHERE = @WHERE + ' AND CONVERT(DATETIME, A.CHECK_IN, 121) >= CONVERT(DATETIME, @START_DATE, 121) AND CONVERT(DATETIME, A.CHECK_IN, 121) < DATEADD(DAY, 1, CONVERT(DATETIME, @END_DATE, 121)) '
			SET @WHERE = @WHERE + ' ORDER BY A.CHECK_IN ASC '
		END

		IF @DATE_TYPE = 'O'
		BEGIN
			SET @WHERE = @WHERE + ' AND CONVERT(DATETIME, A.CHECK_OUT, 121) >= CONVERT(DATETIME, @START_DATE, 121) AND CONVERT(DATETIME, A.CHECK_OUT, 121) < DATEADD(DAY, 1, CONVERT(DATETIME, @END_DATE, 121)) '
			SET @WHERE = @WHERE + ' ORDER BY A.CHECK_OUT ASC '
		END
	END

	SET @SQLSTRING = N'
		SELECT
			A.RES_CODE,
			E.AGT_CODE,
			I.NEW_DATE,
			A.CHECK_IN,
			A.CHECK_OUT,
			H.KOR_NAME AS REGION_NAME,
			F.KOR_NAME AS CITY_NAME,
			A.HOTEL_NAME,
			E.KOR_NAME AS AGT_NAME,
			B.RES_NAME,
			M.TEAM_NAME,
			M.TEAM_SEQ,
			A.SALE_PRICE,
			A.NET_PRICE
		FROM RES_HTL_ROOM_MASTER A
		LEFT JOIN RES_MASTER_damo B ON A.RES_CODE = B.RES_CODE
		LEFT JOIN COM_BIZTRIP_DETAIL C ON C.RES_CODE = B.RES_CODE
		LEFT JOIN COM_BIZTRIP_MASTER D ON D.BT_CODE = C.BT_CODE
		LEFT JOIN AGT_MASTER E ON E.AGT_CODE = D.AGT_CODE
		LEFT JOIN PUB_CITY F ON F.CITY_CODE = D.BT_CITY_CODE
		LEFT JOIN PUB_NATION G ON G.NATION_CODE = F.NATION_CODE
		LEFT JOIN PUB_REGION H ON H.REGION_CODE = G.REGION_CODE
		LEFT JOIN PAY_MATCHING I ON I.RES_CODE = B.RES_CODE
		LEFT JOIN COM_EMPLOYEE_MATCHING J ON J.CUS_NO = B.CUS_NO AND J.AGT_CODE = E.AGT_CODE
		LEFT JOIN COM_EMPLOYEE K ON K.EMP_SEQ = J.EMP_SEQ AND K.AGT_CODE = E.AGT_CODE
		LEFT JOIN COM_TEAM M ON M.TEAM_SEQ = K.TEAM_SEQ AND M.AGT_CODE = E.AGT_CODE
		' + @WHERE

	SET @PARMDEFINITION = N'
		@AGT_CODE VARCHAR(10),
		@TEAM_SEQ INT,
		@EMP_SEQ INT,
		@START_DATE VARCHAR(10),
		@END_DATE VARCHAR(10),
		@DATE_TYPE VARCHAR(1)';

	--PRINT @SQLSTRING

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@AGT_CODE,
		@TEAM_SEQ,
		@EMP_SEQ,
		@START_DATE,
		@END_DATE,
		@DATE_TYPE

END
GO
