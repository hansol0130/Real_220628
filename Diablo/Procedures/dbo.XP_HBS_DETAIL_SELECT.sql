USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_HBS_DETAIL_SELECT
■ DESCRIPTION				: 게시물 검색
■ INPUT PARAMETER			: 
	@MASTER_SEQ				: 마스터 순번
	@BOARD_SEQ				: 게시물 순번
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	exec XP_HBS_DETAIL_SELECT 4, 35008, 'N'
	exec XP_HBS_DETAIL_SELECT 1, 14010, 'N'

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2013-05-29		김성호			최초생성
   2013-05-31		김성호			행사 정보 검색 추가
   2013-06-07		김성호			ORDER BY 추가
   2017-10-25		ibsolution		마스터상품코드 있을경우 정보 추가
   2020-02-28		김성호			WITH(NOLOCK) 추가
================================================================================================================*/ 

CREATE  PROCEDURE [dbo].[XP_HBS_DETAIL_SELECT]
(
	@MASTER_SEQ	INT,
	@BOARD_SEQ	INT,
	@COUNT_YN	VARCHAR(1)
)

AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000)

	IF @COUNT_YN = 'Y'
	BEGIN
		UPDATE HBS_DETAIL SET SHOW_COUNT = ISNULL(SHOW_COUNT, 0) + 1
		WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ;
	END

	SET @SQLSTRING = N'
	-- 본문
	WITH LIST AS
	(
		SELECT MASTER_SEQ, BOARD_SEQ FROM HBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ
		UNION
		SELECT MASTER_SEQ, BOARD_SEQ FROM HBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = @MASTER_SEQ AND PARENT_SEQ = @BOARD_SEQ
	)
	SELECT A.*, E.MASTER_NAME
		, F.FILE_CODE, F.REGION_CODE, F.NATION_CODE, F.STATE_CODE, F.CITY_CODE, F.FILE_TYPE
		, F.FILE_NAME, F.EXTENSION_NAME, F.FILE_NAME_L, F.FILE_NAME_M, F.FILE_NAME_S
		, (CASE WHEN B.CATEGORY_NAME IS NULL THEN D.KOR_NAME ELSE B.CATEGORY_NAME END) AS CATEGORY_NAME
		, CASE WHEN A.EMP_CODE IS NULL THEN DBO.FN_CUS_GET_CUS_NAME(A.NEW_CODE) ELSE DBO.FN_CUS_GET_EMP_NAME(A.EMP_CODE) END AS [CUS_NAME]
		, E.SHOW_YN, E.ATT_CODE
	FROM LIST Z
	INNER JOIN HBS_DETAIL A WITH(NOLOCK) ON A.MASTER_SEQ = Z.MASTER_SEQ AND A.BOARD_SEQ = Z.BOARD_SEQ
	LEFT JOIN HBS_CATEGORY B WITH(NOLOCK) ON B.MASTER_SEQ = A.MASTER_SEQ AND B.CATEGORY_SEQ = A.CATEGORY_SEQ
	LEFT JOIN HBS_EMPLOYEE C WITH(NOLOCK) ON C.SEQ_NO = A.CATEGORY_SEQ
	LEFT JOIN EMP_MASTER D WITH(NOLOCK) ON C.EMP_CODE = D.EMP_CODE
	LEFT JOIN PKG_MASTER E WITH(NOLOCK) ON E.MASTER_CODE = A.MASTER_CODE OR E.MASTER_CODE IN (SELECT AA.MASTER_CODE FROM PKG_DETAIL AA WITH(NOLOCK) WHERE AA.PRO_CODE = A.MASTER_CODE)
	LEFT JOIN INF_FILE_MASTER F WITH(NOLOCK) ON F.FILE_CODE = E.MAIN_FILE_CODE
	ORDER BY A.LEVEL;

	-- 파일
	WITH LIST AS
	(
		SELECT MASTER_SEQ, BOARD_SEQ FROM HBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ
		UNION
		SELECT MASTER_SEQ, BOARD_SEQ FROM HBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = @MASTER_SEQ AND PARENT_SEQ = @BOARD_SEQ
	)
	SELECT A.*
	FROM LIST Z
	INNER JOIN HBS_FILE A WITH(NOLOCK) ON A.MASTER_SEQ = Z.MASTER_SEQ AND A.BOARD_SEQ = Z.BOARD_SEQ;

	-- 댓글
	WITH LIST AS
	(
		SELECT MASTER_SEQ, BOARD_SEQ FROM HBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = @MASTER_SEQ AND BOARD_SEQ = @BOARD_SEQ
		UNION
		SELECT MASTER_SEQ, BOARD_SEQ FROM HBS_DETAIL WITH(NOLOCK) WHERE MASTER_SEQ = @MASTER_SEQ AND PARENT_SEQ = @BOARD_SEQ
	)
	SELECT A.*
	FROM LIST Z
	INNER JOIN HBS_COMMENT A WITH(NOLOCK) ON A.MASTER_SEQ = Z.MASTER_SEQ AND A.BOARD_SEQ = Z.BOARD_SEQ
	ORDER BY A.COMMENT_SEQ DESC;
	'

	SET @PARMDEFINITION = N'
		@MASTER_SEQ	INT,
		@BOARD_SEQ	INT';

--	PRINT @SQLSTRING
		
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@MASTER_SEQ,
		@BOARD_SEQ;

END

GO
