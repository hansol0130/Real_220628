USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


/*================================================================================================================
■ USP_NAME					: XP_ASG_EVT_REPORT_TC_DETAIL_SELECT
■ DESCRIPTION				: 대외업무시스템 인솔자관리 인솔자평가현황 상세 검색
■ INPUT PARAMETER			: 
	@PAGE_INDEX  INT		: 현재 페이지
	@PAGE_SIZE  INT			: 한 페이지 표시 게시물 수
	@KEY		VARCHAR(400): 검색 키
	@ORDER_BY	INT			: 정렬 순서
■ OUTPUT PARAMETER			: 
	@TOTAL_COUNT INT OUTPUT	: 총 검색된 수       
■ EXEC						: 
	DECLARE @PAGE_INDEX INT,
	@PAGE_SIZE  INT,
	@TOTAL_COUNT INT, 
	@KEY		VARCHAR(400),
	@ORDER_BY	INT

	SELECT @PAGE_INDEX=1,@PAGE_SIZE=2,@KEY=N'NewCode=T130114',@ORDER_BY=0
	
	exec XP_ASG_EVT_REPORT_TC_DETAIL_SELECT @page_index, @page_size, @total_count output, @key, @order_by
	SELECT @TOTAL_COUNT
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2013-04-25		이상일			최초생성    
================================================================================================================*/ 

 CREATE  PROCEDURE [dbo].[XP_ASG_EVT_REPORT_TC_DETAIL_SELECT]
(
	@PAGE_INDEX		INT,
	@PAGE_SIZE		INT,
	@TOTAL_COUNT	INT OUTPUT,
	@KEY			VARCHAR(400),
	@ORDER_BY		INT
)

AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED

	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000);

	DECLARE
		@NEW_CODE	VARCHAR(7)--인솔자코드

	SELECT 
		@NEW_CODE = DBO.FN_PARAM(@KEY, 'NewCode')

   

	SET @SQLSTRING = N'				
				SELECT @TOTAL_COUNT = COUNT(NEW_CODE)
					FROM (SELECT A.NEW_CODE
						FROM OTR_MASTER A
						INNER JOIN OTR_POL_MASTER B ON (A.OTR_SEQ = B.OTR_SEQ AND B.POL_TYPE =''5'' AND B.CLIENT_CALL_YN = ''Y'')
						INNER JOIN AGT_MEMBER C ON A.NEW_CODE = C.MEM_CODE
						INNER JOIN PKG_DETAIL D ON A.PRO_CODE = D.PRO_CODE
						INNER JOIN PKG_MASTER E ON D.MASTER_CODE = E.MASTER_CODE
						INNER JOIN OTR_POL_QUESTION F ON B.OTR_POL_MASTER_SEQ = F.OTR_POL_MASTER_SEQ AND F.QUS_TYPE=''1'' AND F.OTR_POL_QUESTION_SEQ = 0
						INNER JOIN OTR_POL_ANSWER G ON F.OTR_POL_MASTER_SEQ = G.OTR_POL_MASTER_SEQ AND F.OTR_POL_QUESTION_SEQ = G.OTR_POL_QUESTION_SEQ
						INNER JOIN OTR_POL_DETAIL H ON G.OTR_POL_MASTER_SEQ = H.OTR_POL_MASTER_SEQ AND G.OTR_POL_QUESTION_SEQ = H.OTR_POL_QUESTION_SEQ AND G.OTR_POL_EXAMPLE_SEQ =H.OTR_POL_EXAMPLE_SEQ 
						WHERE A.OTR_STATE =''3'' AND A.NEW_CODE = @NEW_CODE) A	 ;


					SELECT A.NEW_CODE,
						   A.PRO_CODE, 
						   C.KOR_NAME AS NEW_NAME, 
						   C.AGT_GRADE,
						   D.DEP_DATE,
						   E.SIGN_CODE,
						   B.OTR_POL_MASTER_SEQ,
						   (SELECT KOR_NAME FROM PUB_REGION WHERE SIGN = E.SIGN_CODE) AS SIGN_NAME,
						   (SELECT TOP 1 ANSWER_TEXT 
						      FROM OTR_POL_ANSWER 
							 WHERE OTR_POL_MASTER_SEQ = B.OTR_POL_MASTER_SEQ AND OTR_POL_QUESTION_SEQ = 4) AS VALUATION_TEXT,
						   H.EXAMPLE_DESC AS AVG_NUM,
						   B.CLIENT_NAME,
						   G.NEW_DATE AS VA_DATE,
						   (SELECT COUNT(OTR_SEQ) FROM OTR_MASTER WHERE OTR_STATE =''3'' AND NEW_CODE = A.NEW_CODE) AS T_COUNT,
						   (SELECT ROUND((SUM(CONVERT(INT,L.EXAMPLE_DESC)) / CONVERT(FLOAT,COUNT(K.OTR_POL_EXAMPLE_SEQ))),1)
							  FROM OTR_MASTER H
							 INNER JOIN dbo.OTR_POL_MASTER I ON H.OTR_SEQ = I.OTR_SEQ AND I.POL_TYPE =''5''
							 INNER JOIN dbo.OTR_POL_QUESTION J ON I.OTR_POL_MASTER_SEQ = J.OTR_POL_MASTER_SEQ AND J.QUS_TYPE=''1'' AND J.OTR_POL_QUESTION_SEQ = 0
							 INNER JOIN dbo.OTR_POL_ANSWER K ON J.OTR_POL_MASTER_SEQ = K.OTR_POL_MASTER_SEQ AND J.OTR_POL_QUESTION_SEQ = K.OTR_POL_QUESTION_SEQ
							 INNER JOIN dbo.OTR_POL_DETAIL L ON K.OTR_POL_MASTER_SEQ = L.OTR_POL_MASTER_SEQ AND K.OTR_POL_QUESTION_SEQ = L.OTR_POL_QUESTION_SEQ AND K.OTR_POL_EXAMPLE_SEQ =L.OTR_POL_EXAMPLE_SEQ 
							 WHERE H.OTR_STATE = ''3'' AND I.CLIENT_CALL_YN = ''Y'' AND H.NEW_CODE = A.NEW_CODE) AS T_AVG_NUM
					FROM OTR_MASTER A
					INNER JOIN OTR_POL_MASTER B ON (A.OTR_SEQ = B.OTR_SEQ AND B.POL_TYPE =''5'' AND B.CLIENT_CALL_YN = ''Y'')
					INNER JOIN AGT_MEMBER C ON A.NEW_CODE = C.MEM_CODE
					INNER JOIN PKG_DETAIL D ON A.PRO_CODE = D.PRO_CODE
					INNER JOIN PKG_MASTER E ON D.MASTER_CODE = E.MASTER_CODE
					INNER JOIN OTR_POL_QUESTION F ON B.OTR_POL_MASTER_SEQ = F.OTR_POL_MASTER_SEQ AND F.QUS_TYPE=''1'' AND F.OTR_POL_QUESTION_SEQ = 0
					INNER JOIN OTR_POL_ANSWER G ON F.OTR_POL_MASTER_SEQ = G.OTR_POL_MASTER_SEQ AND F.OTR_POL_QUESTION_SEQ = G.OTR_POL_QUESTION_SEQ
					INNER JOIN OTR_POL_DETAIL H ON G.OTR_POL_MASTER_SEQ = H.OTR_POL_MASTER_SEQ AND G.OTR_POL_QUESTION_SEQ = H.OTR_POL_QUESTION_SEQ AND G.OTR_POL_EXAMPLE_SEQ =H.OTR_POL_EXAMPLE_SEQ 
					WHERE A.OTR_STATE =''3'' AND A.NEW_CODE = @NEW_CODE
					ORDER BY A.PRO_CODE DESC 
					OFFSET ((@PAGE_INDEX - 1) * @PAGE_SIZE) ROWS FETCH NEXT @PAGE_SIZE
					ROWS ONLY '

			

				 
	SET @PARMDEFINITION = N'@PAGE_INDEX  INT, @PAGE_SIZE  INT, @TOTAL_COUNT INT OUTPUT, @NEW_CODE VARCHAR(7)';

	--PRINT @SQLSTRING
		
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @PAGE_INDEX, @PAGE_SIZE, @TOTAL_COUNT OUTPUT, @NEW_CODE;

END



GO
