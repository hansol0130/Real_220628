USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_WEB_PKG_MASTER_CATEGORY_LIST_SELECT
■ DESCRIPTION				: 마스터 리스트 카테고리 검색
■ INPUT PARAMETER			: 
	@KEY		VARCHAR(400): 검색 키
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	DECLARE @KEY		VARCHAR(400)
	SELECT @KEY=N'Provider=0&BrandType=&RegionCode=&CityCode=BKK&NationCode=TN&AttCode=&GroupCode=&MinPrice=&MaxPrice=&DepDate=&Day=&Keyword=&Flag=c'
	exec XP_WEB_PKG_MASTER_CATEGORY_LIST_SELECT @key

■ MEMO						: 수정 시 XP_WEB_PKG_MASTER_LIST_SELECT 프로시저와 검색 조건 동기화 해야함
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2013-03-21		김성호			최초생성
   2013-05-21		김성호			날짜가 없을때 LAST_DATE >= GETDATE() 조건 추가
   2013-06-10		김성호			WITH(NOLOCK) 추가
   2013-07-09		김성호			BranchCode 검색어 추가
   2013-07-29		김성호			@BRAND_TYPE VARCHAR(1) 선언
   2017-04-04		정지용			MIN_PRICE / MAX_PRICE CONVERT시에 INT -> BIGINT로 변경
================================================================================================================*/ 

CREATE  PROCEDURE [dbo].[XP_WEB_PKG_MASTER_CATEGORY_LIST_SELECT]
(
	@KEY		VARCHAR(400)
)

AS  
BEGIN

	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
	
	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000);
	DECLARE @INNER_TABLE NVARCHAR(1000), @WHERE NVARCHAR(4000)
	DECLARE @PUSAN_TEAM_CODE VARCHAR(100), @TEMP NVARCHAR(1000)

	DECLARE @PROVIDER VARCHAR(3), @BRAND_TYPE VARCHAR(1), @REGION_CODE VARCHAR(50), @CITY_CODE VARCHAR(500), @NATION_CODE VARCHAR(50), @ATT_CODE VARCHAR(50), @GROUP_CODE VARCHAR(300)
	DECLARE @MIN_PRICE VARCHAR(10), @MAX_PRICE VARCHAR(10), @DEP_DATE VARCHAR(10), @DAY VARCHAR(5), @BRANCH_CODE VARCHAR(1), @KEYWORD NVARCHAR(1000), @FLAG VARCHAR(1), @MAIN_ATT_CODE VARCHAR(10)

	-- 검색 제외팀
	--SET @PUSAN_TEAM_CODE = '''514'', ''515'', ''516'', ''524'', ''531''';

	SELECT
		@PROVIDER = DBO.FN_PARAM(@KEY, 'Provider'),
		@BRAND_TYPE = DBO.FN_PARAM(@KEY, 'BrandType'),
		@FLAG = DBO.FN_PARAM(@KEY, 'Flag'),
		@REGION_CODE = DBO.FN_PARAM(@KEY, 'RegionCode'),
		@CITY_CODE = DBO.FN_PARAM(@KEY, 'CityCode'),
		@NATION_CODE = DBO.FN_PARAM(@KEY, 'NationCode'),
		@ATT_CODE = DBO.FN_PARAM(@KEY, 'AttCode'),
		@MAIN_ATT_CODE = DBO.FN_PARAM(@KEY, 'MainAttCode'),
		@GROUP_CODE = DBO.FN_PARAM(@KEY, 'GroupCode'),
		@MIN_PRICE = DBO.FN_PARAM(@KEY, 'MinPrice'),
		@MAX_PRICE = DBO.FN_PARAM(@KEY, 'MaxPrice'),
		@DEP_DATE = DBO.FN_PARAM(@KEY, 'DepDate'),
		@DAY = DBO.FN_PARAM(@KEY, 'Day'),
		@BRANCH_CODE = DBO.FN_PARAM(@KEY, 'BranchCode'),
		@KEYWORD = DBO.FN_PARAM(@KEY, 'Keyword'),
		@WHERE = 'WHERE SHOW_YN = ''Y'''

	-- 속성코드가 'BU' 부산일때는 지점코드를 정의한다
	IF @MAIN_ATT_CODE = 'BU'
	BEGIN
		SELECT @MAIN_ATT_CODE = NULL, @BRANCH_CODE = 1
	END
	ELSE IF ISNULL(@MAIN_ATT_CODE, '') <> ''
	BEGIN
		SELECT @BRANCH_CODE = 0
	END

	-- 그룹코드 사용시 다른 조건 무시
	IF ISNULL(@GROUP_CODE, '') <> ''
	BEGIN
		SELECT @GROUP_CODE = STUFF((SELECT (',''' + DATA + '''') AS [text()] FROM [DBO].[FN_SPLIT] (@GROUP_CODE, ',') FOR XML PATH('')), 1, 1, '')
		SET @WHERE = @WHERE + ' AND A.MASTER_CODE IN (SELECT MASTER_CODE FROM PKG_GROUP WITH(NOLOCK) WHERE GROUP_CODE IN (' + @GROUP_CODE + '))'
	END
	ELSE
	BEGIN
		IF ISNULL(@REGION_CODE, '') <> ''
		BEGIN
			SELECT @REGION_CODE = STUFF((SELECT (',''' + DATA + '''') AS [text()] FROM [DBO].[FN_SPLIT] (@REGION_CODE, ',') FOR XML PATH('')), 1, 1, '')
			SET @WHERE = @WHERE + ' AND A.SIGN_CODE IN (' + @REGION_CODE + ')'
		END

		IF ISNULL(@ATT_CODE, '') <> ''
		BEGIN
			SELECT @ATT_CODE = STUFF((SELECT (',''' + DATA + '''') AS [text()] FROM [DBO].[FN_SPLIT] (@ATT_CODE, ',') FOR XML PATH('')), 1, 1, '')

			SET @WHERE = @WHERE + ' AND A.MASTER_CODE IN (SELECT MASTER_CODE FROM PKG_ATTRIBUTE AA WITH(NOLOCK) WHERE A.MASTER_CODE = AA.MASTER_CODE AND AA.ATT_CODE IN (' + @ATT_CODE + '))'    
		END

		-- 대표속성 검색
		IF ISNULL(@MAIN_ATT_CODE, '') <> ''
		BEGIN
			SET @WHERE = @WHERE + ' AND A.ATT_CODE = @MAIN_ATT_CODE'
		END

		-- 그룹코드가 아닐 시 부산 담당 상품 제외
		--SET @WHERE = @WHERE + ' AND A.NEW_CODE NOT IN (SELECT EMP_CODE FROM EMP_MASTER WITH(NOLOCK) WHERE TEAM_CODE IN (' + @PUSAN_TEAM_CODE + '))'
	END

	IF (ISNULL(@CITY_CODE, '') <> '' OR ISNULL(@NATION_CODE, '') <> '')
	BEGIN
		SET @TEMP = ''

		IF ISNULL(@CITY_CODE, '') <> ''
		BEGIN
			SELECT @CITY_CODE = STUFF((SELECT (',''' + DATA + '''') AS [text()] FROM [DBO].[FN_SPLIT] (@CITY_CODE, ',') FOR XML PATH('')), 1, 1, '')

			SET @TEMP = (' OR AA.CITY_CODE IN (' + @CITY_CODE + ')')
			--SET @TEMP = (' OR B.CITY_CODE IN (' + @CITY_CODE + ')')
		END

		IF ISNULL(@NATION_CODE, '') <> ''
		BEGIN
			SELECT @NATION_CODE = STUFF((SELECT (',''' + DATA + '''') AS [text()] FROM [DBO].[FN_SPLIT] (@NATION_CODE, ',') FOR XML PATH('')), 1, 1, '')

			SET @TEMP = @TEMP + ' OR AA.CITY_CODE IN (SELECT CITY_CODE FROM PUB_CITY AAA WITH(NOLOCK) WHERE AAA.NATION_CODE IN (' + @NATION_CODE + '))'
			--SET @TEMP = @TEMP + ' OR B.CITY_CODE IN (SELECT CITY_CODE FROM PUB_CITY AA WHERE AA.NATION_CODE IN (' + @NATION_CODE + '))'
		END

		SET @WHERE = @WHERE + ' AND A.MASTER_CODE IN (SELECT MASTER_CODE FROM PKG_MASTER_SCH_CITY AA WITH(NOLOCK) WHERE A.MASTER_CODE = AA.MASTER_CODE AND AA.MAINCITY_YN = ''Y'' AND (' + SUBSTRING(@TEMP, 4, LEN(@TEMP)) + '))'
		--SET @WHERE = @WHERE + ' AND (' + SUBSTRING(@TEMP, 4, LEN(@TEMP)) + ')'
	END

	IF ISNULL(@MIN_PRICE, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND A.LOW_PRICE >= CONVERT(BIGINT, @MIN_PRICE)'
	END

	IF ISNULL(@MAX_PRICE, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND A.LOW_PRICE <= CONVERT(BIGINT, @MAX_PRICE)'
	END

	IF (ISNULL(@DEP_DATE, '') <> '') OR (ISNULL(@DAY, '') <> '')
	BEGIN
		SET @TEMP = ''

		IF (ISNULL(@DEP_DATE, '') <> '')
			SET @TEMP = ' AND AA.DEP_DATE = CONVERT(DATETIME, @DEP_DATE)'
		IF (ISNULL(@DAY, '') <> '')
		BEGIN
			IF @DAY = '10'
				SET @TEMP = @TEMP + ' AND AA.TOUR_DAY >= @DAY'
			ELSE
				SET @TEMP = @TEMP + ' AND AA.TOUR_DAY = @DAY'
		END

		SET @WHERE = @WHERE + ' AND A.MASTER_CODE IN (SELECT MASTER_CODE FROM PKG_DETAIL AA WITH(NOLOCK) WHERE A.MASTER_CODE = AA.MASTER_CODE ' + @TEMP + ')'
	END
	ELSE
	BEGIN
		SET @WHERE = @WHERE + ' AND A.LAST_DATE >= GETDATE() '
	END

	IF ISNULL(@BRANCH_CODE, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + ' AND A.BRANCH_CODE = CONVERT(INT, @BRANCH_CODE)'
	END

	IF ISNULL(@KEYWORD, '') <> ''
	BEGIN
		SELECT @TEMP = STUFF((SELECT (' AND "' + DATA + '"') AS [text()] FROM [DBO].[FN_SPLIT](@KEYWORD, ' ') WHERE DATA <> '' FOR XML PATH('')), 1, 5, '')
		SELECT @KEYWORD = ' AND (CONTAINS(A.MASTER_NAME, ''' + @TEMP + ''') OR CONTAINS(A.KEYWORD, ''' + @TEMP + ''') OR CONTAINS(A.PKG_COMMENT, ''' + @TEMP + ''') OR CONTAINS(A.MASTER_CODE, ''' + REPLACE(@TEMP,'" AND "','" OR "') + ''') OR A.MASTER_CODE IN (SELECT MASTER_CODE FROM PKG_DETAIL WITH(NOLOCK) WHERE PRO_CODE = ''' + @KEYWORD + '''))'
--		SELECT @TEMP = STUFF((SELECT (' AND "' + DATA + '"') AS [text()] FROM [DBO].[FN_SPLIT](@KEYWORD, ' ') WHERE DATA <> '' FOR XML PATH('')), 1, 5, '')
--		SELECT @KEYWORD = ' AND (CONTAINS(A.MASTER_NAME, ''' + @TEMP + ''') OR CONTAINS(A.KEYWORD, ''' + @TEMP + ''') OR CONTAINS(A.MASTER_CODE, ''' + REPLACE(@TEMP,'" AND "','" OR "') + '''))'

		SET @WHERE = @WHERE + @KEYWORD
	END

	IF ISNULL(@BRAND_TYPE, '') <> ''
	BEGIN
		IF @BRAND_TYPE = '9'
		BEGIN
			SET @WHERE = @WHERE + ' AND A.BRAND_TYPE IN(''1'', ''2'', ''3'', ''4'') '
		END 
		ELSE 
		BEGIN
			SET @WHERE = @WHERE + ' AND A.BRAND_TYPE = @BRAND_TYPE'
		END 
	END
	
	IF @FLAG = 'A'
	BEGIN
		SET @SQLSTRING = N'
		SELECT A.ATT_CODE AS [CODE], COUNT(A.ATT_CODE) AS [COUNT]
			, (SELECT PUB_VALUE FROM COD_PUBLIC WITH(NOLOCK) WHERE PUB_TYPE = ''PKG.ATTRIBUTE'' AND PUB_CODE = A.ATT_CODE) AS [NAME]
		FROM (
			SELECT C.ATT_CODE, A.MASTER_CODE
			FROM PKG_MASTER A WITH(NOLOCK)
			LEFT JOIN PKG_MASTER_SCH_CITY B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE
			INNER JOIN PKG_ATTRIBUTE C WITH(NOLOCK) ON A.MASTER_CODE = C.MASTER_CODE
			'
			IF @PROVIDER NOT IN('0','5','16','18')
				SET @SQLSTRING = @SQLSTRING + N'INNER JOIN PKG_MASTER_AFFILIATE Z WITH(NOLOCK) ON Z.PROVIDER = CONVERT(INT, @PROVIDER) AND A.MASTER_CODE = Z.MASTER_CODE AND Z.USE_YN=''Y''
			'
			SET @SQLSTRING = @SQLSTRING + @WHERE + N'
			GROUP BY C.ATT_CODE, A.MASTER_CODE
		) A
		GROUP BY A.ATT_CODE
		ORDER BY NAME';
	END
	ELSE
	BEGIN
		SET @SQLSTRING = N'
		SELECT A.CITY_CODE AS [CODE], COUNT(A.CITY_CODE) AS [COUNT]
			, DBO.FN_PUB_GET_CITY_NAME(A.CITY_CODE) AS [NAME]
		FROM (
			SELECT B.CITY_CODE, A.MASTER_CODE
			FROM PKG_MASTER A WITH(NOLOCK) 
			INNER JOIN PKG_MASTER_SCH_CITY B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE AND B.MAINCITY_YN = ''Y''
			'
			IF @PROVIDER NOT IN('0','5','16','18')
				SET @SQLSTRING = @SQLSTRING + N'INNER JOIN PKG_MASTER_AFFILIATE Z WITH(NOLOCK) ON Z.PROVIDER = CONVERT(INT, @PROVIDER) AND A.MASTER_CODE = Z.MASTER_CODE AND Z.USE_YN=''Y''
			'
			SET @SQLSTRING = @SQLSTRING + @WHERE + N'
			GROUP BY B.CITY_CODE, A.MASTER_CODE
		) A
		GROUP BY A.CITY_CODE
		ORDER BY NAME';
	END
	

	SET @PARMDEFINITION = N'
		@PROVIDER VARCHAR(3),
		@BRAND_TYPE VARCHAR(1),
		@MIN_PRICE VARCHAR(10),
		@MAX_PRICE VARCHAR(10),
		@DEP_DATE VARCHAR(10),
		@DAY VARCHAR(5),
		@BRANCH_CODE VARCHAR(1),
		@KEYWORD NVARCHAR(1000),
		@MAIN_ATT_CODE VARCHAR(10)';

	--PRINT @SQLSTRING
		
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@PROVIDER,
		@BRAND_TYPE,
		@MIN_PRICE,
		@MAX_PRICE,
		@DEP_DATE,
		@DAY,
		@BRANCH_CODE,
		@KEYWORD,
		@MAIN_ATT_CODE;

END

GO
