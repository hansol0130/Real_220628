USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_EMPLOYEE_VISA_LIST_SELECT
■ DESCRIPTION				: BTMS 거래처 직원 비자 리스트 검색
■ INPUT PARAMETER			: 
	@PAGE_INDEX  INT		: 현재 페이지
	@PAGE_SIZE  INT			: 한 페이지 표시 게시물 수
	@KEY		VARCHAR(400): 검색 키
	@ORDER_BY	INT			: 정렬 순서
■ OUTPUT PARAMETER			: 
	@TOTAL_COUNT INT OUTPUT	: 총 항목 수
■ EXEC						: 

	DECLARE @TOTAL INT
	EXEC DBO.XP_COM_EMPLOYEE_VISA_LIST_SELECT 1, 10, @TOTAL OUTPUT, 'AgentCode=92756&TeamSeq=100&EmpSeq=114&EmpName=테스트', 1
	
	DECLARE @TOTAL INT
	EXEC DBO.XP_COM_EMPLOYEE_VISA_LIST_SELECT 1, 20, @TOTAL OUTPUT, 'AgentCode=92756', 1

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-01-26		김성호			최초생성
   2016-01-28		김성호			EMP_SEQ 검색 조건 추가
   2016-01-29		정지용			게시물 자동증가 번호 추가
   2016-02-13		강태영			EXPIRE_REMAINDER 추가(기간만료 남은 일수), 정렬 추가
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_EMPLOYEE_VISA_LIST_SELECT]
	@PAGE_INDEX  INT,
	@PAGE_SIZE  INT,
	@TOTAL_COUNT INT OUTPUT,
	@KEY		VARCHAR(400),
	@ORDER_BY	INT
AS 
BEGIN

	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000);
	DECLARE @WHERE NVARCHAR(4000), @SORT_STRING VARCHAR(50);

	DECLARE @AGT_CODE VARCHAR(10), @TEAM_SEQ INT, @EMP_SEQ INT, @KOR_NAME VARCHAR(20), @NATION_CODE VARCHAR(2)

	SELECT
		@AGT_CODE = DBO.FN_PARAM(@KEY, 'AgentCode'),
		@TEAM_SEQ = DBO.FN_PARAM(@KEY, 'TeamSeq'),
		@EMP_SEQ = DBO.FN_PARAM(@KEY, 'EmpSeq'),
		@KOR_NAME = DBO.FN_PARAM(@KEY, 'EmpName'),
		@NATION_CODE = DBO.FN_PARAM(@KEY, 'NationCode')

	SELECT @WHERE = 'WHERE A.AGT_CODE = @AGT_CODE'

	IF @EMP_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND A.EMP_SEQ = @EMP_SEQ'
	END
	ELSE IF @TEAM_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND A.TEAM_SEQ = @TEAM_SEQ'
	END

	IF LEN(@KOR_NAME) > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND A.KOR_NAME LIKE ''%'' + @KOR_NAME + ''%'''
	END

	IF LEN(@NATION_CODE) > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND B.NATION_CODE = @NATION_CODE'
	END

	SET @WHERE = @WHERE + ' AND B.USE_YN = ''Y'''

	-- SORT 조건 만들기  
	SELECT @SORT_STRING = (  
		CASE @ORDER_BY
			WHEN 1 THEN 'DATEDIFF(DD, B.VISA_ISSUE, B.VISA_EXPIRE) ASC'
			WHEN 2 THEN 'B.VISA_EXPIRE ASC'
			WHEN 3 THEN 'B.VISA_ISSUE ASC'
			WHEN 4 THEN 'A.KOR_NAME ASC'
			--WHEN 5 THEN 'A.WORK_TYPE'
			ELSE 'B.VISA_EXPIRE ASC'
		END
	)

	SET @SQLSTRING = N'
		-- 전체 조회 갯수
		SELECT @TOTAL_COUNT = COUNT(*)
		FROM COM_EMPLOYEE A WITH(NOLOCK)
		INNER JOIN COM_EMP_VISA B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.EMP_SEQ = B.EMP_SEQ
		' + @WHERE + N';

		-- 리스트 조회
		WITH LIST AS
		(
			SELECT 
				ROW_NUMBER() OVER (ORDER BY ' + @SORT_STRING + ') AS ROW_NUM,
				A.AGT_CODE, A.EMP_SEQ, B.VISA_SEQ
			FROM COM_EMPLOYEE A WITH(NOLOCK)
			INNER JOIN COM_EMP_VISA B WITH(NOLOCK) ON A.AGT_CODE = B.AGT_CODE AND A.EMP_SEQ = B.EMP_SEQ
			' + @WHERE + N'
			ORDER BY ROW_NUM
			OFFSET ((@PAGE_INDEX - 1) * @PAGE_SIZE) ROWS FETCH NEXT @PAGE_SIZE
			ROWS ONLY
		)
		SELECT @TOTAL_COUNT - ( ROW_NUM - 1) AS SEQ_NO, A.AGT_CODE, A.EMP_SEQ, A.EMP_ID, A.KOR_NAME, A.LAST_NAME, A.FIRST_NAME, A.BIRTH_DATE, A.GENDER, C.TEAM_NAME
			, D.POS_NAME, B.NATION_CODE, E.KOR_NAME AS [NATION_NAME], B.VISA_TYPE, B.VISA_NUM, B.VISA_ISSUE, B.VISA_EXPIRE
			, DATEDIFF(DD, B.VISA_ISSUE, B.VISA_EXPIRE) AS EXPIRE_REMAINDER
		FROM LIST Z
		INNER JOIN COM_EMPLOYEE A WITH(NOLOCK) ON Z.AGT_CODE = A.AGT_CODE AND Z.EMP_SEQ = A.EMP_SEQ
		INNER JOIN COM_EMP_VISA B WITH(NOLOCK) ON Z.AGT_CODE = B.AGT_CODE AND Z.EMP_SEQ = B.EMP_SEQ AND Z.VISA_SEQ = B.VISA_SEQ
		LEFT JOIN COM_TEAM C WITH(NOLOCK) ON A.AGT_CODE = C.AGT_CODE AND A.TEAM_SEQ = C.TEAM_SEQ
		LEFT JOIN COM_POSITION D WITH(NOLOCK) ON A.AGT_CODE = D.AGT_CODE AND A.POS_SEQ = D.POS_SEQ
		LEFT JOIN PUB_NATION E WITH(NOLOCK) ON B.NATION_CODE = E.NATION_CODE
		ORDER BY ' + @SORT_STRING

	SET @PARMDEFINITION = N'
		@PAGE_INDEX INT,
		@PAGE_SIZE INT,
		@TOTAL_COUNT INT OUTPUT,
		@AGT_CODE VARCHAR(10),
		@TEAM_SEQ INT,
		@EMP_SEQ INT,
		@KOR_NAME VARCHAR(20),
		@NATION_CODE VARCHAR(2)';

	--PRINT @SQLSTRING
		
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@PAGE_INDEX,
		@PAGE_SIZE,
		@TOTAL_COUNT OUTPUT,
		@AGT_CODE,
		@TEAM_SEQ,
		@EMP_SEQ,
		@KOR_NAME,
		@NATION_CODE;

END 





GO
