USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: SP_CUS_MEMBER_SLEEP_INSERT
■ DESCRIPTION				: 고객 정보 휴면 처리
■ INPUT PARAMETER			: 
	@CUS_NO INT				:
	@TYPE	INT				:
■ OUTPUT PARAMETER			:
■ EXEC						: 
■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2015-09-01		김성호			최초생성
   2017-10-25		박형만			최종로그인 날짜 업데이트 제외 MWController.LoginMember() 에서 처리 
   2018-07-23		박형만			휴대폰인증 PHONE_AUTH  , SNS_MEM_YN 추가 
================================================================================================================*/ 
CREATE PROCEDURE [dbo].[XP_CUS_MEMBER_SLEEP_INSERT]
	@CUS_NO INT,
	@TYPE	INT		-- 1: 휴면처리, 2: 복구
AS
BEGIN

	BEGIN TRAN;

	BEGIN TRY;

		IF @TYPE = 1 AND EXISTS(SELECT 1 FROM CUS_MEMBER WITH(NOLOCK) WHERE CUS_NO = @CUS_NO AND CUS_ID IS NOT NULL)	-- 휴면처리
		BEGIN
		
			INSERT INTO CUS_MEMBER_SLEEP (
				CUS_NO, CUS_ID, CUS_PASS, CUS_STATE, CUS_NAME, LAST_NAME, FIRST_NAME, NICKNAME, EMAIL, GENDER, SOC_NUM1, NOR_TEL1, NOR_TEL2, NOR_TEL3, 
				COM_TEL1, COM_TEL2, COM_TEL3, HOM_TEL1, HOM_TEL2, HOM_TEL3, FAX_TEL1, FAX_TEL2, FAX_TEL3, VISA_YN, PASS_YN, PASS_NUM, PASS_EXPIRE, PASS_ISSUE, 
				[NATIONAL], FOREIGNER_YN, CUS_GRADE, ETC, ETC2, BIRTHDAY, LUNAR_YN, RCV_EMAIL_YN, RCV_SMS_YN, ADDRESS1, ADDRESS2, ZIP_CODE, NEW_CODE, NEW_DATE,
				EDT_MESSAGE, OLD_YN, CU_YY, CU_SEQ, RCV_DM_YN, POINT_CONSENT, POINT_CONSENT_DATE, VSOC_NUM, 
				IPIN_DUP_INFO, IPIN_CONN_INFO, IPIN_ACC_DATE, TERMS2_YN, TERMS3_YN, SAFE_ID, BIRTH_DATE, OCB_AGREE_YN, OCB_AGREE_DATE, OCB_AGREE_EMP_CODE, OCB_CARD_NUM, 
				JOIN_TYPE, CERT_YN, LAST_LOGIN_DATE , PHONE_AUTH_YN , PHONE_AUTH_DATE , SNS_MEM_YN 
			)
			SELECT
				A.CUS_NO, A.CUS_ID, A.CUS_PASS, A.CUS_STATE, A.CUS_NAME, A.LAST_NAME, A.FIRST_NAME, A.NICKNAME, A.EMAIL, A.GENDER, A.SOC_NUM1, A.NOR_TEL1, A.NOR_TEL2, A.NOR_TEL3, 
				A.COM_TEL1, A.COM_TEL2, A.COM_TEL3, A.HOM_TEL1, A.HOM_TEL2, A.HOM_TEL3, A.FAX_TEL1, A.FAX_TEL2, A.FAX_TEL3, A.VISA_YN, A.PASS_YN, A.PASS_NUM, A.PASS_EXPIRE, A.PASS_ISSUE, 
				A.[NATIONAL], A.FOREIGNER_YN, A.CUS_GRADE, A.ETC, A.ETC2, A.BIRTHDAY, A.LUNAR_YN, A.RCV_EMAIL_YN, A.RCV_SMS_YN, A.ADDRESS1, A.ADDRESS2, A.ZIP_CODE, A.NEW_CODE, A.NEW_DATE,
				A.EDT_MESSAGE, A.OLD_YN, A.CU_YY, A.CU_SEQ, A.RCV_DM_YN, A.POINT_CONSENT, A.POINT_CONSENT_DATE, A.VSOC_NUM, 
				A.IPIN_DUP_INFO, A.IPIN_CONN_INFO, A.IPIN_ACC_DATE, A.TERMS2_YN, A.TERMS3_YN, A.SAFE_ID, A.BIRTH_DATE, A.OCB_AGREE_YN, A.OCB_AGREE_DATE, A.OCB_AGREE_EMP_CODE, A.OCB_CARD_NUM, 
				A.JOIN_TYPE, A.CERT_YN, A.LAST_LOGIN_DATE , A.PHONE_AUTH_YN , A.PHONE_AUTH_DATE , A.SNS_MEM_YN 
			FROM CUS_MEMBER A WITH(NOLOCK)
			WHERE A.CUS_NO = @CUS_NO;

			UPDATE CUS_MEMBER SET 
				CUS_ID = NULL, CUS_PASS = NULL, CUS_NAME = '', LAST_NAME = NULL, FIRST_NAME = NULL, NICKNAME = NULL, EMAIL = NULL, GENDER = NULL, SOC_NUM1 = NULL, 
				NOR_TEL1 = NULL, NOR_TEL2 = NULL, NOR_TEL3 = NULL, COM_TEL1 = NULL, COM_TEL2 = NULL, COM_TEL3 = NULL, HOM_TEL1 = NULL, HOM_TEL2 = NULL, HOM_TEL3 = NULL, 
				FAX_TEL1 = NULL, FAX_TEL2 = NULL, FAX_TEL3 = NULL, VISA_YN = NULL, PASS_YN = NULL, PASS_NUM = NULL, PASS_EXPIRE = NULL, PASS_ISSUE = NULL, [NATIONAL] = NULL, 
				FOREIGNER_YN = NULL, CUS_GRADE = NULL, ETC = NULL, ETC2 = NULL, BIRTHDAY = NULL, LUNAR_YN = NULL, RCV_EMAIL_YN = NULL, RCV_SMS_YN = NULL, 
				ADDRESS1 = NULL, ADDRESS2 = NULL, ZIP_CODE = NULL, EDT_MESSAGE = NULL, OLD_YN = NULL, CU_YY = NULL, CU_SEQ = NULL, RCV_DM_YN = NULL, 
				POINT_CONSENT = NULL, POINT_CONSENT_DATE = NULL, VSOC_NUM = NULL, IPIN_DUP_INFO = NULL, IPIN_CONN_INFO = NULL, IPIN_ACC_DATE = NULL, 
				TERMS2_YN = NULL, TERMS3_YN = NULL, SAFE_ID = NULL, BIRTH_DATE = NULL, OCB_AGREE_YN = NULL, OCB_AGREE_DATE = NULL, OCB_AGREE_EMP_CODE = NULL, OCB_CARD_NUM = NULL, 
				CERT_YN = NULL, LAST_LOGIN_DATE = NULL , PHONE_AUTH_YN = NULL , PHONE_AUTH_DATE = NULL , SNS_MEM_YN = NULL  
			WHERE CUS_NO = @CUS_NO;

		END
		ELSE IF @TYPE = 2	-- 복구
		BEGIN
			IF EXISTS(SELECT 1 FROM CUS_MEMBER_SLEEP WITH(NOLOCK) WHERE CUS_NO = @CUS_NO)
			BEGIN
				UPDATE A SET
					A.CUS_ID = B.CUS_ID, A.CUS_PASS = B.CUS_PASS, A.CUS_NAME = B.CUS_NAME, A.LAST_NAME = B.LAST_NAME, A.FIRST_NAME = B.FIRST_NAME, A.NICKNAME = B.NICKNAME, 
					A.EMAIL = B.EMAIL, A.GENDER = B.GENDER, A.SOC_NUM1 = B.SOC_NUM1, A.NOR_TEL1 = B.NOR_TEL1, A.NOR_TEL2 = B.NOR_TEL2, A.NOR_TEL3 = B.NOR_TEL3, 
					A.COM_TEL1 = B.COM_TEL1, A.COM_TEL2 = B.COM_TEL2, A.COM_TEL3 = B.COM_TEL3, A.HOM_TEL1 = B.HOM_TEL1, A.HOM_TEL2 = B.HOM_TEL2, A.HOM_TEL3 = B.HOM_TEL3, 
					A.FAX_TEL1 = B.FAX_TEL1, A.FAX_TEL2 = B.FAX_TEL2, A.FAX_TEL3 = B.FAX_TEL3, A.VISA_YN = B.VISA_YN, A.PASS_YN = B.PASS_YN, 
					A.PASS_NUM = B.PASS_NUM, A.PASS_EXPIRE = B.PASS_EXPIRE, A.PASS_ISSUE = B.PASS_ISSUE, A.[NATIONAL] = B.[NATIONAL], A.FOREIGNER_YN = B.FOREIGNER_YN, 
					A.CUS_GRADE = B.CUS_GRADE, A.ETC = B.ETC, A.ETC2 = B.ETC2, A.BIRTHDAY = B.BIRTHDAY, A.LUNAR_YN = B.LUNAR_YN, 
					A.RCV_EMAIL_YN = B.RCV_EMAIL_YN, A.RCV_SMS_YN = B.RCV_SMS_YN, A.ADDRESS1 = B.ADDRESS1, A.ADDRESS2 = B.ADDRESS2, A.ZIP_CODE = B.ZIP_CODE, A.EDT_MESSAGE = B.EDT_MESSAGE, 
					A.OLD_YN = B.OLD_YN, A.CU_YY = B.CU_YY, A.CU_SEQ = B.CU_SEQ, A.RCV_DM_YN = B.RCV_DM_YN, A.POINT_CONSENT = B.POINT_CONSENT, A.POINT_CONSENT_DATE = B.POINT_CONSENT_DATE, 
					A.VSOC_NUM = B.VSOC_NUM, A.IPIN_DUP_INFO = B.IPIN_DUP_INFO, A.IPIN_CONN_INFO = B.IPIN_CONN_INFO, A.IPIN_ACC_DATE = B.IPIN_ACC_DATE, 
					A.TERMS2_YN = B.TERMS2_YN, A.TERMS3_YN = B.TERMS3_YN, A.SAFE_ID = B.SAFE_ID, A.BIRTH_DATE = B.BIRTH_DATE, A.OCB_AGREE_YN = B.OCB_AGREE_YN, 
					A.OCB_AGREE_DATE = B.OCB_AGREE_DATE, A.OCB_AGREE_EMP_CODE = B.OCB_AGREE_EMP_CODE, A.OCB_CARD_NUM = B.OCB_CARD_NUM, 
					A.JOIN_TYPE = B.JOIN_TYPE, A.CERT_YN = B.CERT_YN, A.LAST_LOGIN_DATE = B.LAST_LOGIN_DATE,
					A.PHONE_AUTH_YN = B.PHONE_AUTH_YN, A.PHONE_AUTH_DATE = B.PHONE_AUTH_DATE  , A.SNS_MEM_YN = B.SNS_MEM_YN
				FROM CUS_MEMBER A
				INNER JOIN CUS_MEMBER_SLEEP B ON A.CUS_NO = B.CUS_NO
				WHERE B.CUS_NO = @CUS_NO;

				DELETE FROM CUS_MEMBER_SLEEP WHERE CUS_NO = @CUS_NO;
			END

			---- 최종 로그인 날짜 업데이트
			--UPDATE CUS_MEMBER SET LAST_LOGIN_DATE = GETDATE() WHERE CUS_NO = @CUS_NO;
		END

		IF @@TRANCOUNT > 0
			COMMIT TRAN

	END TRY
	BEGIN CATCH 

		IF @@TRANCOUNT > 0
			ROLLBACK TRAN

		-- 에러로그
		INSERT INTO VGLog.dbo.SYS_ERP_LOG(
			LOG_TYPE,
			CATEGORY,
			TITLE, 
			BODY, 
			REQUEST, 
			TRACE)
		VALUES(
			1,				-- LogTypeEnum { None = 0, Error, Warning, Information };
			'고객휴면처리',
			ERROR_PROCEDURE(),
			ERROR_MESSAGE(),
			'CUS_NO:' + CONVERT(VARCHAR(10), @CUS_NO) + ' / TYPE:' + CONVERT(VARCHAR(1), @TYPE),
			ERROR_LINE());

	END CATCH

END



GO
