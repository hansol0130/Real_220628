USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO

/*================================================================================================================
■ USP_NAME					: SP_MOV2_RESERVE_AIR_SEGMENT_SELECT
■ DESCRIPTION				: 검색_항공세그먼트
■ INPUT PARAMETER			: RES_CODE, CUS_NO
■ EXEC						: 
    -- SP_MOV2_RESERVE_AIR_SEGMENT_SELECT 'RP1703032908'

■ MEMO						: 항공 세그먼트 정보를 가져온다.
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			        DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2017-08-02		오준욱(IBSOLUTION)		최초생성
================================================================================================================*/ 
CREATE PROCEDURE [dbo].[SP_MOV2_RESERVE_AIR_SEGMENT_SELECT]
	@RES_CODE		VARCHAR(20)
AS
BEGIN

	SELECT 
		A.DIRECTION,
		ROW_NUMBER() OVER( PARTITION BY A.DIRECTION ORDER BY A.DIRECTION , A.SEQ_NO) AS SEG_NUM ,
		(SELECT COUNT(*) FROM RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE AND DIRECTION = A.DIRECTION ) AS SEG_CNT,
		CASE WHEN ROW_NUMBER() OVER( PARTITION BY A.DIRECTION ORDER BY A.DIRECTION , A.SEQ_NO) 
			= (SELECT COUNT(*) FROM RES_SEGMENT WHERE RES_CODE = A.RES_CODE AND DIRECTION = A.DIRECTION ) 
			THEN 'Y' ELSE 'N' END AS LAST_YN,
		A.RES_CODE,			A.SEQ_NO,			A.DEP_AIRPORT_CODE,		A.ARR_AIRPORT_CODE,	
		A.DEP_CITY_CODE,	A.ARR_CITY_CODE,	A.AIRLINE_CODE,			A.SEAT_STATUS,
		A.FLIGHT,			A.NEW_DATE,			A.NEW_CODE,				A.[START_DATE],
		A.END_DATE,			A.FLYING_TIME,		A.GROUND_TIME,			A.AIRLINE_PNR,
		A.BKG_CLASS,		A.DIRECTION,		A.OP_AIRLINE_CODE,
		dbo.FN_GET_TIME_BY_MINUTE(DATEDIFF(MINUTE, A.END_DATE,(SELECT TOP 1 START_DATE FROM RES_SEGMENT WITH(NOLOCK) 
			WHERE RES_CODE = A.RES_CODE AND DIRECTION = A.DIRECTION AND SEQ_NO > A.SEQ_NO ORDER BY SEQ_NO)) )AS WAIT_TIME ,
		(SELECT TOP 1 GROUND_TIME FROM RES_TRANSIT_SEGMENT WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE AND SEQ_NO = A.SEQ_NO ORDER BY [NO] DESC) AS [TRANSIT_GROUND_TIME],
		(
			SELECT KOR_NAME FROM PUB_CITY WITH(NOLOCK) WHERE CITY_CODE IN (
				SELECT TOP 1 DEP_CITY_CODE FROM RES_TRANSIT_SEGMENT WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE AND SEQ_NO = A.SEQ_NO ORDER BY [NO] DESC
			)
		) AS [TRANSIT_CITY_NAME],
		(SELECT KOR_NAME FROM PUB_AIRLINE WITH(NOLOCK) WHERE AIRLINE_CODE = A.AIRLINE_CODE) AS AIRLINE_NAME,
		(SELECT KOR_NAME FROM PUB_AIRLINE WITH(NOLOCK) WHERE AIRLINE_CODE = A.OP_AIRLINE_CODE) AS OP_AIRLINE_NAME,
		(SELECT KOR_NAME FROM PUB_AIRPORT WITH(NOLOCK) WHERE AIRPORT_CODE = A.DEP_AIRPORT_CODE) AS DEP_AIRPORT_NAME,
		(SELECT KOR_NAME FROM PUB_AIRPORT WITH(NOLOCK) WHERE AIRPORT_CODE = A.ARR_AIRPORT_CODE) AS ARR_AIRPORT_NAME,
		(SELECT KOR_NAME FROM PUB_CITY WITH(NOLOCK) WHERE CITY_CODE IN (SELECT CITY_CODE FROM PUB_AIRPORT WITH(NOLOCK) WHERE AIRPORT_CODE = A.DEP_AIRPORT_CODE)) AS [DEP_CITY_NAME],
		(SELECT KOR_NAME FROM PUB_CITY WITH(NOLOCK) WHERE CITY_CODE IN (SELECT CITY_CODE FROM PUB_AIRPORT WITH(NOLOCK) WHERE AIRPORT_CODE = A.ARR_AIRPORT_CODE)) AS [ARR_CITY_NAME],
		(CASE WHEN SEAT_STATUS = 'HK' THEN 'Y' ELSE 'N' END) AS SEAT_YN	 ,
		A.FREE_BAG_INFO ,
		(SELECT TOP 1 FARE_SEAT_TYPE FROM RES_AIR_DETAIL WITH(NOLOCK) WHERE RES_CODE = A.RES_CODE ) AS FARE_SEAT_TYPE 
	FROM RES_SEGMENT A WITH(NOLOCK)
	WHERE A.RES_CODE = @RES_CODE
	ORDER BY A.SEQ_NO

END           



GO
