USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: XP_COM_BIZTRIP_EMPLOYEE_LOCATION
■ DESCRIPTION				: BTMS 출장자 위치 현황
■ INPUT PARAMETER			: 
	@KEY		VARCHAR(400): 검색 키
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=92756&TeamSeq=100&EmpName=테스트&ProType=3'
	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=92756&Date=2016-05-05'
	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=93831&ProType=2'
	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=92756&TeamSeq=&EmpName=&ProType=3&Date=2016-05-02'
	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=92756&TeamSeq=&EmpName=&ProType=2&Date=2016-05-02'
	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=93831&TeamSeq=&EmpName=&ProType=2&Date=2016-07-24'
	EXEC DBO.XP_COM_BIZTRIP_EMPLOYEE_LOCATION 'AgentCode=93853&TeamSeq=&EmpName=&ProType=3&Date=2016-07-29'

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2016-03-08	 저스트고_강태영	  최초생성
================================================================================================================*/ 
CREATE PROC [dbo].[XP_COM_BIZTRIP_EMPLOYEE_LOCATION]

@KEY VARCHAR(400)

AS
BEGIN
	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000);
	DECLARE @WHERE NVARCHAR(4000), @TABLE_JOIN_TYPE NVARCHAR(1000), @SELECT_LIST NVARCHAR(100), @GROUP_BY NVARCHAR(100);
	DECLARE @AGT_CODE VARCHAR(10), @TEAM_SEQ INT, @EMP_NAME VARCHAR(20), @PRO_TYPE INT, @DATE varchar(20)

	SELECT
		@AGT_CODE = DBO.FN_PARAM(@KEY, 'AgentCode'),
		@TEAM_SEQ = DBO.FN_PARAM(@KEY, 'TeamSeq'),
		@EMP_NAME = DBO.FN_PARAM(@KEY, 'EmpName'),
		@PRO_TYPE = DBO.FN_PARAM(@KEY, 'ProType'),
		@DATE = DBO.FN_PARAM(@KEY, 'Date')

	SELECT @WHERE = ' WHERE A.RES_CODE IN (SELECT RES_CODE FROM COM_BIZTRIP_DETAIL WHERE BT_CODE IN (SELECT BT_CODE FROM COM_BIZTRIP_MASTER WHERE AGT_CODE = @AGT_CODE)) '
	SELECT @WHERE = @WHERE + ' AND B.AGT_CODE = @AGT_CODE AND C.RES_STATE <> 9 AND C.RES_STATE <> 7 AND D.APPROVAL_STATE = 2 '
	
	IF @TEAM_SEQ > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND I.TEAM_SEQ = @TEAM_SEQ '
	END

	IF LEN(@EMP_NAME) > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND A.CUS_NAME LIKE ''%'' + @EMP_NAME + ''%'' '
	END

	IF @PRO_TYPE > 0
	BEGIN
		SET @WHERE = @WHERE + ' AND C.PRO_TYPE = @PRO_TYPE '
	END
	
	IF @DATE != ''
	BEGIN
		IF @PRO_TYPE = 2
		BEGIN
			SET @WHERE = @WHERE + ' AND (SELECT START_DATE FROM RES_SEGMENT WHERE SEQ_NO = K.SEQ_NO AND RES_CODE = A.RES_CODE) < DATEADD(DAY, 1, CONVERT(DATETIME, @DATE)) '
			SET @WHERE = @WHERE + 'AND (CASE WHEN (SELECT COUNT(*) FROM RES_SEGMENT WHERE RES_CODE = A.RES_CODE) > 2
									THEN ((DATEADD(DAY, 1, ISNULL((SELECT START_DATE FROM RES_SEGMENT WHERE SEQ_NO = (K.SEQ_NO + 1) AND RES_CODE = A.RES_CODE), CONVERT(DATETIME, @DATE)))))
									ELSE
									(DATEADD(DAY, 1, ISNULL((SELECT END_DATE FROM RES_SEGMENT WHERE RES_CODE = A.RES_CODE AND SEQ_NO = (SELECT COUNT(*) FROM RES_SEGMENT WHERE RES_CODE = A.RES_CODE)), CONVERT(DATETIME, ''9999-12-30''))) - 1)
									END) > CONVERT(DATETIME, @DATE)'
			SET @SELECT_LIST = ' DISTINCT A.CUS_NAME, F.REGION_CODE, DBO.FN_RES_AIR_GET_PRO_NAME(A.RES_CODE) AS JOURNEY '
			SET @GROUP_BY = ' GROUP BY A.CUS_NAME, F.REGION_CODE, K.DEP_CITY_CODE, DBO.FN_RES_AIR_GET_PRO_NAME(A.RES_CODE) '

			SET @TABLE_JOIN_TYPE = 'LEFT JOIN RES_SEGMENT K ON K.RES_CODE = A.RES_CODE
									LEFT JOIN PUB_AIRPORT L ON L.AIRPORT_CODE = K.ARR_AIRPORT_CODE
									LEFT JOIN PUB_CITY E WITH(NOLOCK) ON E.CITY_CODE = L.CITY_CODE'
		END

		ELSE

		BEGIN
			SET @WHERE = @WHERE + ' AND C.DEP_DATE < DATEADD(DAY, 1, CONVERT(DATETIME, @DATE)) AND DATEADD(DAY, 1, ISNULL(C.ARR_DATE, CONVERT(DATETIME, ''9999-12-30''))) > CONVERT(DATETIME, @DATE) '
			SET @TABLE_JOIN_TYPE = 'LEFT JOIN PUB_CITY E WITH(NOLOCK) ON E.CITY_CODE = D.BT_CITY_CODE'
			SET @SELECT_LIST = ' A.CUS_NAME, F.REGION_CODE '
			SET @GROUP_BY = ''
		END
	END

	SET @SQLSTRING = N'
		SELECT
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 230 THEN 1 ELSE 0 END), 0) AS AFRICA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 210 THEN 1 ELSE 0 END), 0) AS EUROPE,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 220 THEN 1 ELSE 0 END), 0) AS MIDDLE_EAST,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 110 THEN 1 ELSE 0 END), 0) AS NORTH_AMERICA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 120 THEN 1 ELSE 0 END), 0) AS MIDDLE_AMERICA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 130 THEN 1 ELSE 0 END), 0) AS SOUTH_AMERICA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 320 THEN 1 ELSE 0 END), 0) AS CHINA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 330 THEN 1 ELSE 0 END), 0) AS SOUTH_EAST_ASIA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 310 THEN 1 ELSE 0 END), 0) AS JAPAN,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 340 THEN 1 ELSE 0 END), 0) AS OCEANIA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 140 THEN 1 ELSE 0 END), 0) AS CARIBBEAN,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 350 THEN 1 ELSE 0 END), 0) AS WEST_INDIA,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 360 THEN 1 ELSE 0 END), 0) AS SAIPAN_GUAM,
			ISNULL(SUM(CASE WHEN Z.REGION_CODE = 370 THEN 1 ELSE 0 END), 0) AS FAR_EAST
			--ISNULL(SUM(CASE WHEN Z.REGION_CODE = 999 THEN 1 ELSE 0 END), 0) AS ETC
		FROM
		(SELECT'
		+ @SELECT_LIST + '
		FROM RES_CUSTOMER A WITH(NOLOCK)
		INNER JOIN COM_BIZTRIP_DETAIL B WITH(NOLOCK) ON B.RES_CODE = A.RES_CODE
		INNER JOIN RES_MASTER_damo C WITH(NOLOCK) ON C.RES_CODE = A.RES_CODE
		INNER JOIN COM_BIZTRIP_MASTER D WITH(NOLOCK) ON D.BT_CODE = B.BT_CODE
		' + @TABLE_JOIN_TYPE +'
		LEFT JOIN PUB_NATION F WITH(NOLOCK) ON F.NATION_CODE = E.NATION_CODE
		LEFT JOIN PUB_REGION G WITH(NOLOCK) ON G.REGION_CODE = F.REGION_CODE
		LEFT JOIN COM_EMPLOYEE_MATCHING H WITH(NOLOCK) ON H.CUS_NO = A.CUS_NO AND H.AGT_CODE = B.AGT_CODE
		LEFT JOIN COM_EMPLOYEE I WITH(NOLOCK) ON I.EMP_SEQ = H.EMP_SEQ AND I.AGT_CODE = B.AGT_CODE
		' + @WHERE + @GROUP_BY + N'
		)Z '

	SET @PARMDEFINITION = N'
		@AGT_CODE VARCHAR(10),
		@TEAM_SEQ INT,
		@EMP_NAME VARCHAR(20),
		@PRO_TYPE INT,
		@DATE DATETIME';

	PRINT @SQLSTRING

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@AGT_CODE,
		@TEAM_SEQ,
		@EMP_NAME,
		@PRO_TYPE,
		@DATE

END
GO
