USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*
2011-09-02 상품코드 추가 
2011-11-01 날짜 하루씩 +1 
2012-01-05 월별 예약현황 수정사항 반영 
*/
CREATE PROCEDURE [dbo].[SP_RES_HTL_CALENDAR]
	@SEARCH_TYPE	INT,
	@PRO_TYPE		INT,
	@EMP_CODE		VARCHAR(7),
	@TEAM_CODE		VARCHAR(3),
	@START_DATE		VARCHAR(10),
	@END_DATE		VARCHAR(10),
	@RES_STATE	INT 
AS
BEGIN
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED
--DECLARE @SEARCH_TYPE	INT,
--	@PRO_TYPE		INT,
--	@EMP_CODE		VARCHAR(7),
--	@TEAM_CODE		VARCHAR(3),
--	@START_DATE		VARCHAR(10),
--	@END_DATE		VARCHAR(10),
--	@RES_STATE	INT  

--SELECT @SEARCH_TYPE=2,@PRO_TYPE=3,@EMP_CODE=N'',@TEAM_CODE=N'',@START_DATE=N'2011-12-01',@END_DATE=N'2011-12-31'
--, @RES_STATE = 0 

	DECLARE @SQLSTRING NVARCHAR(4000), @PARMDEFINITION NVARCHAR(1000)--, @OUTERJOINTABLE NVARCHAR(1000), @ORDERBY NVARCHAR(100)
	DECLARE @WHERE NVARCHAR(2000) ,@ORDER_BY  NVARCHAR(500) , @DIS_DATE NVARCHAR(100)
	SET @WHERE = ''
	SET @ORDER_BY = ''
	SET @DIS_DATE = ''
	
	SET @SQLSTRING = ''

	-- WHERE 조건 만들기
		-- 담당자코드( 없으면 팀 전체, 팀 코드가 없으면 전체)
	IF ISNULL(@TEAM_CODE, '') <> ''
	BEGIN
		IF ISNULL(@EMP_CODE, '') <> ''
			SET @WHERE = ' AND A.NEW_CODE = @EMP_CODE'
		ELSE
			SET @WHERE = ' AND A.NEW_CODE IN (SELECT EMP_CODE FROM EMP_MASTER WHERE TEAM_CODE = @TEAM_CODE)'
	END
	
	IF( @RES_STATE = 2 ) --확정
	BEGIN
		SET @WHERE = @WHERE + ' AND A.RES_STATE IN (2) '
	END 
	ELSE IF ( @RES_STATE = 4 ) -- 결제완료
	BEGIN
		SET @WHERE = @WHERE + ' AND A.RES_STATE IN (4) '
	END 
	ELSE IF ( @RES_STATE = 9 ) --취소 
	BEGIN
		SET @WHERE = @WHERE + ' AND A.RES_STATE IN ( 7,8,9) '
	END 

	-- 서브테이블
	IF @SEARCH_TYPE = 0				-- 예약일
	BEGIN
		SET @DIS_DATE = @DIS_DATE + ' A.NEW_DATE   '  
		SET @WHERE = @WHERE + ' AND A.NEW_DATE BETWEEN CONVERT(DATETIME,@START_DATE) AND DATEADD(D,1,CONVERT(DATETIME,@END_DATE)) '
		SET @ORDER_BY = @ORDER_BY + ' ORDER BY A.NEW_DATE, A.RES_CODE '
	END 
	ELSE IF  @SEARCH_TYPE = 1  --체크인일
	BEGIN
		SET @DIS_DATE = @DIS_DATE + ' B.CHECK_IN   '  
		SET @WHERE = @WHERE + ' AND B.CHECK_IN BETWEEN CONVERT(DATETIME,@START_DATE) AND DATEADD(D,1,CONVERT(DATETIME,@END_DATE))  '
		SET @ORDER_BY = @ORDER_BY + ' ORDER BY A.CHECK_IN, A.RES_CODE '
	END 
	ELSE IF  @SEARCH_TYPE = 2  --취소마감일
	BEGIN
		SET @DIS_DATE = @DIS_DATE + ' B.LAST_CXL_DATE   '  
		SET @WHERE = @WHERE + ' AND B.LAST_CXL_DATE BETWEEN CONVERT(DATETIME,@START_DATE) AND DATEADD(D,1,CONVERT(DATETIME,@END_DATE))  '
		SET @ORDER_BY = @ORDER_BY + ' ORDER BY A.LAST_CXL_DATE, A.RES_CODE '
	END 
	ELSE 
	BEGIN
		SET @DIS_DATE = @DIS_DATE + ' '''' '  
	END 
		
	SET @SQLSTRING = N'
	-- 리스트
	SELECT A.DIS_DATE, A.*
		, DBO.FN_RES_HTL_GET_PAY_STATE(A.RES_CODE) AS [PAY_STATE]
		, (SELECT ISNULL(MIN(ROOM_YN), ''N'') FROM RES_HTL_ROOM_MASTER WHERE RES_CODE = A.RES_CODE) AS [ROOM_YN]
		, B.DAY_COUNT
	FROM (
		SELECT A.RES_CODE, A.PRO_TYPE, A.RES_STATE, A.RES_NAME, A.NEW_DATE, A.NEW_CODE , A.PRO_CODE 
		, B.SUP_CODE , DBO.FN_RES_GET_RES_COUNT(A.RES_CODE) AS CUS_COUNT , 
		B.CHECK_IN  , B.LAST_CXL_DATE ,  '+@DIS_DATE+' AS DIS_DATE 
		FROM RES_MASTER_damo A WITH(NOLOCK) 
			INNER JOIN RES_HTL_ROOM_MASTER B WITH(NOLOCK) 
				ON A.RES_CODE = B.RES_CODE 
		WHERE A.PRO_TYPE = @PRO_TYPE
		' + @WHERE + '
	) A
	LEFT OUTER JOIN (
		SELECT COUNT(*) AS [DAY_COUNT], CONVERT(VARCHAR(10), A.DIS_DATE, 120) AS DIS_DATE
		FROM (
			SELECT A.RES_CODE, '+ @DIS_DATE + ' AS DIS_DATE
			FROM RES_MASTER_damo A WITH(NOLOCK) 
				INNER JOIN RES_HTL_ROOM_MASTER B WITH(NOLOCK) 
					ON A.RES_CODE = B.RES_CODE 
			WHERE A.PRO_TYPE = @PRO_TYPE
			' + @WHERE + '
		) A
		WHERE DIS_DATE BETWEEN CONVERT(DATETIME,@START_DATE) AND DATEADD(D,1,CONVERT(DATETIME,@END_DATE))
		GROUP BY CONVERT(VARCHAR(10), A.DIS_DATE, 120)
	) B ON CONVERT(VARCHAR(10), A.DIS_DATE, 120) = B.DIS_DATE
	' + @ORDER_BY 
	
	SET @PARMDEFINITION = N'@SEARCH_TYPE INT, @PRO_TYPE INT, @EMP_CODE VARCHAR(7), @TEAM_CODE VARCHAR(3), @START_DATE VARCHAR(10), @END_DATE VARCHAR(10)'

	--PRINT @SQLSTRING
	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION, @SEARCH_TYPE, @PRO_TYPE, @EMP_CODE , @TEAM_CODE, @START_DATE, @END_DATE

END

--	@SEARCH_TYPE, @EMP_CODE , @TEAM_CODE, @START_DATE, @END_DATE
--	SP_RES_HTL_CALENDAR 0, 3, '2008006', '529', '2009-03-01', '2009-03-31'
--	SP_RES_HTL_CALENDAR 0, 3, '', '', '2009-03-01', '2009-03-31'
GO
