USE [Diablo]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: SP_SND_EARLY_MORNING_RES_CUSTOMER_LIST
■ DESCRIPTION				: 새벽 출발 고객 리스트 ( 00시 ~ 05시 까지 )
■ INPUT PARAMETER			: 
■ OUTPUT PARAMETER			: 
■ EXEC						: 
	EXEC SP_SND_EARLY_MORNING_RES_CUSTOMER_LIST 
■ MEMO						: 8시간 전 문자 보내기 ( 매일기준 배치 16시 부터 21시까지 )
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2018-01-08		정지용			최초생성
================================================================================================================*/ 
CREATE PROC [dbo].[SP_SND_EARLY_MORNING_RES_CUSTOMER_LIST]
AS 
BEGIN	
	SET TRANSACTION ISOLATION LEVEL READ UNCOMMITTED;
		
	WITH LIST AS (
		SELECT 	
			  A.RES_CODE, A.CUS_NAME
			, CC.NOR_TEL1, CC.NOR_TEL2, CC.NOR_TEL3
			, D.DEP_TRANS_CODE AS AIRLINE_CODE
			, ABS(DATEDIFF(HH, GETDATE(), CONVERT(DATETIME,CONVERT(VARCHAR(10),D.DEP_DEP_DATE,121) + ' ' + D.DEP_DEP_TIME + ':00'))) AS HOUR_DIFF
			, CONVERT(DATETIME,CONVERT(VARCHAR(10),D.DEP_DEP_DATE,121) + ' ' + D.DEP_DEP_TIME + ':00') AS DEP_DEP_DATE
			, DATEADD(DAY, 1, CONVERT(DATETIME,CONVERT(VARCHAR(10), GETDATE(), 121) + ' 00:00:00')) AS DEP_START_DATE
			, DATEADD(DAY, 1, CONVERT(DATETIME,CONVERT(VARCHAR(10), GETDATE(), 121) + ' 05:00:00')) AS DEP_END_DATE
		FROM RES_CUSTOMER_damo A WITH(NOLOCK)	
		INNER JOIN RES_MASTER B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE AND B.PRO_TYPE = 1
		INNER JOIN PKG_DETAIL C WITH(NOLOCK) ON B.PRO_CODE = C.PRO_CODE
		INNER JOIN CUS_CUSTOMER CC WITH(NOLOCK) ON A.CUS_NO = CC.CUS_NO
		LEFT JOIN PRO_TRANS_SEAT D WITH(NOLOCK) ON C.SEAT_CODE = D.SEAT_CODE
		WHERE B.DEP_DATE > GETDATE() AND A.RES_STATE = 0 
				AND CC.NOR_TEL1 IS NOT NULL AND CC.NOR_TEL2 IS NOT NULL AND CC.NOR_TEL3 IS NOT NULL
		UNION ALL
		SELECT
				A.RES_CODE, A.CUS_NAME
			, CC.NOR_TEL1, CC.NOR_TEL2, CC.NOR_TEL3
			, C.AIRLINE_CODE
			, ABS(DATEDIFF(HH, GETDATE(), B.DEP_DATE)) AS HOUR_DIFF
			, B.DEP_DATE AS DEP_DEP_DATE
			, DATEADD(DAY, 1, CONVERT(DATETIME,CONVERT(VARCHAR(10), GETDATE(), 121) + ' 00:00:00')) AS DEP_START_DATE
			, DATEADD(DAY, 1, CONVERT(DATETIME,CONVERT(VARCHAR(10), GETDATE(), 121) + ' 05:00:00')) AS DEP_END_DATE
		FROM RES_CUSTOMER_damo A WITH(NOLOCK)
		INNER JOIN RES_MASTER B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE AND B.PRO_TYPE = 2
		INNER JOIN RES_AIR_DETAIL C WITH(NOLOCK) ON B.RES_CODE = C.RES_CODE
		INNER JOIN CUS_CUSTOMER CC WITH(NOLOCK) ON A.CUS_NO = CC.CUS_NO
		WHERE B.DEP_DATE > GETDATE() AND A.RES_STATE = 0 
				AND CC.NOR_TEL1 IS NOT NULL AND CC.NOR_TEL2 IS NOT NULL AND CC.NOR_TEL3 IS NOT NULL
	)
	SELECT 
		RES_CODE, CUS_NAME, NOR_TEL1, NOR_TEL2, NOR_TEL3, AIRLINE_CODE, HOUR_DIFF, DEP_DEP_DATE, DEP_START_DATE, DEP_END_DATE,
		'[참좋은여행]고객님께서는 금일 야간 비행편 탑승 예정입니다. 이 점 꼭 참고하시어 금일 저녁 공항에 도착하시기 바랍니다.' AS ETC_REMARK
	FROM LIST WHERE HOUR_DIFF = 8 AND DEP_DEP_DATE >= DEP_START_DATE AND DEP_DEP_DATE <= DEP_END_DATE ORDER BY DEP_DEP_DATE;

END
GO
