USE [Sirens]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: SP_CTI_ESTI_LIST_UPDATE
■ DESCRIPTION				: 평가 등록 저장(CTI_ESTI_MASTER, CTI_ESTI_LIST)
■ INPUT PARAMETER			: 
	@SHEET_CODE				:쉬트코드
	@TITLE					:제목
	@MODIFY_FLAG			:수정가능여부
	@EMP_CODE				:직원코드
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	EXECUTE SP_CTI_ESTI_LIST_UPDATE 

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2014-12-22		곽병삼			최초생성
================================================================================================================*/ 
CREATE PROCEDURE [cti].[SP_CTI_ESTI_LIST_UPDATE]
--DECLARE
	@ESTI_WOL		VARCHAR(10),
	@SHEET_CODE		INT,
	@IROW			INT,
	@ESTI_TEAM_CODE	VARCHAR(3),
	@ESTI_EMP_CODE	VARCHAR(7),
	@TEAM_CODE		VARCHAR(3),
	@EMP_CODE		VARCHAR(7),
	@CONSULT_SEQ	VARCHAR(14),
	@ITEM_CODE		VARCHAR(4),
	@ITEM_VALUE		INT,
	@ITEM_MEMO		NVARCHAR(1000),
	@COMMENT		NVARCHAR(2000)

AS

--SET @ESTI_WOL = '2014-12-01'
--SET @SHEET_CODE = 1
--SET @IROW = 1
--SET @ESTI_TEAM_CODE = '538'
--SET @ESTI_EMP_CODE = '2013069'
--SET @TEAM_CODE = '538'
--SET @EMP_CODE = '2012019'
--SET @CONSULT_SEQ = '20141221000007'
--SET @ITEM_CODE = '0101'
--SET @ITEM_VALUE = 5
--SET @ITEM_MEMO = '메모사항입력'
--SET @COMMENT = '코멘트입력'

BEGIN

	SET @ESTI_WOL = REPLACE(LEFT(@ESTI_WOL,7),'-','')

	-- 평가결과 마스터
	MERGE Sirens.cti.CTI_ESTI_MASTER AS TARGET
	USING (SELECT @ESTI_WOL AS ESTI_WOL, @SHEET_CODE AS SHEET_CODE, @ESTI_EMP_CODE AS ESTI_EMP_CODE) AS SOURCE
	ON (TARGET.ESTI_WOL = SOURCE.ESTI_WOL AND TARGET.SHEET_CODE = SOURCE.SHEET_CODE AND TARGET.EMP_CODE = SOURCE.ESTI_EMP_CODE)
	WHEN MATCHED THEN
	UPDATE SET 
		TARGET.ESTI_VALUE = CASE WHEN @IROW > 0 THEN ISNULL(TARGET.ESTI_VALUE,0) + @ITEM_VALUE ELSE @ITEM_VALUE END,
		TARGET.COMMENT = @COMMENT,
		TARGET.EDT_DATE = GETDATE(), 
		TARGET.EDT_CODE = @EMP_CODE
	WHEN NOT MATCHED BY TARGET THEN
	INSERT (ESTI_WOL, SHEET_CODE, CONSULT_SEQ, EMP_CODE, TEAM_CODE, ESTI_VALUE, COMMENT, NEW_DATE, NEW_CODE)
	VALUES (
		@ESTI_WOL, 
		@SHEET_CODE,
		@CONSULT_SEQ,
		@ESTI_EMP_CODE,
		@ESTI_TEAM_CODE,
		@ITEM_VALUE,
		@COMMENT,
		GETDATE(),
		@EMP_CODE
	);

	-- 평가결과 항목별
	MERGE Sirens.cti.CTI_ESTI_LIST AS TARGET
	USING (SELECT @ESTI_WOL AS ESTI_WOL, @SHEET_CODE AS SHEET_CODE, @ESTI_EMP_CODE AS ESTI_EMP_CODE, @ITEM_CODE AS ITEM_CODE) AS SOURCE
	ON (TARGET.ESTI_WOL = SOURCE.ESTI_WOL AND TARGET.SHEET_CODE = SOURCE.SHEET_CODE AND TARGET.EMP_CODE = SOURCE.ESTI_EMP_CODE AND TARGET.ITEM_CODE = SOURCE.ITEM_CODE)
	WHEN MATCHED THEN
	UPDATE SET 
		TARGET.ITEM_VALUE = @ITEM_VALUE,
		TARGET.ITEM_MEMO = @ITEM_MEMO
	WHEN NOT MATCHED BY TARGET THEN
	INSERT (ESTI_WOL, SHEET_CODE, EMP_CODE, ITEM_CODE, ITEM_VALUE, ITEM_MEMO)
	VALUES (
		@ESTI_WOL, 
		@SHEET_CODE,
		@ESTI_EMP_CODE,
		@ITEM_CODE,
		@ITEM_VALUE,
		@ITEM_MEMO
	);
END
GO
