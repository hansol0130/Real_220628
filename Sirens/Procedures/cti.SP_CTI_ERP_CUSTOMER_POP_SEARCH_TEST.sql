USE [Sirens]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO


/*================================================================================================================
■ USP_NAME					: SP_CTI_ERP_CUSTOMER_POP_SEARCH
■ DESCRIPTION				: ERP 고객정보 팝업 검색
■ INPUT PARAMETER			: 
	@CUS_ID					: Web고객ID
	@CUS_NAME				: 고객명
	@CUS_BIRTH				: 생년월일
	@GENDER					: 성별
	@RES_CODE				: 예약코드
	@CUS_NUMBER1			: 고객전화번호
	@CUS_NUMBER2			: 고객전화번호
	@CUS_NUMBER3			: 고객전화번호
	@CUS_EMAIL				: 고객이메일
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	exec CTI.SP_CTI_ERP_CUSTOMER_POP_SEARCH  'nolran','김성호','1977-09-12','M','','010-3444-1147','junggun@verygoodtour.com'

	exec CTI.SP_CTI_ERP_CUSTOMER_POP_SEARCH  '','','','','','01089368090',''

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2014-10-17		곽병삼			최초생성
   2014-10-27		박형만			연락처 4자리 검색시 뒷자리 번호로만 검색되도록 , 예약건수 IPIN_DUP_INFO - >CUS_NO
   2014-11-03		김성호			수정
   2014-11-15		김성호			고객 검색 예약 수 표시 수정
   2014-11-17		김성호			NVARCHAR(MAX) 문자열 잘림 현상 수정 - 다른 형의 문자열을 한꺼번에 저장하는것을 분할 저장
   2014-11-20		김성호			행사코드, 예약코드 같은 항목으로 처리
   2014-12-05		김성호			EMAIL 인덱스 사용 가능하도록 수정
   2014-12-17		곽병삼			예약건수 1년이내 조회조건 제외.
   2014-12-17		김성호			7, 8자리 전화번호 처리
   2014-12-22		곽병삼			국번이 빈값이면 TEL2, TEL3내용 NOR_TEL, HOM_TEL, COM_TEL 구성
   2015-01-22		김성호			고객약속 우선순위 변경 (고객약속 등록일이 늦을수록 약속일이 빠를수록)
   2015-09-04		정지용			회원 조회 변경 ( 휴면 계정 ) # 만일에 조건 추가되면 회원테이블쪽도 같이 필드 추가해주어야되요!! ( VARCHAR MAX 걸려서 필요값만 SELECT 함 )
   2015-09-11		김성호			상담 조회 쿼리 튜닝 -> INDEX(IX_CTI_CONSULT_SEARCH) 인덱스 힌트 적용
   2015-11-26		정지용			조건없이 들어올 경우가 있음.. WHERE 생성해줌..
   2018-02-27		정지용			CUS_CONSULT_CNT(상담내역 30일 이내 기준 카운팅), CUS_GRADE 추가 
   2019-03-20		김남훈			전화번호 처리중에 01로 들어오는건 핸드폰만 검색, 4자리일경우 폰번호만 검색하게, 이름 LIKE 검색 의미없음
================================================================================================================*/ 

CREATE PROCEDURE [cti].[SP_CTI_ERP_CUSTOMER_POP_SEARCH_TEST]
--DECLARE
	@CUS_ID		VARCHAR(20),
	@CUS_NAME	VARCHAR(20),
	@CUS_BIRTH	VARCHAR(10),
	@GENDER		VARCHAR(1),
	@RES_CODE	VARCHAR(20),
	@CUS_TEL	VARCHAR(20),
	@CUS_EMAIL	VARCHAR(40)

AS
--DECLARE @TEL VARCHAR(20)
--SET @TEL = '3076841'
--SELECT SUBSTRING(@TEL, 1, (LEN(@TEL) - 4)), SUBSTRING(@TEL, (LEN(@TEL) - 3), 4)

	DECLARE @SQLSTRING NVARCHAR(MAX), @PARMDEFINITION NVARCHAR(1000), @WHERE NVARCHAR(1000)
	DECLARE @CUS_TEL1 VARCHAR(4), @CUS_TEL2 VARCHAR(4), @CUS_TEL3 VARCHAR(20)

	SELECT @WHERE = '', @CUS_TEL = REPLACE(@CUS_TEL, '-', '');

	-- 전화번호 처리
	IF ISNULL(@CUS_TEL, '') <> ''
	BEGIN
		IF LEN(@CUS_TEL) = 7 OR LEN(@CUS_TEL) = 8
		BEGIN
			SELECT @CUS_TEL1 = '', @CUS_TEL2 = SUBSTRING(@CUS_TEL, 1, (LEN(@CUS_TEL) - 4)), @CUS_TEL3 = SUBSTRING(@CUS_TEL, (LEN(@CUS_TEL) - 3), 4)
		END
		ELSE IF LEN(@CUS_TEL) = 9
		BEGIN
			SELECT @CUS_TEL1 = SUBSTRING(@CUS_TEL, 1, 2), @CUS_TEL2 = SUBSTRING(@CUS_TEL, 3, 3), @CUS_TEL3 = SUBSTRING(@CUS_TEL, 6, 4)
		END
		ELSE IF LEN(@CUS_TEL) = 10 AND SUBSTRING(@CUS_TEL, 1, 2) = '02'
		BEGIN
			SELECT @CUS_TEL1 = SUBSTRING(@CUS_TEL, 1, 2), @CUS_TEL2 = SUBSTRING(@CUS_TEL, 3, 4), @CUS_TEL3 = SUBSTRING(@CUS_TEL, 7, 4)
		END
		ELSE IF LEN(@CUS_TEL) = 10 AND SUBSTRING(@CUS_TEL, 1, 2) <> '02'
		BEGIN
			SELECT @CUS_TEL1 = SUBSTRING(@CUS_TEL, 1, 3), @CUS_TEL2 = SUBSTRING(@CUS_TEL, 4, 3), @CUS_TEL3 = SUBSTRING(@CUS_TEL, 7, 4)
		END
		ELSE IF LEN(@CUS_TEL) = 11
		BEGIN
			SELECT @CUS_TEL1 = SUBSTRING(@CUS_TEL, 1, 3), @CUS_TEL2 = SUBSTRING(@CUS_TEL, 4, 4), @CUS_TEL3 = SUBSTRING(@CUS_TEL, 8, 4)
		END
		ELSE
		BEGIN
			SELECT @CUS_TEL3 = @CUS_TEL
		END 

		IF LEN(@CUS_TEL) = 4
		BEGIN
			SET @WHERE = @WHERE + N' AND (A.NOR_TEL3 = @CUS_TEL3)'
		END
		ELSE IF LEN(@CUS_TEL) > 4 AND @CUS_TEL LIKE '01%'
		BEGIN
			SET @WHERE = @WHERE + N' AND ((A.NOR_TEL3 = @CUS_TEL3 AND A.NOR_TEL2 = @CUS_TEL2 AND ISNULL(A.NOR_TEL1, '''') = @CUS_TEL1))'
		END
		ELSE IF LEN(@CUS_TEL) > 4 AND @CUS_TEL NOT LIKE '01%'
		BEGIN
			SET @WHERE = @WHERE + N' AND ((A.COM_TEL3 = @CUS_TEL3 AND A.COM_TEL2 = @CUS_TEL2 AND ISNULL(A.COM_TEL1, '''') = @CUS_TEL1)
				OR (A.HOM_TEL3 = @CUS_TEL3 AND A.HOM_TEL2 = @CUS_TEL2 AND ISNULL(A.HOM_TEL1, '''') = @CUS_TEL1))'
		END
	END

	-- 예약코드가 12자리를 넘기면 행사코드로 인지해서 동작
	IF LEN(@RES_CODE) > 12
	BEGIN
		SET @WHERE = @WHERE + N' AND A.CUS_NO IN (
			SELECT A.CUS_NO 
			FROM Diablo.dbo.RES_MASTER_damo A WITH(NOLOCK) 
			WHERE A.PRO_CODE = @RES_CODE 
			UNION ALL
			SELECT B.CUS_NO 
			FROM Diablo.dbo.RES_MASTER_damo A WITH(NOLOCK) 
			INNER JOIN Diablo.dbo.RES_CUSTOMER_damo B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE
			WHERE A.PRO_CODE = @RES_CODE
		)'
	END
	ELSE IF ISNULL(@RES_CODE, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + N' AND A.CUS_NO IN (SELECT AA.CUS_NO FROM Diablo.DBO.RES_MASTER_damo AA WITH(NOLOCK) WHERE AA.RES_CODE = @RES_CODE UNION SELECT BB.CUS_NO FROM Diablo.DBO.RES_CUSTOMER_damo BB WITH(NOLOCK) WHERE BB.RES_CODE = @RES_CODE)'
	END

	IF ISNULL(@CUS_ID, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + N' AND A.CUS_ID = @CUS_ID'
	END

	IF ISNULL(@CUS_NAME, '') <> ''
	BEGIN 
		SET @WHERE = @WHERE + N' AND A.CUS_NAME = @CUS_NAME'
		--SET @WHERE = @WHERE + ' AND A.CUS_NAME = @CUS_NAME'
	END

	IF ISNULL(@CUS_EMAIL, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + N' AND A.EMAIL LIKE @CUS_EMAIL + ''%'''
	END

	IF ISNULL(@CUS_BIRTH, '') <> '' AND ISDATE(@CUS_BIRTH) > 0
	BEGIN
		SET @WHERE = @WHERE + N' AND A.BIRTH_DATE = CONVERT(DATETIME, @CUS_BIRTH)'
	END

	IF ISNULL(@GENDER, '') <> ''
	BEGIN
		SET @WHERE = @WHERE + N' AND A.GENDER = @GENDER'
	END

	IF ISNULL(@WHERE, '') <> ''
	BEGIN
		SELECT @WHERE = N'WHERE ' + SUBSTRING(@WHERE, 5, 3000)
	END
	ELSE
	BEGIN 
		SELECT @WHERE = N'WHERE 1 = 1'
	END

	SET @SQLSTRING = N'WITH LIST AS
(
	-- 웹회원 우선
	SELECT A.CUS_NO, MAX(A.MEMBER_TYPE) AS [MEMBER_TYPE]
	FROM (
		SELECT TOP 10 A.CUS_NO, (CASE WHEN B.CUS_NO IS NULL THEN ''C'' WHEN B.CUS_NO > 0 THEN ''M'' END) AS [MEMBER_TYPE]
		FROM Diablo.DBO.CUS_CUSTOMER_damo A WITH(NOLOCK)
		LEFT JOIN Diablo.DBO.CUS_MEMBER B WITH(NOLOCK) ON A.CUS_NO = B.CUS_NO
		' + @WHERE + N'
		UNION ALL
		SELECT A.CUS_NO, ''M'' AS [MEMBER_TYPE]
		--FROM Diablo.DBO.CUS_MEMBER A WITH(NOLOCK)
		FROM (
			SELECT TOP 10
				A.CUS_NO
			FROM Diablo.dbo.VIEW_MEMBER A WITH(NOLOCK)
			' + @WHERE + N' 
		) A
	) A
	GROUP BY A.CUS_NO
)';

	SET @SQLSTRING = @SQLSTRING + N'

SELECT	TOP 200
	Z.FLAG,
	A.CUS_NO,
	A.CUS_ID,
	A.CUS_NAME,
	CONVERT(VARCHAR(10),A.BIRTH_DATE,120) AS CUS_BIRTH,
	DATEDIFF(YEAR,CAST(A.BIRTH_DATE AS DATETIME),GETDATE()) AS AGE,
	CASE A.GENDER WHEN ''M'' THEN ''남'' WHEN ''F'' THEN ''여'' END AS GENDER,
	CASE WHEN ISNULL(A.NOR_TEL1,'''') = '''' THEN (A.NOR_TEL2+''-''+A.NOR_TEL3) ELSE (A.NOR_TEL1+''-''+A.NOR_TEL2+''-''+A.NOR_TEL3) END AS CUS_TEL,
	A.EMAIL AS CUS_EMAIL,
	B.RES_COUNT,
	B.OLD_RES_COUNT,
	CONVERT(VARCHAR(10),A.NEW_DATE,120) AS NEW_DATE,
	A.LAST_NAME + ''/'' + A.FIRST_NAME AS ENG_NAME,
	CASE WHEN ISNULL(A.HOM_TEL1,'''') = '''' THEN (A.HOM_TEL2+''-''+A.HOM_TEL3) ELSE (A.HOM_TEL1+''-''+A.HOM_TEL2+''-''+A.HOM_TEL3) END AS HOM_TEL,
	CASE WHEN ISNULL(A.COM_TEL1,'''') = '''' THEN (A.COM_TEL2+''-''+A.COM_TEL3) ELSE (A.COM_TEL1+''-''+A.COM_TEL2+''-''+A.COM_TEL3) END AS COM_TEL,
	A.RCV_SMS_YN,
	A.RCV_EMAIL_YN,
	A.CUS_GRADE,
	(SELECT COUNT(1) FROM CTI.CTI_CONSULT WITH(NOLOCK) WHERE CUS_NO = A.CUS_NO AND CONSULT_SEQ >= CONVERT(CHAR(8),DATEADD(M,-1,GETDATE()),112) + ''000000'' AND SAVE_TYPE <> ''0'' ) AS CUS_CONSULT_CNT
FROM (
	SELECT *, ROW_NUMBER() OVER (PARTITION BY A.CUS_NO ORDER BY A.FLAG, A.SEQ) AS [SEQ_NUM]
	FROM (
		-- 고객과의 약속
		SELECT ''1'' AS [FLAG], AA.CUS_NO AS [CUS_NO], ROW_NUMBER() OVER(ORDER BY AA.NEW_DATE DESC, AA.CONSULT_RES_DATE) AS [SEQ]
		FROM CTI.CTI_CONSULT_RESERVATION AA WITH(NOLOCK)
		INNER JOIN CTI.CTI_CONSULT BB WITH(NOLOCK) ON AA.CONSULT_SEQ = BB.CONSULT_SEQ
		WHERE AA.CONSULT_RES_DATE >= DATEADD(M, -1, GETDATE()) AND AA.CUS_NO IN (SELECT CUS_NO FROM LIST)
		UNION ALL
		-- 상담 내역
		SELECT ''2'', AA.CUS_NO, ROW_NUMBER() OVER(ORDER BY AA.CONSULT_DATE DESC)
		FROM CTI.CTI_CONSULT AA WITH(NOLOCK, INDEX(IX_CTI_CONSULT_SEARCH))
		WHERE AA.CONSULT_DATE >= DATEADD(M, -1, GETDATE()) AND AA.CUS_NO IN (SELECT CUS_NO FROM LIST)
		UNION ALL
		-- 예약 정보
		SELECT AA.FLAG, AA.CUS_NO, ROW_NUMBER() OVER(ORDER BY AA.DEP_DATE)
		FROM (
			SELECT (CASE WHEN AA.DEP_DATE > GETDATE() THEN ''3'' ELSE ''4'' END) AS [FLAG], AA.CUS_NO, AA.DEP_DATE
			FROM Diablo.DBO.RES_MASTER_damo AA WITH(NOLOCK)
			WHERE AA.ARR_DATE > DATEADD(D, -15, GETDATE()) AND AA.RES_STATE <> 9 AND AA.CUS_NO IN (SELECT CUS_NO FROM LIST)
			UNION ALL
			SELECT (CASE WHEN AA.DEP_DATE > GETDATE() THEN ''3'' ELSE ''4'' END), BB.CUS_NO, AA.DEP_DATE
			FROM Diablo.DBO.RES_MASTER_damo AA WITH(NOLOCK)
			INNER JOIN Diablo.DBO.RES_CUSTOMER_damo BB WITH(NOLOCK) ON AA.RES_CODE = BB.RES_CODE
			WHERE AA.ARR_DATE > DATEADD(D, -15, GETDATE()) AND AA.RES_STATE <> 9 AND BB.RES_STATE <> 1 AND BB.CUS_NO IN (SELECT CUS_NO FROM LIST)
		) AA
		UNION ALL
		SELECT (CASE WHEN AA.MEMBER_TYPE = ''M'' THEN ''5'' ELSE ''6'' END), AA.CUS_NO, ROW_NUMBER() OVER(ORDER BY AA.CUS_NO DESC)
		FROM LIST AA
	) A
) Z
INNER JOIN Diablo.DBO.CUS_CUSTOMER_DAMO A WITH(NOLOCK) ON Z.CUS_NO = A.CUS_NO
LEFT JOIN (
	SELECT A.CUS_NO, SUM(CASE WHEN A.DEP_DATE > DATEADD(DD, -1, GETDATE()) THEN 1 ELSE 0 END) AS [RES_COUNT]
		, SUM(CASE WHEN A.DEP_DATE < DATEADD(DD, -1, GETDATE()) THEN 1 ELSE 0 END) AS [OLD_RES_COUNT]
	FROM (
		SELECT A.CUS_NO, A.RES_CODE, A.DEP_DATE
		FROM Diablo.DBO.RES_MASTER_DAMO A WITH(NOLOCK)
		WHERE A.RES_STATE <= 7 AND A.CUS_NO IN (SELECT CUS_NO FROM LIST)
		UNION
		SELECT B.CUS_NO, A.RES_CODE, A.DEP_DATE
		FROM Diablo.DBO.RES_MASTER_DAMO A WITH(NOLOCK)
		INNER JOIN Diablo.DBO.RES_CUSTOMER_damo B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE
		WHERE A.RES_STATE <= 7 AND B.RES_STATE = 0 AND B.CUS_NO IN (SELECT CUS_NO FROM LIST)
	) A
	GROUP BY A.CUS_NO
) B ON Z.CUS_NO = B.CUS_NO
WHERE Z.SEQ_NUM = 1
ORDER BY Z.FLAG, Z.SEQ';

/* 예약건수 조회 1년이내 조건
	SELECT A.CUS_NO, A.RES_CODE, A.DEP_DATE
	FROM Diablo.DBO.RES_MASTER_DAMO A WITH(NOLOCK)
	WHERE A.DEP_DATE > DATEADD(YY, -1, GETDATE())
		AND A.RES_STATE <= 7 AND A.CUS_NO IN (SELECT CUS_NO FROM LIST)
	UNION
	SELECT B.CUS_NO, A.RES_CODE, A.DEP_DATE
	FROM Diablo.DBO.RES_MASTER_DAMO A WITH(NOLOCK)
	INNER JOIN Diablo.DBO.RES_CUSTOMER_damo B WITH(NOLOCK) ON A.RES_CODE = B.RES_CODE
	WHERE A.DEP_DATE > DATEADD(YY, -1, GETDATE())
		AND A.RES_STATE <= 7 AND B.RES_STATE = 0 AND B.CUS_NO IN (SELECT CUS_NO FROM LIST)
*/
	SET @PARMDEFINITION = N'
		@CUS_ID	VARCHAR(20),
		@CUS_NAME	VARCHAR(20),
		@CUS_BIRTH	VARCHAR(10),
		@GENDER		VARCHAR(1),
		@RES_CODE	VARCHAR(20),
		@CUS_TEL1	VARCHAR(4),
		@CUS_TEL2	VARCHAR(4),
		@CUS_TEL3	VARCHAR(4),
		@CUS_EMAIL	VARCHAR(40)';

	--SELECT LEN(@SQLSTRING)

	EXEC SP_EXECUTESQL @SQLSTRING, @PARMDEFINITION,
		@CUS_ID,
		@CUS_NAME,
		@CUS_BIRTH,
		@GENDER,
		@RES_CODE,
		@CUS_TEL1,
		@CUS_TEL2,
		@CUS_TEL3,
		@CUS_EMAIL;
/*
DECLARE 
	@SQL	VARCHAR(8000)

SET @CUS_TEL = REPLACE(@CUS_TEL,'-','')

BEGIN

	SET @SQL = 'SELECT TOP 10 * FROM (' + CHAR(13)

	SET @SQL += CHAR(9) + 'SELECT ' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.CUS_NO,' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.CUS_ID,' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.CUS_NAME,' + CHAR(10)
	SET @SQL += CHAR(9) + 'CONVERT(VARCHAR(10),A.BIRTH_DATE,120) AS CUS_BIRTH,' + CHAR(10)
	SET @SQL += CHAR(9) + 'DATEDIFF(YEAR,CAST(A.BIRTH_DATE AS DATETIME),GETDATE()) AS AGE,' + CHAR(10)
	SET @SQL += CHAR(9) + 'CASE A.GENDER WHEN ''M'' THEN ''남'' WHEN ''F'' THEN ''여'' END AS GENDER,' + CHAR(10)
	SET @SQL += CHAR(9) + '(A.NOR_TEL1+''-''+A.NOR_TEL2+''-''+A.NOR_TEL3) AS CUS_TEL,' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.EMAIL AS CUS_EMAIL,' + CHAR(10)
	--SET @SQL += CHAR(9) + '(SELECT COUNT(*) FROM Diablo.DBO.RES_MASTER_DAMO WITH(NOLOCK) WHERE A.IPIN_DUP_INFO IS NOT NULL AND DEP_DATE > GETDATE() AND IPIN_DUP_INFO = A.IPIN_DUP_INFO AND RES_STATE <= 7) AS RES_COUNT,' + CHAR(10)
	
	SET @SQL += CHAR(9) + '(
SELECT TOP 1 COUNT(RES_CODE) FROM 
	(	SELECT RES_CODE FROM Diablo.DBO.RES_MASTER_DAMO AA WITH(NOLOCK) 
		WHERE AA.DEP_DATE > GETDATE() AND AA.CUS_NO = A.CUS_NO AND AA.RES_STATE < 7  
		UNION ALL 
		SELECT AA.RES_CODE FROM Diablo.DBO.RES_MASTER_DAMO AA WITH(NOLOCK) 
			INNER JOIN DIABLO.DBO.RES_CUSTOMER_damo BB 
				ON AA.RES_CODE = BB.RES_CODE  AND AA.CUS_NO <> BB.CUS_NO 
		WHERE AA.DEP_DATE > GETDATE() AND AA.CUS_NO = A.CUS_NO AND AA.RES_STATE < 7 
		AND BB.CUS_NO = A.CUS_NO AND BB.RES_STATE = 0 
	) TBL 
) AS RES_COUNT,' + CHAR(10)
	SET @SQL += CHAR(9) + 'CONVERT(VARCHAR(10),A.NEW_DATE,120) AS NEW_DATE,' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.LAST_NAME + ''/'' + A.FIRST_NAME AS ENG_NAME,' + CHAR(10)
	SET @SQL += CHAR(9) + '(A.HOM_TEL1+''-''+A.HOM_TEL2+''-''+A.HOM_TEL3) AS HOM_TEL,' + CHAR(10)
	SET @SQL += CHAR(9) + '(A.COM_TEL1+''-''+A.COM_TEL2+''-''+A.COM_TEL3) AS COM_TEL,' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.RCV_SMS_YN,' + CHAR(10)
	SET @SQL += CHAR(9) + 'A.RCV_EMAIL_YN' + CHAR(10)
	SET @SQL += 'FROM Diablo.DBO.CUS_CUSTOMER_DAMO A WITH(NOLOCK)' + CHAR(10)
	SET @SQL += 'WHERE 1=1' + CHAR(10)

	IF LEN(ISNULL(@CUS_ID,'')) = 0 AND LEN(ISNULL(@CUS_NAME,'')) = 0 AND LEN(ISNULL(@CUS_BIRTH,''))  = 0 AND LEN(ISNULL(@GENDER,'')) = 0 AND LEN(ISNULL(@CUS_TEL,'')) = 0 AND LEN(ISNULL(@CUS_EMAIL,'')) = 0 AND LEN(ISNULL(@RES_CODE,'')) = 0
		SET @SQL += CHAR(9) + 'AND A.CUS_ID =  ''0'''  + CHAR(10)
	IF LEN(ISNULL(@CUS_ID,'')) > 0
		SET @SQL += CHAR(9) + 'AND A.CUS_ID = ''' + @CUS_ID + ''''  + CHAR(10)
	IF LEN(ISNULL(@CUS_NAME,'')) > 0
		SET @SQL += CHAR(9) + 'AND A.CUS_NAME LIKE ''%' + @CUS_NAME + '%''' + CHAR(10)
	IF LEN(ISNULL(@CUS_BIRTH,'')) > 0
		SET @SQL += CHAR(9) + 'AND A.BIRTH_DATE = CAST(''' + @CUS_BIRTH + ''' AS DATETIME)' + CHAR(10)
	IF LEN(ISNULL(@GENDER,'')) > 0
		SET @SQL += CHAR(9) + 'AND A.GENDER = ''' + @GENDER + '''' + CHAR(10)
	IF LEN(ISNULL(@CUS_TEL,'')) > 0
	BEGIN
		-- 2014-10-27 박형만 
		IF(  LEN(ISNULL(@CUS_TEL,'')) = 4)
		BEGIN
			SET @SQL += CHAR(9) + 'AND (((A.NOR_TEL3) LIKE ''%' + @CUS_TEL + '%'')' + CHAR(10)
			SET @SQL += CHAR(9) + 'OR ((A.COM_TEL3) LIKE ''%' + @CUS_TEL + '%'')' + CHAR(10)
			SET @SQL += CHAR(9) + 'OR ((A.HOM_TEL3) LIKE ''%' + @CUS_TEL + '%''))' + CHAR(10)
		END 
		ELSE 
		BEGIN
			SET @SQL += CHAR(9) + 'AND (((A.NOR_TEL1+A.NOR_TEL2+A.NOR_TEL3) LIKE ''%' + @CUS_TEL + '%'')' + CHAR(10)
			SET @SQL += CHAR(9) + 'OR ((A.COM_TEL1+A.COM_TEL2+A.COM_TEL3) LIKE ''%' + @CUS_TEL + '%'')' + CHAR(10)
			SET @SQL += CHAR(9) + 'OR ((A.HOM_TEL1+A.HOM_TEL2+A.HOM_TEL3) LIKE ''%' + @CUS_TEL + '%''))' + CHAR(10)
		END 
	END

	IF LEN(ISNULL(@CUS_EMAIL,'')) > 0
		SET @SQL += CHAR(9) + 'AND A.EMAIL LIKE ''%' + @CUS_EMAIL + '%''' + CHAR(10)
	IF LEN(ISNULL(@RES_CODE,'')) > 0
		SET @SQL += CHAR(9) + 'AND A.CUS_NO IN (SELECT CUS_NO FROM Diablo.dbo.RES_CUSTOMER_DAMO WHERE RES_CODE = ''' + @RES_CODE + ''')' + CHAR(10)

	SET @SQL += CHAR(9) +' ) TBL ' + CHAR(10) 
	SET @SQL += 'ORDER BY  RES_COUNT DESC , CASE WHEN CUS_ID IS NOT NULL THEN 1 ELSE 9 END , NEW_DATE DESC  '

	--print @sql

	EXEC (@SQL)
END
*/

GO
