USE [Sirens]
GO
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
/*================================================================================================================
■ USP_NAME					: SP_CTI_SMS_MESSAGE_SELECT
■ DESCRIPTION				: CTI SMS 메시지 조회 
■ INPUT PARAMETER			: 
	@RES_CODE				: 예약 코드 
■ OUTPUT PARAMETER			: 
■ EXEC						: 

	exec cti.SP_CTI_SMS_MESSAGE_SELECT @RES_CODE = 'APP5034-141120'

■ MEMO						: 
------------------------------------------------------------------------------------------------------------------
■ CHANGE HISTORY                   
------------------------------------------------------------------------------------------------------------------
   DATE				AUTHOR			DESCRIPTION           
------------------------------------------------------------------------------------------------------------------
   2014-10-31		박형만			최초생성
   2014-11-20		김성호			행사코드, 예약코드 모두 동작하게 수정
   2014-12-05		곽병삼			호텔상품인 경우, SMS메시지 리스트 별도로 처리.
================================================================================================================*/ 
CREATE PROCEDURE [cti].[SP_CTI_SMS_MESSAGE_SELECT]
--DECLARE
	@RES_CODE VARCHAR(20)
--SET @RES_CODE = 'RP1310241758';
AS

SET NOCOUNT ON

--DECLARE @RES_CODE VARCHAR(12)
--SELECT @RES_CODE = 'RP1310241758';

DECLARE @SIGN_CODE VARCHAR(1)
DECLARE @ATT_CODE VARCHAR(1)
DECLARE @FAX_NUMBER VARCHAR(20)
DECLARE @INNER_NUMBER VARCHAR(20)
DECLARE @FIRST_MEET VARCHAR(80)
DECLARE @TEAM_NAME VARCHAR(40)
DECLARE @TEAM_CODE VARCHAR(40)
DECLARE @EMP_NAME VARCHAR(40)
DECLARE @EMP_EMAIL VARCHAR(40)
DECLARE @PRO_CODE VARCHAR(20)
--취소마감일
DECLARE @LAST_CXL_DATE VARCHAR(10)

IF LEN(@RES_CODE) > 12
	SET @PRO_CODE = @RES_CODE;
ELSE
	SELECT @PRO_CODE = A.PRO_CODE FROM Diablo.dbo.RES_MASTER_damo A WITH(NOLOCK) WHERE A.RES_CODE = @RES_CODE;

IF LEFT(@RES_CODE,2) = 'RH'
BEGIN
	-- 호텔상품  SMS 내용
	-- 발송필요정보
	SELECT @EMP_NAME = C.KOR_NAME
		, @INNER_NUMBER = ISNULL(C.INNER_NUMBER1,'') +'-' + ISNULL(C.INNER_NUMBER2,'') +'-' +  ISNULL(C.INNER_NUMBER3,'') 
		, @FAX_NUMBER = ISNULL(C.FAX_NUMBER1,'') +'-' + ISNULL(C.FAX_NUMBER2,'') +'-' +  ISNULL(C.FAX_NUMBER3,'') 
		, @TEAM_NAME = D.TEAM_NAME, @FIRST_MEET = ISNULL(A.FIRST_MEET,'')
		, @TEAM_CODE = D.TEAM_CODE  
		, @EMP_EMAIL = ISNULL(C.EMAIL,'')
	FROM DIABLO.dbo.PKG_DETAIL A WITH(NOLOCK)
	INNER JOIN DIABLO.dbo.EMP_MASTER C WITH(NOLOCK) ON A.NEW_CODE = C.EMP_CODE
	INNER JOIN DIABLO.dbo.EMP_TEAM D WITH(NOLOCK) ON C.TEAM_CODE = D.TEAM_CODE
	WHERE A.PRO_CODE = @PRO_CODE;

	-- 취소마감일
	SELECT
		@LAST_CXL_DATE = CONVERT(VARCHAR(10), LAST_CXL_DATE, 120)
	FROM Diablo.dbo.RES_HTL_ROOM_MASTER
	WHERE RES_CODE = @RES_CODE

	SELECT
		MAIN_CODE,
		MAIN_NAME,
		SORT,
		CASE MAIN_CODE WHEN 'A' THEN REMARK
		WHEN 'B' THEN REMARK
		WHEN 'C' THEN REPLACE(REPLACE(REPLACE(REMARK, '{0}', @TEAM_NAME), '{1}', @EMP_NAME), '{2}', @INNER_NUMBER)
		WHEN 'D' THEN REMARK
		WHEN 'E' THEN REMARK
		WHEN 'F' THEN REPLACE(REMARK, '{0}', @LAST_CXL_DATE)
		WHEN 'G' THEN REMARK
		WHEN 'H' THEN REMARK
		ELSE REMARK END REMARK
	FROM CTI_CODE_MASTER
	WHERE CATEGORY = 'SMS001'
	AND USE_YN = 'Y'
	ORDER BY SORT , MAIN_CODE 
END
ELSE
BEGIN
	-- 패키지상품/항공상품 SMS 내용
	-- 발송필요정보
	SELECT  @SIGN_CODE = B.SIGN_CODE, @ATT_CODE = B.ATT_CODE, @EMP_NAME = C.KOR_NAME
		, @INNER_NUMBER = ISNULL(C.INNER_NUMBER1,'') +'-' + ISNULL(C.INNER_NUMBER2,'') +'-' +  ISNULL(C.INNER_NUMBER3,'') 
		, @FAX_NUMBER = ISNULL(C.FAX_NUMBER1,'') +'-' + ISNULL(C.FAX_NUMBER2,'') +'-' +  ISNULL(C.FAX_NUMBER3,'') 
		, @TEAM_NAME = D.TEAM_NAME, @FIRST_MEET = ISNULL(A.FIRST_MEET,'')
		, @TEAM_CODE = D.TEAM_CODE  
		, @EMP_EMAIL = ISNULL(C.EMAIL,'')
	FROM DIABLO.dbo.PKG_DETAIL A WITH(NOLOCK)
	INNER JOIN DIABLO.dbo.PKG_MASTER B WITH(NOLOCK) ON A.MASTER_CODE = B.MASTER_CODE
	INNER JOIN DIABLO.dbo.EMP_MASTER C WITH(NOLOCK) ON A.NEW_CODE = C.EMP_CODE
	INNER JOIN DIABLO.dbo.EMP_TEAM D WITH(NOLOCK) ON C.TEAM_CODE = D.TEAM_CODE
	WHERE A.PRO_CODE = @PRO_CODE;

	--항공정보 담기 
	DECLARE 
	@DEP_INFO_STR VARCHAR(100),
	@ARR_INFO_STR VARCHAR(100),
	@DEP_TRANS_CODE VARCHAR(2) , @DEP_TRANS_NUMBER  VARCHAR(4) , 
	@DEP_DEP_AIRPORT_CODE CHAR(3), @DEP_DEP_DATE DATETIME, @DEP_DEP_TIME CHAR(5),
	@DEP_ARR_AIRPORT_CODE CHAR(3), @DEP_ARR_DATE DATETIME, @DEP_ARR_TIME CHAR(5),
	@ARR_TRANS_CODE VARCHAR(2), @ARR_TRANS_NUMBER VARCHAR(4), 
	@ARR_DEP_AIRPORT_CODE CHAR(3), @ARR_DEP_DATE DATETIME, @ARR_DEP_TIME CHAR(5),
	@ARR_ARR_AIRPORT_CODE CHAR(3), @ARR_ARR_DATE DATETIME, @ARR_ARR_TIME CHAR(5)

	-- 좌석코드 유무
	IF EXISTS(SELECT 1 FROM Diablo.dbo.PKG_DETAIL A WITH(NOLOCK) WHERE A.PRO_CODE = @PRO_CODE AND A.SEAT_CODE > 0)
	BEGIN
		SELECT 
			@DEP_TRANS_CODE = DEP_TRANS_CODE, @DEP_TRANS_NUMBER = DEP_TRANS_NUMBER, 
			@DEP_DEP_AIRPORT_CODE = DEP_DEP_AIRPORT_CODE, @DEP_DEP_DATE = DEP_DEP_DATE, @DEP_DEP_TIME = DEP_DEP_TIME,
			@DEP_ARR_AIRPORT_CODE = DEP_ARR_AIRPORT_CODE, @DEP_ARR_DATE = DEP_ARR_DATE, @DEP_ARR_TIME = DEP_ARR_TIME,  
			@ARR_TRANS_CODE = ARR_TRANS_CODE, @ARR_TRANS_NUMBER = ARR_TRANS_NUMBER, 
			@ARR_DEP_AIRPORT_CODE = ARR_DEP_AIRPORT_CODE, @ARR_DEP_DATE = ARR_DEP_DATE, @ARR_DEP_TIME = ARR_DEP_TIME,
			@ARR_ARR_AIRPORT_CODE = ARR_ARR_AIRPORT_CODE, @ARR_ARR_DATE = ARR_ARR_DATE, @ARR_ARR_TIME = ARR_ARR_TIME   
		FROM Diablo.DBO.PRO_TRANS_SEAT E 
		WHERE SEAT_CODE IN (SELECT SEAT_CODE FROM Diablo.DBO.PKG_DETAIL WHERE PRO_CODE = (@PRO_CODE))
	END
	ELSE IF( SUBSTRING(@RES_CODE,1,2) ='RT')
	BEGIN
		SELECT @DEP_TRANS_CODE = AIRLINE_CODE ,@DEP_TRANS_NUMBER = FLIGHT,
		@DEP_DEP_AIRPORT_CODE = DEP_AIRPORT_CODE , @DEP_DEP_DATE = CONVERT(VARCHAR(10),START_DATE,121) ,@DEP_DEP_TIME = CONVERT(VARCHAR(5),START_DATE,114)  
		FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND SEQ_NO = (SELECT MIN(SEQ_NO) FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND DIRECTION = 0 )
		SELECT 
		@DEP_ARR_AIRPORT_CODE = DEP_AIRPORT_CODE , @DEP_ARR_DATE = CONVERT(VARCHAR(10),END_DATE,121) ,@DEP_ARR_TIME = CONVERT(VARCHAR(5),END_DATE,114)  
		FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND SEQ_NO = (SELECT MIN(SEQ_NO) FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND DIRECTION = 0 )

		SELECT @ARR_TRANS_CODE = AIRLINE_CODE ,@ARR_TRANS_NUMBER = FLIGHT,
		@ARR_DEP_AIRPORT_CODE = DEP_AIRPORT_CODE , @ARR_DEP_DATE = CONVERT(VARCHAR(10),START_DATE,121) ,@ARR_DEP_TIME = CONVERT(VARCHAR(5),START_DATE,114)  
		FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND SEQ_NO = (SELECT MIN(SEQ_NO) FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND DIRECTION = 1 )
		SELECT 
		@ARR_ARR_AIRPORT_CODE = DEP_AIRPORT_CODE , @ARR_ARR_DATE = CONVERT(VARCHAR(10),END_DATE,121) ,@ARR_ARR_TIME = CONVERT(VARCHAR(5),END_DATE,114)  
		FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND SEQ_NO = (SELECT MIN(SEQ_NO) FROM Diablo.DBO.RES_SEGMENT WITH(NOLOCK) WHERE RES_CODE = @RES_CODE AND DIRECTION = 1 )
	END

	SET @DEP_INFO_STR = '한국출발' + SUBSTRING(CONVERT(VARCHAR(10),@DEP_DEP_DATE,121),6,2) +'월' +SUBSTRING(CONVERT(VARCHAR(10),@DEP_DEP_DATE,121),9,2) +'일'
	+ '[' + @DEP_TRANS_CODE + @DEP_TRANS_NUMBER +']' + @DEP_DEP_AIRPORT_CODE + ' '+@DEP_DEP_TIME + '>' + @DEP_ARR_AIRPORT_CODE + ' ' + @DEP_ARR_TIME + ' ' 

	SET @ARR_INFO_STR = 
	 '/현지출발' + SUBSTRING(CONVERT(VARCHAR(10),@ARR_DEP_DATE,121),6,2) +'월' +SUBSTRING(CONVERT(VARCHAR(10),@ARR_DEP_DATE,121),9,2)  +'일'
	+ '[' + @ARR_TRANS_CODE + @ARR_TRANS_NUMBER +']' + @ARR_DEP_AIRPORT_CODE + ' '+@ARR_DEP_TIME + '>' + @ARR_ARR_AIRPORT_CODE + ' ' + @ARR_ARR_TIME  
  

	--('[참좋은여행] {0}팀 예약담당자 {1} 입니다. 직통전화번호 {2}')
	/*
	REPLACE(REMARK ,'{0}', @TEAM_NAME)
	REPLACE(REPLACE(REMARK ,'{0}', @TEAM_NAME) ,'{1}', @TEAM_NAME)
	REPLACE(REPLACE(REPLACE(REMARK ,'{0}', @TEAM_NAME) ,'{1}', @TEAM_NAME) ,'{2}', @TEAM_NAME)
	REPLACE(REPLACE(REPLACE(REPLACE(REMARK ,'{0}', @TEAM_NAME) ,'{1}', @TEAM_NAME) ,'{2}', @TEAM_NAME) ,'{3}', @TEAM_NAME)
	*/
	
	SELECT
		MAIN_CODE,
		MAIN_NAME,
		SORT,
		CASE WHEN MAIN_CODE = 'C' THEN REPLACE(REMARK ,'{0}', @FAX_NUMBER)  
			 WHEN MAIN_CODE = 'D' THEN REPLACE(REPLACE(REPLACE(REMARK ,'{0}', @TEAM_NAME) ,'{1}', @EMP_NAME) ,'{2}', @INNER_NUMBER)
			 WHEN MAIN_CODE = 'E' THEN REPLACE(REMARK ,'{0}', @EMP_NAME)
			 WHEN MAIN_CODE = 'F' THEN REPLACE(REMARK ,'{0}', @FIRST_MEET)
			 WHEN MAIN_CODE = 'G' THEN REPLACE(REMARK ,'{0}', '010-5858-9075')  -- 공항팀연락처
			 WHEN MAIN_CODE = 'I' THEN REPLACE(REPLACE(REMARK ,'{0}', @TEAM_NAME) ,'{1}', @EMP_NAME)
			 WHEN MAIN_CODE = 'J' THEN REPLACE(REPLACE(REPLACE(REPLACE(REMARK ,'{0}', @EMP_NAME) ,'{1}', @TEAM_NAME) ,'{2}', @FAX_NUMBER) ,'{3}', @EMP_EMAIL)

			 WHEN MAIN_CODE = 'K' THEN REPLACE(REPLACE(REMARK ,'{0}', '{0}') ,'{1}', @EMP_NAME)
			 WHEN MAIN_CODE = 'L' THEN REPLACE(REPLACE(REMARK ,'{0}', @DEP_INFO_STR) ,'{1}', @ARR_INFO_STR)
			 ELSE REMARK 
		END AS REMARK 
	FROM CTI_CODE_MASTER
	WHERE (@RES_CODE <> '' AND CATEGORY = 'SMS000') OR (@RES_CODE = '' AND CATEGORY = 'SMS002')
	AND USE_YN = 'Y'
	ORDER BY SORT , MAIN_CODE 
END

---
/*

INSERT INTO CTI_CODE_MASTER 
		SELECT 'SMS000','SMS전송내용','K','일정표',NULL,	NULL,0,'Y','[참좋은여행]일정표보기{0} 담당자 {1}',GETDATE(),'2013007',	NULL,NULL
UNION ALL SELECT 'SMS000','SMS전송내용','L','항공정보',NULL,	NULL,0,'Y','[참좋은여행]{0}{1}',GETDATE(),'2013007',	NULL,NULL
*/

SET NOCOUNT OFF 

GO
